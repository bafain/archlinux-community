# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: waterbear <st92.at.freeshell.org>

pkgname=simh
pkgver=3.9.0
_pkgver=39-0
pkgrel=3
pkgdesc="simulator for assorted historical computers, from Altair to VAX"
arch=('x86_64')
url="http://simh.trailing-edge.com"
license=("BSD")
depends=("libpcap")
makedepends=("unzip")
install=simh.install
source=("http://simh.trailing-edge.com/sources/simhv${_pkgver}.zip"
	"LICENSE")
sha512sums=('5e8cbf92e980b7cb40d68ff60a266f70c0bef0eecdc169a8077ae72a6a71fe12a7be5c62bd033ca36727e2cb8f266a5a6f8a5ed6dd509afbf0a9d7b618f1a4f6'
            '084faf8a66c95cb337bca19a6116c916aa04fc23ec5d57d5d8c2fc168231272123a705e83f8da2bf38f404da51727a69db68ffd87b36028e0cde52b54b92b2dd')

build() {
  cd "$srcdir"
  mkdir -p BIN
  find VAX -type f -exec sed -i 's/SIM_INLINE//' {} +
  make USE_NETWORK=1 NETWORK_OPT='-DUSE_NETWORK -isystem /usr/include -lpcap'
}

package() {
  cd "$srcdir/BIN"
  for i in *; do
    install -D $i "$pkgdir/usr/bin/simh-$i"
  done

  cd "$srcdir/VAX"
  mkdir -p "$pkgdir/usr/lib/simh"
  cp *.bin "$pkgdir/usr/lib/simh"

  install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
