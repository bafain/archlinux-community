# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-django
pkgname=('python-pytest-django' 'python2-pytest-django')
pkgver=3.4.1
pkgrel=1
pkgdesc="A Django plugin for py.test"
arch=('any')
license=('BSD')
url="https://github.com/pytest-dev/pytest-django"
makedepends=('python-pytest' 'python2-pytest' 'python-setuptools-scm' 'python2-setuptools-scm'
             'python2-pathlib')
checkdepends=('python-django' 'python2-django' 'python-pytest-xdist' 'python2-pytest-xdist')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-django/archive/$pkgver.tar.gz")
sha512sums=('d449b9b3badc2bd4cba0249deabdee1161df751674b49ef1a1023a77a2a19a03bfead7a1b1e4d73a63818a157d9887942b2b01117bf364ca8aedd7860e05aa35')

prepare() {
  sed -i 's/setuptools_scm==1.11.1/setuptools_scm/' pytest-django-$pkgver/setup.py

  cp -a pytest-django-$pkgver{,-py2}

  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "$srcdir"/pytest-django-$pkgver
  python setup.py build

  cd "$srcdir"/pytest-django-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-django-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.7/site-packages:$PYTHONPATH:$PWD" \
  DJANGO_SETTINGS_MODULE=pytest_django_test.settings_sqlite \
    py.test tests

  cd "$srcdir"/pytest-django-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH:$PWD" \
  DJANGO_SETTINGS_MODULE=pytest_django_test.settings_sqlite \
    py.test2 tests
}

package_python-pytest-django() {
  depends=('python-pytest')

  cd pytest-django-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm664 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest-django() {
  depends=('python2-pytest' 'python2-pathlib')

  cd pytest-django-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm664 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
