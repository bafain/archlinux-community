# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=gnome-boxes
pkgver=3.30.0
pkgrel=3
pkgdesc="Simple GNOME application to access remote or virtual systems"
arch=('x86_64')
url="https://wiki.gnome.org/Boxes"
license=('LGPL')
groups=('gnome')
depends=('cdrtools' 'gtk-vnc' 'libarchive' 'libgovirt' 'libgudev' 'libosinfo' 'libsecret'
         'libvirt-glib' 'mtools' 'qemu' 'spice-gtk3' 'tracker' 'webkit2gtk' 'freerdp')
makedepends=('git' 'gobject-introspection' 'itstool' 'meson' 'spice-protocol' 'vala')
_commit=1070fca80a43472576b5641935831f8d21d53341  # tags/v3.30.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-boxes.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/gtk-frdp.git"
        "git+https://gitlab.gnome.org/felipeborges/libovf-glib.git"
        rpath.diff)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            '5d8b03dab802794b4f416504434212cfc8e6908f66d0304f45566219e4ae7748')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  git submodule init
  git config --local submodule.subprojects/gtk-frdp.url "$srcdir/gtk-frdp"
  git config --local submodule.subprojects/libovf-glib.url "$srcdir/libovf-glib"
  git submodule update

  # disable broken tests
  git -C subprojects/libovf-glib cherry-pick -n 4bef85e97c636ac4747cb815cf12a0591271b218

  # fix linking
  git apply -3 ../rpath.diff
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
