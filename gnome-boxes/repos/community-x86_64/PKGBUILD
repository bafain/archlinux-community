# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=gnome-boxes
pkgver=3.28.5+7+g0c8a57d5
pkgrel=1
pkgdesc="Simple GNOME application to access remote or virtual systems"
arch=('x86_64')
url="https://wiki.gnome.org/Boxes"
license=('LGPL')
groups=('gnome')
depends=('cdrtools' 'gtk-vnc' 'libarchive' 'libgovirt' 'libgudev' 'libosinfo' 'libsecret'
         'libvirt-glib' 'mtools' 'qemu' 'spice-gtk3' 'tracker' 'webkit2gtk')
makedepends=('git' 'gobject-introspection' 'itstool' 'meson' 'spice-protocol' 'vala')
_commit=0c8a57d563b0781f3ab091358c3c45d65a0a5922  # gnome-3-28
source=("git+https://gitlab.gnome.org/GNOME/gnome-boxes.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
