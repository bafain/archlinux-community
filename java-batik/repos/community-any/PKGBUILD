# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Simon Lipp <sloonz+aur@gmail.com>

pkgname=java-batik
pkgver=1.10
pkgrel=1
pkgdesc='SVG library for Java.'
arch=('any')
url='http://xmlgraphics.apache.org/batik/'
license=(APACHE)
depends=('java-runtime' 'java-commons-io' 'java-commons-logging' 'java-xmlgraphics-commons')
makedepends=('java-environment' 'apache-ant' 'unzip')
source=("https://www.apache.org/dist/xmlgraphics/batik/source/batik-src-${pkgver}.tar.gz"{,.asc})
sha512sums=('a5a0f16144d5688bd72a76d664086dca10e6d2679eb3c18e5a7e774c01833d336a6b7f2b3822ca1e185dd919aa800a2b77d4965cce0dacde19e5027f709bfbe7'
            'SKIP')
validpgpkeys=('9D5F9D9B3E21B585E8F4551082036989D56B5F17'  # Luis Bernardo
              '5C9A30FF22B2C02F30261C305B93F1DF7CDB6DEA') # Simon Steiner

build() {
	cd "$srcdir/batik-$pkgver"
	ant all-jar
}

package() {
	install -d "$pkgdir/usr/share/java/batik"

	install -m644 "$srcdir/batik-$pkgver/batik-$pkgver/lib/batik-all-${pkgver}.jar" \
	  "$pkgdir/usr/share/java/batik/"
	ln -s batik-all-${pkgver}.jar "$pkgdir/usr/share/java/batik/batik-all.jar"
	cp "$srcdir/batik-$pkgver/lib"/*.jar "$pkgdir/usr/share/java/batik/"
}
