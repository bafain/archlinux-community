# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=servant-swagger
pkgname=haskell-servant-swagger
pkgver=1.1.5
pkgrel=53
pkgdesc="Generate Swagger specification for your servant API."
url="https://github.com/haskell-servant/servant-swagger"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-http-media' 'haskell-insert-ordered-containers'
         'haskell-lens' 'haskell-servant' 'haskell-singleton-bool' 'haskell-swagger2'
         'haskell-unordered-containers' 'haskell-hspec' 'haskell-quickcheck')
makedepends=('ghc' 'haskell-aeson-qq' 'haskell-cabal-doctest' 'haskell-doctest'
             'haskell-hspec-discover')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('f71bfa4b19bfa8ce747e97d537b592875a8e96b449e37b397cb42a698c0d252eadf5430781d443dcaf57774239fa1304a6b51709873f22289752ad23ec73d726')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e 's/<.*1.3/<2/' -e 's/<.*4.11/<5/' -e 's/<.*0.14/<1/' -e 's/<.*4.17/<5/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test || warning "Tests failed"
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
