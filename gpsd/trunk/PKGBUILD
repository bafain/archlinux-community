# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Giacomo Rizzo <alt@free-os.it>

pkgname=gpsd
pkgver=3.17
pkgrel=1
pkgdesc="GPS daemon and library to support USB/serial GPS devices"
arch=('x86_64')
url="http://catb.org/gpsd/"
license=('BSD')
depends=('python2' 'libusb' 'bluez-libs' 'desktop-file-utils')
optdepends=('php: generate a PHP status page for your GPS'
            'php-gd: image support for the PHP status page'
            'pygtk: GUI frontends')
makedepends=('scons' 'docbook-xsl')
backup=('etc/gpsd')
source=(https://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz{,.sig}
        $pkgname.conf)
sha256sums=('68e0dbecfb5831997f8b3d6ba48aed812eb465d8c0089420ab68f9ce4d85e77a'
            'SKIP'
            'dcfa71c4c730b486269995468bdcfa8e96bf3c282db1dadea9384d32015d277f')
validpgpkeys=('41876B2F579463A499843D1DECC8208F8C6C738D') # Eric S. Raymond

prepare() {
  cd $pkgname-$pkgver

  # fix python 2.7 path
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
    -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    $(find . -name '*.py')
}

build() {
  cd $pkgname-$pkgver

  export LINKFLAGS="${LDFLAGS}"
  scons prefix=/usr \
        systemd=yes \
        libQgpsmm=no \
        gpsd_group=uucp \
        PYTHONPATH=/usr/bin/python2 \
        sbindir=/usr/bin
  scons build
}

package() {
  cd $pkgname-$pkgver

  # Fix man pages path (FS#21715)
  sed -i 's|.so gps.1|.so man1/gps.1|' cgps.1 lcdgps.1 xgps.1 xgpsspeed.1

  export DESTDIR="$pkgdir"
  scons install
  install -Dm755 gpsinit -t "$pkgdir/usr/bin"
  chmod 755 "$pkgdir"/usr/bin/*

  install -Dm644 "$srcdir/gpsd.conf" "$pkgdir/etc/gpsd"

  install -Dm644 "gpsd.rules" "$pkgdir/usr/lib/udev/rules.d/25-gpsd-usb.rules"

  sed -e 's|/etc/default/gpsd|/etc/gpsd|' -i gpsd.hotplug -i systemd/*.service
  install -Dm755 gpsd.hotplug "$pkgdir/usr/lib/udev/gpsd.hotplug"

  install -Dm644 packaging/X11/xgps.desktop \
    "${pkgdir}/usr/share/applications/xgps.desktop"
  install -Dm644 packaging/X11/xgpsspeed.desktop \
    "${pkgdir}/usr/share/applications/xgpsspeed.desktop"
  install -Dm644 packaging/X11/gpsd-logo.png \
    "${pkgdir}/usr/share/gpsd/gpsd-logo.png"

  install -Dm644 systemd/gpsd.service "$pkgdir/usr/lib/systemd/system/gpsd.service"
  install -Dm644 systemd/gpsd.socket "$pkgdir/usr/lib/systemd/system/gpsd.socket"
  install -Dm644 systemd/gpsdctl@.service "$pkgdir/usr/lib/systemd/system/gpsdctl@.service"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
