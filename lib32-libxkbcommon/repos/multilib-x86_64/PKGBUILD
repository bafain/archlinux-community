# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan Alexander Steffens <jan.steffens@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Mladen Pejakovic <pejakm@gmail.com>

pkgbase=lib32-libxkbcommon
pkgname=('lib32-libxkbcommon' 'lib32-libxkbcommon-x11')
pkgver=0.8.2
pkgrel=1
arch=('x86_64')
url='https://xkbcommon.org/'
license=('custom')
makedepends=('git' 'lib32-libxcb' 'lib32-wayland' 'meson' 'wayland-protocols'
             'xorg-util-macros')
source=("git+https://github.com/xkbcommon/libxkbcommon.git#tag=xkbcommon-${pkgver}")
sha256sums=('SKIP')

prepare() {
  mkdir -p build x11/usr/lib32/pkgconfig
}

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  arch-meson libxkbcommon build \
    --libdir='/usr/lib32' \
    -Denable-docs='false'
  ninja -C build
}

package_lib32-libxkbcommon() {
  pkgdesc='Keymap handling library for toolkits and window systems'
  depends=('lib32-glibc' 'libxkbcommon')

  DESTDIR="${pkgdir}" ninja -C build install
  rm -rf "${pkgdir}"/usr/include

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libxkbcommon "${pkgdir}"/usr/share/licenses/lib32-libxkbcommon

  mv "${pkgdir}"/usr/lib32/*x11* x11/usr/lib32
  mv "${pkgdir}"/usr/lib32/pkgconfig/*x11* x11/usr/lib32/pkgconfig
}

package_lib32-libxkbcommon-x11() {
  pkgdesc='Keyboard handling library using XKB data for X11 XCB clients'
  depends=('lib32-libxcb' 'lib32-libxkbcommon' 'libxkbcommon-x11')

  mv x11/* "${pkgdir}"/
  find "${pkgdir}" -type d -exec chmod 755 {} +

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libxkbcommon-x11 "${pkgdir}"/usr/share/licenses/lib32-libxkbcommon-x11
}

# vim: ts=2 sw=2 et:
