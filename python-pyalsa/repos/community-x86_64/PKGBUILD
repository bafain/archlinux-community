# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
_name=pyalsa
pkgbase=python-pyalsa
pkgname=('python-pyalsa' 'python2-pyalsa')
pkgver=1.1.6
pkgrel=4
pkgdesc="Python binding for the ALSA library"
arch=('x86_64')
url="https://www.alsa-project.org"
license=('LGPL2.1')
groups=('alsa')
depends=('alsa-lib')
makedepends=('python-setuptools' 'python2-setuptools')
source=("ftp://ftp.alsa-project.org/pub/${_name}/${_name}-${pkgver}.tar.bz2")
sha512sums=('6c6aee1ad32c2208ab3f91defee5651cca8bb414479ff80e8a1bd6791ec1df2218a61dd8e7c01f95bd5d51807776034ff96cca2b935e9e4e23d13b6c3c40a0dd')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgbase-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
  python2 setup.py build
}

package_python-pyalsa() {
  pkgdesc="Python binding for the ALSA library"
  depends=('alsa-lib' 'python')
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
}

package_python2-pyalsa() {
  pkgdesc="Python2 binding for the ALSA library"
  depends=('alsa-lib' 'python2')
  cd "$pkgbase-$pkgver"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
}
