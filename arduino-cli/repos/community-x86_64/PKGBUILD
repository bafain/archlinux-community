# $Id$
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=arduino-cli
_pkgver=0.2.0-alpha.preview
pkgver=${_pkgver//-/.}
pkgrel=1
arch=('x86_64')
pkgdesc="Arduino command line interface"
url="https://github.com/arduino/arduino-cli"
license=('GPL3')
groups=('arduino')
depends=('glibc')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/arduino/arduino-cli/archive/$_pkgver.tar.gz")
sha512sums=('7feca1ad586480d0db3c7e421e0f2962fc0408aeb3b96541ca56f17b91ed34d91fac1dd2ec7f6ada48f2219b7319f7d92030f630905827263c297915d3da7dc8')

build() {
  cd $pkgname-$_pkgver

  export GOPATH="$srcdir"/gopath
  export GOBIN="$GOPATH"/bin
  mkdir -p $GOBIN

  go get
  go install
}

package() {
  install -Dm 755 gopath/bin/arduino-cli-$_pkgver "$pkgdir"/usr/bin/arduino-cli
}
