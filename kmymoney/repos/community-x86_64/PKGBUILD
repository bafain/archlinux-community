 #$Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Vamp898 <vamp898@web.de>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Todd Maynard <arch@toddmaynard.com>

pkgname=kmymoney
pkgver=5.0.0
pkgrel=2
pkgdesc="Personal finance manager for KDE which operates similarly to MS-Money or Quicken"
arch=('x86_64')
url="https://kmymoney.org/"
license=('GPL')
depends=('aqbanking' 'boost-libs' 'libalkimia' 'sqlcipher' 'qt5-base' 'qgpgme' 'karchive'
         'kcoreaddons' 'kconfig' 'kwidgetsaddons' 'ki18n' 'kcompletion' 'kcmutils' 'kitemmodels'
         'kitemviews' 'kservice' 'kwallet' 'kiconthemes' 'kxmlgui' 'kidentitymanagement'
         'ktextwidgets' 'knotifications' 'kio' 'kdoctools' 'kholidays' 'kross' 'kactivities'
         'kqtquickcharts' 'kdiagram' 'kdewebkit' 'libical' 'akonadi' 'shared-mime-info')
makedepends=('boost' 'extra-cmake-modules' 'doxygen' 'kcontacts')
changelog=$pkgname.changelog
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
validpgpkeys=('D69A745A55331F44F404D8258D4DE062AA2EB01C')
sha256sums=('984f5866aa7d7bf53d2ef9bbfbb6e5de0d69972f225031ebfd86b46df131f5ba'
            'SKIP')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  mkdir build
  cd build

  cmake ../ \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=RELEASE \
    -DCMAKE_SKIP_RPATH=YES \
    -Wno-dev
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver/build

  make DESTDIR="${pkgdir}" install
}
