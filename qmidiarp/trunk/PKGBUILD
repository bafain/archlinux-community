# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=qmidiarp
pkgver=0.6.5
pkgrel=3
pkgdesc="A MIDI arpeggiator, phrase generator and controller LFO for the ALSA sequencer."
arch=(x86_64)
url="https://sourceforge.net/projects/qmidiarp/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('jack' 'hicolor-icon-theme' 'liblo' 'qt5-base')
makedepends=('lv2' 'qt5-tools')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('171d74230a77b15453367fd38f9bcc94bbbd025a9665853180172bdfd812c2ee5ae0954124706d46c7e5cec0aafaebd5de68435f717789f052e7f86c48917379')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-qt5
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
