# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Kasei Wang <cnsdwpc at gmail.com>

pkgname=brook
pkgver=20180909
pkgrel=1
pkgdesc="A cross-platform proxy/vpn software"
arch=('x86_64')
url="https://github.com/txthinking/brook"
license=('GPL')
depends=('glibc')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/txthinking/brook/archive/v$pkgver.tar.gz")
sha512sums=('788067f01d79be5d35737ce04e4d3b4cf26111e0b567b27b1e295820759d46332f4cc8645c955b5c317ce5998e059baf4963be32ea4ecd7015abb77f98d8213e')

prepare() {
  cd brook-$pkgver

  mkdir build
  mkdir -p .gopath/src/github.com/txthinking
  ln -sf "$PWD" .gopath/src/github.com/txthinking/brook
  export GOPATH="$PWD/.gopath"

  go get github.com/mdp/qrterminal github.com/patrickmn/go-cache github.com/txthinking/ant \
         github.com/txthinking/socks5 golang.org/x/net/proxy \
         github.com/urfave/cli github.com/txthinking/gotun2socks
}

build() {
  cd brook-$pkgver/cli/brook
  go build -o brook
}

package() {
  cd brook-$pkgver/cli/brook
  install -Dm755 brook "$pkgdir"/usr/bin/brook
}
