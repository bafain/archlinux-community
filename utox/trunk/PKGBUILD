# Maintainer: NicoHood <archlinux {cat} nicohood {dog} de>
# PGP ID: 97312D5EB9D7AE7D0BD4307351DAE9B7C1AE9161
# Contributor: Grayhatter
# Contributor: Vlad M. <vlad@archlinux.net>
# Contributor: HÃ¥vard Pettersson <mail@haavard.me>
# Contributor: Madotsuki <madotsuki at cock dot li>

pkgname=utox
_pkgname=uTox
pkgver=0.17.0
pkgrel=2
_signature=""
pkgdesc='Lightweight Tox client'
arch=('x86_64')
url="https://github.com/uTox/uTox"
license=('GPL3')
depends=('fontconfig'
         'libfilteraudio'
         'libxext'
         'libxrender'
         'openal'
         'toxcore'
         'v4l-utils'
         'hicolor-icon-theme')
optdepends=('gtk3: GTK file picker'
            'libdbus: Notification support')
makedepends=('toxcore>=0.1.0' 'cmake' 'gtk3' 'libdbus' 'check' git)
_commit=3f90fdb5093cca66073a498f113f7eef485f22c2  # tags/v0.17.0^0
source=("git+https://github.com/uTox/uTox#commit=$_commit"
        "git+https://github.com/nothings/stb"
        "git+https://github.com/compuphase/minIni"
        "git+https://github.com/nayuki/QR-Code-generator")
sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
    cd "${_pkgname}"
    git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
    cd "${_pkgname}"
    git config submodule.third-party/stb.url "$srcdir/stb"
    git config submodule.third-party/minini.url "$srcdir/minIni"
    git config submodule.third-party/qrcodegen.url "$srcdir/QR-Code-generator"
    git submodule update --init
}

build() {
    cd "${_pkgname}"
    cmake . -DCMAKE_BUILD_TYPE=Release -DENABLE_ASAN=OFF -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd "${_pkgname}"
    make PREFIX="/usr" DESTDIR="${pkgdir}" install
}
