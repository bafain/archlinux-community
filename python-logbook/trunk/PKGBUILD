# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Tomas Schertel<tschertel@gmail.com>

pkgbase=python-logbook
pkgname=(python-logbook python2-logbook)
pkgver=1.2.1
pkgrel=1
pkgdesc="Logging sytem for Python that replaces the standard libraryâ€™s logging module"
arch=('x86_64')
url="http://packages.python.org/Logbook/index.html"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'cython' 'cython2' 'python-six' 'python2-six')
checkdepends=('redis' 'python-pytest' 'python2-pytest' 'python-sqlalchemy' 'python2-sqlalchemy'
              'python-mock' 'python2-mock' 'python-redis' 'python2-redis' 'python-pyzmq'
              'python2-pyzmq' 'python-execnet' 'python2-execnet' 'python-jinja' 'python2-jinja'
              'python-brotli' 'python2-brotli' 'python-pifpaf')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/mitsuhiko/logbook/archive/$pkgver.tar.gz")
sha512sums=('5e214c662490944f74573aa2e6637de4801a0919387a1a30f7e2895b60cf2539e63bbed7149b8714d05b4722a41d3bf334c264f160a304ad3eda1cc186491b1a')

prepare() {
  cp -a logbook-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/logbook-$pkgver
  cython logbook/_speedups.pyx
  python setup.py build

  cd "$srcdir"/logbook-$pkgver-py2
  cython2 logbook/_speedups.pyx
  python2 setup.py build
}

check() {
  cd "$srcdir"/logbook-$pkgver
  LC_CTYPE=en_US.UTF-8 pifpaf run redis py.test tests

  cd "$srcdir"/logbook-$pkgver-py2
  LC_CTYPE=en_US.UTF-8 pifpaf run redis py.test2 tests
}

package_python-logbook() {
  depends=('python-six')

  cd logbook-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm664 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-logbook() {
  depends=('python2-six')

  cd logbook-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm664 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
