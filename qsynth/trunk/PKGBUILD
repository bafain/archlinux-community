# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=qsynth
pkgver=0.5.2
pkgrel=1
pkgdesc="Qt GUI for FluidSynth"
arch=('x86_64')
url="https://qsynth.sourceforge.net/"
license=('GPL')
groups=('pro-audio')
depends=('fluidsynth' 'qt5-x11extras' 'hicolor-icon-theme')
makedepends=('qt5-tools')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('cf8df810bc57fde66a2217cc1dd2699080e83df0eb5641d2fc0ee6a0055c024b041a0ca498e050c3ccda44f0e6d25e73fa4b8e9fd321ceabac3787098caf5c51')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO,TRANSLATORS}
}

# vim:set ts=2 sw=2 et:
