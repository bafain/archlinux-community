# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>

pkgname=ring-daemon
pkgver=20180802.5.14d8a66
pkgrel=1
epoch=2
_pjprojectver=2.7.2
pkgdesc="Ring is a free and universal communication platform which preserves the users' privacy and freedoms (daemon)"
arch=("x86_64")
url="https://ring.cx"
license=('GPL3')
groups=("ring")
depends=('opendht' 'yaml-cpp' 'alsa-lib' 'libpulse' 'jack' 'jsoncpp'
         'libsamplerate' 'libsndfile' 'dbus-c++' 'ffmpeg' 'gnutls'
         'expat' 'libupnp' 'libnatpmp' 'libva' 'libvdpau' 'restbed'
         'libsecp256k1' 'util-linux' 'opus')
makedepends=('git' 'boost' 'msgpack-c' 'autoconf-archive')
#checkdepends=('cppunit')
source=("git+https://gerrit-ring.savoirfairelinux.com/ring-daemon#commit=d7fee9ca953ea0e272539aea52405eeaedb89074"
        "http://www.pjsip.org/release/${_pjprojectver}/pjproject-${_pjprojectver}.tar.bz2")
noextract=("pjproject-${_pjprojectver}.tar.bz2")
sha256sums=('SKIP'
            '9c2c828abab7626edf18e04b041ef274bfaa86f99adf2c25ff56f1509e813772')
sha512sums=('SKIP'
            '3d355ffcbbeed62cfc711e574a987dc06043ccf4f2625820adffa89167022b8306fcee3fada71d3d45e7b902fc9c65ac8221de101cbafed25362a3921f702afd')

prepare() {
  cd "${pkgname}"
  cp ../pjproject-${_pjprojectver}.tar.bz2 contrib/tarballs
  mkdir contrib/native
  autoreconf -fvi
}

build() {
  cd "${pkgname}/contrib/native"
  ../bootstrap \
      --disable-downloads \
      --disable-all \
      --enable-pjproject
  make DEPS_pjproject=

  cd ../..
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --sysconfdir=/etc \
    --enable-ipv6
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

# Disabled because some tests (TURN) use the network.
#check() {
#  cd "${pkgname}"
#  make -k check
#}

package() {
  cd "${pkgname}"

  msg2 'Installing...'
  make DESTDIR="$pkgdir" install

  msg2 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}

# vim:set ts=2 sw=2 et:
