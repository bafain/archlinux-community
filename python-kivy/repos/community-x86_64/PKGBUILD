# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Lev Lybin <lev.lybin@gmail.com>
# Contributor: lagrange <flkazemakase@gmail.com>
# Contributor: mid-kid <esteve.varela@gmail.com>

pkgbase=python-kivy
pkgname=('python-kivy' 'python2-kivy')
pkgver=1.10.1
pkgrel=2
pkgdesc="A software library for rapid development of hardware-accelerated multitouch applications."
arch=('x86_64')
url="https://kivy.org/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-docutils' 'python2-docutils' 'cython'
             'cython2' 'gstreamer' 'sdl2_ttf' 'sdl2_mixer' 'sdl2_image')
checkdepends=('python-nose' 'python2-nose' 'python2-mock' 'python-coverage' 'python2-coverage'
              'python-gobject' 'python2-gobject' 'xorg-server-xvfb' 'opencv' 'python-pyenchant'
              'python2-pyenchant' 'git' 'mtdev' 'xclip' 'xsel' 'gtk3' 'gst-plugins-base'
              'gst-plugins-good' 'gst-libav' 'python-dbus' 'python2-dbus')
source=("https://pypi.io/packages/source/K/Kivy/Kivy-$pkgver.tar.gz")
sha512sums=('d0fb32e18bab86e74b7b7b67b11c46150511a9f549aa564b821e7cb46592750633ac94e9a12b1b8df3e59bd9acffa2b2394861608f46cebd10df4fb52ee604b8')

prepare() {
  # For better metadata
  export KIVY_USE_SETUPTOOLS=1

  export LC_CTYPE=en_US.UTF-8

  cp -a Kivy-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/Kivy-$pkgver
  python setup.py build

  cd "$srcdir"/Kivy-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/Kivy-$pkgver
  python setup.py build_ext --inplace
  xvfb-run -s "-screen 0 1280x720x24 -ac +extension GLX" nosetests3 kivy/tests

  cd "$srcdir"/Kivy-$pkgver-py2
  python2 setup.py build_ext --inplace
  xvfb-run -s "-screen 0 1280x720x24 -ac +extension GLX" nosetests2 kivy/tests
}

package_python-kivy() {
  depends=('python-docutils' 'gstreamer' 'sdl2_ttf' 'sdl2_mixer' 'sdl2_image')

  cd Kivy-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-kivy() {
  depends=('python2-docutils' 'gstreamer' 'sdl2_ttf' 'sdl2_mixer' 'sdl2_image')

  cd Kivy-$pkgver-py2
  python2 setup.py install --prefix=/usr --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/share/kivy{,2}-examples
}
