# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Francois Boulogne <fboulogne@april.org>
# Contributor: TDY <tdy@gmx.com>

pkgbase='spyder'
pkgname=('spyder2' 'spyder3')
pkgver=3.3.1
pkgrel=1
pkgdesc='The Scientific Python Development Environment'
makedepends=('python2-setuptools' 'python-setuptools' 'python-sphinx' 'python2-sphinx'
             'desktop-file-utils')
arch=('any')
url='https://www.spyder-ide.org/'
license=('MIT')
source=("${pkgbase}-${pkgver}.tar.gz"::"https://github.com/spyder-ide/${pkgbase}/archive/v${pkgver}.tar.gz"
        "python2.patch"
        "bug_53489_python2-optional-nbconvert.patch"
        "bug_54508_missing_pyqt4_qtwebkit.patch"
        "bug_51437_spyder2.patch")
sha256sums=('a28c9eb919389739432d7243b533beb5973ed8415b3f00de894a6a15552a0b5d'
            '84b8d5e9640d5d347ac4564130f217368cc3f823de30b458d467a05e60bbc8da'
            '7486faba90da062ef8da6912bfabd22b59343b1275758f04e4a41df2b3b8eec0'
            '28d4aa8938d809c299ab6bf6b00a2f82677f9a98a9763b7127970881b76276f5'
            'bc94729fcf378ff1fa4ed028c4f3092acab1a098f608a121af5f8801772c28a0')

prepare() {
  cd "${pkgbase}-${pkgver}"
  # Forced QT API to pyqt5 (see https://bugs.archlinux.org/task/54508)
  echo "Applying patches bug_54508_missing_pyqt4_qtwebkit.patch"
  patch -p1 -i "../bug_54508_missing_pyqt4_qtwebkit.patch"
  # Replace spyder with spyder2 (see https://bugs.archlinux.org/task/51437)
  echo "Applying patches bug_51437_spyder2.patch"
  patch -p1 -i "../bug_51437_spyder2.patch"
  mv "img_src/spyder.png" "img_src/spyder2.png"
  mv "scripts/spyder.desktop" "scripts/spyder2.desktop"

  # Prepare sources for spyder2 build
  cp -r "${srcdir}/${pkgbase}-${pkgver}" "${srcdir}/spyder2-${pkgver}"
  cd "${srcdir}/spyder2-${pkgver}"
  echo "Applying patches python2.patch"
  patch -p1 -i "../python2.patch"
  echo "Applying patches bug_53489_python2-optional-nbconvert.patch"
  patch -p1 -i "../bug_53489_python2-optional-nbconvert.patch"

  # Prepare sources for spyder3 build
  cp -r "${srcdir}/${pkgbase}-${pkgver}" "${srcdir}/spyder3-${pkgver}"
}

build() {
  cd "${srcdir}/spyder2-${pkgver}"
  python2 setup.py build

  cd "${srcdir}/spyder3-${pkgver}"
  python3 setup.py build
}

package_spyder2() {
  pkgdesc='The Scientific Python Development Environment (Python 2 version)'
  depends=('python2-pyqt5' 'qt5-webkit' 'python2-qtconsole' 'python2-qtpy' 'python2-qtawesome'
           'python2-pyflakes' 'python2-pyzmq' 'python2-pygments' 'python2-psutil' 'python2-pickleshare'
           'python2-rope' 'python2-jedi' 'python2-pylint' 'python2-pycodestyle' 'python2-sphinx'
           'python2-opengl' 'python2-spyder-kernels<1.0' 'icu' 'gtk-update-icon-cache'
           'desktop-file-utils')
  provides=('spyder')
  conflicts=('spyder')
  replaces=('spyder')
  optdepends=('python2-numpy: N-dimensional arrays'
              'python2-scipy: signal/image processing'
              'python2-h5py: HDF5 support'
              'python2-matplotlib: interactive 2D/3D data plotting'
              'python2-sympy: symbolic mathematics for the IPython console'
              'python2-pandas: view and edit DataFrames and Series in the Variable Explorer')
  install="${pkgname}.install"

  cd "${pkgname}-${pkgver}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Install icon and desktop file
  install -D -m644 "spyder/images/spyder.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  # Change main executable name
  mv "${pkgdir}/usr/bin/spyder" "${pkgdir}/usr/bin/${pkgname}"
  # Remove useless spyder_win_post_install script
  rm -f "${pkgdir}/usr/bin/spyder_win_post_install.py"
}

package_spyder3() {
  pkgdesc='The Scientific Python Development Environment (Python 3 version)'
  depends=('python-pyqt5' 'qt5-webkit' 'python-qtconsole' 'python-qtpy' 'python-qtawesome'
           'python-pyflakes' 'python-pyzmq' 'python-pygments' 'python-psutil' 'python-pickleshare'
           'jupyter' 'jupyter-nbconvert'
           'python-rope' 'python-jedi' 'python-pylint' 'python-pycodestyle' 'python-sphinx'
           'python-opengl' 'python-spyder-kernels<1.0' 'icu' 'gtk-update-icon-cache'
           'desktop-file-utils')
  optdepends=('python-numpy: N-dimensional arrays'
              'python-scipy: signal/image processing'
              'python-h5py: HDF5 support'
              'python-matplotlib: interactive 2D/3D data plotting'
              'python-sympy: symbolic mathematics for the IPython console'
              'python-pandas: view and edit DataFrames and Series in the Variable Explorer')

  cd "${pkgname}-${pkgver}"
  python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Install icon and desktop file
  install -D -m644 "spyder/images/spyder.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  # Remove useless spyder_win_post_install script
  rm -f "${pkgdir}/usr/bin/spyder_win_post_install.py"
  # Add symlink /usr/bin/spyder to /usr/bin/spyder3 (see https://bugs.archlinux.org/task/51437)
  ln -s "/usr/bin/spyder3" "${pkgdir}/usr/bin/spyder"
}
