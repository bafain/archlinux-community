# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=pysolfc
pkgver=2.0
pkgrel=6
pkgdesc="PySol (Fan Club edition) is an exciting collection of more than 1000 solitaire games"
arch=('any')
url="http://pysolfc.sourceforge.net/"
license=('GPL3')
depends=('tk' 'pysol-sound-server' 'python2-pillow')
source=(http://downloads.sourceforge.net/pysolfc/PySolFC-${pkgver}.tar.bz2)
sha512sums=('ec5a11565bd43e097cf1854fe246b8f92acd74077c0b3b158c999f9dc45966e8f805c60a2cfbd815137d970e0e6409d0ae437b9807bac9c39353657cf596b6fc')

prepare() {
  cd PySolFC-${pkgver}
  find . -name \*.py -exec sed -i -e "s|#!/usr/bin/env python|#!/usr/bin/env python2|" {} \;
  sed -i 's/pysol.py/pysol/' data/pysol.desktop
  sed -i 's/import Image/from PIL import Image/' scripts/cardset_viewer.py pysollib/mfxutil.py
  sed -i 's/import GifImagePlugin/from PIL import GifImagePlugin/' pysollib/mfxutil.py
  sed -i 's/import PngImagePlugin/from PIL import PngImagePlugin/' pysollib/mfxutil.py
  sed -i 's/import JpegImagePlugin/from PIL import JpegImagePlugin/' pysollib/mfxutil.py
  sed -i 's/import BmpImagePlugin/from PIL import BmpImagePlugin/' pysollib/mfxutil.py
  sed -i 's/import PpmImagePlugin/from PIL import PpmImagePlugin/' pysollib/mfxutil.py
}

package() {
  cd PySolFC-${pkgver}
  python2 setup.py install --root="${pkgdir}"
  mv "${pkgdir}/usr/bin/pysol.py" "${pkgdir}/usr/bin/pysol"
}
