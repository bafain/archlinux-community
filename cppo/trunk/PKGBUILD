# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=cppo
pkgver=1.6.4
pkgrel=1
pkgdesc="C-style preprocessor for OCaml"
arch=('x86_64')
url="https://github.com/mjambon/cppo"
license=('BSD')
depends=('glibc')
makedepends=('ocamlbuild' 'dune')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/v${pkgver}.tar.gz")
sha256sums=('470a5225a5d3b06ced29581250562ef84b7a8bc6b9a632c96443391a443bab49')

build() {
    cd ${pkgname}-${pkgver}
    make
}

package() {
    cd ${pkgname}-${pkgver}

    # Initialize OPAM
    export OPAMROOT="${srcdir}"/opam
    opam init -n

    # Work around the install command
    make OCAMLFIND_DESTDIR="${pkgdir}$(ocamlfind printconf destdir)" install
    install -Dm755 "${srcdir}"/opam/system/bin/cppo -t "${pkgdir}"/usr/bin
    install -Dm644 "${srcdir}"/opam/system/doc/${pkgname}/LICENSE.md -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
