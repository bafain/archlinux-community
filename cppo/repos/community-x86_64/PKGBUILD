# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=cppo
pkgver=1.6.1
pkgrel=1
pkgdesc="C-style preprocessor for OCaml"
arch=('x86_64')
url="https://github.com/mjambon/cppo"
license=('BSD')
depends=('glibc')
makedepends=('ocaml-findlib' 'ocamlbuild' 'dune' 'opam')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/v${pkgver}.tar.gz")
sha256sums=('0e93522c5e51a9433fc2327f33ebc56f66beee7abaacab8d98de57591a0626e6')

build() {
    cd ${pkgname}-${pkgver}
    make
}

package() {
    cd ${pkgname}-${pkgver}

    # Initialize OPAM
    export OPAMROOT="${srcdir}"/opam
    opam init -n

    # Work around the install command
    make OCAMLFIND_DESTDIR="${pkgdir}$(ocamlfind printconf destdir)" install
    install -Dm755 "${srcdir}"/opam/system/bin/cppo -t "${pkgdir}"/usr/bin
    install -Dm644 "${srcdir}"/opam/system/doc/${pkgname}/LICENSE.md -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
