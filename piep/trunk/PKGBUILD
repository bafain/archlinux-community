# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=piep
pkgdesc='Bringing the power of python to stream editing'
pkgver=0.8.1
pkgrel=1
arch=("any")
url="https://github.com/gfxmonk/piep"
license=("GPL")
depends=('python2-pygments' 'python2-setuptools')
makedepends=('python2-nose')
source=("https://github.com/gfxmonk/$pkgname/archive/version-$pkgver.tar.gz")
sha512sums=('772834cee6a705811823046373c61eb505421a9c97aab3b08ba1d644ec30d7a16e37538c429b651c5465ca7d737e8fc5ef2ec9560de9af003067d5456717e319')

prepare() {
  cd $pkgname-version-$pkgver
  sed -i "s/'python'/'python2'/" test/test_helper.py
}

check() {
  cd $pkgname-version-$pkgver
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages" nosetests2
}

package() {
  cd $pkgname-version-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
