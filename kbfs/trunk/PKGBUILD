# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Michael Hansen <zrax0111 gmail com>

pkgname=kbfs
pkgver=2.5.0
pkgrel=1
pkgdesc="The Keybase filesystem"
arch=('x86_64')
url="https://keybase.io/docs/kbfs"
license=('BSD')
makedepends=('go-pie')
source=("$pkgname-$pkgver.tar.gz::https://github.com/keybase/kbfs/archive/v$pkgver.tar.gz")
sha512sums=('a12f3491c3d7dd42b78d24957f3b469837a8e6d682e91dfe6020e74e24374ce95f9de31578579680ee35513637512e84849d598d34b3e8e78f96cf99ac9cd701')

prepare() {
  cd kbfs-$pkgver

  export GOPATH="$srcdir/.gopath"
  mkdir -p "$GOPATH"/src/github.com/keybase
  ln -sf "$PWD" "$GOPATH"/src/github.com/keybase/kbfs
}

build() {
  cd kbfs-$pkgver
  export GOPATH="$srcdir/.gopath"
  go build -a -tags production -o bin/kbfsfuse github.com/keybase/kbfs/kbfsfuse
  go build -a -tags production -o bin/git-remote-keybase github.com/keybase/kbfs/kbfsgit/git-remote-keybase
}

package() {
  depends=('fuse' 'keybase')
  cd kbfs-$pkgver/

  install -Dm755 -t "${pkgdir}"/usr/bin/ bin/*
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 packaging/linux/systemd/kbfs.service "$pkgdir"/usr/lib/systemd/user/kbfs.service
}
