# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Michael Hansen <zrax0111 gmail com>

pkgname=kbfs
pkgver=1.0.44
pkgrel=1
pkgdesc="The Keybase filesystem"
arch=('x86_64')
url="https://keybase.io/docs/kbfs"
license=('BSD')
depends=('fuse' 'keybase')
makedepends=('go-pie')
source=("$pkgname-$pkgver.tar.gz::https://github.com/keybase/kbfs/archive/v$pkgver.tar.gz")
sha512sums=('03e92303f140dbff059f8c954142d6180f388c1e50c2f8af02a30a740d440d5f79be47ad851d021a2b90525bf7b775e471780438b94d1d6403eddbdc0bd41e8e')


prepare() {
  cd kbfs-$pkgver

  export GOPATH="$srcdir/.gopath"
  mkdir -p "$GOPATH"/src/github.com/keybase
  ln -sf "$PWD" "$GOPATH"/src/github.com/keybase/kbfs
}

build() {
  cd kbfs-$pkgver/kbfsfuse
  # go build -a -tags production -gccgoflags "$CFLAGS $LDFLAGS" github.com/keybase/kbfs/kbfsfuse
  export GOPATH="$srcdir/.gopath"
  go build -a -tags production -o ../bin/kbfsfuse github.com/keybase/kbfs/kbfsfuse
  go build -a -tags production -o ../bin/git-remote-keybase github.com/keybase/kbfs/kbfsgit/git-remote-keybase
}

package() {
  cd kbfs-$pkgver/

  install -Dm755 -t "${pkgdir}"/usr/bin/ bin/*
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm755 packaging/linux/systemd/kbfs.service "$pkgdir"/usr/lib/systemd/user/kbfs.service
}
