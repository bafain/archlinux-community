# $Id$
# Maintainer  : Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer  : Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor : Daniel Micay <danielmicay@gmail.com>
_alternative_pkgname=gvm-libs
pkgname=openvas-libraries
pkgver=9.0.2
pkgrel=1
pkgdesc='The OpenVAS libraries'
arch=('x86_64')
url="http://www.openvas.org/"
license=('GPL')
depends=('gnutls' 'libpcap' 'gpgme' 'libssh' 'glib2' 'libldap' 'hiredis')
makedepends=('cmake' 'doxygen')
groups=('openvas')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/greenbone/gvm-libs/releases/download/v${pkgver}/openvas-libraries-${pkgver}.tar.gz"
        "https://github.com/greenbone/gvm-libs/releases/download/v${pkgver}/openvas-libraries-${pkgver}.tar.gz.sig")
sha512sums=('b8ca65fd636e94ae942e042d7e2390f3c546054e1431f4ad3acd57140b497e3bf165cfbf07effdc7978842b5193182bb1115b89145440daf0e7137c0966a8118'
            'SKIP')
validpgpkeys=(
              'C3B468D2288C68B9D526452248479FF648DB4530' # OpenVAS Transfer Integrity
)

build() {
  cd "${_alternative_pkgname}-${pkgver}"
  LDFLAGS+=" -Wl,--no-as-needed"
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBDIR=/usr/lib -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var .
  make
}

package() {
  cd "${_alternative_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  rmdir "${pkgdir}/var/run"
}
