# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tatsuyuki Ishi <ishitatsuyuki@gmail.com>

pkgbase=python-arrow
pkgname=('python-arrow' 'python2-arrow')
pkgver=0.12.1
pkgrel=3
pkgdesc='Better dates and times for Python'
arch=('any')
url='http://crsmithdev.com/arrow/'
license=('GPL')
makedepends=('python-setuptools' 'python2-setuptools')
source=("python-arrow-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/e0/86/4eb5228a43042e9a80fe8c84093a8a36f5db34a3767ebd5e1e7729864e7b/arrow-0.12.1.tar.gz")
sha256sums=('a558d3b7b6ce7ffc74206a86c147052de23d3d4ef0e17c210dd478c53575c4cd')

prepare() {
  cp -r {,python-}arrow-${pkgver}
  mv {,python2-}arrow-${pkgver}
}

build() {
  for py in python{,2}; do
    pushd ${py}-arrow-${pkgver}
    ${py} setup.py build
    popd
  done
}
package_python-arrow() {
  depends=('python' 'python-dateutil' 'python-nose')

  cd python-arrow-${pkgver}

  python setup.py install --skip-build --root="${pkgdir}" --optimize='1' --skip-build
}

package_python2-arrow() {
  depends=('python2' 'python2-backports.functools_lru_cache' 'python2-dateutil' 'python2-nose')

  cd python2-arrow-${pkgver}

  python2 setup.py install --skip-build --root="${pkgdir}" --optimize='1' --skip-build
}

# vim: ts=2 sw=2 et:
