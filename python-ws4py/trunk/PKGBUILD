# Maintainer: David Runge <dave@sleepmap.de>
_name=ws4py
pkgname=python-ws4py
pkgver=0.5.1
pkgrel=1
pkgdesc="Library providing an implementation of the WebSocket protocol defined in RFC 6455"
arch=('any')
url="https://github.com/Lawouach/WebSocket-for-Python"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-mock' 'python-cherrypy')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('e3202d7bc547a5447eaeeeabe2ff18bd884c89c02cf54e27d88ca9cd14bcd9f080f7ff7e02d36538200c096df0442809c171c99e37b16ff818866c45b40eeef9')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH=build:${PYTHONPATH}
  py.test
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {CHANGELOG,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
