# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kimap2
pkgver=0.2.1
pkgrel=1
pkgdesc="Job-based API for interacting with IMAP servers"
arch=(x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(kcoreaddons kmime)
makedepends=(extra-cmake-modules)
#source=("https://download.kde.org/unstable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz")
source=($pkgname-$pkgver.tar.gz::"https://github.com/KDE/kimap2/archive/v$pkgver.tar.gz")
sha256sums=('91a2cc8abb9867dda7024b54d9b352267b75d5420336640ab36d7177e5d3b95b')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
