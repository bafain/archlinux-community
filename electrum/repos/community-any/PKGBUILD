# $Id$
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=electrum
pkgver=3.2.2
pkgrel=2
pkgdesc="Lightweight Bitcoin wallet"
arch=('any')
makedepends=('python-setuptools')
depends=('python-pyaes' 'python-ecdsa' 'python-pbkdf2' 'python-requests' 'python-qrcode'
         'python-protobuf' 'python-dnspython' 'python-jsonrpclib-pelix' 'python-pysocks'
         'python-pyqt5' 'python-pycryptodomex' 'python-websocket-client')
optdepends=('python-btchip: BTChip hardware wallet support'
            'python-matplotlib: plot transaction history in graphical mode'
            'zbar: QR code reading support')
url="https://electrum.org"
license=('MIT')
source=("https://download.electrum.org/${pkgver}/${pkgname^}-${pkgver}.tar.gz"{,.asc})
sha512sums=('32c4a24c2d3e2e38b9d66f6102176533a991b1c1fd25173bcd3bdd2e78cf42fc5fd2f0d2ee0c6a8d99db1b17d47bd3e7f3cf1303964f0566f4eef4edb3c87f15'
            'SKIP')
validpgpkeys=('6694D8DE7BE8EE5631BED9502BD5824B7F9470E6')

prepare() {
  cd "${pkgname^}-${pkgver}"
}

build() {
  cd "${pkgname^}-${pkgver}"

  pyrcc5 icons.qrc -o gui/qt/icons_rc.py
  python setup.py build
}

package() {
  cd "${pkgname^}-${pkgver}"

  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENCE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
