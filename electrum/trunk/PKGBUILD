# $Id$
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=electrum
pkgver=3.1.0
pkgrel=2
pkgdesc="Lightweight Bitcoin wallet"
arch=('any')
depends=('python-pyaes' 'python-ecdsa' 'python-pbkdf2' 'python-requests' 'python-qrcode'
         'python-protobuf' 'python-dnspython' 'python-jsonrpclib-pelix' 'python-pysocks'
         'python-pyqt5' 'python-pycryptodomex')
makedepends=('python-setuptools')
optdepends=('python-btchip: BTChip hardware wallet support'
            'python-matplotlib: plot transaction history in graphical mode'
            'zbar: QR code reading support')
url="https://electrum.org"
license=(MIT)
source=("https://download.electrum.org/${pkgver}/${pkgname^}-${pkgver}.tar.gz"{,.asc}
        '0001-prevent-requirements.txt-from-being-installed-to-usr.patch')
sha512sums=('516ebb34711045abf145eca916233cfb37cca22bfb197a02df67f33886e6988c27a14b428f3ccea3f3d208cce520971bda08c20f274e0cf5dcdadbc76ed15847'
            'SKIP'
            '89f07323c049b7665ded169f8293b696f8ddcd49f2e37186a4c7cb68e05abe3cc4c4fe6b2608c05d1d3d9a7477f8dc1b8e84e62c68583e45c5ff1d34f4b17b1b')
validpgpkeys=('6694D8DE7BE8EE5631BED9502BD5824B7F9470E6')

prepare() {
  cd "${pkgname^}-${pkgver}"
  patch -Np1 -i "${srcdir}/0001-prevent-requirements.txt-from-being-installed-to-usr.patch"
}

build() {
  cd "${pkgname^}-${pkgver}"

  pyrcc5 icons.qrc -o gui/qt/icons_rc.py
  python setup.py build
}

package() {
  cd "${pkgname^}-${pkgver}"

  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENCE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
