# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Maato <maato softwarebakery com>

pkgname=volumeicon
pkgver=0.5.1
pkgrel=3
pkgdesc='Volume control for your system tray'
arch=('x86_64')
url='http://softwarebakery.com/maato/volumeicon.html'
license=('GPL3')
depends=('gtk3' 'alsa-lib' 'libnotify')
makedepends=('intltool' 'git')
install='volumeicon.install'
source=("git://github.com/Maato/volumeicon.git#tag=$pkgver"
        "volumeicon-notify.patch::https://github.com/Maato/volumeicon/commit/78a9c23775ebe12d3f3be93c0798144a6e732d90.patch"
        'volumeicon.desktop')
md5sums=('SKIP'
         'a528651bbf93b1fbd731103a3431b895'
         '1822d5e80a86b80ace9565aebf24b71d')

prepare() {
  cd "$pkgname"
  patch -Np1 -i ../volumeicon-notify.patch
  ./autogen.sh
}

build() {
  cd "$pkgname"
  ./configure --prefix=/usr --enable-notify
  make
}

package() {
  make -C "$pkgname" DESTDIR="$pkgdir" install
  install -Dm644 volumeicon.desktop "$pkgdir/usr/share/applications/volumeicon.desktop"
}

# vim:set ts=2 sw=2 et:
