# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-editorconfig
pkgname=('python-editorconfig' 'python2-editorconfig')
pkgver=0.12.2
pkgrel=2
pkgdesc='EditorConfig File Locator and Interpreter for Python'
arch=('any')
license=('PSF')
url='https://github.com/editorconfig/editorconfig-core-py'
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.io/packages/source/E/EditorConfig/EditorConfig-$pkgver.tar.gz")
sha512sums=('15648904683e04b68966651ebf2046b60473a47a408dff257d64d06b6c0cc6765882ebf5ec0e0ccaf5dc7e78e93b5f9bdf129ae0e2a23f7f489cb50c9ae3ff16')

prepare() {
  cp -a EditorConfig-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/EditorConfig-$pkgver
  python setup.py build

  cd "$srcdir"/EditorConfig-$pkgver-py2
  python2 setup.py build
}

package_python-editorconfig() {
  depends=('python')

  cd EditorConfig-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  # FS#55201
  mv "$pkgdir"/usr/bin/editorconfig{,3}
}

package_python2-editorconfig() {
  depends=('python2')

  cd EditorConfig-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/editorconfig{,2}
}

# vim:set ts=2 sw=2 et:
