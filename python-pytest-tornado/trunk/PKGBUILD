# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-tornado
pkgver=0.5.0
pkgrel=1
pkgdesc='A py.test plugin providing fixtures and markers to simplify testing of asynchronous tornado applications.'
arch=('any')
license=('Apache')
url='https://github.com/eugeniy/pytest-tornado'
depends=('python-pytest' 'python-tornado')
source=("$pkgname-$pkgver.tar.gz::https://github.com/eugeniy/pytest-tornado/archive/v$pkgver.tar.gz")
sha512sums=('d246bd920933b2a5155b4808afb23580f0f78c1f200a698face550f18e6ec1f53317ac010fe882b4114539495c1e4ef650bc68011069850ea0a5bd20e6004df0')

build() {
  cd pytest-tornado-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-tornado-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.7/site-packages:$PYTHONPATH" py.test
}

package() {
  cd pytest-tornado-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
