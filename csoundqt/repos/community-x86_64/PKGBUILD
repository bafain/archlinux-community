# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ainola
# Contributor: rtfreedman

_name=CsoundQt
_rtmidiver=3.0.0
pkgname=csoundqt
pkgver=0.9.5
pkgrel=7
pkgdesc="Csound frontend with highlighting editor, autocomplete, interactive widgets and integrated help"
arch=('x86_64')
url="https://csoundqt.github.io/"
license=('CCPL' 'FDL' 'GPL' 'LGPL2.1')
groups=('pro-audio')
# TODO: somehow include pythonqt
depends=('csound' 'csound-doc' 'qt5-declarative')
makedepends=('shared-mime-info')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz"
        "rtmidi-${_rtmidiver}.tar.gz::https://github.com/thestk/rtmidi/archive/v${_rtmidiver}.tar.gz")
sha512sums=('136fb395b955e21c153ccde3642362d4451bd458af78e235808a8b911a87e24fc5614c72b0e09f3dcd6ab1f49d563eb547dc1371b8ebd75cb8e051a2d629fb74'
            '8975a63e7be9102af70401cef48c702597b87efe2d8fa30a978fe280e26da1dfa90d6f30cfbd3df587462f0dd085d0f29e1c014e67d7fcd3a36960b6bcfb3e55')

prepare() {
  cd "${_name}-${pkgver}"
  # fix exptected location for html, examples and scripts
  sed -e 's,csdocdir = \"\",csdocdir = \"/usr/share/doc/csound-doc/html\",' \
    -i src/csoundoptions.cpp
  sed -e 's,/usr/share/qutecsound/Examples/,/usr/share/csoundqt/Examples/,g' \
    -i src/qutecsound.cpp
  sed -e 's,../../csoundqt/src/Scripts,/usr/share/csoundqt/Scripts,' \
    -i src/types.h

  # fix install location of examples on filesystem
  sed -e 's@DIR/qutecsound/@DIR/csoundqt/@' -i qcs.pro

  # fix desktop file
  chmod -c 644 ${_name}.desktop
  mv -v ${_name}.desktop ${pkgname}.desktop
  sed -e "s/${_name}.desktop/${pkgname}.desktop/" -i qcs.pro

  # fix application icon
  sed -e "s/qtcs/${pkgname}/" -i "${pkgname}.desktop"
  sed -e "s,icons,icons/hicolor/scalable/apps," \
      -e "s/qtcs.svg/${pkgname}.svg/" \
      -i qcs.pro
  mv -v images/qtcs.svg "images/${pkgname}.svg"

  # move rtmidi sources
  mv ${srcdir}/rtmidi-${_rtmidiver} .
}

build() {
  cd "${_name}-${pkgver}"
  qmake-qt5 qcs.pro \
    SHARE_DIR="${pkgdir}/usr/share" \
    INSTALL_DIR="${pkgdir}/usr" \
    CONFIG+=rtmidi \
    RTMIDI_DIR="rtmidi-${_rtmidiver}/"
  make
}

package() {
  cd "${_name}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm644 {ChangeLog,README.md}
}

# vim:set ts=2 sw=2 et:
