# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

pkgbase=python-pam
pkgname=(python-pam python2-pam)
pkgver=1.8.4
pkgrel=2
pkgdesc=" Pure Python interface to the Pluggable Authentication Modules system on Linux"
arch=('any')
url="https://github.com/FirefighterBlu3/${pkgbase}"
license=('MIT')
depends=('pam')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/source/${pkgbase:0:1}/${pkgbase}/${pkgbase}-${pkgver}.tar.gz")
sha256sums=('c856d9c89fedb33951dd8a95727ae57c6887b02d065bbdffd2fd9dbc0183909b')
validpgpkeys=('766460A814AE0D0066C3EE922AC27DE01E1DFAD6') # David Ford <david@blue-labs.org>

build() {
	cd "${srcdir}/${pkgbase}-${pkgver}"
	python setup.py build
	python2 setup.py build
}

package_python-pam() {
    depends+=('python')
	cd "${srcdir}/${pkgbase}-${pkgver}"

	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pam() {
    depends+=('python2')
	cd "${srcdir}/${pkgbase}-${pkgver}"

	python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
