# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sebastian Wiesner <lunaryorn googlemail com>
# Contributor: Igor Ramos Tiburcio <irtigor@yahoo.com.br>
# Contributor: Wieland Hoffmann <themineo+aur@gmail.com>
# Contributor: Hervé Cauwelier <herve ¤ oursours.net>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgbase=python-py
pkgname=('python-py' 'python2-py')
pkgver=1.6.0
pkgrel=1
pkgdesc="library with cross-python path, ini-parsing, io, code, log facilities"
arch=('any')
license=('MIT')
url="http://py.readthedocs.io/"
makedepends=('python-setuptools' 'python2-setuptools' 'python-setuptools-scm'
             'python2-setuptools-scm' 'python-iniconfig' 'python2-iniconfig'
             'python-apipkg' 'python2-apipkg')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pytest-dev/py/archive/$pkgver.tar.gz")
sha512sums=('9ff5a2444cbcbc5863446262c45604c8ce47cae2365ae96e00b15025e567d3cf832e058a29be9c0b6007676e8725c70757c448288e7180a68e99905133fdf2da')

prepare() {
  rm -r py-$pkgver/py/_vendored_packages
  cp -a py-$pkgver{,-py2}

  export LC_CTYPE=en_US.UTF-8
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "$srcdir"/py-$pkgver
  python setup.py build

  cd "$srcdir"/py-$pkgver-py2
  python2 setup.py build
}

check() {
  # Known failure: https://github.com/pytest-dev/py/issues/104

  cd "$srcdir"/py-$pkgver
  python setup.py pytest || warning "Tests failed"

  cd "$srcdir"/py-$pkgver-py2
  python2 setup.py pytest || warning "Tests failed"
}

package_python-py() {
  depends=('python-iniconfig' 'python-apipkg')

  cd py-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-py() {
  depends=('python2-iniconfig' 'python2-apipkg')

  cd py-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
