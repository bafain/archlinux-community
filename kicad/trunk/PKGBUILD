# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Marq Schneider <queueRAM@gmail.com>

pkgname=kicad
pkgver=5.0.0
pkgrel=1
pkgdesc="Electronic schematic and printed circuit board (PCB) design tools"
arch=('x86_64')
#url="http://iut-tice.ujf-grenoble.fr/kicad/"
url="http://kicad-pcb.org/"
license=('GPL')
#wxgtk3 is still not supported yet (FS#54839)
depends=('wxgtk2' 'python2' 'boost-libs' 'glew' 'curl' 'glm' 'ngspice')
# the AUR git package uses python3 but this release does not support it
# can ngspice be an optdep?
# aur deps?  oce
makedepends=('cmake' 'zlib' 'mesa' 'boost' 'swig')
optdepends=('kicad-library: for footprints and symbols'
            'kicad-library-3d: for 3d models of components')
source=("https://launchpad.net/kicad/5.0/$pkgver/+download/kicad-$pkgver.tar.xz"
        #"kicad-i18n.$pkgver.tar.gz::https://github.com/KiCad/kicad-i18n/archive/$pkgver.tar.gz"
        "kicad-i18n.$pkgver-rc3.tar.gz::https://github.com/KiCad/kicad-i18n/archive/$pkgver-rc3.tar.gz")
sha512sums=('79d4d34e8eaa5b6e4c7baac39a76f86af89adbaf3f9181b8cd846ed3a8db1b4c6dd7a36efefda3394d33ef828917909ad3bd8bb22435af9c6f671efbff125b42'
            '8183545d636f64ee2cd6780acb7fec6491c580e6abea8acc1967881644acc79472569b2c21f494d1a6bde5d98b6736655baebb103d646319725edad9c82626f7')

prepare() {
  cd "$srcdir/kicad-$pkgver"
  find -name '*.py' -exec sed -i '1s/python$/&2/' {} +
}

build() {
  cd "$srcdir/kicad-$pkgver"

  mkdir -p build/Release
  cd build/Release
  cmake ../.. -DCMAKE_BUILD_TYPE=Release    \
              -DCMAKE_INSTALL_PREFIX=/usr   \
              -DCMAKE_INSTALL_LIBDIR=lib    \
              -DKICAD_USE_OCE=OFF           \
              -DKICAD_SCRIPTING=ON          \
              -DKICAD_SCRIPTING_MODULES=ON  \
              -DKICAD_SCRIPTING_WXPYTHON=OFF \
              -DKICAD_SCRIPTING_ACTION_MENU=ON \
              -DBUILD_GITHUB_PLUGIN=ON
  make

  cd "$srcdir/kicad-i18n-$pkgver-rc3"
  mkdir -p build/Release
  cd build/Release
  cmake ../.. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/kicad-$pkgver/build/Release"

  make DESTDIR="$pkgdir" install

  # copy updated linux icons
  #cp -r -n "$srcdir/$pkgname-icons/resources/linux/mime/icons" "$pkgdir/usr/share/"

  cd "$srcdir/kicad-i18n-$pkgver-rc3/build/Release"
  make DESTDIR="$pkgdir" install
}
