# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=swagger2
pkgname=haskell-swagger2
pkgver=2.2.2
pkgrel=51
pkgdesc="Swagger 2.0 data model"
url="https://github.com/GetShopTV/swagger2"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-base-compat-batteries' 'haskell-hashable'
         'haskell-generics-sop' 'haskell-http-media' 'haskell-insert-ordered-containers'
         'haskell-lens' 'haskell-network' 'haskell-scientific' 'haskell-transformers-compat'
         'haskell-unordered-containers' 'haskell-vector' 'haskell-uuid-types')
makedepends=('ghc' 'haskell-aeson-qq' 'haskell-hspec' 'haskell-hunit' 'haskell-quickcheck'
             'haskell-quickcheck-instances' 'haskell-hspec-discover' 'haskell-doctest'
             'haskell-cabal-doctest' 'haskell-glob')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('b34f9bbe3d4bf1deb315ade8da9d99a91d2c32e47f8a4573b5dd73fabc4094267ae2b7ae1241773d2ce267bcaf9a13ee16a29e9d01c9fa9b30500663432e4bb5')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e 's/<.*2.7/<3/' -e 's/<.*4.17/<5/' -e 's/<.*1.4/<2/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
