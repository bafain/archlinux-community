# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Llewelyn Trahaearn <WoefulDerelict at GMail dot com>
# Contributor: rtfreedman  <rob dot til dot freedman at gmail dot com>
# Contributor: Limao Luo <luolimao+AUR at gmail dot com>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=rtaudio
pkgver=5.0.0
pkgrel=6
pkgdesc="A set of C++ classes that provide a common API for realtime audio input/output."
arch=('x86_64')
url="https://www.music.mcgill.ca/~gary/rtaudio/"
license=('custom:MIT')
depends=('jack' 'libpulse' 'python2')
makedepends=('doxygen')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/thestk/${pkgname}/archive/v${pkgver}.tar.gz"
        '0001-fix-jack-requirement.diff')
sha512sums=('0bc60c189795626ff65ffaf7c7ae3dd72b4d249fdfe8dc9c3cc4cda8871b8f6ba151c60cd10b81286c3800f326a16028745cf8c67b8eafde0d8a68145ecb2258'
            '3cb2c19cdf2d25d1bb62f6f37d30f8677405e325966e9cbfbf39e57088637e5ab1eb70af224335569ba39931e75f57592223b33f65f8d3fc7be6bd230d67f901')

prepare() {
  cd "${pkgname}-${pkgver}"
  # https://github.com/thestk/rtaudio/issues/115
  patch -Np1 -i "${srcdir}/0001-fix-jack-requirement.diff"
  autoreconf -fi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr --with-alsa --with-jack --with-pulse
  make
  cd contrib/python/pyrtaudio
  python2 setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {readme,doc/release.txt}

  # html
  install -t "${pkgdir}/usr/share/doc/${pkgname}/html" \
    -vDm644 doc/html/*
  install -t "${pkgdir}/usr/share/doc/${pkgname}/images" \
    -vDm644 doc/images/*

  # license
  csplit -s readme "%LEGAL AND%"
  install -vDm644 xx00 "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # python2 bindings
  cd contrib/python/pyrtaudio
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
}

