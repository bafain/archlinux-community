# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Arnaud Taffanel <dev@taffanel.org>
# Contributor: Victor HÃ¤ggqvist <aur a snilius d com>

_name=Solaar
pkgname=solaar
pkgver=0.9.2
pkgrel=12
pkgdesc="Device manager for Logitech's Unifying receiver peripherals"
url="https://pwr.github.com/Solaar/"
license=('GPL2')
arch=('any')
depends=('gtk3' 'libnotify' 'python-dbus' 'python-gobject' 'python-pyudev')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pwr/${_name}/archive/${pkgver}.tar.gz"
        'py37-compatibility.patch')
sha512sums=('13f469df753cc8fab55f5259ccc3e62b6e44471d277d240cd56ce0580144d2b1f95d731268176648663dd15e5607bfb512f477362f4f100aa0a653b001dfe04b'
            '4b8830898cb4debd5d7c6174a0372559d2a438ddde0e0ceb9582eed0d876ee28c955fda72742326b171641ca213f8cddf97d90d5f21524493d8511964905ef76')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i ../py37-compatibility.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
  # udev
  install -vDm 644 rules.d/42-logitech-unify-permissions.rules \
    "${pkgdir}/usr/lib/udev/rules.d/42-logitech-unify-permissions.rules"
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:
