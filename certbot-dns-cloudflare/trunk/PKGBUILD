# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=certbot-dns-cloudflare
pkgver=0.21.1
pkgrel=1
pkgdesc="Cloudflare DNS Authenticator plugin for Certbot"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
depends=("certbot=$pkgver" "python-acme=$pkgver" 'python-cloudflare' 'python-mock'
         'python-setuptools' 'python-zope-interface')
source=("https://pypi.io/packages/source/c/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('9ae2f9c42e8728eed76757e3a74075c88c466859b212fc5e22c181e877b6b75943ec4579e6d16068abb2ef915092968a3b02c635749b3044e941e16059160329'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py test
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py install --root="$pkgdir"
}
