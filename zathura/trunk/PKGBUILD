# $Id$
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: ML <neldoreth>

pkgname=zathura
pkgver=0.4.0
pkgrel=4

pkgdesc="Minimalistic document viewer"
url="http://pwmt.org/projects/zathura/"
arch=('x86_64')
license=('custom')

makedepends=('python-sphinx' 'texlive-bin' 'meson' 'ninja')
depends=('girara>=0.2.7' 'sqlite' 'desktop-file-utils' 'file' 'libsynctex')
optdepends=('zathura-djvu: DjVu support'
            'zathura-pdf-poppler: PDF support using Poppler'
            'zathura-pdf-mupdf: PDF support using MuPDF'
            'zathura-ps: PostScript support'
            'zathura-cb: Comic book support')

source=(https://pwmt.org/projects/zathura/download/zathura-$pkgver.tar.xz
        bash-completion
        0001-meson.build-Don-t-do-broken-synctex-version-check.patch)

sha1sums=('4278a1cbccd414fdff5693598c996b2bf2a7cf71'
          '94a8236c483626a7f3acee053a1ea885aed45a82'
          '601b5345b4e2c688ec0eb78c66b3b40872d1e91d')

prepare() {
  cd zathura-$pkgver
  patch -p1 <"$srcdir"/0001-meson.build-Don-t-do-broken-synctex-version-check.patch
}

build() {
  cd zathura-$pkgver
  arch-meson build

  cd build
  ninja
}

package() {
  cd zathura-$pkgver/build
  DESTDIR="$pkgdir" ninja install

  install -D -m664 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm0644 "$srcdir"/bash-completion "$pkgdir"/usr/share/bash-completion/completions/zathura
}
