# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-keystoneclient
pkgname=(python-keystoneclient python2-keystoneclient)
pkgver=3.17.0
pkgrel=2
pkgdesc="Client Library for OpenStack Identity"
arch=('any')
url="https://docs.openstack.org/python-keystoneclient/latest/"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-iso8601'
             'python2-iso8601' 'python-prettytable' 'python2-prettytable' 'python-debtcollector'
             'python2-debtcollector' 'python-keystoneauth1' 'python2-keystoneauth1' 'python-oslo-utils'
             'python2-oslo-utils' 'python-oslo-i18n' 'python2-oslo-i18n' 'python-oslo-serialization'
             'python2-oslo-serialization' 'python-oslo-config' 'python2-oslo-config'
             'python-requests' 'python2-requests' 'python-stevedore' 'python2-stevedore')
checkdepends=('python-oslotest' 'python2-oslotest' 'python-testresources' 'python2-testresources'
              'python-requests-mock' 'python2-requests-mock' 'python-pycodestyle'
              'python2-pycodestyle' 'python-lxml' 'python2-lxml' 'python-testrepository' 'python2-testrepository')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/python-keystoneclient/archive/$pkgver.tar.gz")
sha512sums=('8a8864fc83ebfc61b052367f587acf947715990507b388a09cb9b0524404e7770c3f7baf1f0c2b17fe8794cc3c068d89176c57e4b9de39d6b6a60094a950c76c')

export PBR_VERSION=$pkgver

prepare() {
  cp -a python-keystoneclient-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-keystoneclient-$pkgver
  python setup.py build

  cd "$srcdir"/python-keystoneclient-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-keystoneclient-$pkgver
  python setup.py testr

  cd "$srcdir"/python-keystoneclient-$pkgver-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-keystoneclient() {
  depends=('python-six' 'python-pbr' 'python-iso8601' 'python-prettytable' 'python-debtcollector'
           'python-keystoneauth1' 'python-oslo-utils' 'python-oslo-i18n'
           'python-oslo-serialization' 'python-oslo-config' 'python-requests' 'python-stevedore')

  cd "$srcdir"/python-keystoneclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-keystoneclient() {
  depends=('python2-six' 'python2-pbr' 'python2-iso8601' 'python2-prettytable' 'python2-debtcollector'
           'python2-keystoneauth1' 'python2-oslo-utils' 'python2-oslo-i18n'
           'python2-oslo-serialization' 'python2-oslo-config' 'python2-requests' 'python2-stevedore')

  cd "$srcdir"/python-keystoneclient-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
