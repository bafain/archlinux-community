# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=io-streams-haproxy
pkgname=haskell-io-streams-haproxy
pkgver=1.0.0.2
pkgrel=24
pkgdesc="HAProxy protocol 1.5 support for io-streams"
url="https://github.com/snapframework/io-streams-haproxy"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-io-streams' 'haskell-network')
makedepends=('ghc' 'haskell-hunit' 'haskell-test-framework' 'haskell-test-framework-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('08de179bbb2c446329fc9c213ed99f8c19ce647b1203bde9e298676a0e0f9b5e617e897d14a74cd6bf80d7ff8ab13f0a38866f0569a065c7d6f6956535699543')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e 's/<.*4.11/<5/' -e 's/<.*2.7/<3/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
