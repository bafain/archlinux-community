# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=buoh
pkgver=0.8.2
pkgrel=10
pkgdesc="Online strips comics reader for GNOME"
arch=(x86_64)
url="http://buoh.steve-o.org/"
license=(GPL)
depends=(gtk3 libsoup)
makedepends=(git meson)
_commit=5aed558ce52b53830f06e7328b3f38cb7db0d6f0  # master
source=("git+https://gitlab.gnome.org/GNOME/buoh.git#commit=$_commit"
        0001-Various-fixes-related-to-gtk3-port.patch)
sha256sums=('SKIP'
            '1597fb8507421e5a85e8bc89243ede8bbf027cf2d87e58ad84dd63ccf6054eb4')

prepare() {
  cd $pkgname

  # Various fixes related to gtk3 port
  patch -Np1 -i ../0001-Various-fixes-related-to-gtk3-port.patch
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
