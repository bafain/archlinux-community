# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=scrot
pkgver=0.8.18
pkgrel=1
pkgdesc='Simple command-line screenshot utility for X'
url='https://tracker.debian.org/pkg/scrot'
arch=('x86_64')
license=('MIT')
depends=('giblib')
source=(http://http.debian.net/debian/pool/main/s/${pkgname}/${pkgname}_${pkgver%.*}.orig.tar.gz
        http://http.debian.net/debian/pool/main/s/${pkgname}/${pkgname}_${pkgver%.*}-${pkgver##*.}.debian.tar.xz)
sha256sums=('613d1cf524c2b62ce3c65f1232ea4f05c7daf248d5e82ff2a6892c98093994f2'
            'ea000f1f824a57e3058ef83de8d11112d4930cddeb51fb68253fc0756ed3a95d')
sha512sums=('cba8f589e45758ddbfe4e276399a1ecb0dbe29569be5d85d97733e7f64de2911bd2d03e62700ad0c718a1fc886c2e3def9dee1de5cac884f9e65e772cebe838c'
            '225a857c2586a0eaaa17e8f3dacdb01c395cefac99be80665c3a26be25b6cc9785a26f16de464829eb26e0e51d7d5420fe7ed79c1bbbd6931043d01a72d9d832')

prepare(){
    for patch in $(<"${srcdir}/debian/patches/series"); do
        msg2 "Applying ${patch}..."
        patch -Np1 -d "${srcdir}/${pkgname}-${pkgver%.*}" -i "${srcdir}/debian/patches/${patch}"
    done
}

build() {
  cd ${pkgname}-${pkgver%.*}
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man
  make
}

package() {
  cd ${pkgname}-${pkgver%.*}
  make DESTDIR=${pkgdir} docsdir=/usr/share/doc/scrot install
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
