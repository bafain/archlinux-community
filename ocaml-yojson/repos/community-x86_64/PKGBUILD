# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkgname=yojson
pkgname=ocaml-${_pkgname}
pkgver=1.4.1
pkgrel=2
pkgdesc="An optimized parsing and printing library for JSON"
arch=('x86_64')
url="https://github.com/mjambon/${_pkgname}"
license=('BSD')
options=('!strip' 'staticlibs')
depends=('ocaml-biniou' 'ocaml-easy-format')
makedepends=('dune' 'cppo')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/v${pkgver}.tar.gz")
sha256sums=('c081a8cb5a03bddbcac4614f468cf5edafe11805277572af4071e362be6611fb')

build() {
    cd ${_pkgname}-${pkgver}
    make all
}

check() {
    cd ${_pkgname}-${pkgver}
    make test
}

package() {
    cd ${_pkgname}-${pkgver}

    # Initialize OPAM, this should be removed once opam is “removed” from dune
    export OPAMROOT="${srcdir}"/opam
    opam init -n

    # Work around the install command
    make OCAMLFIND_DESTDIR="${pkgdir}$(ocamlfind printconf destdir)" install

    # Install LICENSE
    install -Dm644 "${srcdir}"/opam/system/doc/${_pkgname}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
