# Maintainer: David Runge <dave@sleepmap.de>
_name=PyRSS2Gen
pkgbase=python-pyrss2gen
pkgname=('python-pyrss2gen' 'python2-pyrss2gen')
pkgver=1.1
pkgrel=8
pkgdesc="Generate RSS2 using a Python data structure"
arch=('any')
url="http://www.dalkescientific.com/Python/PyRSS2Gen.html"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest')
# pypi source doesn't contain LICENSE
#source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("http://www.dalkescientific.com/Python/${_name}-${pkgver}.tar.gz")
sha512sums=('2240686914c87984626eb739676927fcd53488158bdebf589e07e8a778e6d24305964e032581c61836f0fabc1264e42d05c88eac8f4c5f6536c31c775434ce02')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgbase-$pkgver"
}

build() {
  cd "$pkgbase-$pkgver"
  python setup.py build
  python2 setup.py build
}

package_python-pyrss2gen() {
  cd "$pkgbase-$pkgver"
  depends=('python')
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python2-pyrss2gen() {
  cd "$pkgbase-$pkgver"
  depends=('python2')
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README -t "${pkgdir}/usr/share/doc/${pkgname}"
}
