# $Id$
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Aaron Schaefer <aaron@elasticdog.com>
# Contributor: Erwin Van de Velde <erwin.vandevelde@gmail.com>

pkgname=rdiff-backup
pkgver=1.2.8
pkgrel=8
pkgdesc='A utility for local/remote mirroring and incremental backups.'
arch=('x86_64')
url='http://www.nongnu.org/rdiff-backup/'
license=('GPL')
depends=('python2' 'librsync')
optdepends=('pylibacl: access control list support'
            'pyxattr: extended attribute support')
source=("http://savannah.nongnu.org/download/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.asc}
        rdiff-backup-1.2.8-librsync-1.0.0.patch)
sha512sums=('4230bdfca7fc2a5ffd7bdc92ca899cde3276042115b4f488255a201e6ea361eab4ce4ebaae15841c357f189e1c88ad037e2727dec0a05c61b1c6016fa807739c'
            'SKIP'
            '641894eb2527c0031d420345e8dfd4d7a0e18dc656feabf70c7200d04ea5eb533d5cd06bbaa46721a03578e2f373e0ed64643edb4edffc7f85e813d1c2812055')
validpgpkeys=('B572DE9BC6537B9D5A4735B114C2601BDCA52872') # Andrew Ferguson

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ../rdiff-backup-1.2.8-librsync-1.0.0.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  python2 setup.py install --root="${pkgdir}" -O1

  # Fix for FS#18848
  sed -i 's|python2|python2 -W ignore::DeprecationWarning|' "${pkgdir}/usr/bin/rdiff-backup"
}
