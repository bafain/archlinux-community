# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Radim Hvizd√°k <hvizdakr at gmail dot com>

pkgname=ibus-hangul
pkgver=1.5.1
pkgrel=1
pkgdesc='Korean input engine for IBus'
arch=('x86_64')
url='http://ibus.googlecode.com'
license=('GPL')
depends=('ibus' 'libhangul' 'iso-codes' 'gtk-update-icon-cache' 'python-gobject')
makedepends=('intltool' 'gnome-common')
source=("$pkgname-$pkgver.tar.gz::https://github.com/choehwanjin/ibus-hangul/archive/${pkgver}.tar.gz")
sha512sums=('12052570d953a73ba8b26f5903b2911b8354a89196e8222922320d0deaf1220fadc7e4bade86b5d8862ea0d44a9040558f28ef8bdafa43e95281080ec824c641')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  autoreconf --verbose --force --install
  intltoolize --automake --force --copy
  ./configure --prefix=/usr --libexecdir=/usr/lib/ibus
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make NO_INDEX=true DESTDIR="${pkgdir}" install

  # Fix symlink
  ln -sf ../lib/ibus/ibus-setup-hangul "$pkgdir/usr/bin/ibus-setup-hangul"
}
