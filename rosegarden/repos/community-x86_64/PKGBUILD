# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=rosegarden
pkgver=17.12.1
pkgrel=1
pkgdesc="MIDI/audio sequencer and notation editor"
arch=('x86_64')
url="https://www.rosegardenmusic.com/"
license=('GPL2')
groups=('pro-audio')
depends=('liblrdf' 'dssi' 'fftw' 'lirc'
         'hicolor-icon-theme' 'qt5-base' 'shared-mime-info' 'liblo')
makedepends=('imake' 'cmake' 'qt5-tools')
optdepends=('lilypond: notation display'
            'cups: printing support'
            'okular: print preview, or any other PDF viewer'
            'timidity++: MIDI playback, or any other softsynth'
            'flac'
            'wavpack')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('8bd4a464bbf2825d6c2fb8d36123e68d94de7f4e997c6ed8efc5670e87799781eb8251995890979c9d107d4fc58c636fe73e699c859c8e95aa567c4289958495')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DBUILD_TESTING=OFF -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {AUTHORS,README}
}

# vim:set ts=2 sw=2 et:
