# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tatsuyuki Ishi <ishitatsuyuki@gmail.com>

pkgname=python-blessed
pkgver=1.15.0
pkgrel=2
pkgdesc='A thin, practical wrapper around terminal styling, screen positioning, and keyboard input'
arch=('any')
url='https://github.com/jquast/blessed'
license=('GPL')
depends=('python-six' 'python-wcwidth')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest')
source=("https://pypi.python.org/packages/source/b/blessed/blessed-${pkgver}.tar.gz")
sha256sums=('777b0b6b5ce51f3832e498c22bc6a093b6b5f99148c7cbf866d26e2dec51ef21')

build() {
  cd blessed-${pkgver}

  python setup.py build
}

check() {
  cd blessed-${pkgver}

  python -m pytest
}

package() {
  cd blessed-${pkgver}

  python setup.py install --root="${pkgdir}" --optimize='1' --skip-build
}

# vim: ts=2 sw=2 et:
