# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-invoke
pkgname=(python-invoke python2-invoke)
pkgver=1.1.1
pkgrel=1
pkgdesc="Pythonic task execution"
url="https://github.com/pyinvoke/invoke"
license=('BSD')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock'
              'python-pytest-relaxed' 'python2-pytest-relaxed')
source=("https://pypi.io/packages/source/i/invoke/invoke-$pkgver.tar.gz")
sha512sums=('2dabf88428a53f244cab45cbc073b3a91fea5dd97a7590a58701a32bfaa14a4afe5d6a2e4ff4ed03ae38e1dfecc5a7c0f811bb18313e0af1f4e116f56c5e9ba7')

prepare() {
  cp -a invoke-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/invoke-$pkgver
  python setup.py build

  cd "$srcdir"/invoke-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/invoke-$pkgver
  python setup.py pytest || warning "Tests failed"

  cd "$srcdir"/invoke-$pkgver-py2
  python2 setup.py pytest || warning "Tests failed"
}

package_python-invoke() {
  depends=('python')

  cd invoke-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-invoke() {
  depends=('python2')

  cd invoke-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  for _exe in "$pkgdir"/usr/bin/*; do
    mv "$_exe"{,2}
  done
}
