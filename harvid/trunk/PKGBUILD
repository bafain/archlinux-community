# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps dot aur dot archlinux dot org>

pkgname=harvid
pkgver=0.8.2
pkgrel=2
pkgdesc="HTTP Ardour Video Daemon"
arch=('x86_64')
url="https://x42.github.io/harvid/"
license=('GPL')
depends=('ffmpeg')
makedepends=('xxd')
source=("$pkgname-$pkgver.tar.gz::https://github.com/x42/${pkgname}/archive/v$pkgver.tar.gz")
sha512sums=('fde352124008cd3d6230950c79a3a2b8d581803f952c7313c941226b00e80f8078053d941f2c5ead04672639a8db696f0c33482f69dab78187801d7ebce23fce')

build() {
  cd "${pkgname}-${pkgver}"
  # different job setting needed to not crash build
  make PREFIX=/usr -j1
}

package() {
  cd "${pkgname}-${pkgver}"
  make PREFIX=/usr DESTDIR="${pkgdir}/" install

  # ffmpeg and ffprobe symlinks (for ardour)
  ln -sv ffmpeg "${pkgdir}/usr/bin/ffmpeg_${pkgname}"
  ln -sv ffprobe "${pkgdir}/usr/bin/ffprobe_${pkgname}"

  install -vDm 644 README.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
