# Maintainer: David Runge <dave@sleepmap.de>
_name=libtmux
pkgbase=python-libtmux
pkgname=('python-libtmux' 'python2-libtmux')
pkgver=0.8.0
pkgrel=2
pkgdesc="Python api for tmux"
arch=('any')
url="https://libtmux.git-pull.com/"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'tmux')
checkdepends=('python-pytest' 'python2-pytest' 'procps-ng')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tony/${_name}/archive/v${pkgver}.tar.gz"
        "https://github.com/tmux-python/libtmux/commit/7443b60f6df60335bf2ecb199f25e57ec888fe65.patch")
sha512sums=('9828e8d7c471b9f228c8a9e4e4369478a445ce4e25dece2a9f50652d30f4a778f9e769440fd34f582ab43436003bb06ae201475ba4116f5844bc44dc7f558874'
            '55f8290ee7367b22282df57c1fa27dea20197003157d0c57270e3754ee2212588d09b3b09993205c0a2420c86185db142ae39cb51232c7cf6b893396d795ec4d')

prepare() {
  # https://github.com/tmux-python/libtmux/issues/117 is not BSD-specific
  patch -p1 -d "${_name}-${pkgver}" < 7443b60f6df60335bf2ecb199f25e57ec888fe65.patch
  mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${pkgname[0]}-${pkgver}"
  python -m pytest

  cd ../"${pkgname[1]}-${pkgver}"
  python2 -m pytest
}

package_python-libtmux() {
  depends=('python' 'tmux')
  cd "${pkgname[0]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # license
  install -vDm644 LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname[0]}/LICENSE"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname[0]}" \
    -vDm644 {CHANGES,README.rst,TODO}
}

package_python2-libtmux() {
  depends=('python2' 'tmux')
  cd "${pkgname[1]}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # license
  install -vDm644 LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname[1]}/LICENSE"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname[1]}" \
    -vDm644 {CHANGES,README.rst,TODO}
}

# vim:set ts=2 sw=2 et:
