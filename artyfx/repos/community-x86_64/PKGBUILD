# Maintainer: David Runge <dave@sleepmap.de>
pkgname=artyfx
pkgver=1.3
pkgrel=2
pkgdesc="A plugin bundle of artistic real-time audio effects"
arch=('x86_64')
url="http://openavproductions.com/artyfx/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('cairo' 'libsndfile')
makedepends=('cmake' 'lv2')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openAVproductions/openAV-${pkgname}/archive/release-${pkgver}.tar.gz")
sha512sums=('51931d0497c91150db738ffe7199e1cd3a86d43d34b371b9719b8d0b504bebf3b9cf14e20d299081b0482f51ef7a6e825e8bf0c832b0caf3bc4a8f8221990a97')

prepare() {
  mv -v "openAV-ArtyFX-release-${pkgver}" "${pkgname}-${pkgver}"
  cd "$pkgname-$pkgver"
  mkdir build
}

build() {
  cd "$pkgname-$pkgver/build"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DAVTK_SNDFILE_SUPPORT=true \
        ..
  make
}

package() {
  cd "$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir/" install
  # docs
  install -vDm 644 ../{CHANGELOG,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
