# Maintainer: David Runge <dave@sleepmap.de>
pkgname=zopfli
pkgver=1.0.2
pkgrel=2
pkgdesc="Compression algorithm library"
arch=('x86_64')
url="https://github.com/google/zopfli/tree/ae43a8b73827577c4b19b005b6eed81f5cf9bbac"
license=('Apache')
depends=('gcc-libs')
provides=('zopfli' 'zopflipng')
source=("https://github.com/google/${pkgname}/archive/${pkgname}-${pkgver}.tar.gz")
sha512sums=('290a9506a0c1f3d0510230ca21017e1d2bed830f3f96f9d38c9f27210d89123c3baebd05a1da988eccd5395a7b007f6690f08eb9087550a06eaa69d7531f074e')

prepare() {
  mv -v "$pkgname-$pkgname-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # add LDFLAGS to get full relro
  sed -e "/^CFLAGS = / s/$/ $CFLAGS/" \
      -e "/^CXXFLAGS = / s/$/ $CXXFLAGS/" \
      -e "s/\-\-shared/\-shared/" \
      -e "/\-shared / s/$/ $LDFLAGS/" \
      -e "s/\-o\ zopflipng$/& $LDFLAGS/" \
      -e "s/\-o\ zopfli$/& $LDFLAGS/" \
      -i Makefile
}

build() {
  cd "$pkgname-$pkgver"
  make libzopfli zopfli libzopflipng zopflipng
}

package() {
  cd "$pkgname-$pkgver"
  # there is no install target
  install -vDm 755 ${pkgname}{,png} -t "${pkgdir}/usr/bin/"
  install -vDm 644 src/${pkgname}{,png}/${pkgname}*.h \
    -t "${pkgdir}/usr/include/"
  install -vDm 755 lib${pkgname}*.so.* -t "${pkgdir}/usr/lib/"
  ldconfig -N "${pkgdir}/usr/lib/"
  install -vDm 644 README* -t "${pkgdir}/usr/share/doc/${pkgname}/"
}

