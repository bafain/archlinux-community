# $Id$
# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=(qpid-proton python2-qpid-proton)
pkgver=0.23.0
pkgrel=2
pkgdesc='High-performance, lightweight messaging library'
arch=('x86_64')
url='https://qpid.apache.org/proton'
license=('Apache')
makedepends=('cmake' 'doxygen' 'python' 'python2' 'swig')
source=("http://www.apache.org/dist/qpid/proton/${pkgver}/qpid-proton-${pkgver}.tar.gz")
sha512sums=('5000916c9b44c799bdcd78932def57b13b9c24cd73885c9e3c769e2b1d55c7f7851feef439d6e9c7ae9be4a602e3370ec78cf51a551b4e0ccdcacf91e18dd8bd')

build() {
    cd ${pkgname}-${pkgver}

    mkdir build
    cd build
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DLIB_SUFFIX='' \
        -DSYSINSTALL_BINDINGS=ON \
        ..
    make

    cd python/dist
    python2 setup.py build
}

package_qpid-proton() {
    depends=('python' 'ruby')

    cd ${pkgname}-${pkgver}/build
    make DESTDIR="${pkgdir}" install
}

package_python2-qpid-proton() {
    depends=('python2')

    cd qpid-proton-${pkgver}/build/python/dist
    python2 setup.py install --root="${pkgdir}" --skip-build --optimize=1
}
