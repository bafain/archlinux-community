# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

_pkgbasename=keyutils
pkgname=lib32-$_pkgbasename
pkgver=1.5.11
pkgrel=1
pkgdesc="Linux Key Management Utilities (32-bit)"
arch=('x86_64')
url="http://www.kernel.org"
license=('GPL2' 'LGPL2.1')
depends=(lib32-glibc lib32-krb5 $_pkgbasename)
makedepends=(gcc-multilib)
validpgpkeys=('A86E54B0D5E1B4E0AB7C640FFBB7576BA7CB0B6B') # David Howells <dhowells@redhat.com>
source=("https://people.redhat.com/~dhowells/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.bz2"{,.sig})
sha256sums=('11584488206edc30736263eb855bf7793f16e661987241d333d3cb5ea479b1b5'
            'SKIP')

build() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  sed -i -e 's/^\(USR\)\?LIBDIR\s*:=.*$/\1LIBDIR=\/usr\/lib32/' Makefile
  make CFLAGS="${CFLAGS}" LDFLAGS="${LDFLAGS}"
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make DESTDIR="$pkgdir" install

  rm -rf "${pkgdir}"/{usr/{include,share,bin,sbin},etc,{s,}bin}
}
