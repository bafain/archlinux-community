# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-netbook
pkgver=1.20.1
pkgrel=1
pkgdesc="A simple window management tool (GTK2 version)"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('libfakekey' 'mate-panel')
makedepends=('intltool')
groups=('mate-extra')
conflicts=('mate-netbook-gtk3')
replaces=('mate-netbook-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('58331b140c7b2c38edba9ab200aebd2e3a4655fbc65ff1df4ce748b315059ac6')

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --libexec=/usr/lib/${pkgname} \
        --sysconfdir=/etc
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
