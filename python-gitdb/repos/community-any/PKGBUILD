# Maintainer: NicoHood <archlinux {cat} nicohood {dog} de>
# PGP ID: 97312D5EB9D7AE7D0BD4307351DAE9B7C1AE9161
# Contributor: Carl George < arch at cgtx dot us >

_pkgname=python-gitdb
_pypiname=gitdb2
pkgbase=python-gitdb
pkgdesc="A pure-Python git object database"
pkgname=('python-gitdb' 'python2-gitdb')
pkgver=2.0.4
pkgrel=3
url="https://github.com/gitpython-developers/gitdb"
license=('BSD')
arch=("any")
makedepends=('python' 'python-setuptools' 'python2' 'python2-setuptools')
source=("${_pkgname}-${pkgver}.tar.gz::https://pypi.org/packages/source/g/${_pypiname}/${_pypiname}-${pkgver}.tar.gz"
        "${_pkgname}-${pkgver}.tar.gz.asc::https://pypi.org/packages/source/g/${_pypiname}/${_pypiname}-${pkgver}.tar.gz.asc")
sha512sums=('be0f89e35fd800ede9f29ab6e2dc97a7bdc918c22ed1272d348e570f3f732295a56c6138fcfa6d0c0027c0b12f20e23deff71fb24b9e7b5cd7dbd47816a14acd'
            'SKIP')
validpgpkeys=('2CF6E0B51AAF73F09B1C21174D1DA68C88710E60') # Sebastian Thiel (In Rust I trust!) <byronimo@gmail.com>

prepare() {
    cp -a "${srcdir}/${_pypiname}-${pkgver}"{,-py2}
}

build() {
    cd "${srcdir}/${_pypiname}-${pkgver}"
    python setup.py build

    cd "${srcdir}/${_pypiname}-${pkgver}-py2"
    python2 setup.py build
}

package_python-gitdb() {
    depends=('python' 'python-smmap>=2.0.0')

    cd "${srcdir}/${_pypiname}-${pkgver}"
    python setup.py install --skip-build --root="${pkgdir}" --optimize=1
    install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-gitdb() {
    depends=('python2' 'python2-smmap>=2.0.0')

    cd "${srcdir}/${_pypiname}-${pkgver}-py2"
    python2 setup.py install --skip-build --root="${pkgdir}" --optimize=1
    install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
