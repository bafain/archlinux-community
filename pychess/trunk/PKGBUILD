# $Id$
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk@gmail.com>

pkgname=pychess
pkgver=0.99.2
pkgrel=2
pkgdesc='Chess client'
arch=('any')
url='http://pychess.org/'
license=('GPL')
depends=(gst-plugins-base gtksourceview3 python-cairo python-gobject
         python-ptyprocess python-psutil python-sqlalchemy python-pexpect)
makedepends=(git python-pexpect xorg-server-xvfb)
# commit 6179d56 contains a fix for Python 3.7. 0.99.3 will be out soon.
source=("git+https://github.com/$pkgname/$pkgname#commit=6179d56")
sha256sums=('SKIP')

package() {
  cd pychess

  xvfb-run python setup.py install --prefix=/usr --root="$pkgdir"

  # FS#59882
  find "$pkgdir" -wholename "external/pexpect" -type d -delete
}

# getver: raw.githubusercontent.com/pychess/pychess/master/lib/pychess/__init__.py
# vim: ts=2 sw=2 et:
