# $Id$
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=shellharden
pkgver=4.0.0
pkgrel=1
pkgdesc='Bash linter and syntax highlighter'
arch=('x86_64')
url='https://github.com/anordal/shellharden'
license=('MPL')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v${pkgver/.0}.tar.gz")
sha256sums=('91660e4908bd07105f091a62e6f77bc9ed42045096b38abe31503cd2609cb7a0')

build() {
  cd "$pkgname-${pkgver/.0}"

  cargo build --release
}

package() {
  cd "$pkgname-${pkgver/.0}"

  install -Dm755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/anordal/shellharden/blob/master/Cargo.toml
# vim: ts=2 sw=2 et:
