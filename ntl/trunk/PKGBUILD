# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Brad Conte <brad AT bradconte.com>

pkgname=ntl
pkgver=11.3.0
pkgrel=1
pkgdesc="A Library for doing Number Theory"
arch=(x86_64)
url="http://www.shoup.net/ntl/"
license=(LGPL)
depends=(gf2x gmp)
makedepends=()
source=("http://www.shoup.net/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('3ae0d3647e5d52bc0907e1f4cb4a958460ee8e5b3c9862c1a4b8e9f0f75891dd')

build() {
  cd $pkgname-$pkgver/src
  ./configure DEF_PREFIX=/usr SHARED=on NTL_GF2X_LIB=on NATIVE=off TUNE=generic
  make
}

check() {
  cd $pkgname-$pkgver/src
  make -k check
}

package() {
  cd $pkgname-$pkgver/src
  make PREFIX="$pkgdir/usr" install
}
