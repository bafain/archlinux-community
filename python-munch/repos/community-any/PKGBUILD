# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-munch
pkgname=(python-munch python2-munch)
pkgver=2.2.0
pkgrel=1
pkgdesc="A dot-accessible dictionary (a la JavaScript objects)"
url="https://github.com/Infinidat/munch"
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest' 'python-yaml' 'python2-yaml')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/Infinidat/munch/archive/$pkgver.tar.gz")
sha512sums=('f6ad5a380664246fecf65ba32d8f168f047192d9ca6b02b941d99173677eec235951c59d6fadbce94e3fc32de0e0e7358f319f8c34f623f8da85ee8878048350')

prepare() {
  cp -a munch-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/munch-$pkgver
  python setup.py build

  cd "$srcdir"/munch-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/munch-$pkgver
  pytest

  cd "$srcdir"/munch-$pkgver-py2
  pytest2
}

package_python-munch() {
  depends=('python-six')

  cd munch-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-munch() {
  depends=('python2-six')

  cd munch-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
