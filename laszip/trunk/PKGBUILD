# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Maciej Sieczka <msieczka at sieczka dot org>
# Contributor: Michael <mbostwick89@gmail.com>

pkgname=laszip
pkgver=3.2.2
pkgrel=1
pkgdesc='ASPRS LAS lossless compression library'
arch=('x86_64')
url='https://laszip.org'
license=('LGPL')
makedepends=('cmake')
source=("https://github.com/LASzip/LASzip/releases/download/${pkgver}/laszip-src-${pkgver}.tar.gz")
sha512sums=('e89707c0569c6435394a3a07ba5e6ea972d65cf793c157a698d1ec293f5477deccd613733cb544b907d6c1f13d0313f0d24ae7e5dddc426d7bdb9a7e58709cf8')

build() {
  cd "${srcdir}/${pkgname}-src-${pkgver}"

  mkdir build && cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd "${srcdir}/${pkgname}-src-${pkgver}/build"
  make DESTDIR="$pkgdir" install
}
