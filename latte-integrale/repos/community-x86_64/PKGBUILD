# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=latte-integrale
pkgver=1.7.4
pkgrel=1
pkgdesc="Computer software dedicated to the problems of counting lattice points and integration inside convex polytopes"
arch=(x86_64)
url="https://www.math.ucdavis.edu/~latte/"
license=(GPL)
depends=(4ti2 ntl cddlib lidia)
source=("https://github.com/latte-int/latte/releases/download/version_${pkgver//./_}/latte-int-$pkgver.tar.gz")
sha256sums=('a1178605c9c560073edb8177d4a6dd157de7277ffc23a4911409e13982c8d09e')

build() {
  cd latte-int-$pkgver

  export CXXFLAGS="-DNTL_STD_CXX $CXXFLAGS"
  export LDFLAGS+=" -lpthread"
  ./configure --prefix=/usr --enable-shared --with-ntl=/usr
  make
}

package() {
  cd latte-int-$pkgver
  make DESTDIR="$pkgdir" install

# Fix conflicts with LLVM
  mv "$pkgdir"/usr/bin/{count,latte-count}
}

