# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=resolv
pkgname=haskell-resolv
pkgver=0.1.1.1
pkgrel=11
pkgdesc="Domain Name Service (DNS) lookup via the libresolv standard library routines"
url="https://github.com/hvr/resolv"
license=('GPL')
arch=('x86_64')
depends=('ghc-libs' 'haskell-base16-bytestring')
makedepends=('ghc' 'haskell-tasty' 'haskell-tasty-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('0bc87f922072077d08d49e583d7fe60127380044ff2433e2e794029b77b4334f854c8cec451c4ad5a938bb52144b6f0fd9cad9d550ba208cd96fb0acdb631536')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e 's/<.*4.11/<5/' -e 's/<.*0.12/<2/' -e 's/<.*0.10/<1/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --disable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    # https://github.com/haskell-hvr/resolv/issues/1
    # runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
