# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Llewelyn Trahaearn <WoefulDerelict at GMail dot com>
# Contributor: rtfreedman  <rob dot til dot freedman at gmail dot com>
# Contributor: speps <speps at aur dot archlinux dot org> 

pkgname=rtmidi
pkgver=3.0.0
pkgrel=6
pkgdesc="A set of C++ classes that provides a common API for realtime MIDI input/output."
arch=('x86_64')
url="https://www.music.mcgill.ca/~gary/rtmidi/"
license=('custom:MIT')
depends=('jack')
makedepends=('doxygen')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/thestk/${pkgname}/archive/v${pkgver}.tar.gz"
        '0001-fix-tests-with-prefix.diff'
        '0002-fix-jack-requirement.diff')
sha512sums=('8975a63e7be9102af70401cef48c702597b87efe2d8fa30a978fe280e26da1dfa90d6f30cfbd3df587462f0dd085d0f29e1c014e67d7fcd3a36960b6bcfb3e55'
            'cab71dd641eaa3c7fa3c83b81fb4517bcf455a6037e103c29edbc2d540feed1b4fc0e5e3f9ea5f03845c98c8246cbb9bd12d1cf1f16c4bcbb2c0e874bd66674a'
            '4cd4f49a639688736fef3ebea84f5ca05aef54c85b83713e9aa9ae5467c871e7edda7272279e8f2e131e3e70cc9aa394bab4e157be9521f41d1811c90a66b357')

prepare() {
  cd "${pkgname}-${pkgver}"
  # https://github.com/thestk/rtmidi/pull/127
  patch -Np2 -i "${srcdir}/0001-fix-tests-with-prefix.diff"
  # https://github.com/thestk/rtmidi/issues/154
  patch -Np1 -i "${srcdir}/0002-fix-jack-requirement.diff"
  autoreconf -fi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr --with-alsa --with-jack
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {README.md,doc/release.txt}

  # html
  install -t "${pkgdir}/usr/share/doc/${pkgname}/html" \
    -vDm644 doc/html/*
  install -t "${pkgdir}/usr/share/doc/${pkgname}/images" \
    -vDm644 doc/images/*

  # license
  csplit -s README.md "%Legal and%"
  install -vDm644 xx00 "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

