# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=cipher-aes128
pkgname=haskell-cipher-aes128
pkgver=0.7.0.3
pkgrel=6
pkgdesc="AES and common modes using AES-NI when available."
url="https://github.com/TomMD/cipher-aes128"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-crypto-api' 'haskell-tagged' 'haskell-cereal')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('72bd1cca4ccd31be08096a53dffa9fa855b24b226904327a417f797b8b8a799d368ef00f75f9a81234a12c659b63d595d58f263cc3a4d544942be7edada413b7')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
