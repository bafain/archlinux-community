# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gordian Edenhofer <gordian.edenhofer[at]yahoo[dot]de>

pkgname=certbot-apache
pkgver=0.27.1
pkgrel=1
pkgdesc="Apache plugin for Letâ€™s Encrypt client"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
replaces=("letsencrypt-apache")
depends=('certbot' 'python-acme' 'python-augeas' 'python-mock' 'python-setuptools'
         'python-zope-component' 'python-zope-interface')
source=("https://pypi.io/packages/source/c/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('438c893c101509c5961d61e68c7c52d5b99a3d33fdcf998415699b9ac39bdd43fc0a1f9a3ce2430e4c71c6aed9a0bb3e7031d38a59f47260392e3af03acb9d9e'
            'SKIP')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  # https://github.com/certbot/certbot/issues/5510
  cd $pkgname-$pkgver
  python setup.py test || warning "Tests failed"
}

package() {
  cd $pkgname-$pkgver
  python setup.py install -O1 --root="$pkgdir"
}
