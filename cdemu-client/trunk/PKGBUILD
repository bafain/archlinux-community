# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=cdemu-client
pkgver=3.2.0
pkgrel=1
pkgdesc="Simple command-line client for controlling cdemu-daemon"
arch=(any)
url="http://cdemu.sourceforge.net/"
license=(GPL)
depends=(python-gobject cdemu-daemon)
makedepends=(intltool cmake ninja bash-completion)
conflicts=('cdemu')
source=("https://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.bz2")
sha256sums=('a9fea8c54433b6827ea23e13cb10b931609854370afd99a69bea8a750f4a9fff')

prepare() {
  mkdir build
  cd $pkgname-$pkgver
}

build() {
  cd build
  cmake -G Ninja ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build .
}

package() {
  DESTDIR="$pkgdir" cmake --build build --target install
}
