# $Id$
# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Frederik “Freso” S. Olesen <freso.dk@gmail.com>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>

pkgname=lutris
pkgver=0.4.19
pkgrel=1
pkgdesc='Open Gaming Platform'
arch=('any')
url='https://lutris.net'
license=('GPL3')
depends=('python-gobject' 'python-yaml' 'python-evdev' 'gtk3' 'glib2' 'psmisc' 'cabextract' 'unrar' 'unzip' 'p7zip' 'curl')
makedepends=('python-setuptools')
checkdepends=('xorg-server-xvfb' 'xterm' 'xorg-xrandr' 'python-nose-cover3' 'wine')
optdepends=('wine: Run windows games'
            'wine-staging: Run windows games - Staging patches')
makedepends=('python-setuptools')
source=("$url/releases/${pkgname}_$pkgver.tar.xz")
sha512sums=('d206fd787467d43745e5d3347f7267d599975d7a3886b68114e60e93a8fc6f0beecb97e0be66beb7754b9c59f00e00846e00d7165e5dff0e200b57c4b6d45096')

build() {
  cd $pkgname

  python setup.py build
}

check() {
  cd $pkgname

  xvfb-run nosetests --cover-erase --with-xunit --xunit-file=nosetests.xml --with-coverage --cover-package=lutris --cover-xml-file=coverage.xml
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

