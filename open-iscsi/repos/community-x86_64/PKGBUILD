# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Stefan Kirrmann <stefan.kirrmann at gmail dot com>

pkgname=open-iscsi
pkgver=2.0.876
pkgrel=2
pkgdesc="userland tools"
arch=('x86_64')
url="http://www.open-iscsi.com/"
license=('GPL')
depends=('libutil-linux' 'openssl' 'open-isns')
install=$pkgname.install
backup=('etc/iscsi/iscsid.conf'
	'etc/iscsi/initiatorname.iscsi')
options=('docs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/open-iscsi/open-iscsi/archive/$pkgver.tar.gz"
        '0001-gcc8.patch'
        'open-iscsi.service')
sha256sums=('9f01327d5e100ed794dc5083fc18dc4a06a0c29c77b252e21abd1b8f56edd9a7'
            'a8e6920f5e135dda4d8eb76eb9c5a00f793ed6ccfa9b51006b083c4b8177f171'
            '7b8e37dd10a909a67ba7f7126f699920639be39adfa65f1d2b2bcd8846e58db7')

prepare() {
  cd "$srcdir"/${pkgname}-${pkgver}

  # Merge pull request #94 from gonzoleeman/gcc-8-fixes
  # Gcc 8 fixes -- clean up some string handling corner cases, and to make compiler happy
  patch -Np1 < '../0001-gcc8.patch'

  # include iscsistart in the package
  sed -i -e '/^PROGRAMS = /s/$/ usr\/iscsistart/' Makefile

  # build breaks if the openslp package is installed
  sed -i -e 's/\(\.\/configure\)/ \1 --without-slp/g' Makefile
}

build() {
  cd "$srcdir"/${pkgname}-${pkgver}

  make user LIB_DIR=/usr/lib
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}

  make DESTDIR="$pkgdir" LIB_DIR=/usr/lib install

  install -D -m644 "$srcdir"/${pkgname}-${pkgver}/etc/iscsid.conf "$pkgdir"/etc/iscsi
  install -D -m644 "$srcdir"/open-iscsi.service "$pkgdir"/usr/lib/systemd/system/open-iscsi.service

  echo -n >"$pkgdir"/etc/iscsi/initiatorname.iscsi

  # copy docs
  mkdir -p "$pkgdir"/usr/share/doc/${pkgname}
  install -m644 Changelog "$pkgdir"/usr/share/doc/${pkgname}/
  install -m644 README "$pkgdir"/usr/share/doc/${pkgname}/

  mv "$pkgdir"/sbin "$pkgdir"/usr/bin
}
