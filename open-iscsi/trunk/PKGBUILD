# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Stefan Kirrmann <stefan.kirrmann at gmail dot com>

pkgname=open-iscsi
pkgver=2.0.875
pkgrel=1
pkgdesc="userland tools"
arch=('x86_64')
url="http://www.open-iscsi.com/"
license=('GPL')
depends=('libutil-linux' 'openssl' 'open-isns')
install=$pkgname.install
backup=('etc/iscsi/iscsid.conf'
	'etc/iscsi/initiatorname.iscsi')
options=('docs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/open-iscsi/open-iscsi/archive/$pkgver.tar.gz"
        "open-iscsi.service")
sha256sums=('7b6459dcbd3fc5d6e1500eeeb8a680acc773ffcb5901f268db9d53d2979338bf'
            '7b8e37dd10a909a67ba7f7126f699920639be39adfa65f1d2b2bcd8846e58db7')

build() {
  cd "$srcdir"/${pkgname}-${pkgver}

  # include iscsistart in the package
  sed -i -e '/^PROGRAMS = /s/$/ usr\/iscsistart/' Makefile

  # build breaks if the openslp package is installed
  sed -i -e 's/\(\.\/configure\)/ \1 --without-slp/g' Makefile

  make user
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}

  make DESTDIR="$pkgdir" install_user

  install -D -m644 "$srcdir"/${pkgname}-${pkgver}/etc/iscsid.conf "$pkgdir"/etc/iscsi
  install -D -m644 "$srcdir"/open-iscsi.service "$pkgdir"/usr/lib/systemd/system/open-iscsi.service

  echo -n >"$pkgdir"/etc/iscsi/initiatorname.iscsi

  # copy docs
  mkdir -p "$pkgdir"/usr/share/doc/${pkgname}
  install -m644 Changelog "$pkgdir"/usr/share/doc/${pkgname}/
  install -m644 README "$pkgdir"/usr/share/doc/${pkgname}/

  mv "$pkgdir"/sbin "$pkgdir"/usr/bin
}
