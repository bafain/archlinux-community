# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Husam Bilal <husam212 AT gmail DOT com>

_name=furl
pkgbase=python-furl
pkgname=('python-furl' 'python2-furl')
pkgver=1.0.1
pkgrel=2
pkgdesc="A small Python library that makes parsing and manipulating URLs easy"
url="https://github.com/gruns/furl"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pycodestyle' 'python2-pycodestyle')
license=('custom')
arch=('any')
source=("https://pypi.io/packages/source/f/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('ef910331211fc4fb22e226279438b0b7acf347aa6d2d0058bafbb591f631c88c9c0095971749d1935429b8a730dc6f67dbbe965de46af531b9119c28eacb095d')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py test
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py test
}

package_python-furl() {
  depends=('python-six' 'python-orderedmultidict')
  cd "${pkgname[0]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -t "${pkgdir}/usr/share/doc/${pkgname}" -vDm644 {API,README}.md
}

package_python2-furl() {
  depends=('python2-six' 'python2-orderedmultidict')
  cd "${pkgname[0]}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -t "${pkgdir}/usr/share/doc/${pkgname}" -vDm644 {API,README}.md
}
