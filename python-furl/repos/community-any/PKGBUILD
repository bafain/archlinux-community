# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Husam Bilal <husam212 AT gmail DOT com>

_name=furl
pkgbase=python-furl
pkgname=('python-furl' 'python2-furl')
pkgver=1.2.1
pkgrel=1
pkgdesc="A small Python library that makes parsing and manipulating URLs easy"
url="https://github.com/gruns/furl"
makedepends=('python-orderedmultidict' 'python-setuptools' 'python-six' 'python2-orderedmultidict' 'python2-setuptools' 'python2-six')
checkdepends=('flake8' 'python-pycodestyle' 'python-pyflakes' 'python2-flake8' 'python2-pycodestyle' 'python2-pyflakes')
license=('custom:Unlicense')
arch=('any')
# pypi upstream lost the tests temporarily: https://github.com/gruns/furl/issues/105
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gruns/${_name}/archive/${pkgver}.tar.gz")
#source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('0c62cca8bbffcaaa5d2144aad6072a6e3e88c0ce7e04d33999a46c894edacc43be61ca46f4e4e5b009f9c957dc878208ebde289046c321b005d40f5e5d59f135')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgbase}-${pkgver}"
}

build() {
  cd "${pkgbase}-${pkgver}"
  python setup.py build
  python2 setup.py build
}

check() {
  cd "${pkgbase}-${pkgver}"
  python setup.py test
  python2 setup.py test
}

package_python-furl() {
  depends=('python-six' 'python-orderedmultidict')
  cd "${pkgbase}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -t "${pkgdir}/usr/share/doc/${pkgname}" -vDm644 {API,README}.md
}

package_python2-furl() {
  depends=('python2-six' 'python2-orderedmultidict')
  cd "${pkgbase}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -t "${pkgdir}/usr/share/doc/${pkgname}" -vDm 644 {API,README}.md
}
