# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: DonVla <donvla/users.sourceforge/net>
# Contributor: Herv√© Cauwelier <herve/oursours/net>

pkgbase=python-xapian
pkgname=('python-xapian' 'python2-xapian')
_realname=xapian-bindings
pkgver=1.4.5
epoch=1
pkgrel=2
pkgdesc="Python bindings for Xapian"
arch=('x86_64')
url="http://xapian.org/docs/bindings/python/"
license=('GPL')
makedepends=('python-sphinx' 'python2-sphinx' "xapian-core=$epoch:$pkgver")
source=("http://oligarchy.co.uk/xapian/${pkgver}/${_realname}-${pkgver}.tar.xz")
sha512sums=('e39ece495e25077990db3d06554306567c52c6f45ad55a65005cb97e18086ee18947e41d0d612157b891b61e015881385bcb082a03aa0c6452565e90d09e8275')

prepare() {
  mv -v "${_realname}-$pkgver" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-$pkgver" "${pkgname[1]}-${pkgver}"
  cd "${pkgname[1]}-${pkgver}"
  # fix shebang of examples
  sed -i '1s/python/python2/' python/docs/examples/*.py
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  ./configure XAPIAN_CONFIG=/usr/bin/xapian-config \
    --prefix=/usr --with-python3
  make
  cd ../"${pkgname[1]}-${pkgver}"
  ./configure XAPIAN_CONFIG=/usr/bin/xapian-config \
    --prefix=/usr --with-python \
    PYTHON=/usr/bin/python2
  make
}

package_python-xapian() {
  depends=('python' "xapian-core=$epoch:$pkgver")
  cd "${pkgname[0]}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

package_python2-xapian() {
  depends=('python2' "xapian-core=$epoch:$pkgver")
  cd "${pkgname[1]}-${pkgver}"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
