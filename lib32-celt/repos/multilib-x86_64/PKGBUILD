# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: see .contrib

_realname=celt
pkgname=lib32-$_realname
pkgver=0.11.3
pkgrel=3
pkgdesc='Low-latency audio communication codec (32 bit)'
arch=('x86_64')
url='http://www.celt-codec.org'
license=('BSD')
depends=('lib32-glibc' "$_realname=$pkgver")
makedepends=('gcc-multilib')
source=("http://downloads.xiph.org/releases/$_realname/$_realname-$pkgver.tar.gz")
sha512sums=('1f7b8c9b0fcb3a31ba81c220f89fb15173f6e7a25f8fa9d6669790cf29cccb5f75fb37a9f41c76bec5e8d8b9de47d65949831d5a843dcffb2f0747801a8c9bde')

build() {
  cd "$srcdir/$_realname-$pkgver"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --libdir=/usr/lib32 --disable-static
  make -C libcelt
}

package() {
  cd "$srcdir/$_realname-$pkgver"

  make DESTDIR="$pkgdir" install-pkgconfigDATA
  make -C libcelt DESTDIR="$pkgdir" install

  # remove unneeded stuff
  rm -rf "$pkgdir/usr/include"
}

# vim:set ts=2 sw=2 et:
