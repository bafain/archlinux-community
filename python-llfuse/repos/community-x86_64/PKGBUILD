# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Brendan MacDonell <macdonellba at gmail dot com>

pkgbase=python-llfuse
pkgname=(python-llfuse python2-llfuse)
pkgver=1.3.4
pkgrel=3
pkgdesc="A set of Python bindings for the low level FUSE API."
arch=('x86_64')
url="https://bitbucket.org/nikratio/python-llfuse/"
license=('LGPL')
depends=('fuse2')
makedepends=('python-setuptools' 'python2-setuptools' 'python2-contextlib2' 'cython')
checkdepends=('python-pytest' 'python2-pytest')
options=(!emptydirs)
source=("https://bitbucket.org/nikratio/python-llfuse/downloads/llfuse-${pkgver}.tar.bz2"
        "https://github.com/python-llfuse/python-llfuse/commit/dfb7d2ba3db8131cd348800c8d91df11bf930324.patch"
        "llfuse-xattr-2.4.48.patch::https://github.com/python-llfuse/python-llfuse/pull/3.patch")
md5sums=('43a123c46d5438f15cd6bcafa16a0094'
         '398b6c1b73427454d8839788426de7a9'
         'aba45413f7426ae8e93b654ebbfb9dc0')

prepare() {
  cd "${srcdir}/llfuse-${pkgver}"

  patch -p1 -i ../llfuse-xattr-2.4.48.patch
  # don't use -Werror
  patch -p1 -i ../dfb7d2ba3db8131cd348800c8d91df11bf930324.patch

  python setup.py build_cython
}

build() {
  cd "${srcdir}/llfuse-${pkgver}"

  python setup.py build
  python2 setup.py build
}

check() {
  cd "${srcdir}/llfuse-${pkgver}"

  set -x
  pythonpath=("$PWD/build/lib.linux-$CARCH"-3* "$PWD/build/lib.linux-$CARCH"-2*)
  PYTHONPATH="${pythonpath[0]}" python -m pytest
  PYTHONPATH="${pythonpath[1]}" python2 -m pytest
  set +x
}

package_python-llfuse() {
  depends+=(python)

  cd "${srcdir}/llfuse-${pkgver}"
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-llfuse() {
  depends+=(python2-contextlib2)

  cd "${srcdir}/llfuse-${pkgver}"
  python2 setup.py install --root="$pkgdir" --optimize=1
}
