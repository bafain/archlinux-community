# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgbase=python-qtconsole
pkgname=(python-qtconsole python2-qtconsole)
pkgver=4.4.1
pkgrel=1
pkgdesc="Qt-based console for Jupyter with support for rich media output"
arch=('any')
url="https://pypi.org/project/qtconsole/"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-ipykernel' 'python2-ipykernel'
             'python-jupyter_core' 'python2-jupyter_core' 'python-jupyter_client'
             'python2-jupyter_client' 'python-traitlets' 'python2-traitlets'
             'python-pygments' 'python2-pygments' 'python-pyzmq' 'python2-pyzmq'
             'python-pyqt5' 'python2-pyqt5' 'python-sip' 'python2-sip' 'qt5-svg')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tgz::https://github.com/jupyter/qtconsole/archive/$pkgver.tar.gz")
md5sums=('5044c3bcb626a7a35f47ac96654f2ad0')

prepare() {
  cd "$srcdir"
  cp -r qtconsole-$pkgver python2-qtconsole-$pkgver
}

build() {
  cd "$srcdir/qtconsole-$pkgver"
  python setup.py build

  cd "$srcdir/python2-qtconsole-$pkgver"
  python2 setup.py build
}

check() {
  cd "$srcdir/qtconsole-$pkgver"
  nosetests qtconsole

  cd "$srcdir/python2-qtconsole-$pkgver"
  nosetests2 qtconsole
}

package_python-qtconsole() {
depends=('python-ipykernel' 'python-jupyter_core' 'python-jupyter_client'
         'python-traitlets' 'python-pygments' 'python-pyzmq'
         'qt5-svg' 'python-pyqt5' 'python-sip')
  cd "$srcdir/qtconsole-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd examples
  # FS#47046 fix .desktop icon
  # FS#57556 fix .desktop icon again
  sed -i 's|^Icon=.*$|Icon=/usr/lib/python3.7/site-packages/qtconsole/resources/icon/JupyterConsole|' \
    jupyter-qtconsole.desktop
  install -Dm644 jupyter-qtconsole.desktop \
    "$pkgdir/usr/share/applications/jupyter-qtconsole.desktop"

}

package_python2-qtconsole() {
  depends=('python2-ipykernel' 'python2-jupyter_core' 'python2-jupyter_client'
           'python2-traitlets' 'python2-pygments' 'python2-pyzmq'
           'qt5-svg' 'python2-pyqt5' 'python2-sip')

  cd "$srcdir/python2-qtconsole-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "$pkgdir/usr/bin"
  mv jupyter-qtconsole jupyter-qtconsole-py2
}
