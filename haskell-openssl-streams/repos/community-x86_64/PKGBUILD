# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=openssl-streams
pkgname=haskell-openssl-streams
pkgver=1.2.1.3
pkgrel=24
pkgdesc="OpenSSL network support for io-streams"
url="https://github.com/snapframework/openssl-streams"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-hsopenssl' 'haskell-io-streams' 'haskell-network')
makedepends=('ghc' 'haskell-hunit' 'haskell-test-framework' 'haskell-test-framework-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('cdfa1d6946b1ed8750de55ecd8acfa1e65bcaa2b7531fefaf1fba4a9fc8e59f37261e2f8baee3ba04c4622fcc9af0d2b04986f5680e03729ba966946629c1ab2')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/<.*2.7/<3/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
