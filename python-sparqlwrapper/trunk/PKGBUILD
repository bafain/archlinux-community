# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-sparqlwrapper
pkgname=("python-sparqlwrapper" "python2-sparqlwrapper")
pkgver=1.8.1
pkgrel=1
pkgdesc="SPARQL Endpoint interface to Python"
arch=('any')
url="http://rdflib.github.io/sparqlwrapper/"
license=('W3C')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/RDFLib/sparqlwrapper/archive/$pkgver.tar.gz")
sha512sums=('d9d14d7e5317a587ae4f5af596a39dec9d59781d438571e776b3a395dd3b0160483e0cf94311ea93b043b23805052c5fdea3302fbad93d9591828e9c2fa40f1f')

prepare() {
  cp -a sparqlwrapper-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/sparqlwrapper-$pkgver
  python setup.py build

  cd "$srcdir"/sparqlwrapper-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/sparqlwrapper-$pkgver
  ./run_tests_py3.sh

  cd "$srcdir"/sparqlwrapper-$pkgver-py2
  nosetests2
}

package_python-sparqlwrapper() {
  depends=('python')

  cd sparqlwrapper-$pkgver
  LC_CTYPE=en_US.UTF-8 python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-sparqlwrapper() {
  depends=('python2')

  cd sparqlwrapper-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
