# $Id: PKGBUILD 175443 2016-05-16 05:54:00Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-ddt
pkgname=('python-ddt' 'python2-ddt')
pkgver=1.1.2
pkgrel=1
pkgdesc="Data-Driven/Decorated Tests"
arch=('any')
license=('MIT')
url="https://github.com/txels/ddt"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose' 'python-mock' 'python2-mock' 'python-yaml' 'python2-yaml')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/txels/ddt/archive/$pkgver.tar.gz")
sha512sums=('1965e877ff907d3fbabae7ccb51f45d63fdd82f4f9811982000f97134afb89a7291ac6b5b1d9c16ea4a1b736e06e1b7f948e1b4a265d4e06325773244f24f83f')

prepare() {
  cp -a ddt-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/ddt-$pkgver
  python setup.py build

  cd "$srcdir"/ddt-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/ddt-$pkgver
  nosetests3

  cd "$srcdir"/ddt-$pkgver-py2
  nosetests2
}

package_python-ddt() {
  depends=('python')

  cd ddt-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}

package_python2-ddt() {
  depends=('python2')

  cd ddt-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}
