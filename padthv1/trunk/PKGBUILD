# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
pkgname=padthv1
pkgver=0.9.2
pkgrel=1
pkgdesc="An old-school polyphonic additive synthesizer"
arch=('x86_64')
url="https://padthv1.sourceforge.io/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio' 'vee-one')
depends=('desktop-file-utils' 'fftw' 'hicolor-icon-theme' 'jack' 'liblo'
'qt5-base')
makedepends=('lv2' 'qt5-tools')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('7d4828e1a6f00b88d8ea05f37d87067294bdc4350182f2f4078ce65b3a1113e5595a678a259d0d16c3b4f90175878619b571f78682754a0840e903789bf25fee')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO}
}
# vim:set ts=2 sw=2 et:
