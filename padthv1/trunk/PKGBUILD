# Maintainer: David Runge <dave@sleepmap.de>
pkgname=padthv1
pkgver=0.9.0
pkgrel=1
pkgdesc="An old-school polyphonic additive synthesizer"
arch=('x86_64')
url="https://padthv1.sourceforge.io/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio' 'vee-one')
depends=('jack' 'liblo' 'lv2' 'qt5-base' 'desktop-file-utils'
'hicolor-icon-theme' 'shared-mime-info' 'fftw')
makedepends=('qt5-tools')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('cab770d8417ee3a163af302a183390b4aa513da62964500125ed645694c3d8e03d99ceaf65ab439c7922d208d6a73d41be3a2fced0d2874fcc2a00755b1beb2f')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {AUTHORS,ChangeLog,README,TODO}
}
# vim:set ts=2 sw=2 et:
