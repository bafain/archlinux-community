# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: yugrotavele <yugrotavele at archlinux dot us>
# Contributor: Damir Perisa <damir@archlinux.org>

pkgname=glabels
pkgver=3.4.1
pkgrel=1
pkgdesc="Creating labels and business cards the very easy way"
arch=('x86_64')
url="http://glabels.org/"
license=('GPL' 'LGPL')
depends=('evolution-data-server' 'iec16022' 'qrencode' 'zint')
makedepends=('intltool' 'itstool' 'python')
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('18e457298abb1c617187361109eeae769317686303fc07726af31e11519c5938')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
