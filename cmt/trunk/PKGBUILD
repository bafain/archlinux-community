# $Id$
#Maintainer: David Runge <dave@sleepmap.de>
#Contributor: Damir Perisa <damir.perisa@bluewin.ch>
#Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=cmt
pkgver=1.16
pkgrel=8
pkgdesc="LADSPA plugins for use with software synthesis and recording packages on Linux"
arch=('x86_64')
license=('GPL2')
url="http://www.ladspa.org/cmt/overview.html"
depends=('gcc-libs')
makedepends=('ladspa')
groups=('ladspa-plugins' 'pro-audio')
source=("http://www.ladspa.org/download/${pkgname}_src_${pkgver}.tgz")
sha512sums=('be4aaa064183b6121b4ca8d7d2535cb9a3bcb557e3a2f22b45ec9775b0e296f70a9a7edf697b78095d6ef1564bb5459bb742649082ac724d1774c0790379af21')

prepare(){
  mv -v ${pkgname} ${pkgname}-${pkgver}
  cd ${pkgname}-${pkgver}
  # fix CFLAGS
  sed -e "/^CFLAGS/ s/-O3/${CFLAGS}/" \
      -e 's|-Werror||g' \
      -i src/makefile
}

build() {
  cd ${pkgname}-${pkgver}/src
  make
}

package() {
  cd ${pkgname}-${pkgver}
  install -vDm 755 plugins/cmt.so -t "${pkgdir}/usr/lib/ladspa/"
  # docs
  install -vDm 644 {README,doc/*.html} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
