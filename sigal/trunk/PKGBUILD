# Maintainer: David Runge <dave@sleepmap.de>
pkgname=sigal
pkgver=1.4.0
pkgrel=4
pkgdesc="Yet another simple static gallery generator"
arch=('any')
url="http://sigal.saimon.org/en/latest/"
license=('MIT')
depends=('ffmpeg' 'python-blinker' 'python-click' 'python-jinja'
'python-markdown' 'python-pillow' 'python-pilkit' 'python-setuptools')
checkdepends=('python-pytest-cov' 'python-boto' 'python-brotli' 'python-feedgenerator' 'python-zopfli')
optdepends=('python-boto: upload to S3 plugin'
            'python-brotli: compress assets plugin'
            'python-feedgenerator: feed plugin'
            'python-zopfli: compress assets plugin')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/saimn/${pkgname}/archive/${pkgver}.tar.gz")
# tests are not included in pypi source: https://github.com/saimn/sigal/issues/337
#source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('b5352992fc2d9b397c8c60a614c57ca78749cd729a431c16c212b8ef48527c63612bb7f4bd12cd525be46bf5ad3b7534b8e0b332d56e941b2eed7dff5da5b908')

prepare() {
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH=build:${PYTHONPATH}
  # test_album fails because of missing fr_FR.UTF-8 locale:
  # https://github.com/saimn/sigal/issues/338
  py.test -k "not test_album"
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {AUTHORS,{CONTRIBUTING,README}.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
