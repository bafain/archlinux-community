# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=18.08
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
makedepends=("eric-common>=${pkgver}")
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'ru             "Russian"')

pkgname=()
source=()
_url="https://downloads.sourceforge.net/eric-ide"

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}

  pkgname+=(eric-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_locale}-${pkgver}.tar.gz")

  eval "package_eric-i18n-${_locale,,}() {
    _package ${_locale}
  }"
done

_package() {
  pkgdesc="${1} language pack for Eric IDE"
  depends=("eric-common>=${pkgver}")

  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/qt/translations/eric6_${1}.qm"
}

sha512sums=('d352fb9be9a882e68d1eb37564ba2d406448d3096b2cb28d7c3ff8b6c2524440161e863dd80a95906456c7747537f63664fb2dbdea044f6aea5513d5213713c3'
            'be3ea54c91ff203f5ae42775cfa9ab79c86f28675f5983709bf52de98122d7eb52eea202f33d67dc32320846b9704e815f0e41e40668e6e99ddc509f7fa473ba'
            'e65afaa52401d96624fd6b55c4fbb2f942ee4294e2d17ccd645b6217d816cd476fb1f6d1087bf85650d10399f8c87398bcd8ba7115525b0228f431ec6a79896b'
            '13dd8c0a5ceadef1bd09e6c0b4439ac97dd917c440649dc540254b7898e019611e4e52068a4addefe60f8852d1abab4bb6efc7545a71873e6018b752d8756b7b')
