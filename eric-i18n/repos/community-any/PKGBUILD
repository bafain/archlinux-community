# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=18.02
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
makedepends=("eric-common>=${pkgver}")
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'ru             "Russian"')

pkgname=()
source=()
_url="https://downloads.sourceforge.net/eric-ide"

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}

  pkgname+=(eric-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_locale}-${pkgver}.tar.gz")

  eval "package_eric-i18n-${_locale,,}() {
    _package ${_locale}
  }"
done

_package() {
  pkgdesc="${1} language pack for Eric IDE"
  depends=("eric-common>=${pkgver}")

  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/qt/translations/eric6_${1}.qm"
}

sha512sums=('079c2ecd8f63e9d4028e5d032bcd8c8be7219e715484e4d15c719ac7a99d572586cf435f0f00f94d1d68a063498b4152c773ce4b14cacfcac404de9ae38aa60d'
            '2e80a003d8ce21ef3acbd9f57fd3a7f9ea9ff6174efd3542fcf9949efa0fd58bccee9a24c8617c5929e44134a509f7152cd0371caedc6d2611edadbec751c15a'
            '3e803148cb36d0a435a8b85f01cefbd1c9b9eabc55f34bd9ac81ea264c6a2cf403d59304567117755cea0ef899381b1e433839b8afe78bf1c1d7147804e13665'
            '41cc7756ad5cb15dac0413f7fba997ace63267ae7bb6fe008bfb5e179ad7f29316272d97d917410c8bced66158607e765b45664ba5ce4261c54a2ba9a75daf69')
