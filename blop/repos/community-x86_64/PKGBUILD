# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=blop
pkgver=0.2.8
pkgrel=3
pkgdesc="Bandlimited LADSPA Oscillator Plugins"
arch=('x86_64')
url="http://blop.sourceforge.net/"
license=('GPL')
depends=('glibc')
makedepends=('ladspa')
groups=('ladspa-plugins' 'pro-audio')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('5a1bb2e4ba976dd87f8b34538b3a8d2ed21ba0b8818d4b0a713061aeeae7fd52377477314a758cc7082a932315be8ec99006dfc3f5c9712d8d566852adbf671f')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package(){
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README,THANKS,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
