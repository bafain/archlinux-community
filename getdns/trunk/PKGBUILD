# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Remi Gacogne <rgacogne@archlinux.org>

pkgname=getdns
pkgver=1.4.2
pkgrel=1
pkgdesc="A modern asynchronous DNS API"
arch=('x86_64')
url="https://getdnsapi.net"
license=('BSD')
depends=('libbsd' 'libev' 'libevent' 'libidn2' 'libuv' 'libyaml' 'unbound')
source=("${url}/dist/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha256sums=('1685b82dfe297cffc4bae08a773cdc88a3edf9a4e5a1ea27d8764bb5affc0e80' 'SKIP')
validpgpkeys=('DC34EE5DB2417BCC151E5100E5F8F8212F77A498') # Willem Toorop <willem@nlnetlabs.nl>

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --disable-rpath \
        --enable-static=no \
        --with-libev \
        --with-libevent \
        --with-libuv \
        --with-trust-anchor=/etc/trusted-key.key
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install

    install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
