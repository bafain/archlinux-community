# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>

pkgname='matrix-appservice-irc'
pkgver=0.11.0
pkgrel=1
pkgdesc="Node.js IRC bridge for Matrix"
arch=('any')
url="https://github.com/matrix-org/matrix-appservice-irc"
license=('Apache')
depends=('nodejs')
makedepends=('npm' 'git')
optdepends=('python: for maintenance scripts')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/${pkgver}.tar.gz")
sha256sums=('1a00eb9db98f1e6a7e401365b2db8687544974311d78e321a5640f7ba36bb225')

package() {
  cd ${pkgbase}-${pkgver}
  npm install --cache "${srcdir}"/npm-cache -g --user root --prefix "${pkgdir}"/usr

  # Apparently it now symlinks to the source folderâ€¦
  rm -f "${pkgdir}"/usr/lib/node_modules/matrix-appservice-irc
  mkdir -p "${pkgdir}"/usr/lib/node_modules/matrix-appservice-irc
  cp -r * "${pkgdir}"/usr/lib/node_modules/matrix-appservice-irc/
  chmod -R go-w "${pkgdir}"/usr/lib/node_modules/
  chmod go-w "${pkgdir}"/usr/bin
}
