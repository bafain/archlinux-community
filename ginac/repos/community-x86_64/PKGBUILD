# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=ginac
pkgver=1.7.3
pkgrel=1
pkgdesc="C++ library for symbolic calculations"
arch=('x86_64')
url='https://www.ginac.de/'
license=('GPL')
depends=('cln')
makedepends=('transfig' 'doxygen' 'texlive-bin' 'python')
source=(https://www.ginac.de/$pkgname-$pkgver.tar.bz2)
sha256sums=('7f6efd7fd1c1a72af1f08b068077be66d0216700ec6f529460899e14c1bbf75e')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr --libexecdir=/usr/lib/ginac --disable-static
  make
}

check() {
  cd "${srcdir}"/$pkgname-$pkgver

  make check
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
