# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=kmplayer
pkgver=0.12.0.b
_pkgver=0.12.0b
pkgrel=3
pkgdesc="Video player plugin for Konqueror and basic MPlayer/Xine/ffmpeg/ffserver/VDR frontend for KDE"
arch=(x86_64)
license=(GPL LGPL FDL)
url="http://kmplayer.kde.org"
depends=(kmediaplayer kdelibs4support phonon-qt5 hicolor-icon-theme)
makedepends=(extra-cmake-modules kdesignerplugin kdoctools kinit)
optdepends=('mplayer: mplayer backend')
source=("http://download.kde.org/stable/$pkgname/${_pkgver%.*}/$pkgname-$_pkgver.tar.bz2"
        kmplayer-desktop.patch::"https://cgit.kde.org/kmplayer.git/patch/?id=2126d76b"
        kmplayer-qt5.9.patch::"https://cgit.kde.org/kmplayer.git/patch/?id=3def6507"
        kmplayer-qt5.11.patch::"https://cgit.kde.org/kmplayer.git/patch/?id=7dafda63")
sha256sums=('5e98cea9bd958860d90003d37037172a95ed8b699133e8f1e6033147aaefed73'
            '81eab18e854415de6bd916e4c516e05c34abacf2981378306f20c6b0302f0667'
            'de9450abf302d7e4ace23782986e9bec924a033775f77f7f38efd1e73208639b'
            'cf282ce5c09376feaf32e7aa5fffdb176a85b35816caf0faaf27d18f6bd0250a')

prepare() {
  mkdir -p build

  sed -e 's/org.kde.//g' -i kmplayer-desktop.patch
  cd $pkgname-$_pkgver
  patch -p1 -i ../kmplayer-desktop.patch # Fix launching from desktop file
  patch -p1 -i ../kmplayer-qt5.9.patch # Fix build with Qt 5.9
  patch -p1 -i ../kmplayer-qt5.11.patch # Fix build with Qt 5.11
}

build() {
  cd build
  cmake ../$pkgname-$_pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
