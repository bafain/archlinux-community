# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=strictures
pkgname=perl-${_pkg}
pkgver=2.000005
pkgrel=2
pkgdesc="Turn on strict and make most warnings fatal"
arch=('any')
url="https://metacpan.org/release/${_pkg}"
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
checkdepends=('perl-test-simple')
source=("https://cpan.metacpan.org/authors/id/E/ET/ETHER/${_pkg}-${pkgver}.tar.gz")
sha512sums=('e4309c01fd729fe8c42e4a0ddbc95e8e14503362fb5be4556fef42edbbe14567a13cf2e9af206e91ff911b961316dfbff3711f278afaa4772bf284bb2d44a309')

build() {
    cd ${_pkg}-${pkgver}
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
    perl Makefile.PL
    make
}

check() {
    cd ${_pkg}-${pkgver}
    make test
}

package() {
    cd ${_pkg}-${pkgver}
    make INSTALLDIRS=vendor DESTDIR="$pkgdir" install
}
