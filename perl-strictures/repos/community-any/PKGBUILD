# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=strictures
pkgname=perl-${_pkg}
pkgver=2.000003
pkgrel=2
pkgdesc="Turn on strict and make most warnings fatal"
arch=('any')
url="https://metacpan.org/release/${_pkg}"
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
checkdepends=('perl-test-simple')
source=("https://cpan.metacpan.org/authors/id/H/HA/HAARG/${_pkg}-${pkgver}.tar.gz")
sha512sums=('c187a32f70c2be97cbe783e700d54d1e7b7ce7a8ea0b5ce72851b1efbe9b64121fb4b626463c4c62fb384893e4e3102afeaa21cd3e500fefc61c5b4b3924d21b')

build() {
    cd ${_pkg}-${pkgver}
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
    perl Makefile.PL
    make
}

check() {
    cd ${_pkg}-${pkgver}
    make test
}

package() {
    cd ${_pkg}-${pkgver}
    make INSTALLDIRS=vendor DESTDIR="$pkgdir" install
}
