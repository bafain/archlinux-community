# $Id$
# Maintianer:
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

_pypiname=pyopencl
pkgbase=python-pyopencl
pkgname=('python2-pyopencl' 'python-pyopencl' 'pyopencl-headers')
pkgver=2018.1.1
pkgrel=3
epoch=1
pkgdesc="A complete, object-oriented language binding of OpenCL to Python"
arch=('x86_64')
url="https://mathema.tician.de/software/pyopencl"
license=('MIT' 'Apache' 'BSD')
depends=('opencl-icd-loader' 'opencl-headers' 'mesa' 'boost-libs')
makedepends=('ctags' 'boost' 'python-setuptools' 'python2-setuptools' 'python-mako' 'python2-mako'
             'python-numpy' 'python2-numpy' 'python-cffi' 'python2-cffi')
checkdepends=('python-six' 'python2-six' 'python-appdirs' 'python2-appdirs')
source=("https://pypi.io/packages/source/p/$_pypiname/$_pypiname-${pkgver}.tar.gz")
sha1sums=('40d036172fd4367a0243356a617c5f91bee1f3cc')

build() {
   cp -a pyopencl-$pkgver{,-python2}

   cd pyopencl-${pkgver}
   python3 ./configure.py --cl-enable-gl --python-exe=python3 --cl-pretend-version=1.2 # --boost-python-libname=boost_python3
   make

   cd "$srcdir/pyopencl-$pkgver-python2"
   python2 ./configure.py --cl-enable-gl --python-exe=python2 --cl-pretend-version=1.2 # --boost-python-libname=boost_python
   make
}

#check(){
#   cd pyopencl-${pkgver}
#   python3 setup.py test
#
#   cd "$srcdir/pyopencl-$pkgver-python2"
#   python2 setup.py test
#}

package_python-pyopencl() {
   depends+=('python' 'python-numpy' 'python-mako' 'python-pytools' 'pyopencl-headers' 'python-setuptools' 'python-cffi')

   cd pyopencl-${pkgver}
   python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

   rm -fr "${pkgdir}"/usr/include

   install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-pyopencl() {
   depends+=('python2' 'python2-numpy' 'python2-mako' 'python2-pytools' 'pyopencl-headers' 'python2-setuptools' 'python2-cffi')

   cd pyopencl-${pkgver}-python2
   python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

    rm -fr "${pkgdir}"/usr/include/

   install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_pyopencl-headers() {
   cd pyopencl-${pkgver}
   install -dm755 "${pkgdir}"/usr/include/pyopencl

  for file in pyopencl-airy.cl  pyopencl-bessel-j.cl  pyopencl-bessel-y.cl  pyopencl-complex.h  pyopencl-eval-tbl.cl  pyopencl-ranluxcl.cl; do
      install -m644 pyopencl/cl/${file} "${pkgdir}"/usr/include/pyopencl
   done
   install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
