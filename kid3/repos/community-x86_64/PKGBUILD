# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Alois Nespor <alois.nespor@gmail.com>

pkgbase=kid3
pkgname=('kid3' 'kid3-qt')
pkgver=3.6.2
pkgrel=1
arch=('x86_64')
url="http://kid3.sourceforge.net/"
license=('GPL')
makedepends=('qt5-tools' 'qt5-multimedia' 'qt5-declarative' 'chromaprint'
            'id3lib' 'taglib' 'libmp4v2' 'kio' 'hicolor-icon-theme' 'docbook-xml'
            'kdoctools' 'extra-cmake-modules' 'python')
changelog=$pkgbase.changelog
source=(https://downloads.sourceforge.net/$pkgbase/$pkgbase-$pkgver.tar.gz{,.asc})
validpgpkeys=('7D09794C2812F62194B081C14CAD34426E354DD2') # Urs Fleisch
sha256sums=('df03d98a2bfd08b7baea6c1006b89bac8ce4c07e8d91d6194a2f05895d1ad8a7'
            'SKIP')

prepare() {
  cd "${srcdir}"

  [[ ! -d build ]] && mkdir build
  mkdir build-qt
}

build() {
#kid3
  cd "${srcdir}"/build
  cmake \
    ../${pkgbase}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_QT5=ON \
    -DWITH_APPS="CLI;KDE" \
    -DWITH_MP4V2=ON \
    -DWITH_FFMPEG=ON \
    -DWITH_GSTREAMER=ON

#kid3-qt
  cd "$srcdir"/build-qt
  cmake \
    ../${pkgbase}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_QT5=ON \
    -DWITH_APPS="CLI;Qt" \
    -DWITH_MP4V2=ON \
    -DWITH_FFMPEG=ON \
    -DWITH_GSTREAMER=ON
}

package_kid3() {
pkgdesc="An MP3, Ogg/Vorbis and FLAC tag editor, KDE version"
depends=('qt5-multimedia' 'phonon-qt5' 'chromaprint' 'id3lib' 'taglib'
         'libmp4v2' 'kio' 'desktop-file-utils' 'hicolor-icon-theme')
optdepends=('qt5-declarative: QmlCommand extension')
conflicts=('kid3-qt')

  cd "$srcdir"/build
  make DESTDIR="${pkgdir}" install
}

package_kid3-qt() {
pkgdesc="An MP3, Ogg/Vorbis and FLAC tag editor, Qt version"
depends=('qt5-multimedia' 'chromaprint' 'id3lib' 'taglib' 'libmp4v2'
         'desktop-file-utils' 'hicolor-icon-theme' 'qt5-declarative')
conflicts=('kid3')
provides=('kid3')

  cd "$srcdir"/build-qt
  make DESTDIR="${pkgdir}" install
}
