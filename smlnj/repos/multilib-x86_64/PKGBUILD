# $Id$
# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Thomas Weißschuh <thomas@t-8ch.de>

pkgname=smlnj
pkgver=110.84
pkgrel=1
pkgdesc='Standard ML compiler from New Jersey'
url='https://www.smlnj.org/'
license=('BSD')
arch=('x86_64')
provides=('sml')
install=smlnj.install
depends=('lib32-glibc')
makedepends=('gcc-multilib')
_url="http://smlnj.cs.uchicago.edu/dist/working/$pkgver/"
source=(
  "$pkgname-$pkgver-boot.x86-unix.tgz::${_url}boot.x86-unix.tgz"
  "$pkgname-$pkgver-config.tgz::${_url}config.tgz"
  "$pkgname-$pkgver-cm.tgz::${_url}cm.tgz"
  "$pkgname-$pkgver-compiler.tgz::${_url}compiler.tgz"
  "$pkgname-$pkgver-runtime.tgz::${_url}runtime.tgz"
  "$pkgname-$pkgver-system.tgz::${_url}system.tgz"
  "$pkgname-$pkgver-MLRISC.tgz::${_url}MLRISC.tgz"
  "$pkgname-$pkgver-smlnj-lib.tgz::${_url}smlnj-lib.tgz"
  "$pkgname-$pkgver-ckit.tgz::${_url}ckit.tgz"
  "$pkgname-$pkgver-nlffi.tgz::${_url}nlffi.tgz"
  "$pkgname-$pkgver-cml.tgz::${_url}cml.tgz"
  "$pkgname-$pkgver-ml-lpt.tgz::${_url}ml-lpt.tgz"
  "$pkgname-$pkgver-ml-lex.tgz::${_url}ml-lex.tgz"
  "$pkgname-$pkgver-ml-yacc.tgz::${_url}ml-yacc.tgz"
  "$pkgname-$pkgver-ml-burg.tgz::${_url}ml-burg.tgz"
  "$pkgname-$pkgver-trace-debug-profile.tgz::${_url}trace-debug-profile.tgz"
  'urlgetter.sh'
  'profile.d-smlnj.sh'
  'smlnj.sh'
)
sha256sums=('a29e3b0ca623da197e9c18ea64db4c2a08fe5e6add85c0d053c4ecfa653ace82'
            '1810d3ca768222e120c7a3f2f93aafd652705371fc73929423c671dce3cef832'
            '7055c60481a887f27a5135ac1f33233565b99a7c99c4aa73e023e28e629b0663'
            '6d23aff2e75727bf6969b05a070128f187e06f60408aabf6a71e0475326fee87'
            '5fcf4bbdeaddb247f69a333d2295f5f5c0be55b1f86ff4fbffb63f0021d84d6c'
            'f46b1de790000a98f4aa9830325d39cb1e4867338d4db6b8a102084db1512218'
            'da3c5236018fa7608863c2fe1f3c57b60013df98b5a249fb3d4edc738cdfd072'
            '3356924655277416d8a491c1d601a0f567ab3d15cb17e243620e85cb28f0e9dd'
            '7e2534e393d2372cad8ff9ddd306881db91bbbcc2b0e0538e76868c86c658c36'
            '8b96a82a3d63b000b70a1b982aab9750ce9caa77f316f6a923969e86b0133f31'
            '556110c746db863211ac8a97e1bf097b0e5d45dfea4b3e8194b6c389ac2b569a'
            '2f60e1467a69fd4fb5501354244337926bc2a65225dde328f6aa3883b14fb3a5'
            'cc7e045aa9667f1eed055db1defca0e830eed34c08f3dfdf37f698964fe04070'
            'cfebaeb5f08810fcc93c49bf171834b7bf14b5f7e76d206e0afa35e18887bdb9'
            '62ea37b13788078e3ea77d3b870da42788153c348009f827f2c0b05cfc07d918'
            'ecc54281b8654c75f8b9d4ebdf1ff67de71ef31297cf1b14115cd75645e46a5a'
            '4da78effe7d3644c28c731c8e4003a9cecec9f8f61d2fa4553981a729f2d200b'
            'dd20a81a5d2899f60183215ab6a412d522d2c6801d454c142225716899e089f3'
            '3012a08e113b0cf6feff06689c1ae2ad48524e200d30ac46676f3c9e53665218')

prepare() {
  mkdir -p install
}

build() {
  unset SMLNJ_HOME MAKEFLAGS
  export srcdir
  export pkgver
  export INSTALLDIR="$srcdir/install"
  URLGETTER="$srcdir/urlgetter.sh"
  config/install.sh
}

package() {
  install -d "$pkgdir/usr/"{lib/smlnj,share}
  install -Dm755 "$srcdir/profile.d-smlnj.sh" "$pkgdir/etc/profile.d/smlnj.sh"
  install -Dm644 compiler/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cp -R "$srcdir/install/"{bin,lib} "$pkgdir/usr/lib/smlnj"
  cp -R "$srcdir/doc/man" "$pkgdir/usr/share/"
  find "$pkgdir/usr/share/man" -name '._*' -delete
  install -Dm755 smlnj.sh "$pkgdir/usr/bin/smlnj"
}

# getver: smlnj.org
# vim: ts=2 sw=2 et:
