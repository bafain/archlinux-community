# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Florian Loitsch <archlinux@florian.loitsch.com>

pkgname=miniupnpc
pkgver=2.0.20180222
pkgrel=1
pkgdesc='Small UPnP client library/tool to access Internet Gateway Devices'
url='https://miniupnp.tuxfamily.org/'
arch=('x86_64')
license=('BSD')
depends=('sh')
source=(https://miniupnp.tuxfamily.org/files/${pkgname}-${pkgver}.tar.gz)
sha256sums=('587944686469d09f739744b3aed70c7ce753a79c40d6f1227f68a3e962665b75')
sha512sums=('0cceeedff6c584d2d7037ff91fbba0fe580fabfda810b9a9ed7c8d8495445339e3ff115d63d4429bcd3eff2b13b123e1bd5855ff1bc9dcd986b968a82c368591')

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 man3/miniupnpc.3 -t "${pkgdir}/usr/share/man/man3"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
