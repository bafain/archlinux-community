# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Cravix <dr.neemous@gmail.com>
# Contributor: AlexanderR <rvacheva@nxt.ru>
# Contributor: zhn <zhangn1985@gmail.com>

pkgbase=renpy
pkgname=('renpy' 'renpy-demos')
pkgver=7.0.0
pkgrel=1
pkgdesc="The Ren'Py Visual Novel Engine"
arch=('x86_64')
url='https://www.renpy.org'
license=('MIT')
depends=('ffmpeg' 'glew' 'python2-pygame-sdl2')
makedepends=('cython2')
optdepends=('atom: Default editor')
source=("https://www.renpy.org/dl/${pkgver}/renpy-${pkgver}-source.tar.bz2"
        'renpy'
        'renpy.desktop'
        'renpy.png')
sha256sums=('4f2019e5056196d92cb0f5e569f3b20d5c011fda3810d0d63c4f3ac263b93c7b'
            'e10630ec0b5d0e479f20b15a4f5ceec8e6992dd8cbc7d3cd5c487da380858a28'
            'fccde3461617a098a78d938d9db782d403eda410a84ab52825a597498ab95834'
            '611edc07a40ccb8e04e8858847fc1d2a066d29c2ed54e5b357880a0605818dc5')

prepare() {
  cd renpy-${pkgver}-source

  sed 's/python/python2/' -i atom/Atom.edit.py launcher/game/tkaskdir.py
}

build() {
  cd renpy-${pkgver}-source

  export RENPY_CYTHON='cython2'

  python2 module/setup.py build
}

package_renpy() {
  optdepends=('renpy-demos: Tutorial and The Question demos'
              'tk: Set projects directory')
  replaces=('python-renpy' 'python2-renpy')

  cd renpy-${pkgver}-source

  python2 module/setup.py install --root="${pkgdir}" --prefix='/usr' --optimize='1' --skip-build

  install -dm 755 "${pkgdir}"/usr/share/{renpy,doc}

  cp -dr --no-preserve='ownership' atom doc gui launcher renpy renpy.py "${pkgdir}"/usr/share/renpy/
  ln -s /usr/share/renpy/doc "${pkgdir}"/usr/share/doc/renpy

  install -Dm 755 ../renpy -t "${pkgdir}"/usr/bin/
  install -Dm 644 ../renpy.desktop -t "${pkgdir}"/usr/share/applications/
  install -Dm 644 ../renpy.png -t "${pkgdir}"/usr/share/pixmaps/

  install -Dm 644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/renpy/
}

package_renpy-demos() {
  depends=('renpy')

  cd renpy-${pkgver}-source

  install -dm 755 "${pkgdir}"/usr/share/renpy

  cp -dr --no-preserve='ownership' the_question tutorial "${pkgdir}"/usr/share/renpy/

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s renpy "${pkgdir}"/usr/share/licenses/renpy-demos
}

# vim: ts=2 sw=2 et:
