# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Mirco Tischler <mt-ml at gmx dot de>

pkgname=fwupdate
pkgver=10
pkgrel=2
pkgdesc="Tools for using the ESRT and UpdateCapsule() to apply firmware updates"
arch=('x86_64')
url="https://github.com/rhinstaller/fwupdate"
license=('GPL2')
depends=('efivar' 'libsmbios' 'bash')
makedepends=('pesign' 'gnu-efi-libs')
source=("${url}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        'esp-as-boot.hook'
        'esp-as-boot-efi.hook')
sha256sums=('73f42c6097049ab4490e88bd7742ac962f892e6660ee2af40254605d011ef33a'
            '20f696fbcba5a5de644eb06b71954adfde6dcc4f619075392977a248f96a59b6'
            '3ad20522b5e3118312a31cab7ab915c2427ecaf72a83759ffb7229bccfb65259')
install=fwupdate.install

_efidir=arch

build() {
    cd ${pkgname}-${pkgver}
    make EFIDIR="${_efidir}" GNUEFIDIR=/usr/lib
}

package() {
    cd ${pkgname}-${pkgver}

    make LIBDIR=/usr/lib EFIDIR="${_efidir}" DESTDIR="${pkgdir}" libexecdir=/usr/lib/ install

    # Remove cleanup script, it requires hardcoding of ESP path
    rm -r "${pkgdir}"/usr/lib/systemd/
    rm "${pkgdir}"/usr/lib/fwupdate/cleanup

    # Do not install anything under /boot. Copy files to /usr/lib/fwupdate for manual or hook installation.
    install -d "${pkgdir}"/usr/{lib,share/doc}/${pkgname}
    mv "${pkgdir}"/boot/efi/EFI "${pkgdir}"/usr/lib/${pkgname}/EFI
    rm -rf "${pkgdir}"/boot
    rm -rf "${pkgdir}"/usr/src
    rm -rf "${pkgdir}"/usr/lib/debug
    cd ..
    install -Dm644 esp-as-boot.hook -t "${pkgdir}"/usr/share/doc/${pkgname}/
    install -Dm644 esp-as-boot-efi.hook -t "${pkgdir}"/usr/share/doc/${pkgname}/
}
