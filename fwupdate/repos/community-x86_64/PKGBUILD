# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Mirco Tischler <mt-ml at gmx dot de>

pkgname=fwupdate
pkgver=12
pkgrel=1
pkgdesc="Tools for using the ESRT and UpdateCapsule() to apply firmware updates"
arch=('x86_64')
url="https://github.com/rhinstaller/fwupdate"
license=('GPL2')
depends=('efivar' 'libsmbios' 'bash')
makedepends=('pesign' 'gnu-efi-libs')
source=("${url}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('4167d569f14e00ecf620c369ab34709e4bb1574ed588ef2e2398667e9e968703')

_efidir=arch

build() {
    cd ${pkgname}-${pkgver}
    make EFIDIR="${_efidir}" GNUEFIDIR=/usr/lib
}

package() {
    cd ${pkgname}-${pkgver}

    make LIBDIR=/usr/lib EFIDIR="${_efidir}" DESTDIR="${pkgdir}" libexecdir=/usr/lib/ install

    # Remove cleanup script, it requires hardcoding of ESP path
    rm -r "${pkgdir}"/usr/lib/systemd/
    rm "${pkgdir}"/usr/lib/fwupdate/cleanup

    # Do not install anything under /boot. Copy files to /usr/lib/fwupdate for manual or hook installation.
    install -d "${pkgdir}"/usr/{lib,share/doc}/${pkgname}
    mv "${pkgdir}"/boot/efi/EFI "${pkgdir}"/usr/lib/${pkgname}/EFI
    rm -r "${pkgdir}"/boot
    rm -r "${pkgdir}"/usr/src
    rm -r "${pkgdir}"/usr/lib/debug
}
