# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Muges <git@muges.fr>
# Contributor: Alexis "Horgix" Chotad <alexis.horgix.chotard@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Markus Golser <elmargol@googlemail.com>

_pkgbase=mygpoclient
pkgbase=python-$_pkgbase
pkgname=(python-mygpoclient python2-mygpoclient)
pkgver=1.8
pkgrel=2
pkgdesc='Python client Library for the gpodder.net web service'
arch=(any)
url='https://github.com/gpodder/mygpoclient'
license=(GPL3)
depends=(python python2)
source=($pkgbase-$pkgver.tar.gz::https://github.com/gpodder/$_pkgbase/archive/$pkgver.tar.gz)
sha256sums=('65fcb3ab220a2ba2d32a77f969b423a2f663413b6c485b7b02e5a955b28e68d2')

package_python-mygpoclient() {
  depends=(python)

  cd $_pkgbase-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}

package_python2-mygpoclient() {
  depends=(python2)

  cd $_pkgbase-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1

  for file in "$pkgdir"/usr/bin/*; do mv "$file" "${file}2"; done
  mv "$pkgdir"/usr/share/man/man1/mygpo-bpsync{,2}.1
}
