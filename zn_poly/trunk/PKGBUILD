# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=zn_poly
pkgver=0.9
pkgrel=4
pkgdesc="Polynomial arithmetic over Z/nZ"
arch=(x86_64)
url="http://web.maths.unsw.edu.au/~davidharvey/code/zn_poly/"
license=(GPL)
depends=(gmp)
makedepends=(python2)
source=("http://web.maths.unsw.edu.au/~davidharvey/code/zn_poly/releases/$pkgname-$pkgver.tar.gz")
sha256sums=('a1a085897c68889720ad27e98d2b54753397bd5a85433126327bc4b35511b4cf')

build() {
  cd $pkgname-$pkgver

  python2 makemakefile.py --cflags="$CFLAGS -fPIC" --prefix=/usr --ntl-prefix=/usr --gmp-prefix=/usr --flint-prefix=/usr > makefile
  make

# build shared library
  make libzn_poly.so
}

package() {
  cd $pkgname-$pkgver

  install -Dm644 libzn_poly.so -t "$pkgdir"/usr/lib
  install -Dm644 include/*.h -t "$pkgdir"/usr/include/zn_poly
}
