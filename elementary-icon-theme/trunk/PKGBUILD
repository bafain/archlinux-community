# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Diego Principe <cdprincipe@gmail.com>
# Contributor: paterbrown <paterbrown@silberhelme.de>
# Contributor: Josip <josipponjavic@gmail.com>

pkgname=elementary-icon-theme
pkgver=4.3.1
pkgrel=2
pkgdesc='Named, vector icons for elementary OS'
arch=('any')
url='https://github.com/elementary/icons'
license=('GPL3')
groups=('pantheon')
depends=('hicolor-icon-theme')
makedepends=('cmake' 'git')
source=("elementary-icon-theme::git+https://github.com/elementary/icons.git#tag=${pkgver}")
sha256sums=('SKIP')

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  cmake ../elementary-icon-theme \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  make DESTDIR="${pkgdir}" -C build install
  rm "${pkgdir}"/.VolumeIcon.*
}

# vim: ts=2 sw=2 et:
