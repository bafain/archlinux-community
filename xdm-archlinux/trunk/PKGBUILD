# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Truls Becken <becken@stud.ntnu.no>
# Contributor: whompus

pkgname=xdm-archlinux
pkgver=0.6
pkgrel=1
pkgdesc="An XDM setup that looks better than the defaults"
arch=(any)
license=('GPL')
url="http://xorg.freedesktop.org"
depends=('xorg-xdm' 'bash'
	 'xorg-xmessage' 'xorg-xclock' 'xorg-xsetroot' 'xorg-xwininfo' 'xorg-xkill')
optdepends=('systemd-sysvcompat: if you use systemd')
backup=(etc/X11/xdm/archlinux/Xsetup
	etc/X11/xdm/archlinux/Xresources)
install=xdm-archlinux.install
source=(xdm-config
	Xsetup
	Xstartup
	Xresources
	buttons
	arch.xpm.gz
	xdm-archlinux.service)
sha256sums=('c79853a4fb674c505b8d542b19320eb6fcb0ec9e836b6fd986ba70691c93e370'
            '12720450ec5430e8c8875f75b8045b88cc8155e03fc97841c28eca5d278e102d'
            '7f9d62c29425f03f098d42b68d6d8bbd2335be6235a83f6142cfa4e9f8a4727a'
            '642193b0f91265f726a6b1ce9cd8a0271f0d6df8544402c5ae709e7a89bbc980'
            '09a51309824380e5c61ef06c3c5a4e9d76185ddd10a9668838141af1a8a0c084'
            '625087946ff746d87fde41da490dbd28bdc9c23c6ff25a915d2ea4b5ef08ebfd'
            'eb79a6bcb03c3909afea88ac63aba0464e6f07ae32ca29130b016dc0207f5a6a')

package() {
  mkdir -p "$pkgdir"/etc/X11/xdm/archlinux
  cp arch.xpm buttons xdm-config Xresources Xsetup Xstartup "$pkgdir"/etc/X11/xdm/archlinux
  chmod 0755 "$pkgdir"/etc/X11/xdm/archlinux/{Xsetup,Xstartup,buttons}
  install -Dm0644 "$srcdir"/xdm-archlinux.service "$pkgdir"/usr/lib/systemd/system/xdm-archlinux.service
}
