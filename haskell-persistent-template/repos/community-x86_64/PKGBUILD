# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=persistent-template
pkgname=haskell-persistent-template
pkgver=2.5.3.1
pkgrel=32
pkgdesc="Type-safe, non-relational, multi-backend persistence"
url="http://www.yesodweb.com/book/persistent"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' "haskell-aeson" "haskell-aeson-compat" "haskell-http-api-data"
         "haskell-monad-control" "haskell-monad-logger" "haskell-path-pieces" "haskell-persistent"
         "haskell-tagged" "haskell-text" "haskell-unordered-containers")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('3a206ea7e4964c3da43a1111f6f0c7a2acf862f66c8140b4ac8998e5f9ceda2a8c95ed23f6cd0a824b6c4a9e67b2f68b5410b6f636af7a8983f20ab925c4df86')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
