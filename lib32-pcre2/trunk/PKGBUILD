# $Id$

pkgname=lib32-pcre2
_pkgname=pcre2
pkgver=10.30
pkgrel=1
pkgdesc='A library that implements Perl 5-style regular expressions. 2nd version (32-bit)'
arch=('x86_64')
url='http://www.pcre.org/'
license=('BSD')
depends=('lib32-gcc-libs' 'lib32-readline' 'lib32-zlib' 'lib32-bzip2' 'bash')
source=("ftp://ftp.csx.cam.ac.uk/pub/software/programming/pcre/$_pkgname-$pkgver.tar.bz2")
sha512sums=('f247a9f917c75920793b9919a45bb1426d126246e7a5d04e39d9407e44b5781f894a90cd3d232b385436b2f22be391335ab782664dd3a28c79058a2fcc74dc3e')

build() {
  cd $_pkgname-$pkgver

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --enable-pcre2-16 \
    --enable-pcre2-32 \
    --enable-jit \
    --enable-pcre2grep-libz \
    --enable-pcre2grep-libbz2 \
    --enable-pcre2test-libreadline
  make
}

check() {
  cd $_pkgname-$pkgver

  make -j1 check
}

package() {
  cd $_pkgname-$pkgver
  
  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/usr/{share,bin,include}

  install -Dm644 LICENCE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
