# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-parsel
pkgname=('python-parsel' 'python2-parsel')
pkgver=1.5.0
pkgrel=2
pkgdesc="Parsel is a library to extract data from HTML and XML using XPath and CSS selectors"
arch=('any')
license=('BSD')
url="https://github.com/scrapy/parsel"
makedepends=('python-cssselect' 'python2-cssselect' 'python-lxml' 'python2-lxml'
             'python-pytest-runner' 'python2-pytest-runner' 'python-six' 'python2-six'
             'python-w3lib' 'python2-w3lib' 'python2-functools32')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/scrapy/parsel/archive/v$pkgver.tar.gz")
sha512sums=('bb2d44f65efbf3e57db10e61100da8cc0c6fa4bb6412a251efdc3c0e0cc8f2c26262d593d689f8237944a64c66479bfc5e0e94eb1d7e6f1d948cde519698e9c4')

prepare() {
  cp -a parsel-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/parsel-$pkgver
  python setup.py build

  cd "$srcdir"/parsel-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/parsel-$pkgver
  python setup.py pytest

  cd "$srcdir"/parsel-$pkgver-py2
  python2 setup.py pytest
}

package_python-parsel() {
  depends=('python-cssselect' 'python-lxml' 'python-six' 'python-w3lib')

  cd parsel-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-parsel() {
  depends=('python2-cssselect' 'python2-lxml' 'python2-six' 'python2-w3lib' 'python2-functools32')

  cd parsel-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
