# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Oliver Herold <oliver - at - akephalos - dot - de>

pkgname=scrypt
pkgver=1.2.1
pkgrel=4
pkgdesc='key derivation function and encryption utility'
arch=('x86_64')
depends=('openssl')
url='https://www.tarsnap.com/scrypt/'
license=('BSD')
source=(https://www.tarsnap.com/scrypt/scrypt-${pkgver}.tgz)
sha512sums=('ffda0ee90d283985245b77cc1d04b13c52aff78e925f323075d3b2c0439f16bbba1c432de8607820af945e6fa4e3be0e22a422b0b43a1fca0504cee3761004cd')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYRIGHT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
