# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Orivej Desh <smpuj@bk.ru>

pkgname=vamp-plugin-sdk
pkgver=2.7.1
_dir=2206
pkgrel=2
pkgdesc="The Vamp audio analysis plug-in system"
arch=('x86_64')
url="https://www.vamp-plugins.org/"
license=('BSD')
groups=('pro-audio')
depends=('gcc-libs' 'libsndfile')
source=("https://code.soundsoftware.ac.uk/attachments/download/${_dir}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('91dd7cae72334718dcbdac06b1ffa3f3ba76e977b82b928ab634d9982aa15ca4e30978a7778215753c61e56831077d7634d24e65e468ab6a8a87a5957fb09bf3')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  # license
  install -vDm644 COPYING \
    "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {CHANGELOG,README}
}

# vim:set ts=2 sw=2 et:
