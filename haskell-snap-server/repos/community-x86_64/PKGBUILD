# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=snap-server
pkgname=haskell-snap-server
pkgver=1.1.0.0
pkgrel=46
pkgdesc="A web server for the Snap Framework"
url="https://github.com/snapframework/snap-server"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-blaze-builder' 'haskell-case-insensitive'
         'haskell-clock' 'haskell-io-streams' 'haskell-io-streams-haproxy' 'haskell-lifted-base'
         'haskell-network' 'haskell-old-locale' 'haskell-snap-core' 'haskell-unix-compat'
         'haskell-vector')
makedepends=('ghc')
checkdepends=('haskell-base16-bytestring' 'haskell-monad-control' 'haskell-random'
             'haskell-threads' 'haskell-hunit' 'haskell-quickcheck' 'haskell-http-streams'
             'haskell-http-common' 'haskell-parallel' 'haskell-test-framework'
             'haskell-test-framework-hunit' 'haskell-test-framework-quickcheck2')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('ccd438310746c59bd7456df69c072ad3a2d473eb69f721fe2c91e086f4916d386c8b83a786d7ff9de11f0ce5294e36034baf10b0a109c65e16ec771e688a1184')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e '/bytestring-builder/d' -e 's/<.*2.7/<3/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    if (( CHECKFUNC )); then
        _opts=('--enable-tests')
    else
        _opts=('--disable-tests')
    fi

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" "${_opts[@]}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
