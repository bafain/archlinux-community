# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Thomas Jost <thomas.jost@gmail.com>

pkgname=subdownloader
pkgver=2.1.0rc4
pkgrel=3
pkgdesc="Automatic download/upload of subtitles using fast hashing"
arch=('any')
url="https://github.com/subdownloader/subdownloader"
license=('GPL3')
depends=('python-argcomplete' 'python-argparse' 'python-commonmark'
'python-langdetect' 'python-progressbar' 'python-pyqt5'  'python-pymediainfo'
'python-setuptools' 'python-sip')
makedepends=('python-sphinx')
checkdepends=('python-pytest')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('99946402622db535c5261324b2b39986ab0b9e433a459f3b9100ec2fe3ddf4159de591ac472de7ea438a0a2ecfdd60fd88c2a1c3e827c7e642be7f2dbde5ac65')

prepare() {
  cd "${pkgname}-${pkgver}"

  # remove broken PyQt5 detection: https://github.com/subdownloader/subdownloader/issues/25
  sed -i '/PyQt5/d' setup.py
}

build(){
  cd "${pkgname}-${pkgver}"
  python setup.py build
  python setup.py build_sphinx
}

check() {
  cd "${pkgname}-${pkgver}"
  py.test
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"

  # man page
  install -vDm 644 "build/sphinx/man/${pkgname}.1" \
    "${pkgdir}/usr/share/man/man1/${pkgname}.1"

  # freedesktop.org
  install -vDm 644 "${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -vDm 644 "scripts/gui/rc/images/${pkgname}.png" \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"

  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {ChangeLog,README.md}
}

# vim:set ts=2 sw=2 et:
