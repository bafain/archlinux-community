# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Thomas Jost <thomas.jost@gmail.com>

pkgname=subdownloader
pkgver=2.1.0rc3
pkgrel=1
pkgdesc="Automatic download/upload of subtitles using fast hashing"
arch=('any')
url="https://github.com/subdownloader/subdownloader"
license=('GPL3')
depends=('python-argcomplete' 'python-argparse' 'python-commonmark' 'python-langdetect' 'python-progressbar' 'python-pyqt5'  'python-pymediainfo' 'python-sip')
makedepends=('python-setuptools' 'python-sphinx')
checkdepends=('python-pytest')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('767962c8b1f9a7cfc4a8e44be61b2070b56f18572a6e156e9f19d4116b3531ec05a8170f66fa558f19fbd496e8d5d808f1f1c6396fa61405f570f6e099e0c80e')

build(){
  cd "${pkgname}-${pkgver}"
  python setup.py build
  python setup.py build_sphinx
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="${srcdir}/${pkgname}-${pkgver}/build/lib/":${PYTHONPATH}
  python setup.py test
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"

  # man page
  install -vDm644 "build/sphinx/man/${pkgname}.1" \
    "${pkgdir}/usr/share/man/man1/${pkgname}.1"

  # freedesktop.org
  install -vDm644 "${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -vDm644 "scripts/gui/rc/images/${pkgname}.png" \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"

  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {ChangeLog,README.md} \

  # remove tests folder
  rm -r "${pkgdir}/usr/lib/python3.6/site-packages/tests"
  # remove broken PyQt5 detection: https://github.com/subdownloader/subdownloader/issues/25
  sed -e '/PyQt5/d' \
    -i "${pkgdir}/usr/lib/python3.6/site-packages/SubDownloader-2.1.0rc3-py3.6.egg-info/requires.txt"
}

# vim:set ts=2 sw=2 et:
