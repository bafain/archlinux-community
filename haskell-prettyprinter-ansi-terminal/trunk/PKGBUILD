# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=prettyprinter-ansi-terminal
pkgname=haskell-prettyprinter-ansi-terminal
pkgver=1.1.1.2
pkgrel=21
pkgdesc="ANSI terminal backend for the prettyprinter package."
url="https://github.com/quchen/prettyprinter-ansi-terminal"
license=('custom:BSD2')
arch=('x86_64')
depends=('ghc-libs' 'haskell-ansi-terminal' 'haskell-prettyprinter')
makedepends=('ghc' 'haskell-doctest')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('b2ac6c76ce6570b285370c9b1a466272aa5a3ba86a9f1de996392d750d212ff62a743e9c3c28f7de7951c9b270d64c97cb695166b65d642751942d682ff1a71b')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-buildReadme
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE.md"
}
