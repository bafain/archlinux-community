# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=maven
pkgver=3.5.4
pkgrel=1
pkgdesc='Java project management and project comprehension tool'
url='https://maven.apache.org'
arch=('any')
license=('APACHE')
depends=('java-environment=8' 'bash' 'procps-ng')
makedepends=('maven' 'java-environment=8')
backup=('opt/maven/conf/settings.xml')
source=(https://www-eu.apache.org/dist/maven/maven-3/${pkgver}/source/apache-maven-${pkgver}-src.tar.gz{,.asc}
        maven.sh)
validpgpkeys=('042B29E928995B9DB963C636C7CA19B7B620D787') # StephenConnolly<stephenc@apache.org>
sha256sums=('f3ba1f1b24bbd4c345174ac616d40e26e72dad6022d56317d3ff6f7dd003e2f5'
            'SKIP'
            '24dc65607ff9e25c74ff2f75711911471910eaf415bce1856789c9d047209b8b')

build() {
  cd apache-maven-${pkgver}

  . /etc/profile.d/jre.sh

  mvn package \
    -Dmaven.repo.local="${srcdir}/repo" \
    -Dproject.build.sourceEncoding=UTF-8 -e
}

package() {
  cd apache-maven-${pkgver}

  . /etc/profile.d/jre.sh

  install -Dm 755 ../maven.sh -t "${pkgdir}/etc/profile.d"
  install -d "${pkgdir}/opt/maven"

  # skip tests as they were already executed
  mvn install \
    -Dmaven.repo.local="${srcdir}/repo" \
    -Dproject.build.sourceEncoding=UTF-8 -e \
    -DdistributionTargetDir="${pkgdir}/opt/maven" \
    -DskipTests -Dmaven.test.skip=true

  install -d "${pkgdir}/usr/bin"
  ln -s /opt/maven/bin/mvn "${pkgdir}/usr/bin/mvn"
  ln -s /opt/maven/bin/mvnDebug "${pkgdir}/usr/bin/mvnDebug"
}

# vim: ts=2 sw=2 et:
