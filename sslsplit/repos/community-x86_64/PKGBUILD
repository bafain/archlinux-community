# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=sslsplit
pkgver=0.5.2
pkgrel=1
pkgdesc='Tool for man-in-the-middle attacks against SSL/TLS encrypted network connections'
url='https://www.roe.ch/SSLsplit'
arch=('x86_64')
license=('BSD')
depends=('libevent' 'openssl')
checkdepends=('check')
source=(https://mirror.roe.ch/rel/${pkgname}/${pkgname}-${pkgver}.tar.bz2{,.asc})
sha512sums=('6c8a679c8016255e11adabb4f2a2e94cc23386ce33bc128e5bab2e5187f042e8daf8c720eef0f4fd3e7e94250deaf33b3c9253ffe05f1e4bdbd9a31f77785cc2'
            'SKIP')
validpgpkeys=('BFF9C7D7EA0EAC7F1AA55B3EFABE3324B5D3397E') # Daniel Roethlisberger <daniel@roe.ch>

build() {
  cd ${pkgname}-${pkgver}
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -j1 test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr install
  install -Dm 644 LICENSE* -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md NEWS.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
