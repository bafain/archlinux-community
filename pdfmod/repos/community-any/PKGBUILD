# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Claudio Riva <firetux83@gmail.com>

pkgname=pdfmod
pkgver=0.9.1
pkgrel=9
pkgdesc="Simple application for modifying PDF written in C Sharp"
arch=('any')
url="https://wiki.gnome.org/Apps/PdfMod"
license=('GPL' 'MIT')
depends=('gtk-sharp-2' 'poppler-glib')
makedepends=('hyena' 'poppler-sharp' 'intltool' 'gnome-doc-utils' 'dos2unix')
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        pdfmod-mono-2.10.patch
        fix-build.patch
        Drop-gconf-dependency.patch
        LICENSE-PDFsharp)
sha256sums=('eb7c987514a053106ddf03f26544766c751c801d87762909b36415d46bc425c9'
            'f606f29f881f7171b43fed97aab58c1e1f5b78aa96e33d1c43e2526736651acd'
            'd2ef10804b87ad843d0897cc784c95b6700583896fab95dd0646f5c79fdd0413'
            '6a235f93ce7321cbe2e7f20094ece0659fb4648602e84a4824a0fb527be90914'
            '5d2b6e1a9e5582f9c5d129f222ff01ca6ce3dfa4a5c1f5c41ba827fe0fbeb4bd')

prepare() {
  cd $pkgname-$pkgver

  # Some files are distributed in DOS format
  find . -type f -exec dos2unix -q '{}' \;

  # https://bugzilla.gnome.org/show_bug.cgi?id=644516
  patch -Np1 -i ../pdfmod-mono-2.10.patch

  # Workaround for build error (FS#39501)
  patch -Np1 -i ../fix-build.patch

  # Remove deprecated GConf dependency
  patch -Np1 -i ../Drop-gconf-dependency.patch

  # Fix build
  sed -i 's/gmcs/mcs/' configure{,.ac}
  sed -i 's|pkglib_SCRIPTS|monolibdir = $(pkglibdir)/\nmonolib_DATA|' Makefile.include

  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-scrollkeeper
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
  install -Dm644 "$srcdir/LICENSE-PDFsharp" "$pkgdir/usr/share/licenses/$pkgname/LICENSE-PDFsharp"
}
