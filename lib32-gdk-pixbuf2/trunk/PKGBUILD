# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=lib32-gdk-pixbuf2
pkgver=2.36.12
pkgrel=1
pkgdesc="An image loading library (32-bit)"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/GdkPixbuf"
license=(LGPL2.1)
depends=(lib32-glib2 lib32-libpng lib32-libtiff lib32-libjpeg lib32-libx11 gdk-pixbuf2)
makedepends=(gtk-doc gobject-introspection git docbook-xsl meson)
install=lib32-gdk-pixbuf2.install
_commit=12aa6f9264514c62781124a7287c2e997d3ea482  # tags/2.36.12^0
source=("git+https://git.gnome.org/browse/gdk-pixbuf#commit=$_commit"
        gdk-pixbuf-query-loaders-32.hook)
sha256sums=('SKIP'
            '3fed95ad7075698313caba78c84b4f7eb9935cc5c8cc99412fc56d8d34ef700f')

pkgver() {
  cd gdk-pixbuf
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd gdk-pixbuf
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="-I/usr/include/libtiff32 $CFLAGS"

  arch-meson gdk-pixbuf build --libdir=/usr/lib32
  ninja -C build
}

check() {
  cd build
  # pixbuf-randomly-modified fails randomly
  meson test -t 3 || :
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  rm -rf "$pkgdir"/usr/{include,lib,share}
  find "$pkgdir/usr/bin" -type f -not -name gdk-pixbuf-query-loaders -delete
  mv "$pkgdir"/usr/bin/gdk-pixbuf-query-loaders{,-32}
  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
}

# vim:set ts=2 sw=2 et:
