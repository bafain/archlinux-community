# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=bsb-http-chunked
pkgname=haskell-bsb-http-chunked
pkgver=0.0.0.2
pkgrel=3
pkgdesc="Chunked HTTP transfer encoding for bytestring builders"
url="http://github.com/sjakobi/bsb-http-chunked"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('8ce8ba1065e2ec219d6bb663b597e049bac403dc8d061907e677781333d878be1fed48d4a3f6c34801afa5e89bdbd965b8dee71515c7fb2e6f58e8957194fb22')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/bytestring-builder < 0.11/bytestring < 0.11/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
