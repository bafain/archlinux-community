# Contributor: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Jakub "Kubuxu" Sztandera  <kubuxu@protonmail.ch>

pkgname=gx-go
pkgver=1.7.0
pkgrel=1

pkgdesc="A tool to use with the gx package manager for packages written in go"
url="https://github.com/whyrusleeping/gx-go"
arch=('x86_64')
license=('MIT')

makedepends=('git' 'go-pie')
depends=('glibc' 'go')

source=("git+https://github.com/whyrusleeping/gx-go.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  mkdir -p "$srcdir"/src/github.com/whyrusleeping/
  ln -fs "$srcdir/gx-go" "$srcdir"/src/github.com/whyrusleeping/gx-go
}

build() {
  # Required for go get
  export GOPATH="$srcdir"
  export GOBIN="$GOPATH/bin"
  cd "$srcdir"/src/github.com/whyrusleeping/gx-go

  msg2 'Installing dependencies...'
  go get -v

  msg2 'Building binary...'
  go install -v
}

package() {
  msg2 'Packaging binary...'
  install -Dm 755 bin/gx-go "${pkgdir}/usr/bin/gx-go"

  msg2 'Packaging auxiliary files...'
  cd gx-go
  install -Dm 644 -t "${pkgdir}/usr/share/licenses/gx-go" LICENSE
}
