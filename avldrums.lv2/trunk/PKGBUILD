# Maintainer: David Runge <dave@sleepmap.de>
pkgname=avldrums.lv2
pkgver=0.3.0
pkgrel=1
pkgdesc="A simple Drum Sample Player Plugin, dedicated to the AVLinux Drumkits"
arch=('x86_64')
url="https://x42-plugins.com/x42/x42-avldrums"
license=('GPL2')
groups=('pro-audio' 'lv2-plugins')
depends=('glu' 'pango')
makedepends=('git' 'glib2' 'lv2')
source=("$pkgname-$pkgver::git+https://github.com/x42/${pkgname}.git#tag=v${pkgver}"
        "git+https://github.com/x42/robtk")
sha512sums=('SKIP'
            'SKIP')

prepare() {
  cd "${pkgname}-${pkgver}"
  git submodule init
  git config submodule.robtk.url "${srcdir}/robtk"
  git submodule update
}

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" PREFIX='/usr' install
}
