# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=mustache
pkgname=haskell-mustache
pkgver=2.3.0
pkgrel=83
pkgdesc="A mustache template parser library."
url="https://github.com/JustusAdam/mustache"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-either' 'haskell-unordered-containers' 'haskell-vector'
         'haskell-scientific' 'haskell-th-lift' 'haskell-cmdargs' 'haskell-yaml')
makedepends=('ghc' 'haskell-hspec' 'haskell-base-unicode-symbols' 'haskell-wreq' 'haskell-zlib'
             'haskell-tar' 'haskell-lens' 'haskell-temporary')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('bdefb406a44bb3648ca7129128767be04c780d967757385770111a0da8f91ff7165213038e8abc7799b28b66eb7d2f47383346837fad7e6327dad7aa714971f6')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
