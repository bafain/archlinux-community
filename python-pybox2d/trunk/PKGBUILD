# $Id$
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgbase=python-pybox2d
pkgname=('python2-pybox2d')
pkgver=2.3.2
pkgrel=1
epoch=1
arch=('x86_64')
url='https://github.com/pybox2d/pybox2d'
license=('GPL')
makedepends=('git' 'swig' 'python2-setuptools')
source=("$pkgbase::git+https://github.com/pybox2d/pybox2d.git#tag=$pkgver")
md5sums=('SKIP')

build() {
  cd "$pkgbase"

  python2 setup.py setopt --command=build_ext --option=swig --set-value=swig
  python2 setup.py build
}

package_python2-pybox2d() {
  pkgdesc='Python wrapper for Box2D'

  cd "$pkgbase"

  python2 setup.py install --root="$pkgdir" --optimize=1

  # Fixes the warning about a missing dependency on python
  sed -i 's:python:python2:g' \
    "$pkgdir/usr/lib/python2.7/site-packages/Box2D/__init__.py"
}

# vim: sw=2 ts=2 et:
