# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-multi-writer
pkgver=3.30.0
pkgrel=1
pkgdesc="Write an ISO file to multiple USB devices at once"
arch=('x86_64')
url="https://wiki.gnome.org/Apps/MultiWriter"
license=('GPL')
groups=('gnome-extra')
depends=('gtk3' 'libcanberra' 'libgusb' 'udisks2')
makedepends=('appstream-glib' 'docbook-sgml' 'docbook-utils' 'intltool' 'meson' 'perl-sgmls')
optdepends=('gnome-icon-theme-extras: show device icons')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('6a3c5f1da4a8c04f2a51c196b1aa4ae4bad4ea1d0e5395f9dc62f3542a5f8975')

prepare() {
	mkdir build
}

build() {
	cd build
	meson ../$pkgname-$pkgver \
		--prefix=/usr \
                --libexecdir=lib/$pkgname \
		--buildtype=release
	ninja
}

package() {
	cd build
	DESTDIR="$pkgdir" ninja install
}
