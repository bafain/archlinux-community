# $Id$
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinuc.org>
# Contributor: Johannes Pohl <johannes.pohl90@gmail.com>

pkgname=urh
pkgver=2.2.4
pkgrel=3
pkgdesc="Universal Radio Hacker: investigate wireless protocols like a boss"
arch=('x86_64')
url="https://github.com/jopohl/urh"
license=('GPL3')
depends=('python' 'python-pyqt5' 'python-numpy' 'python-psutil' 'python-pyzmq')
makedepends=('gcc' 'python-setuptools' 'cython'
             'airspy' 'hackrf' 'libuhd' 'limesuite' 'rtl-sdr')
checkdepends=('python-pytest-runner' 'python-pytest-xdist' 'xorg-server-xvfb')
optdepends=('airspy: for native airspy backend'
            'cython: for recompiling native backends right from inside URH'
            'hackrf: for native hackrf backend'
            'libuhd: for native USRP backend'
            'limesuite: for native LimeSDR backend'
            'rtl-sdr: for native RTL-SDR backend'
            'rfcat-hg: for rfcat plugin'
            'gnuradio: for USRP gnuradio backend'
            'gnuradio-osmosdr: for more gnuradio device backends'
            'python-pyaudio: for soundcard support')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jopohl/urh/archive/v$pkgver.tar.gz")
sha512sums=('082cb70aa2594d92c8334dd0676bbe05f087a9f5d79ae526330fcac3c782467135b3eb987de8571b7ae9a53b2d32c1ccba4dbb0515b52a73b3c0358790792e33')

build() {
  cd $pkgname-$pkgver

  python setup.py build build_ext
  python src/urh/cythonext/build.py
}

check() {
  cd $pkgname-$pkgver

  xvfb-run pytest --boxed
}

package() {
  cd $pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 ./data/urh.desktop "$pkgdir"/usr/share/applications/urh.desktop
  install -Dm 644 data/icons/appicon.png "$pkgdir"/usr/share/pixmaps/urh.png
  install -Dm 644 README.md "$pkgdir"/usr/share/doc/$pkgname/README.md
}

