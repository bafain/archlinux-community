# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Phil Pirozhkov <pirjsuka@gmail.com>

pkgname=mtd-utils
pkgver=2.0.1
pkgrel=1
pkgdesc='Utilities for dealing with MTD devices'
arch=('x86_64')
url='http://www.linux-mtd.infradead.org/'
license=('GPL2')
depends=('lzo' 'util-linux')
source=(ftp://ftp.infradead.org/pub/mtd-utils/$pkgname-$pkgver.tar.bz2{,.asc})
md5sums=('ef065490799f5e21e90199dd25d033b6'
         'SKIP')
validpgpkeys=('13063F723C9E584AEACD5B9BBCE5DC3C741A02D1')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
}
