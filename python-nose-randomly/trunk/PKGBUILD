# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-nose-randomly
pkgname=(python-nose-randomly python2-nose-randomly)
pkgver=1.2.5
pkgrel=2
pkgdesc="Nose plugin to randomly order tests and control random.seed."
url="https://github.com/adamchainz/nose-randomly"
license=('BSD')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-nose' 'python2-nose')
checkdepends=('python-numpy' 'python2-numpy')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/adamchainz/nose-randomly/archive/$pkgver.tar.gz")
sha512sums=('46b28a1305b638a4a0b44704bd4fb6c5372eddb3d2a7d5c0fe53b124240385c5ad61a916b98f7e3f059794a1171b77654e8d94143facc2d995077a2fc4eeae84')

prepare() {
  cp -a nose-randomly-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/nose-randomly-$pkgver
  python setup.py build

  cd "$srcdir"/nose-randomly-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/nose-randomly-$pkgver
  python setup.py nosetests

  cd "$srcdir"/nose-randomly-$pkgver-py2
  python2 setup.py nosetests
}

package_python-nose-randomly() {
  depends=('python-nose')

  cd nose-randomly-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-nose-randomly() {
  depends=('python2-nose')

  cd nose-randomly-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
