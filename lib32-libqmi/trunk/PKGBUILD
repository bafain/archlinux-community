# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=lib32-libqmi
pkgver=1.20.2
pkgrel=1
pkgdesc='QMI modem protocol helper library'
arch=('x86_64')
url='https://www.freedesktop.org/wiki/Software/libqmi/'
license=('GPL2')
depends=('lib32-glib2' 'lib32-glibc' 'lib32-libmbim' 'libqmi')
makedepends=('git' 'python')
source=("git+https://gitlab.freedesktop.org/mobile-broadband/libqmi.git#tag=${pkgver}")
sha256sums=('SKIP')

prepare() {
  cd libqmi

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd libqmi

  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --libexecdir='/usr/lib32' \
    --localstatedir='/var' \
    --sysconfdir='/etc' \
    --disable-gtk-doc-html \
    --disable-static
  make
}

package() {
  make DESTDIR="${pkgdir}" -C libqmi install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}

# vim: ts=2 sw=2 et:
