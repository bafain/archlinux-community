# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Rob Til Freedmen <rob.til.freedman@gmail.com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>

pkgname=hydrogen
pkgver=0.9.7
pkgrel=4
pkgdesc="An advanced drum machine"
arch=('x86_64')
license=('GPL')
groups=('pro-audio')
url="http://www.hydrogen-music.org/"
depends=('libarchive' 'liblrdf' 'liblo' 'portaudio' 'portmidi' 'libpulse'
'qt4')
makedepends=('cmake' 'cppunit' 'docbook-xml' 'docbook-sgml' 'docbook-utils'
'docbook-xsl' 'ladspa' 'poxml' 'python2' 'xmlto')
optdepends=('rubberband: Time stretch samples')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}-music/${pkgname}/archive/${pkgver}.tar.gz"
        '1000_portaudio_v2.patch'
        '1010-spelling.patch'
        'fix_cmake_cache.patch'
        'fix_cxx_flags.patch'
        'fix_desktop_file.patch'
        'fix_dtd_version.patch'
        'fix_man_path.patch'
        'fix_missing_locale.patch'
        'fix_rubberband_path.patch')
sha512sums=('1480a18d1181d12e92a53e89a4d1c8f4ae9a8a9a02271e6af4089afaaca9d658abb3876dc7db08a453bbe3ab7f66f31567d992f30102818782b62ebba5141f58'
            '1fa94eeeec1b1d5e685d5fd5dc0a5cce0ed59f38107589ca226bea79c45db9e7377b5a3e13c0f568b6b2611250b70d44d37bc1e29609d959c4eaccbfb2fd47a5'
            '843ab2a1c74f26fe124c7324f52fb22c3d85274de0ccf4552df0a90a83669571db320287f00fc737d1731da7e21b42d883d64c889ac9193cae8ce745eb44d8bb'
            '1bc99593ba75af5ffac932bd48d294fdf7c0005530e76145908485d8f79e7ef35cac50fca9df3fbbe672ab8250f76613c3d73ca89253198bfd5d8d6d298a452e'
            '3bea26ce1fc4fd9518e2a43d3b6474867cf2cb2abbb207a29fd001d95224b6d481aecd32ff6f7231a62ad4b612e107204f4e043c1b8adad09a580b87bb9bed74'
            '656a4f8f6fec88e678f71dd6d77437d66429d9863149ceb850e270b4ae8668a33baeebf4ce8f3b72b3643826bc0404d235dc123f4f194dfa0caf858356798a21'
            'f1024ed99711c18f6e80ccacee119c91ee14cfdc5493ac07c6c152a9ca9875de349f6fcd818dc9a036e79577101ae7afc2ce5b324660a4ebb89a9e0a72129510'
            '1f640706182cffbf1c11fe639bc749ec1bdfa9e0def77fd2673f64fa314586a8067bbea224d7f9be6c534ac9854ae44c6c9de3ba4b6229d3691a95aeb9036420'
            '833c57f342b2a7088df28762a9a0ada4ff5351013f63cc9c499db9da9713ac5612cc06ac359816f2d0ad7e6b7291dcc2e81094e4aed348c38583b31d6115b6b3'
            '514d77d2207322ca57d0e9f872b8d3d5f1a6e41c9765f54d6a5bb88cb68d2c308234a77235e5012c4a12d01137f1772eb1b4ea9454a31c9d1199cb6772b78322')

prepare() {
  cd "${pkgname}-${pkgver}"

  # fix libpulse requirement
  sed -e 's|pulseaudio pulse/|libpulse pulse/|' -i CMakeLists.txt

  # fix raptor2 include path
  sed -e 's|${RAPTOR_INCLUDE_DIR}|/usr/include/raptor2|' \
    -i src/core/CMakeLists.txt

  # fix python2 shebang
  sed -e 's/python/python2/' -i data/i18n/stats.py

  # patches from debian
  patch -Np1 -i "${srcdir}/1000_portaudio_v2.patch"
  patch -Np1 -i "${srcdir}/1010-spelling.patch"

  # various fixes
  patch -Np1 -i "${srcdir}/fix_dtd_version.patch"
  # set path of rubberband executable
  patch -Np1 -i "${srcdir}/fix_rubberband_path.patch"

  # https://github.com/hydrogen-music/hydrogen/issues/559
  patch -Np1 -i "${srcdir}/fix_cmake_cache.patch"

  patch -Np1 -i "${srcdir}/fix_cxx_flags.patch"
  patch -Np1 -i "${srcdir}/fix_man_path.patch"
  patch -Np1 -i "${srcdir}/fix_missing_locale.patch"

  #fix desktop file icon (and other inconsistencies)
  # https://github.com/hydrogen-music/hydrogen/issues/558
  patch -Np1 -i "${srcdir}/fix_desktop_file.patch"

  # out-of-tree build
  mkdir -v build
}

build() {
  cd "${pkgname}-${pkgver}/build"
  # rubberband is disabled (not properly discovered and apparently broken)
  cmake .. -DCMAKE_BUILD_TYPE=Release \
           -DCMAKE_INSTALL_PREFIX=/usr \
           -DWANT_DEBUG=OFF \
           -DWANT_CPPUNIT=ON \
           -DWANT_LRDF=ON \
           -DWANT_PORTAUDIO=ON \
           -DWANT_PORTMIDI=ON \
           -DWANT_PULSEAUDIO=ON \
           -DWANT_RUBBERBAND=OFF
  make
  # build html manual & tutorial
  cd ../data/doc
  make
  # update translations
  cd ../i18n
  ./updateTranslations.sh
}

package() {
  cd "${pkgname}-${pkgver}/build"
  make DESTDIR="$pkgdir" install

  # install desktop file icon to correct location
  # https://github.com/hydrogen-music/hydrogen/issues/558
  install -vDm644 "${pkgdir}/usr/share/${pkgname}/data/img/gray/h2-icon.svg" \
    "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"

  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 ../{ChangeLog,DEVELOPERS,INSTALL.txt,README.txt}

  # clean up data dir
  # https://github.com/hydrogen-music/hydrogen/issues/559
  rm -v "$pkgdir/usr/share/${pkgname}/data/i18n/"{*.ts,stats.py,updateTranslations.sh} \
   "$pkgdir/usr/share/${pkgname}/data/doc/"{*.docbook,*.po,*.pot,Makefile,TODO,README.DOCUMENTATION.txt}
}

# vim:set ts=2 sw=2 et:
