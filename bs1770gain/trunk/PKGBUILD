# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=bs1770gain
pkgver=0.4.12
pkgrel=2
pkgdesc='A loudness scanner compliant with ITU-R BS.1770.'
arch=('x86_64')
url="http://bs1770gain.sourceforge.net"
license=('GPL')
depends=('ffmpeg' 'sox')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('cafc5440cf4940939c675e98c8dbeb839f4965d60f74270a37d4ee70559b3a59')

prepare() {
    cd ${pkgname}-${pkgver}
    # ffmpeg 4.0 changes
    sed -i "s:CODEC_CAP_VARIABLE_FRAME_SIZE:AV_CODEC_CAP_VARIABLE_FRAME_SIZE:g" libffsox-2/ffsox_frame.c
    sed -i "s:CODEC_FLAG_GLOBAL_HEADER:AV_CODEC_FLAG_GLOBAL_HEADER:g" libffsox-2/ffsox_frame_writer.c
    sed -i "s:CODEC_FLAG_GLOBAL_HEADER:AV_CODEC_FLAG_GLOBAL_HEADER:g" libffsox-2/ffsox_packet_writer.c
}

build() {
    cd ${pkgname}-${pkgver}

    ./configure --prefix=/usr
    make
}

package() {
    cd ${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install
}
