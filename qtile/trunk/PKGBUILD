# $Id$
# Maintainer: David Runge <dave@sleepmap.de>

pkgname=qtile
pkgver=0.12.0
pkgrel=2
pkgdesc="A full-featured, pure-Python tiling window manager."
arch=('any')
url="http://www.qtile.org"
license=('MIT')
depends=('pango' 'python-xcffib' 'python-cairocffi' 'python-dbus'
'python-gobject' 'python-setuptools')
makedepends=('jupyter_console'
             'python-ipykernel'
             'python-iwlib'
             'python-keyring'
             'python-mpd2'
             'python-pytest'
             'python-setuptools'
             'python-xdg')
optdepends=('alsa-utils: volume widget'
            'canto-daemon: canto widget'
            'cmus: cmus widget'
            'khal: khal_calendar widget'
            'lm_sensors: sensors widget'
            'moc: moc widget'
            'python-iwlib: wlan widget'
            'python-keyring: imapwidget widget'
            'python-mpd2: mpdwidget, mpd2widget widget'
            'python-setproctitle: change process name to qtile'
            'python-xdg: launchbar widget')
checkdepends=('python-pytest' 'python-xvfbwrapper' 'xorg-server-xephyr')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('a94237c40775524e16aebdd8555d0ed0623c248b65aee32851145e8a6cc54ce4d46ee5cf4c4f583e468de4557a565c4281660219e4b2ee1b73fb483567e1a5b2')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

# # tests disabled for now, as they don't run headless
# # https://github.com/qtile/qtile/issues/1130
# check() {
#   cd "${pkgname}-${pkgver}"
#   export PYTTHONPATH="${srcdir}/${pkgname}-${pkgver}/build/lib/:${PYTHONPATH}"
#   export LC_TYPE=en_US.UTF-8
#   python libqtile/ffi_build.py
#   py.test
# }

package() {
  cd "${pkgname}-${pkgver}"
  # install
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}" \
    --prefix=/usr
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # config
  install -vDm 644 libqtile/resources/default_config.py \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # desktop file
  install -vDm 644 "resources/${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/xsessions/"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {CHANGELOG,README.rst}
}
