# Maintainer: David Runge <dave@sleepmap.de>

pkgname=qtile
pkgver=0.10.7
pkgrel=2
pkgdesc="A full-featured, pure-Python tiling window manager."
arch=('any')
url="http://www.qtile.org"
license=('MIT')
depends=('pango' 'python-xcffib' 'python-cairocffi' 'python-dbus'
'python-gobject')
makedepends=('jupyter_console'
             'python-ipykernel'
             'python-iwlib'
             'python-keyring'
             'python-mpd2'
             'python-pytest'
             'python-setuptools'
             'python-xdg')
optdepends=('alsa-utils: volume widget'
            'canto-daemon: canto widget'
            'cmus: cmus widget'
            'khal: khal_calendar widget'
            'lm_sensors: sensors widget'
            'moc: moc widget'
            'python-iwlib: wlan widget'
            'python-keyring: imapwidget widget'
            'python-mpd2: mpdwidget, mpd2widget widget'
            'python-setproctitle: change process name to qtile'
            'python-xdg: launchbar widget')
#checkdepends=('python-pytest' 'xorg-server-xephyr')
conflicts=('qtile-git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('9b8f056ca9b4e5d3136114cafa47ee640f189c3f188815cfe5845c974b745978ebf0f74a691869517144d8fcaac0a29f5b38ee9265b888cc2bb3bfae865934cd')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

#check() {
#  cd "${pkgname}-${pkgver}"
#  ./scripts/xephyr
#  python libqtile/ffi_build.py
#  pytest
#}

package() {
  cd "${pkgname}-${pkgver}"
  # install
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}" \
    --prefix=/usr
  # license
  install -vDm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # config
  install -vDm644 libqtile/resources/default_config.py \
  "${pkgdir}/usr/share/doc/${pkgname}/default_config.py"
  # desktop file
  install -vDm644 "resources/${pkgname}.desktop" \
  "${pkgdir}/usr/share/xsessions/${pkgname}.desktop"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm644 {CHANGELOG,README.rst}
}
