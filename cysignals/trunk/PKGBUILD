# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=cysignals
pkgname=(python-cysignals python2-cysignals)
pkgver=1.7.2
pkgrel=4
pkgdesc="Interrupt and signal handling for Cython"
arch=(x86_64)
url="https://www.sagemath.org"
license=(LGPL)
makedepends=(cython2 cython pari)
source=("https://github.com/sagemath/cysignals/releases/download/$pkgver/$pkgbase-$pkgver.tar.gz")
sha256sums=('db245c19248a869add480e50e5bd97d282176a74b8c6ca7a0072192e576afc67')

prepare() {
  cp -r $pkgbase-$pkgver{,-py2}
}

build() {
  cd $pkgbase-$pkgver
  python setup.py build

  cd "$srcdir"/$pkgbase-$pkgver-py2
  python2 setup.py build
}

package_python-cysignals() {
  depends=(python pari)

  cd $pkgbase-$pkgver
  python setup.py install --root="$pkgdir"

# Keep scripts in python2 package
  rm -r "$pkgdir"/usr/{bin,share}
}

package_python2-cysignals() {
  depends=(python2 pari)
  conflicts=(cysignals)
  provides=(cysignals)
  replaces=(cysignals)

  cd $pkgbase-$pkgver-py2   
  python2 setup.py install --root="$pkgdir"
}
