# Maintainer: David Runge <dave@sleepmap.de>

pkgname=python-imdbpy
pkgver=6.4.1
pkgrel=1
pkgdesc="Python bindings for the Internet Movie Database (IMDb)"
url="https://imdbpy.sourceforge.io/"
arch=('any')
license=('GPL2')
depends=('python-lxml' 'python-sqlalchemy')
makedepends=('python-pytest' 'python-setuptools')
checkdepends=('python-pytest' 'python-pytest-cov')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/alberanid/${pkgname/python-/}/archive/${pkgver}.tar.gz")
sha512sums=('558c1bb9346d7a109c29057c181f7ceabeaba76165e385ff6aef603f2873af6b89cb77391bec9d496c1419cc3af51b48ac966bb3233f7f374a0a8700ca7cda5e')

prepare() {
  mv -v "${pkgname/python-/}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # fix install folder for docs and config
  sed -e "s/'doc'/'share\/doc'/" \
      -e "s/'etc'/'\/etc'/" -i setup.py
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH=${srcdir}/${pkgname}-${pkgver}/build:${PYTHONPATH}
  py.test
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
  install -vDm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim:set ts=2 sw=2 et:
