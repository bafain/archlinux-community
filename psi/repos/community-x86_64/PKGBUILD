# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgname="psi"
pkgver=1.3
pkgrel=3
pkgdesc="a powerful Jabber client (Qt, C++) designed for the Jabber power users"
url="https://psi-im.org"
license=('GPL2')
arch=('x86_64')
depends=('qt5-base' 'qt5-webengine' 'qt5-multimedia' 'qt5-x11extras' 'qca-qt5'
	 'libidn' 'libxss' 'qt5-svg' 'hunspell' 'qtkeychain')
makedepends=('patch' 'cmake')
source=("https://downloads.sourceforge.net/project/${pkgname}/Psi/${pkgver}/psi-${pkgver}.tar.xz"
	"no-qca-qt4.patch::https://aur.archlinux.org/cgit/aur.git/plain/no-qca-qt4.patch?h=psi-plus-git&id=bc3ee431ca26d6294810636a514c50985540bd23"
	"join.patch")
sha256sums=('59debd16e61ab1d4ff88aca9f41b9caaaca8395f1576418fb99214d5e2c6fa8b'
            'a6ea9a62100b6990b8a6c8590a6d0e9df095b99b6527bb5490a7fd3f6929ac23'
            '67ff9fe4ebfc7b6a04cd325a677ad0cf6252bd7276bfb7968be80fc6f0ad5dc3')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 <"$srcdir"/join.patch
  patch -p1 <"$srcdir"/no-qca-qt4.patch
}

build() {
  cd $pkgname-$pkgver
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release ..
  make
}

package() {
  cd $pkgname-$pkgver/build
  make DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/include/psi/plugins"
  install -m644 ../src/plugins/include/*.h "$pkgdir/usr/include/psi/plugins"
}
