# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgname=('psi' 'psi-nowebengine')
pkgver=1.3
pkgrel=5
pkgdesc="Powerful XMPP (Jabber) client (Qt, C++) designed for power users"
url="https://psi-im.org"
license=('GPL2')
arch=('x86_64')
depends=('hunspell' 'qca-qt5' 'qt5-svg' 'qt5-webengine' 'qt5-x11extras')
makedepends=('cmake' 'qt5-multimedia')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.xz"
	"no-qca-qt4.patch")
sha256sums=('59debd16e61ab1d4ff88aca9f41b9caaaca8395f1576418fb99214d5e2c6fa8b'
            'a6ea9a62100b6990b8a6c8590a6d0e9df095b99b6527bb5490a7fd3f6929ac23')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 <"$srcdir"/no-qca-qt4.patch
}

build() {
  cd $pkgname-$pkgver
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release ..
  make
  cd ..

  mkdir -p build-nowebengine
  cd build-nowebengine
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DENABLE_WEBKIT=OFF ..
  make
}

package_psi() {
  cd "$srcdir"/psi-$pkgver/build
  make DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/include/psi/plugins"
  install -m644 ../src/plugins/include/*.h "$pkgdir/usr/include/psi/plugins"
}

package_psi-nowebengine() {
  pkgdesc+=" without WebEngine support"
  depends=('hunspell' 'minizip' 'qca-qt5' 'qt5-svg' 'qt5-x11extras')
  provides=("$pkgbase=$pkgver")
  conflicts=("$pkgbase")
  replaces=('psi-nowebkit')

  cd "$srcdir"/psi-$pkgver/build-nowebengine
  make DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/include/psi/plugins"
  install -m644 ../src/plugins/include/*.h "$pkgdir/usr/include/psi/plugins"
}
