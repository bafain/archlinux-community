# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=granite
pkgver=5.1.0
pkgrel=1
pkgdesc='Library that extends Gtk+'
arch=('x86_64')
url='https://github.com/elementary/granite'
license=('LGPL3')
groups=('pantheon')
depends=('cairo' 'gdk-pixbuf2' 'glib2' 'gtk3' 'libgee' 'pango')
makedepends=('cmake' 'git' 'gobject-introspection' 'vala')
provides=('libgranite.so')
source=("git+https://github.com/elementary/granite.git#tag=${pkgver}")
sha256sums=('SKIP')

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  cmake ../granite \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib'
  make
}

package() {
  make DESTDIR="${pkgdir}" -C build install
}

# vim: ts=2 sw=2 et:
