# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Joakim Hernberg <jhernberg@alchemy.lu>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=zita-ajbridge
pkgver=0.7.0
pkgrel=4
pkgdesc="Jack client to use additional ALSA devices."
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/zita-ajbridge-doc/quickguide.html"
license=('GPL3')
depends=('jack' 'zita-alsa-pcmi' 'zita-resampler')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('1b875bf1b9dfb8e9284af2ae03b6a6f82ad1cbe06eb24faa8485ac0db52f914cdc1a7447b1e9d46a42fb634848a2ee7904c658180f84cf4db210db59478a4806')

prepare() {
  cd "${pkgname}-${pkgver}/source"
  # fix man installation
  sed -e 's/zita-a2j zita-j2a zita-ajbridge.1.gz zita-a2j.1.gz zita-j2a.1.gz/zita-a2j zita-j2a/' \
      -e 's/zita-ajbridge.1.gz/zita-ajbridge.1 -t/g' \
      -e 's/zita-j2a.1.gz/zita-j2a.1 -t/g' \
      -e 's/zita-a2j.1.gz/zita-a2j.1 -t/g' -i Makefile
  # fix binary installation
  sed -e 's|zita-a2j $(DESTDIR)$(BINDIR)|zita-a2j $(DESTDIR)$(BINDIR)/zita-a2j|' \
      -e 's|zita-j2a $(DESTDIR)$(BINDIR)|zita-j2a $(DESTDIR)$(BINDIR)/zita-j2a|' -i Makefile
}

build() {
  cd "${pkgname}-${pkgver}/source"
  make PREFIX=/usr
}

package() {
  cd "${pkgname}-${pkgver}/source"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 ../{AUTHORS,README}
}
# vim:set ts=2 sw=2 et:

