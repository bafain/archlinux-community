# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Christoph W <c w e g e n e r at gmail dot com>

pkgbase=('python-ntlm-auth')
pkgname=('python-ntlm-auth' 'python2-ntlm-auth')
_module='ntlm-auth'
pkgver=1.2.0
pkgrel=2
pkgdesc="Creates NTLM authentication structures"
url="https://github.com/jborean93/ntlm-auth"
makedepends=(python-setuptools python2-setuptools)
license=(LGPL3)
arch=(any)
source=("https://github.com/jborean93/ntlm-auth/archive/v${pkgver}.tar.gz")
sha512sums=('4fa537c5a6a7f3493df2fdf49261f9588b7aac876f72b7a7ef5b8a7a1dd971d9426dacdc8bbf520cecdbe5b7adb916ddcad9e9cf4df725b69793299304601912')

prepare() {
    cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py build

    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py build
}

package_python-ntlm-auth() {
    depends+=('python' 'python-six')
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-ntlm-auth() {
    depends+=('python2' 'python2-six' 'python2-ordereddict')
    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
