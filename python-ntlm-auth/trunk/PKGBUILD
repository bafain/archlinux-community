# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Christoph W <c w e g e n e r at gmail dot com>

pkgbase=('python-ntlm-auth')
pkgname=('python-ntlm-auth' 'python2-ntlm-auth')
_module='ntlm-auth'
pkgver=1.1.0
pkgrel=1
pkgdesc="Creates NTLM authentication structures"
url="https://github.com/jborean93/ntlm-auth"
makedepends=(python-setuptools python2-setuptools)
license=(LGPL3)
arch=(any)
source=("https://github.com/jborean93/ntlm-auth/archive/v${pkgver}.tar.gz")
sha512sums=('0c41cfab957c48d5fabf391422323556289cc6d8ba5a50c82bb179f6cdbf68528f005c3987e0b47fb47aff2fbcf00deebb0ac4220df6bd08d335879344f8470a')

prepare() {
    cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py build

    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py build
}

package_python-ntlm-auth() {
    depends+=('python' 'python-six')
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-ntlm-auth() {
    depends+=('python2' 'python2-six' 'python2-ordereddict')
    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
