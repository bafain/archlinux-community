# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkgname=easy-format
pkgname=ocaml-${_pkgname}
pkgver=1.3.1
pkgrel=2
pkgdesc="An optimized parsing and printing library for JSON"
arch=('x86_64')
url="https://github.com/mjambon/${_pkgname}"
license=('BSD')
options=('!strip' 'staticlibs')
depends=('glibc')
makedepends=('ocaml-findlib' 'dune' 'opam')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/v${pkgver}.tar.gz")
sha256sums=('489d55ea5de171cea2d7e2114bcd5cebd1fcbf89f839fbf3757769507502e1f0')

build() {
    cd ${_pkgname}-${pkgver}
    make all
}

check() {
    cd ${_pkgname}-${pkgver}
    make test
}

package() {
    cd ${_pkgname}-${pkgver}

    # Initialize OPAM
    export OPAMROOT="${srcdir}"/opam
    opam init -n

    # Work around missing install command
    OCAMLFIND_DESTDIR="${pkgdir}$(ocamlfind printconf destdir)" jbuilder install

    # Install LICENSE
    install -Dm644 "${srcdir}"/opam/system/doc/easy-format/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
