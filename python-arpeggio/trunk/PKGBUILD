# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-arpeggio
pkgver=1.9.0
pkgrel=1
pkgdesc="Packrat parser interpreter"
url="https://github.com/igordejanovic/Arpeggio"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/igordejanovic/Arpeggio/archive/v$pkgver.tar.gz")
sha512sums=('179ec76813476823f316471e1b313f377064a75879c52060e547fd755ae72947594dc4d6e58a798150620e82c8b123fbd88484881c12ccb311c47bc93a45a0cd')

build() {
  cd Arpeggio-$pkgver
  python setup.py build
}

check() {
  cd Arpeggio-$pkgver
  python setup.py pytest --addopts tests/unit
}

package() {
  cd Arpeggio-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
