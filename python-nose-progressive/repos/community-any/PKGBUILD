# Maintainer: David Runge <dave@sleepmap.de>
_name=nose-progressive
pkgname=python-nose-progressive
pkgver=1.5.1
pkgrel=3
pkgdesc="Give your tests a progress bar and smarter tracebacks in 3 lines"
arch=('any')
url="https://github.com/erikrose/nose-progressive"
license=('MIT')
depends=('python-blessings' 'python-nose' 'python-setuptools')
# LICENSE missing in pypi source: https://github.com/erikrose/nose-progressive/issues/89
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"
        "https://raw.githubusercontent.com/erikrose/nose-progressive/master/LICENSE")
sha512sums=('75e10c21b734dbf66842c8c0f2514db27a4c5e1fcf893371072836c5b1c6cda9ad2ac12d847387a730982934397492c238b9d471dcd4cf584a9a43896b5ced17'
            '263b406ff153e82c95fce08921a4b6dc0f627b374b2051a35490d37df8bdbf8269a04b3c11c84d8a16e19f7d60759711a30472032d4fe8f4942e0790f1d69d1e')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

# tests broken: https://github.com/erikrose/nose-progressive/issues/90
#check() {
#  cd "$pkgname-$pkgver"
#  export PYTHONPATH=build:${PYTHONPATH}
#  nosetests noseprogressive
#}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 "${srcdir}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
