# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: alexiobash < me (at) alexiobash (dot) com >

pkgname=wpscan
pkgver=2.9.4
pkgrel=2
epoch=1
pkgdesc='Black box WordPress vulnerability scanner'
url='http://wpscan.org'
arch=('x86_64')
license=('custom:WPScan')
depends=('ruby-bundler' 'libxslt' 'libyaml' 'curl')
makedepends=('unzip')
options=('!strip' '!emptydirs')
install=wpscan.install
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/wpscanteam/wpscan/archive/${pkgver}.tar.gz)
sha256sums=('ad066b48565e82208d5e0451891366f6a9b9a3648d149d14c83d00f4712094d3')
sha512sums=('1ca5fd2e8bc5a7a368576351bce465039a7fdcea33d4abe86a168dbd2725922ae40362264a2f71e0839435e5085a9ebe74415bd3d40c316e3b4d654122a87e67')

prepare() {
  cd ${pkgname}-${pkgver}
  unzip -o data.zip
  rm data.zip
  # replace cache location with local user share
  sed "s|ROOT_DIR, 'cache'|ENV['HOME'] + '/.local/share/${pkgname}/cache'|" -i lib/common/common_helper.rb
  sed "s|ROOT_DIR, 'log.txt'|ENV['HOME'] + '/.local/share/${pkgname}/log.txt'|" -i lib/common/common_helper.rb
  bundle config build.nokogiri --use-system-libraries
}

build() {
  cd ${pkgname}-${pkgver}
  bundle install -j"$(nproc)" --path vendor/bundle --without development test
}

package() {
  cd ${pkgname}-${pkgver}

  install -d "${pkgdir}/opt/${pkgname}"
  cp -ra --no-preserve=owner . "${pkgdir}/opt/${pkgname}"

  install -d "${pkgdir}/usr/bin"
  cat > "${pkgdir}/usr/bin/${pkgname}" << EOF
#!/bin/sh
BUNDLE_GEMFILE=/opt/${pkgname}/Gemfile bundle exec ruby /opt/${pkgname}/${pkgname}.rb "\$@"
EOF
  chmod 755 "${pkgdir}/usr/bin/${pkgname}"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 CHANGELOG.md DISCLAIMER.md README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  find "${pkgdir}" -name gem_make.out -delete
}

# vim: ts=2 sw=2 et:
