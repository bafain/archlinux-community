# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-prctl
pkgname=('python-prctl' 'python2-prctl')
pkgver=1.7
pkgrel=2
pkgdesc='Python(ic) interface to the linux prctl syscall'
arch=('x86_64')
license=('GPL')
url='https://github.com/seveas/python-prctl'
makedepends=('libcap' 'python' 'python2')
checkdepends=('iputils' 'procps-ng')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/seveas/python-prctl/archive/v$pkgver.tar.gz")
sha512sums=('f2a18fc8e9ef469722c6618d5dd6b7b558d4060783e6fb3a8dc868d9ed3fa21ade371f94a4538a24dca92a71f623a96638c96882d9cf0d3d499a9f12dae8538a')

prepare() {
  cp -a python-prctl-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-prctl-$pkgver
  python setup.py build

  cd "$srcdir"/python-prctl-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-prctl-$pkgver
  python -m unittest discover || warning "Tests failed"

  cd "$srcdir"/python-prctl-$pkgver-py2
  python2 -m unittest discover || warning "Tests failed"
}

package_python-prctl() {
  depends=('python' 'libcap')

  cd python-prctl-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-prctl() {
  depends=('python2' 'libcap')

  cd python-prctl-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
