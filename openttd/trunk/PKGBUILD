# $Id$
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>

pkgname=openttd
pkgver=1.8.0
pkgrel=2
pkgdesc='An engine for running Transport Tycoon Deluxe.'
arch=('x86_64')
url='https://www.openttd.org'
license=('GPL')
depends=('libpng' 'sdl' 'icu' 'fontconfig' 'lzo' 'hicolor-icon-theme' 'desktop-file-utils' 'xz')
optdepends=('openttd-opengfx: free graphics' 
            'openttd-opensfx: free soundset')
source=("https://binaries.openttd.org/releases/${pkgver}/${pkgname}-${pkgver}-source.tar.xz")
sha256sums=('c2d32d9d736d27202a020027a3729ae763f5432ae6f424891e57a4095eeb087f')

build() {
  cd ${pkgname}-${pkgver} 

  # http://site.icu-project.org/download/61#TOC-Migration-Issues
  CXXFLAGS+=' -DU_USING_ICU_NAMESPACE=1'

 ./configure \
    --prefix-dir=/usr \
    --binary-name=${pkgname} \
    --binary-dir=bin \
    --data-dir=share/${pkgname} \
    --install-dir="${pkgdir}" \
    --doc-dir=share/doc/${pkgname} \
    --menu-name="OpenTTD"

  make
}

package() {
  cd ${pkgname}-${pkgver} 

  make install
}
