# $Id$
# Maintainer: Christian Rebischke <chris.rebischke at archlinux.org>
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Greg Sutcliffe <facter_aur (at) emeraldreverie.orgr>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>
# Contributor: Thomas S Hatch <thatch45 (at) Gmail.com>
# Contributor: Dave Simons <miouhpi (at) Gmail (dot) com>
# Contributor: Niels Abspoel <aboe76 (at) Gmail (dot) com>

pkgname=facter
pkgver=3.11.3
pkgrel=1
pkgdesc="Collect and display system facts"
arch=('x86_64')
url="http://puppetlabs.com/facter"
license=('APACHE')
depends=('ruby' 'yaml-cpp' 'boost-libs' 'curl')
makedepends=('boost' 'cmake' 'java-environment>=10' 'leatherman' 'cpp-hocon' 'java-environment-common')
optdepends=('java-environment: jruby support')
replaces=('cfacter')
source=("https://github.com/puppetlabs/facter/archive/${pkgver}.tar.gz"
        'java10.patch')
sha512sums=('6b2dbc5a0bc1c70739a4c0987311ef0891a27f64ed1b45a46cc20a37e6d1d24fda0481976fbf743f83f10d2014f6e0df0e2b5cc9eba19924e831652e1a8417ef'
            '0db7f0a636bb31850b4440b3e7acc273cefc4091bd177ffaeb51434f80b142b9a96730d2a31a6ef48bf94dbd1cb21943a18e4f329a75b87718efd06a7124260b')

prepare() {
  cd "${pkgname}-${pkgver}"

  # Replace rb_data_object_alloc symbol with rb_data_object_wrap
  # https://tickets.puppetlabs.com/browse/FACT-1291
  sed -i 's/rb_data_object_alloc/rb_data_object_wrap/g' \
    $( grep -rl rb_data_object_alloc lib/src/ruby )
  patch -p1 < ${srcdir}/java10.patch
}

build() {
  cd "${pkgname}-${pkgver}"

  # Do not treat warnings as errors
  CXXFLAGS+=' -Wno-error'

  JAVA_HOME=/usr/lib/jvm/default cmake -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make install DESTDIR="${pkgdir}"

  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
