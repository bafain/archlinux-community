# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-cachetools
pkgname=(python-cachetools python2-cachetools)
pkgver=2.1.0
pkgrel=2
pkgdesc="Extensible memoizing collections and decorators"
url="https://github.com/tkem/cachetools"
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/tkem/cachetools/archive/v$pkgver.tar.gz")
sha512sums=('359f0b57c34af96d4c8cfac19daa591e357db19c592ce89449001593f3044affb6e6109e1d2b63666dd8c95c6c496387032759d2ef01e5d5ae93eef39df15d72')

prepare() {
  sed -i 's/<2/<3/' cachetools-$pkgver/setup.py
  cp -a cachetools-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/cachetools-$pkgver
  python setup.py build

  cd "$srcdir"/cachetools-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/cachetools-$pkgver
  python setup.py pytest

  cd "$srcdir"/cachetools-$pkgver-py2
  python2 setup.py pytest
}

package_python-cachetools() {
  depends=('python')

  cd cachetools-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-cachetools() {
  depends=('python2')

  cd cachetools-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
