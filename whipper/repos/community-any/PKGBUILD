# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor: Frederik "Freso" S. Olesen <archlinux@freso.dk>
# Contributor: Bastien Traverse <firstname at lastname dot email>
# Contributor: Samantha Baldwin <fuhsaz+git@cryptic.li>
# Contributor: Caleb Reach <jtxx000@gmail.com>
# Contributor: Felix Yan <felixonmars@gmail.com>
# Contributor: Karol "Kenji Takahashi" Woźniak <kenji.sx>
# Contributor: Mantas Mikulėnas <grawity@gmail.com>

pkgname=whipper
pkgver=0.7.0
pkgrel=1
pkgdesc="A Unix CD ripper aiming for accuracy over speed -- forked from morituri"
arch=('any')
url="https://github.com/JoeLametta/whipper"
license=('GPL3')
depends=(
    'accuraterip-checksum'          # for accuraterip-checksum calculation
    'cddb-py'                       # for showing but not using disc info if not in MusicBrainz (issue #28)
    'libcdio-paranoia'              # for the actual ripping
    'cdrdao'                        # for session, TOC, pregap, and ISRC extraction
    'libsndfile'                    # for reading wav files (pulls in flac, also required for reading flac files)
    'mutagen'                       # for metadata handling
    'python2-musicbrainzngs'        # for metadata lookup
    'python2-pycdio'                # for storing drive identification in config file
    'python2-setuptools'            # for plugin support
    'sox'                           # for track peak detection'
    'python2-gobject2'              # Bugfix FS#54074
    'python2-requests'
    )
checkdepends=(
    'python2-twisted'
    )
conflicts=('morituri')
source=("${pkgname}-v${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha512sums=('b4bf4ca64521d8600288addecec6cbd8e43d1ff9a6b9a60872eff1bd5a2b6966af036d6467ba09273ac78aa01d8cac3be29a0e2c4e6600829b9988a74ef8cd88')

check() {
    cd "$srcdir/${pkgname}-${pkgver}"
    python2 -m unittest discover
}

package() {
    cd "$srcdir/${pkgname}-${pkgver}"
    python2 setup.py install --root="${pkgdir}"/ --optimize=1
}
