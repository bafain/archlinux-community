# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=serd
pkgver=0.28.0
pkgrel=1
pkgdesc="A lightweight C library for RDF syntax which supports reading and writing Turtle and NTriples."
arch=('x86_64')
url="https://drobilla.net/software/serd/"
license=('custom:ISC')
depends=('glibc')
makedepends=('python')
source=("https://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2"{,.sig})
sha512sums=('b25cc70af17ff821812ab2a70c2812c1713ff811a0f18fb5d5c54f3b67e0c68293aabedfacd687afe6b7acdfd81602ef81764d7e11bc0d933de738bc6d4fce3e'
            'SKIP')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3')

prepare() {
  cd "${pkgname}-${pkgver}"
  # remove call to local ldconfig
  sed -i "/ldconfig/d" wscript
}

build() {
  cd "${pkgname}-${pkgver}"
  python ./waf configure --prefix=/usr \
                         --mandir=/usr/share/man
  python ./waf
}

package() {
  cd "${pkgname}-${pkgver}"
  python ./waf install --destdir="${pkgdir}"
  # license
  install -vDm644 COPYING \
    "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
  # docs
  install -t "$pkgdir/usr/share/doc/${pkgname}" \
    -vDm644 {AUTHORS,NEWS,README.md}
}

# vim:set ts=2 sw=2 et:
