# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: eolianoe <eolianoe [at] googlemail [dot] com>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Thomas Weißschuh <thomas t-8ch de>
# Contributor: Étienne Deparis <etienne [at] depar [dot] is>
# Contributor: Daniel M. Capella <polyzen@archlinux.info>

_name=khal
pkgname=khal
pkgver=0.9.9
pkgrel=1
pkgdesc='CLI calendar application build around CalDAV'
arch=('any')
url="http://lostpackets.de/khal/"
license=('MIT')
makedepends=('python-setuptools-scm' 'python-sphinxcontrib-newsfeed')
depends=('python-atomicwrites' 'python-click' 'python-click-log'
'python-configobj' 'python-dateutil' 'python-icalendar' 'python-pytz'
'python-setuptools' 'python-tzlocal' 'python-urwid' 'python-xdg')
optdepends=('python-setproctitle: Set process name'
            'vdirsyncer: Synchronize CalDAV calendars')
checkdepends=('python-pytest' 'python-freezegun' 'vdirsyncer')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('cc38d8852e5bd47e266e23d48fbafa66aa1f84d67022780b7b0c8c8d4f54860be06a4d2675c49a48b4551fbdce1ec99762fd3b12a2ada69a0768d200697ef03e')

prepare() {
  cd "${pkgname}-${pkgver}"
  # removing dateutil pinning: https://github.com/pimutils/khal/issues/780
  sed -e 's/python-dateutil\<\=2\.6\.1/python-dateutil/' \
      -i setup.py
  # disabling failing tests: https://github.com/pimutils/khal/issues/801
  sed -e 's/test_event_rrule_recurrence_id/disabled_&/' \
      -e 's/test_this_and_prior/disabled_&/' \
      -e 's/test_event_rrule_this_and_future/disabled_&/' \
      -e 's/test_event_rrule_this_and_future_multi_day_shift/disabled_&/' \
      -e 's/test_two_calendars_same_uid/disabled_&/' \
      -i tests/backend_test.py
  sed -e 's/test_import/disabled_&/' \
      -i tests/controller_test.py
  sed -e 's/test_expand_invalid_exdate/disabled_&/' \
      -e 's/test_expand_dtr_exdatez/disabled_&/' \
      -e 's/test_expand_rrule_notz_until_z/disabled_&/' \
      -e 's/test_until_notz/disabled_&/' \
      -i tests/khalendar_utils_test.py
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
  cd doc
  make man PYTHONPATH="${srcdir}/${pkgname}-${pkgver}"
}

check() {
  cd "${pkgname}-${pkgver}"
  py.test
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # executables
  install -vDm 755 "bin/${pkgname}" "$pkgdir/usr/bin/${pkgname}"
  install -vDm 755 "bin/i${pkgname}" "$pkgdir/usr/bin/i${pkgname}"
  # configuration
  install -vDm 644 "${pkgname}.conf.sample" \
    "${pkgdir}/usr/share/doc/${pkgname}/examples/${pkgname}.conf"
  # man page
  install -vDm 644 doc/build/man/${pkgname}.1 \
    -t "${pkgdir}/usr/share/man/man1/"
  # zsh completion
  install -vDm 644 "misc/__${pkgname}" \
    "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  # license
  install -vDm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS.txt,{CHANGELOG,CONTRIBUTING,README}.rst}
}

