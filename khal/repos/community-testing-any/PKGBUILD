# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: eolianoe <eolianoe [at] googlemail [dot] com>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Thomas Weißschuh <thomas t-8ch de>
# Contributor: Étienne Deparis <etienne [at] depar [dot] is>
# Contributor: Daniel M. Capella <polyzen@archlinux.info>

pkgname=khal
pkgver=0.9.8
pkgrel=2
pkgdesc='CLI calendar application build around CalDAV'
arch=('any')
url='http://lostpackets.de/khal/'
license=('MIT')
makedepends=('python-setuptools-scm' 'python-sphinxcontrib-newsfeed')
depends=('python-click'
         'python-click-log'
         'python-icalendar'
         'python-urwid'
         'python-xdg'
         'python-pytz'
         'python-dateutil'
         'python-configobj'
         'python-atomicwrites'
         'python-tzlocal')
optdepends=('python-setproctitle: Set process name'
            'vdirsyncer: Synchronize CalDAV calendars')
checkdepends=('python-pytest' 'python-freezegun' 'vdirsyncer')
source=("https://lostpackets.de/${pkgname}/downloads/${pkgname}-${pkgver}.tar.gz")
sha512sums=('4c47bd18e7458ca7a6adf0684872923b4e49e6bfe481269cf9f77dee29dd47dbacf0d557890080618b4d3f08766fc8fae32de7682167642f0705e5b3b6b5dc01')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
  cd doc
  make man PYTHONPATH="${srcdir}/${pkgname}-${pkgver}"
}

check() {
  cd "${pkgname}-${pkgver}"
  # When using a clean chroot,
  # one has to choose a proper locale to run the tests
  if [ "${LANG}" == "C" ]; then
    LANG=$(locale -a | grep utf8 | head -n1) py.test
  else
    py.test
  fi
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # executables
  install -vDm755 "bin/${pkgname}" "$pkgdir/usr/bin/${pkgname}"
  install -vDm755 "bin/i${pkgname}" "$pkgdir/usr/bin/i${pkgname}"
  # configuration
  install -vDm644 "${pkgname}.conf.sample" \
    "${pkgdir}/usr/share/doc/${pkgname}/examples/${pkgname}.conf"
  # man page
  install -vDm644 doc/build/man/${pkgname}.1 \
    "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  # zsh completion
  install -vDm644 "misc/__${pkgname}" \
    "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  # license
  install -vDm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {CHANGELOG,CONTRIBUTING,README}.rst \
    -vDm644 AUTHORS.txt
}

