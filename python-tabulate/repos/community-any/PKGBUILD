# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
_name=tabulate
pkgbase=python-tabulate
pkgname=('python-tabulate' 'python2-tabulate')
pkgver=0.8.2
pkgrel=2
pkgdesc="Pretty-print tabular data in Python, a library and a command-line utility."
arch=('any')
url="https://bitbucket.org/astanin/python-tabulate"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('9e34fb84e16f4b2c1378c0f53c5ef803438fcae7bc1637ac8975f358a11653f641bba0ea19529858e8e62aa45bb5bccd3b6f0492fd2d9d9c9a3bf963dd1ac0a7')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
  python2 setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="${PYTHONPATH}:${PWD}"
  # disabling failing tests:
  # https://bitbucket.org/astanin/python-tabulate/issues/149
  py.test -k 'not test_dict_like \
              and not test_list_of_dicts \
              and not test_List_of_dicts_keys \
              and not test_list_of_dicts_firstrow \
              and not test_list_of_dicts_dict_of_headers \
              and not test_list_of_lists_with_supplied_index \
              and not test_list_of_lists_with_index_firstrow \
              and not test_isconvertible_on_set_values'
  py.test2 -k 'not test_list_of_lists_with_supplied_index \
               and not test_list_of_lists_with_index_firstrow'
}

package_python-tabulate() {
  depends=('python-setuptools')
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_python2-tabulate() {
  depends=('python2-setuptools')
  cd "$pkgbase-$pkgver"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
