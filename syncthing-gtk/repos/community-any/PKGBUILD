# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>
# Contributor: kozec <kozec at kozec dot com>

pkgname=syncthing-gtk
pkgver=0.9.4
pkgrel=1
epoch=1
pkgdesc='GTK3 based GUI and notification area icon for Syncthing'
arch=('any')
url='https://github.com/syncthing/syncthing-gtk'
license=('GPL2')
depends=('syncthing>=0.14.48' 'gtk3' 'libnotify' 'python2-bcrypt'
        'python2-cairo' 'python2-dateutil' 'python2-gobject')
makedepends=('python2-setuptools')
optdepends=('python2-caja: file manager integration for Caja'
            'python2-nautilus: file manager integration for Nautilus'
            'nemo-python: file manager integration for Nemo')
source=($pkgname-$pkgver.tar.gz::https://github.com/syncthing/$pkgname/archive/v$pkgver.tar.gz
        kde-statusicon.patch)
sha256sums=('586dc60460973c2664cee9f0fe6360b635a5b04278924f61c4bc9041723549f2'
            '109d8c970045e60251fc64865f05322b23a0995ee6725be02905941cb3a1ae0d')

prepare() {
  cd $pkgname-$pkgver

  # Enable Gtk.StatusIcon in KDE
  patch -Np1 -i ../kde-statusicon.patch
}

build() {
  cd $pkgname-$pkgver
  python2 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
