# $Id: PKGBUILD 162811 2016-02-20 15:24:54Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=duktape
pkgver=2.2.1
pkgrel=1
pkgdesc='Embeddable Javascript engine'
arch=('x86_64')
url='http://duktape.org/'
makedepends=('setconf')
license=('MIT')
source=("http://duktape.org/duktape-$pkgver.tar.xz" # no https available
        'duktape.pc')
sha256sums=('3abe2eed2553305262b892c98f550bb1a94cf4fd73b51dc5c176fe08e7ade7f2'
            'ae6dc7b36bf2c84ea6e7ef2ec247b6a6af048f495643d38531bb81a72c6fa340')

prepare() {
  cd "$pkgname-$pkgver"

  mv Makefile.sharedlibrary Makefile
  sed 's/-Wall -Wextra/$(CFLAGS)/g' -i Makefile
  setconf Makefile INSTALL_PREFIX="$pkgdir/usr"
  setconf "$srcdir/duktape.pc" Version="$pkgver"
}

build() {
  CFLAGS="$CFLAGS -D DUK_USE_FASTINT -w" make -C "$pkgname-$pkgver"
}

package() {
  cd "$pkgname-$pkgver"

  install -d "$pkgdir/usr/lib" "$pkgdir/usr/include"
  make install
  install -Dm644 "$srcdir/duktape.pc" "$pkgdir/usr/lib/pkgconfig/duktape.pc"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/svaarala/duktape
# vim: ts=2 sw=2 et:
