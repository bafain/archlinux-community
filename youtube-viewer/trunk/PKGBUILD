# $Id$
# Maintainer: Alad Wenter <alad@mailbox.org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Daniel Wallace < danielwallace at gtmanfred dot com>
# Contributor: Trizen <echo dHJpemVueEBnbWFpbC5jb20K | base64 -d>

pkgname=youtube-viewer
epoch=1
pkgver=3.3.4
pkgrel=2
pkgdesc='Command line utility for viewing YouTube videos'
arch=('any')
url='https://github.com/trizen/youtube-viewer'
license=('GPL3')
depends=('perl>=5.16.0' 'perl-data-dump' 'perl-json' 'perl-lwp-protocol-https' 'perl-libwww')
makedepends=('perl-module-build')
optdepends=('perl-json-xs: faster JSON to HASH conversion'
            'perl-text-charwidth: to print the results in a fixed-width format (-W)'
            'perl-term-readline-gnu: for better STDIN support'
            'youtube-dl: for playing videos with encrypted signatures'
            'wget: for downloading videos with wget'
            'vlc: for playing the videos with VLC'
            'mpv: for playing the videos with MPV (recommended)'
            'gtk2-perl: for the GTK GUI'
            'perl-file-sharedir: for the GTK GUI')
source=("$pkgname-$pkgver.tar.gz::https://github.com/trizen/$pkgname/archive/$pkgver.tar.gz")
sha256sums=('f65d27c0f7a7e51a4d7f89ab2cb27a064ff82fbb971add7aa5606f5dfeec0ab7')

build() {
    cd "$pkgname-$pkgver"
    perl Build.PL --installdirs vendor --gtk
}

check(){
    cd "$pkgname-$pkgver"
    ./Build test
}

package() {
    cd "$pkgname-$pkgver"
    ./Build install --destdir "$pkgdir"
    rm -r "$pkgdir"/usr/lib

    mkdir "$pkgdir"/usr/share/{applications,pixmaps}
    mv "$pkgdir"/usr/share/perl5/vendor_perl/auto/share/dist/WWW-YoutubeViewer/gtk-youtube-viewer.desktop \
        "$pkgdir"/usr/share/applications/gtk-youtube-viewer.desktop
    cp "$pkgdir"/usr/share/perl5/vendor_perl/auto/share/dist/WWW-YoutubeViewer/icons/gtk-youtube-viewer.png \
        "$pkgdir"/usr/share/pixmaps/gtk-youtube-viewer.png
}
