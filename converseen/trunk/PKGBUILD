# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: archtux <antonio.arias99999 at gmail.com>

pkgname=converseen
pkgver=0.9.7
pkgrel=1
pkgdesc="Batch image converter and resizer"
arch=('x86_64')
url="http://converseen.fasterland.net/"
license=('GPL3')
depends=('ghostscript' 'libheif' 'libmagick' 'libraw' 'librsvg' 'libwebp' 'libwmf' 'openexr' 'qt5-base')
makedepends=('cmake' 'imagemagick' 'qt5-tools')
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2")
sha256sums=('174d306c68cea01a48dd70a9711d58120a8d652b284051a547287a3a5703fb26')

build() {
  mkdir build
  cd build
  cmake ../$pkgname-$pkgver \
          -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/usr 
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
