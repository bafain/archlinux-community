# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=docopt
pkgver=0.6.2
pkgrel=1
pkgdesc='Command line arguments parser for C++11 and later'
arch=('x86_64')
url='https://github.com/docopt/docopt.cpp/'
license=('MIT')
makedepends=('cmake' 'git' 'ninja' 'setconf')
source=('git+https://github.com/docopt/docopt.cpp.git#commit=4f491249e6def236937dbfac7602852e7d99aff8'
        'docopt.pc')
sha256sums=('SKIP'
            'bec63bdb0313610f0d8fc55b508a0c3a4c593fa58c4c6a7241326a9cfb909853')

prepare() {
  setconf docopt.pc Version "$pkgver"
}

build() {
  mkdir -p build
  cd build
  cmake ../docopt.cpp \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -GNinja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 docopt.cpp/LICENSE-MIT "$pkgdir/usr/share/licenses/docopt/LICENSE"
  install -Dm644 docopt.pc "$pkgdir/usr/lib/pkgconfig/docopt.pc"
}

# vim: ts=2 sw=2 et:
