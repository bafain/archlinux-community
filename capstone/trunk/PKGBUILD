# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: M0Rf30

pkgbase=capstone
pkgname=('capstone' 'python-capstone' 'python2-capstone')
pkgver=3.0.5
pkgrel=1
pkgdesc='Lightweight multi-platform, multi-architecture disassembly framework'
url='https://www.capstone-engine.org/index.html'
arch=('x86_64')
license=('BSD')
makedepends=('glibc' 'python' 'python-setuptools' 'python2' 'python2-setuptools')
options=('staticlibs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/aquynh/capstone/archive/${pkgver}.tar.gz)
sha256sums=('913dd695e7c5a2b972a6f427cb31f2e93677ec1c38f39dda37d18a91c70b6df1')
sha512sums=('4d953dd34866046afd4c8896c307f8fdd514ea1331720cb643a6b7095666dbecf6a16264c15c9883a6ed5e06d1332c1e52c5a23bd9dc3cc6493592bc9636bd33')

build() {
  cd ${pkgbase}-${pkgver}
  make
}

package_capstone() {
  depends=('glibc')
  provides=('libcapstone.so')
  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 docs/README -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.TXT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_python-capstone() {
  depends=('capstone' 'python')
  cd ${pkgbase}-${pkgver}/bindings/python
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 ../../LICENSE.TXT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_python2-capstone() {
  depends=('capstone' 'python2')
  cd ${pkgbase}-${pkgver}/bindings/python
  python2 setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 ../../LICENSE.TXT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
