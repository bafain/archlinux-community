# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-anything
pkgver=0.0.3
pkgrel=1
pkgdesc="Deepin Anything file search tool"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-anything"
license=('GPL3')
depends=('dkms' 'qt5-base')
groups=('deepin' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-anything/archive/$pkgver.tar.gz"
        deepin-anything-server.sysusers
        fix-include.patch)
sha512sums=('8a506ba57d6a2ac9c9089719d18013fbde6443cb4f63de79498269e728b7e8725b4e06e7029ae2d2242aaac955c63e56c680fdd18e3fb0d72e9953a9fc5e8402'
            '0ff6a6de1fbfb0c33eaac511b989da321a9e43ece92708af88aee34ad1a05e55572713b1290bc2705d70b91dc7bec4fb4abd3dc664a0abe01de27d88bd9e9c85'
            '55498f8b14e1b233b4052d516b880f4c27797eede46481135dcf76808e9f00f4ac4fdd0e2e2d7d478d335fb12b7137ca1bef0745e1c8b4195e025b81eec94d21')

prepare() {
  cd deepin-anything-$pkgver
  patch -p1 -i ../fix-include.patch
  sed -i 's|^systemd_service.path.*|systemd_service.path = /usr/lib/systemd/system|' server/src/src.pro
}

build() {
  cd deepin-anything-$pkgver
  make VERSION=$pkgver
}

package() {
  cd deepin-anything-$pkgver
  make VERSION=$pkgver DESTDIR="$pkgdir" install

  cp debian/deepin-anything-dkms.dkms "$pkgdir"/usr/src/deepin-anything-$pkgver/dkms.conf
  install -Dm644 ../deepin-anything-server.sysusers "$pkgdir/usr/lib/sysusers.d/deepin-anything-server.conf"
}
