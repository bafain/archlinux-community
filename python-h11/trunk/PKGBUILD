# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgbase=python-h11
pkgname=(python-h11 python2-h11)
pkgver=0.7.0
pkgrel=1
pkgdesc="A pure-Python, bring-your-own-I/O implementation of HTTP/1.1"
url="https://github.com/njsmith/h11"
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("https://pypi.io/packages/source/h/h11/h11-$pkgver.zip")
sha512sums=('abf9b8f7f12870804539049c3bfebbbe476e394636aed9967aa725d501fff983f28c7f14b4b882c17914d00fe3e74417ff8922128d0e4e07bf042a7db6d0b100')

prepare() {
  cp -a h11-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/h11-$pkgver
  python setup.py build

  cd "$srcdir"/h11-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/h11-$pkgver
  python setup.py pytest

  cd "$srcdir"/h11-$pkgver-py2
  python2 setup.py pytest
}

package_python-h11() {
  depends=('python')

  cd h11-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-h11() {
  depends=('python2')

  cd h11-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
