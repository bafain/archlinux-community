# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: sysrq

pkgname=picard
pkgver=2.0.4
pkgrel=1
pkgdesc='Official MusicBrainz tagger'
url='https://picard.musicbrainz.org/'
license=('GPL')
arch=('x86_64')
depends=('python-pyqt5' 'python-mutagen')
optdepends=('chromaprint: fingerprinting'
            'python-discid: cd lookup')
makedepends=('python-discid' 'git' 'python-setuptools')
source=("http://ftp.musicbrainz.org/pub/musicbrainz/${pkgname}/${pkgname}-${pkgver}.tar.gz")
#source=("git+https://github.com/metabrainz/picard.git#commit=3a41cebdbcff9af9791575d2e555c59201b55902")
sha256sums=('0c6ee49e0c6dd61793e5705823570055e55e7601d665c9be75271c8b2ff54337')

build() {
	cd "${srcdir}/picard-release-${pkgver}"
	sed "s/â€˜/'/g" -i setup.cfg
	python setup.py config
}

package() {
	cd "${srcdir}/picard-release-${pkgver}"
	python setup.py install --root="${pkgdir}"
	rm -fr "${pkgdir}"/usr/lib/python*/site-packages/picard-*.egg-info
}
