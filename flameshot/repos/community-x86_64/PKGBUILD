# Maintainer: Jiachen YANG <farseerfc@archlinux.org>
# Contributor: xyzzy <628208@gmail.com>

pkgname=flameshot
pkgver=0.6.0
pkgrel=1
pkgdesc="Powerful yet simple to use screenshot software"
arch=('x86_64')
url="https://github.com/lupoDharkael/flameshot"
license=('GPL')
depends=('qt5-svg' 'hicolor-icon-theme')
makedepends=('make' 'qt5-tools')
source=("https://github.com/lupoDharkael/flameshot/archive/v${pkgver}.tar.gz"
	)
sha256sums=('61b3a1969d6e17d80d5d90a3fce53ca5ae78fa21f9a45e5a19b0b32ea815a589')
install="${pkgname}.install"

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ### fix locale setting
  #sed -i 's|QLocale::system[(][)][.]language[(][)]|QLocale()|' src/main.cpp

  ## add a version number from pkgver
  sed -i "s|TAG_VERSION = .*|TAG_VERSION = v${pkgver}|" flameshot.pro 
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  qmake CONFIG+=packaging BASEDIR="${pkgdir}"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make INSTALL_ROOT="${pkgdir}" install
}
