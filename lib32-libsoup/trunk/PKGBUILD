# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Maximilian Stein <theoddbird@posteo.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-libsoup
pkgver=2.62.3
pkgrel=1
pkgdesc='HTTP client/server library for GNOME'
arch=('x86_64')
url='https://wiki.gnome.org/Projects/libsoup'
license=('LGPL')
depends=('lib32-glib-networking' 'lib32-krb5' 'lib32-libxml2' 'lib32-sqlite'
         'libsoup')
makedepends=('git' 'gobject-introspection' 'intltool' 'python' 'vala')
source=("git+https://gitlab.gnome.org/GNOME/libsoup.git#tag=${pkgver}")
sha256sums=('SKIP')

prepare() {
  cd libsoup

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd libsoup

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  make DESTDIR="${pkgdir}" -C libsoup install
  rm -rf "${pkgdir}"/usr/{include,share}
}

# vim: ts=2 sw=2 et:
