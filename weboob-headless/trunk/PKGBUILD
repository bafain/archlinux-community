# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkgname=weboob
pkgname=weboob-headless
pkgver=1.3
pkgrel=1
pkgdesc="Provides several applications to interact with a lot of websites. Headless version, for use in e.g. Cozy/Kresus."
arch=('any')
url="http://weboob.org/"
license=('AGPL3')
depends=('python2-dateutil'
         'python2-feedparser'
         'python2-lxml'
         'python2-prettytable'
         'python2-requests'
         'python2-cssselect'
         'python2-html2text'
         'python2-unidecode'
         'python2-pillow'
         'python2-simplejson'
         'python2-yaml'
         'python2-mechanize'
         'python2-google-api-python-client'
         'python2-futures'
         'python2-six')
makedepends=('python2-setuptools')
conflicts=('weboob')
source=("https://pypi.io/packages/source/w/${_pkgname}/${_pkgname}-${pkgver}+1.tar.gz")
sha256sums=('eb38fdd48f93cdae3e67042dfb34640159e4978616f74679bac5378ae1f962c4')

build() {
    cd ${_pkgname}-${pkgver}
    python2 setup.py build --no-qt --no-hildon
}

package() {
    cd ${_pkgname}-${pkgver}
    python2 setup.py install --no-xdg --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
}
