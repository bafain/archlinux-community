# Maintainer: David Runge <dave@sleepmap.de>
pkgname=giada
pkgver=0.15.0
pkgrel=2
pkgdesc="A free, minimal, hardcore audio tool for DJs, live performers and electronic musicians"
arch=('x86_64')
url="https://www.giadamusic.com/"
license=('GPL3')
groups=('pro-audio')
depends=('fltk' 'jansson' 'libpulse' 'libxpm' 'rtmidi')
makedepends=('gendesk' 'imagemagick')
source=("$pkgname-$pkgver.tar.gz::https://github.com/monocasual/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('49045691871d31f5870c153f73327a67333ab9878f3aeb9a1e255994be21989a822e44e5681f429d97f5e42c1acc6df1c1658b6ce381b4343e1c0d5914198f41')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
  # XDG desktop file
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --name Giada \
          --categories "AudioVideo;Audio;Midi;Sequencer"
  convert "src/ext/${pkgname}.ico" "${pkgname}.png"
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --target=linux
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  # XDG integration
  install -vDm 644 "${pkgname}.desktop" -t "${pkgdir}/usr/share/applications"
  install -vDm 644 "${pkgname}.png" -t "${pkgdir}/usr/share/pixmaps"
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
