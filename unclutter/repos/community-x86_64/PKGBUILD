# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Callan Barrett <wizzomafizzo@gmail.com>
# Contributor: Anthony Martin <ality@pbrane.org>

pkgname=unclutter
pkgver=8
pkgrel=11
pkgdesc="A small program for hiding the mouse cursor"
url="http://www.ibiblio.org/pub/X11/contrib/utilities/unclutter-8.README"
arch=('x86_64')
license=('Public Domain')
depends=('libxext')
makedepends=('imake')
source=(https://www.ibiblio.org/pub/X11/contrib/utilities/$pkgname-$pkgver.tar.gz
	https://www.ibiblio.org/pub/X11/contrib/utilities/$pkgname-$pkgver.README)
sha512sums=('863dc6d0b15d865a3367d3528373b74a228f5960f2912dd6ad1d74c4feebe7cc637a2ec02cd8e88e0396a0fdaf0f7960dccff0d2b333b554d602d8376f868665'
            '27a0263042cc541a63e3ee6d8028cefbe9d9d8b47193828741e159f8d1782c5bc0f9f83a938b7465b3ed6b6e0f84037110ded8124f18d68d24022e7bde5ee7c4')

build() {
  cd "$srcdir"/$pkgname
  xmkmf -a
  make
}

package() {
  cd "$srcdir"/$pkgname
  make DESTDIR="$pkgdir" install
  install -Dm0644 unclutter.man "$pkgdir"/usr/share/man/man1/unclutter.1
  install -Dm0644 "$srcdir"/unclutter-8.README "$pkgdir"/usr/share/licenses/$pkgname/unclutter-8.README
}
