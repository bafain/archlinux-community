# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>

pkgname=dateutils
pkgver=0.4.4
pkgrel=1
pkgdesc='nifty command line date and time utilities; fast date calculations and conversion in the shell'
url='http://www.fresse.org/dateutils/'
arch=('x86_64')
license=('BSD')
depends=('glibc')
source=("$pkgname-$pkgver.tar.xz::https://github.com/hroptatyr/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz"
        "$pkgname-$pkgver.tar.asc::https://github.com/hroptatyr/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.asc")
validpgpkeys=('6CB0D61E23A4275CC2CFA16194C9A1AC82C9390E')
sha1sums=('575c5bbe6a17c6f5d8c5e9c1bad8133ac0131117'
          'SKIP')
sha512sums=('a5a5a2764993310ee1d5f7d76f34bc9dcb45b05762bbceb291d5d512d74a7aa2a8cdfb7ff3c006466bd0833b00a2c606dc913742f675fd08e367dae0974dea6f'
            'SKIP')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr --with-old-links=no
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    make
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    make check
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
    install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
