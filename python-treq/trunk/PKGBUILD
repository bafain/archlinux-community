# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-treq
pkgname=('python-treq' 'python2-treq')
pkgver=18.6.0
pkgrel=2
pkgdesc="A requests-like API built on top of twisted.web's Agent"
arch=('any')
license=('MIT')
url='https://github.com/twisted/treq'
makedepends=('python-setuptools' 'python2-setuptools' 'python-incremental' 'python2-incremental'
             'python-requests' 'python2-requests' 'python2-pyopenssl' 'python-pyopenssl'
             'python-service-identity' 'python2-service-identity' 'python-idna' 'python2-idna'
             'python-attrs' 'python2-attrs' 'python-twisted' 'python2-twisted' 'httpbin'
             'python2-httpbin')
checkdepends=('python-mock' 'python2-mock' 'python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/twisted/treq/archive/release-$pkgver.tar.gz"
        py3.7.diff)
sha512sums=('1d0d793725647c6b451853f166436040e49095fca43f6c74053f3ba18ec8f8ae0095ed78c7d141cc95dfba9674339ac44262943225bec7be0f0cb05253758688'
            '6806e8186c8742ac3c40e71d4e14b25b96d198995f03a8730755b3594b4a9e854fd799ceec4d6799571e14e02bca2d3797658b2f2eba38b76a8660156e44c84e')

prepare() {
  patch -d treq-release-$pkgver -p1 < py3.7.diff
  cp -a treq-release-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/treq-release-$pkgver
  python setup.py build

  cd "$srcdir"/treq-release-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/treq-release-$pkgver
  python setup.py pytest

  cd "$srcdir"/treq-release-$pkgver-py2
  python2 setup.py pytest
}

package_python-treq() {
  depends=('python-incremental' 'python-requests' 'python-pyopenssl' 'python-service-identity'
           'python-idna' 'python-attrs' 'python-twisted')

  cd treq-release-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-treq() {
  depends=('python2-incremental' 'python2-requests' 'python2-pyopenssl' 'python2-service-identity'
           'python2-idna' 'python2-attrs' 'python2-twisted')

  cd treq-release-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
