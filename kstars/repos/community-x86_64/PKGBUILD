# $Id$
# Maintainer: Felix Yan <felixonamrs@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kstars
pkgver=2.9.3
pkgrel=1
epoch=1
pkgdesc="Desktop Planetarium"
url="https://kde.org/applications/education/kstars/"
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(wcslib libraw kplotting knewstuff knotifyconfig qt5-datavis3d libindi hicolor-icon-theme)
makedepends=(extra-cmake-modules python kdoctools eigen xplanet)
optdepends=('xplanet: XPlanet support')
source=("https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('08c2ca5049848d8c9f75cca60734f72d4062ba6135e9931ec22ea7fc03aea283'
            'SKIP')
validpgpkeys=(259D9FCEE9175351965633696D9CE2AEE028C4F3) # Jasem Mutlaq <mutlaqja@ikarustech.com>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
