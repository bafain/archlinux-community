# Maintainer:
# Contributor: Alex Szczuczko <alex at szc dot ca>
# Contributor: Devin Cofer <ranguvar{AT]archlinux[DOT}us>
# Contributor: Imanol Celaya <ilcra1989@gmail.com>

pkgname=bmake
pkgver=20180222
pkgrel=1
pkgdesc="Portable version of the NetBSD 'make' build tool"
arch=('x86_64')
url="http://www.crufty.net/help/sjg/bmake.html"
license=('BSD')
source=("http://www.crufty.net/ftp/pub/sjg/bmake-$pkgver.tar.gz")
sha256sums=('6069ea5e72943fe3f7fb4ac43a189b99a90fd830bf6c84572f631f3aa7f507f9')

prepare() {
    cd "$srcdir/$pkgname"
    # Add missing bsd.*.mk symlinks
    _links_orig="dep doc init lib links man nls obj own prog subdir"
    _links_new="dep doc inc init lib links man nls obj own prog subdir"
    sed -i -e "s/$_links_orig/$_links_new/" mk/install-mk
}

build() {
    mkdir -p bmake-build
    cd bmake-build
    sh ../bmake/boot-strap --prefix=/usr
}

package() {
    cd bmake-build
    # Fix directory permissions on install
    install -dm0755 "$pkgdir/usr/bin"
    install -dm0755 "$pkgdir/usr/share/man/cat1"
    install -dm0755 "$pkgdir/usr/share/licenses/$pkgname"
    sh ../bmake/boot-strap --prefix=/usr --install-destdir="$pkgdir" op=install
    head -n70 ../bmake/main.c >"$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
