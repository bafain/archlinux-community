# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-desktop
pkgver=1.20.3
pkgrel=1
pkgdesc="Library with common API for various MATE modules"
url="http://mate-desktop.org"
arch=('x86_64')
license=('GPL' 'LGPL')
depends=('gtk3' 'startup-notification')
makedepends=('gobject-introspection' 'intltool')
groups=('mate')
conflicts=('mate-desktop-gtk3' 'mate-desktop-schemas')
replaces=('mate-desktop-gtk3' 'mate-desktop-schemas')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('da395f4f640e8b26d6704a5d63e00eeea0393f343cf5f92e2b211458390f5f8c')

build() {
	cd $pkgname-$pkgver
    ./configure \
        --prefix=/usr \
        --disable-schemas-compile

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd $pkgname-$pkgver

    make DESTDIR="${pkgdir}" install
}
