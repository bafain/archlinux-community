# $Id: PKGBUILD 152508 2015-12-10 18:09:47Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=agda-stdlib
pkgver=0.16
pkgrel=1
pkgdesc="Agda standard libraries"
url="https://github.com/agda/agda-stdlib"
license=("MIT")
arch=('x86_64')
depends=('agda')
makedepends=('ghc' 'haskell-filemanip')
options=('staticlibs')
install=$pkgname.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/agda/agda-stdlib/archive/v$pkgver.tar.gz"
        "standard-library.agda-lib")
sha512sums=('6058214dc75e0317d8eff881553b6dbd559fe1b0d33c497cfb04b58db42a162d3ddeb93939dad851f8cabd007f7d34968246106823153c03f60551c92e7c6f5a'
            '92fad2f9b941af4506bb0d5d9d533d09823df89f7530ee5d787b741816ba07e68f25b96d6653ae6ed949ec23484c1fba47a63cc6401311462a55b5e06cfc032c')

build() {
    cd $pkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanill
    runhaskell Setup build

    dist/build/GenerateEverything/GenerateEverything

    agda -i. -isrc Everything.agda
    agda --html -i. -isrc README.agda
}

package() {
    install -D -m644 standard-library.agda-lib "$pkgdir"/usr/share/agda/lib/standard-library.agda-lib

    cd $pkgname-$pkgver
    install -dm755 "$pkgdir"/usr/share/agda/lib/stdlib
    cp -pr Everything.agda* src/* "$pkgdir"/usr/share/agda/lib/stdlib
    install -D -m644 LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}
