# $Id: PKGBUILD 152508 2015-12-10 18:09:47Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=agda-stdlib
pkgver=0.15
pkgrel=1
pkgdesc="Agda standard libraries"
url="https://github.com/agda/agda-stdlib"
license=("MIT")
arch=('x86_64')
depends=('agda')
makedepends=('ghc' 'haskell-filemanip')
options=('staticlibs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/agda/agda-stdlib/archive/v$pkgver.tar.gz")
sha512sums=('4274b73ea2fa3bd778e345ee9459fa31c01b47ddf127b733efd572ca65a531ab49bdcd09cff2600b9229aad23cee55324653c62280421676036ff587f64abb4b')

build() {
    cd $pkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanill
    runhaskell Setup build

    dist/build/GenerateEverything/GenerateEverything

    agda -i. -isrc Everything.agda
    agda --html -i. -isrc README.agda
}

package() {
    cd $pkgname-$pkgver
    install -dm755 "$pkgdir"/usr/share/agda/lib/prim
    cp -pr Everything.agda* src/* "$pkgdir"/usr/share/agda/lib/prim
    install -D -m644 LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}
