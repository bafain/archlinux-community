# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=pantheon-code
pkgver=2.4.1.r735.ab6994df
pkgrel=1
pkgdesc='The Pantheon Text Editor'
arch=('x86_64')
url='https://launchpad.net/scratch'
license=('GPL3')
depends=('editorconfig-core-c' 'glib2' 'gtk3' 'gtksourceview3' 'libgee'
         'libpeas' 'pango' 'zeitgeist'
         'libgranite.so')
makedepends=('git' 'gobject-introspection' 'gtkspell3' 'intltool' 'libsoup'
             'meson' 'vala' 'vte3' 'webkit2gtk')
optdepends=('gtkspell3: Spell Check plugin'
            'libsoup: Pastebin plugin'
            'vala: Outline plugin'
            'vte3: Terminal plugin'
            'webkit2gtk: Browser Preview plugin')
provides=('scratch-text-editor')
conflicts=('scratch-text-editor')
replaces=('scratch-text-editor')
_commit='ab6994dfc3662c1f481688610b26c40170967a19'
source=("pantheon-code::git+https://github.com/elementary/code.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd pantheon-code

  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson pantheon-code build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
