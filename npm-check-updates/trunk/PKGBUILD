# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=npm-check-updates
pkgdesc='Find newer versions of dependencies than what your package.json or bower.json allows'
pkgver=2.14.2
pkgrel=1
arch=('any')
url='https://github.com/tjunnone/npm-check-updates'
license=('Apache')
depends=('semver' 'npm')
source=(http://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('932acb9c620898f6f858afd2ff8020b31299db5ced0bd28ffba68d4d9377d5c1899b8f86c87f9a36aec04afbc940ede239d83f73ad2a2dd655b4b4ce9f897481')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in npm semver; do
    rm -r $dep;
  done

  # Fix npm derp
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +
}
