# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=npm-check-updates
pkgdesc='Find newer versions of dependencies than what your package.json or bower.json allows'
pkgver=2.14.1
pkgrel=1
arch=('any')
url='https://github.com/tjunnone/npm-check-updates'
license=('Apache')
depends=('semver' 'npm')
source=(http://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('c4f90c7bd612c80d2caea57e6411fec64ceaa7027e41b5eb93f94d8ed5fa6f3c36eba42cb583f35a253296edc7b7f9e130bd24cbdacd1bf1d28179e5c8a61f59')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in npm semver; do
    rm -r $dep;
  done

  # Fix npm derp
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +
}
