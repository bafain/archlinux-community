# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jan-Erik Rediger <badboy at archlinux dot us>

pkgname=kcov
pkgver=35
pkgrel=1
pkgdesc="Code coverage tester for compiled programs, Python scripts and shell scripts"
arch=('x86_64')
url="http://simonkagstrom.github.com/kcov/index.html"
license=('GPL2')
depends=('elfutils' 'curl')
makedepends=('cmake' 'python')
source=("https://github.com/SimonKagstrom/kcov/archive/v${pkgver}.tar.gz")
sha512sums=('a9e04b782eb8444ee4d13be3ceed8a0edd68f0ec3b0e01535730b5d5590e43dbe3fd6485d03df9a57ec6ba9c0440b3208b47dc29e2175816bfe7112004df9fb3')

prepare() {
  cd $pkgname-$pkgver

  rm -rf build
  mkdir -p build
}

build() {
  cd $pkgname-$pkgver/build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver/build

  make DESTDIR="$pkgdir/" install
  make clean
}

# vim:set ts=2 sw=2 et:
