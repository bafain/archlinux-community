# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=guitarix2
pkgver=0.36.1
pkgrel=4
pkgdesc="A simple mono guitar amplifier and FX for JACK using Faust"
arch=('x86_64')
url="https://guitarix.org"
license=('GPL')
depends=('avahi' 'bluez-libs' 'boost-libs' 'fftw' 'gtkmm' 'jack' 'liblrdf' 'lilv' 'libsigc++' 'libsndfile' 'ttf-roboto' 'zita-convolver' 'zita-resampler')
makedepends=('python2' 'boost' 'eigen' 'gperf' 'intltool' 'lv2')
optdepends=('meterbridge: sound meters')
provides=('guitarix' 'gx_head')
conflicts=('guitarix' 'gx_head')
replaces=('guitarix' 'gx_head')
source=("https://download.sourceforge.net/project/guitarix/guitarix/$pkgname-$pkgver.tar.xz")
sha512sums=('7719c4e2a3ecb8194c75bfea571b7f5bc29a15ca0e6e9364085e77d97e2c638eb8dd3b5018228d4380a58ff99574a393e2c91c66837f40d245c1a310572300f4')

prepare() {
  cd "${pkgname%2}-${pkgver}"
  # fixing all (hopefully) relevant scripts: https://sourceforge.net/p/guitarix/bugs/43/
  sed -e 's/python/&2/g' \
    -i src/{faust,LV2/*,plugins,ladspa,gx_head}/wscript \
    -i src/gx_head/builder/make \
    -i src/gx_head/engine/gen_tube_tables \
    -i tools/{check_rpc,gcov.py,convert-0.06-format,ampsim/DK/quik.py,undiag,make_jsonrpc_methods} \
    -i tools/{coverage,dsp2cc,dsp2insert}
}

build() {
  cd "${pkgname%2}-${pkgver}"
  # build without faust support: https://sourceforge.net/p/guitarix/bugs/44/
  python2 waf configure --prefix=/usr \
                        --no-desktop-update \
                        --no-ldconfig \
                        --no-faust \
                        --shared-lib \
                        --lib-dev
  python2 waf build
}

package() {
  cd "${pkgname%2}-${pkgver}"
  python2 waf install --destdir="${pkgdir}"
}
