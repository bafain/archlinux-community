# $Id$
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Patrick Smits <mail@patricksmits.net>

pkgname=yaws
pkgver=2.0.6
pkgrel=1
pkgdesc='Web server/framework written in Erlang'
arch=('x86_64')
url='http://yaws.hyber.org/'
license=('BSD')
depends=('erlang-nox' 'pam')
backup=('etc/yaws/yaws.conf')
options=('!emptydirs')
install="$pkgname.install"
source=("http://yaws.hyber.org/download/$pkgname-$pkgver.tar.gz"
        "$pkgname.service")
sha256sums=('69f96f8b9bb574b129b0f258fb8437fdfd8369d55aabc2b5a94f577dde49d00e'
            '3628a0f6fdacc91c216cb782e3f3da638958cca6eee34a0a1e692482659109db')

prepare() {
  cd "$pkgname-$pkgver"

  # python2 fix
  sed -i 's:n/python:n/python2:' www/cgi{.yaws,-bin/foo.py}

  # treat warnings as warnings
  sed -i 's/-Werror//g' include.mk
}

build() {
  cd "$pkgname-$pkgver"

  export wwwdir=/srv/http/yaws
  ./configure --prefix=/usr --localstatedir=/var --sysconfdir=/etc
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -d "$pkgdir/usr/lib/erlang/lib"
  ln -s /usr/lib/yaws "$pkgdir/usr/lib/erlang/lib/$pkgname-$pkgver"
  install -Dm644 "$srcdir/$pkgname.service" \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 "$srcdir/$pkgname-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # path fix
  cp -ru examples/include "$pkgdir/usr/lib/yaws-$pkgver/examples/"
  mv "$pkgdir/srv/http/yaws/"* "$pkgdir/srv/http/www" || true
  rmdir --ignore-fail-on-non-empty "$pkgdir/srv/http/yaws"
  mv "$pkgdir/srv/http/www" "$pkgdir/srv/http/yaws" || true
}

# getver: raw.githubusercontent.com/klacke/yaws/master/vsn.mk
# vim: ts=2 sw=2 et:
