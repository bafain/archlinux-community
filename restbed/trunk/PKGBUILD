# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: Justin Wilcox <nat1192 at gmail dot com>
pkgname=restbed
pkgver=4.6+17+gdf867a8
pkgrel=1
pkgdesc="A framework for asynchronous RESTful functionality in C++11 applications"
arch=('x86_64')
url="https://github.com/Corvusoft/restbed"
license=('AGPL3')
depends=('openssl')
replaces=('restbed-latest')
conflicts=('restbed-latest')
makedepends=('cmake' 'asio' 'kashmir')
_commit=df867a858dddc4cf6ca8642da02720bd65ba239a
source=("https://github.com/Corvusoft/restbed/archive/${_commit}/$pkgname-$pkgver.tar.gz"
        "strand.patch"
        "async_read_until.patch")
sha256sums=('0b752078d75e4d7f1e896bb208186bb65e2e558ea531c6a05a086c7e7504e060'
            'a67baa5ffce44a851ba6bd47cbd04089665e52abc154b73063f51515e2094a51'
            '9ba679d22448bb567766dccf58f98744cc90e0a851a5ccd37596bb4790396049')

prepare() {
  cd "$srcdir/$pkgname-$_commit"

  # Necessary to build against asio 1.10.X
  patch -p1 < "$srcdir"/strand.patch
  # https://github.com/Corvusoft/restbed/pull/273
  patch -p1 < "$srcdir"/async_read_until.patch
}

build() {
  cd "$srcdir/$pkgname-$_commit"

  mkdir -p build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_SHARED=on
  make
}

package() {
  cd "$srcdir/$pkgname-$_commit"

  cd build/
  make DESTDIR="$pkgdir" install
}
