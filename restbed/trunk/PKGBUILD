# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: Justin Wilcox <nat1192 at gmail dot com>
pkgname=restbed
pkgver=4.6+28+gc3a259b
pkgrel=1
pkgdesc="A framework for asynchronous RESTful functionality in C++11 applications"
arch=('x86_64')
url="https://github.com/Corvusoft/restbed"
license=('AGPL3')
depends=('openssl')
replaces=('restbed-latest')
conflicts=('restbed-latest')
makedepends=('cmake' 'asio' 'kashmir')
checkdepends=('catch2')
_commit=c3a259ba047fa9f5b1c3b65a3ab9e21577bf8bcc
source=("https://github.com/Corvusoft/restbed/archive/${_commit}/$pkgname-$pkgver.tar.gz"
        "0001-cmake-Auto-detect-Catch-dependency-to-decide-whether.patch")
sha256sums=('e1438ce6718e34c0f89c3f02efde3c07df865b89209ae10cc1b5da9ee44b7701'
            '6ddf6157135a8913332c583cf8297657be83f44dce52c25ed677cc611dfdf1e7')

prepare() {
  cd "$srcdir/$pkgname-$_commit"
  patch -p1 < "$srcdir/0001-cmake-Auto-detect-Catch-dependency-to-decide-whether.patch"
}

build() {
  cd "$srcdir/$pkgname-$_commit"

  mkdir -p build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_SHARED=on \
    -DBUILD_SSL=on
  make
}

check() {
  cd "$srcdir/$pkgname-$_commit/build"
  make test
}

package() {
  cd "$srcdir/$pkgname-$_commit"

  cd build/
  make DESTDIR="$pkgdir" install
}
