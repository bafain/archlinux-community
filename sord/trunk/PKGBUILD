# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=sord
pkgver=0.16.0
pkgrel=1
pkgdesc="A lightweight C library for storing RDF data in memory"
arch=('x86_64')
url="https://drobilla.net/software/sord/"
license=('custom:ISC')
depends=('serd' 'pcre')
makedepends=('python')
source=("https://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2"{,.asc})
sha512sums=('e18c6de62f2cbffedd4c200f6452f4fd37f719aed764ccc1b77c8da04a0f3394d47e8b57e19eefd6d600770926f505fdfa5da91564bbff5f4ead49f1adaba249'
            'SKIP')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3')

prepare() {
  cd "${pkgname}-${pkgver}"
  # remove local call to ldconfig
  sed -i "/ldconfig/d" wscript
}

build() {
  cd "${pkgname}-${pkgver}"
  python waf configure --prefix=/usr
  python waf build
}

package() {
  cd "${pkgname}-${pkgver}"
  python waf install --destdir="$pkgdir"
  # license
  install -vDm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  # docs
  install -t "$pkgdir/usr/share/doc/${pkgname}" \
    -vDm644 {AUTHORS,NEWS,README}
}

# vim:set ts=2 sw=2 et:
