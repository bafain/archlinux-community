# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-oslo-context
pkgname=(python-oslo-context python2-oslo-context)
pkgver=2.21.0
pkgrel=2
pkgdesc="OpenStack Context library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.context/$pkgver"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-debtcollector'
             'python2-debtcollector')
checkdepends=('python-mock' 'python2-mock' 'python-oslotest' 'python2-oslotest'
              'python-testrepository' 'python2-testrepository')
options=('!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/oslo.context/archive/$pkgver.tar.gz")
sha512sums=('af8943894134bcf7ce533f91a4dffe5d7347225fe4b58506d18b702df580073575a50e93fda9a632d445beb0f23a2dda42b7c58c1be5d786ab1420afe93b05dd')

prepare() {
  cp -a oslo.context-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/oslo.context-$pkgver
  python setup.py build

  cd "$srcdir"/oslo.context-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/oslo.context-$pkgver
  python setup.py testr

  cd "$srcdir"/oslo.context-$pkgver-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-oslo-context() {
  depends=('python-pbr' 'python-debtcollector')

  cd "$srcdir"/oslo.context-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-oslo-context() {
  depends=('python2-pbr' 'python2-debtcollector')

  cd "$srcdir"/oslo.context-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
