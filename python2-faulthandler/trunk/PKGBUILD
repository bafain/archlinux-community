# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Storm Dragon <stormdragon2976@gmail.com> 

pkgname=python2-faulthandler
pkgver=3.0
pkgrel=1
pkgdesc="Display the Python traceback on a crash"
arch=('x86_64')
url="https://pypi.python.org/pypi/faulthandler/"
license=('BSD')
depends=('python2')
makedepends=('python2-setuptools')
source=("https://pypi.io/packages/source/f/faulthandler/faulthandler-$pkgver.tar.gz"
        https://github.com/vstinner/faulthandler/commit/67b661ebdbbf4067bd173af4d61ba815b9d49616.patch)
sha512sums=('9e31682338e572e77e22a510637877c3e27440bc451854a898c5e58238d28a1bbf824cb2b44d1f1f11d8a4f491ddc299982a3d452eebec38bc2a03a824bac392'
            'c34bee68fbf3066ce2bccc85118c1a55090a550d154bdc34d80340067678b38f1bf820d5d6220a436c871a6874b6613e85688a00a52721286ae55ee0bc839786')

prepare() {
  cd faulthandler-$pkgver
  patch -p1 -i ../67b661ebdbbf4067bd173af4d61ba815b9d49616.patch
}

build() {
  cd faulthandler-$pkgver
  python2 setup.py build
}

check() {
  cd faulthandler-$pkgver
  echo -e "import site\nsite.addsitedir('$PWD/build/lib.linux-$CARCH-2.7')" > sitecustomize.py
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH" python2 tests.py
  rm sitecustomize.py
}

package() {
  cd faulthandler-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
