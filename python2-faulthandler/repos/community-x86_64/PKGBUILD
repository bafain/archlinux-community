# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Storm Dragon <stormdragon2976@gmail.com> 

pkgname=python2-faulthandler
pkgver=3.1
pkgrel=1
pkgdesc="Display the Python traceback on a crash"
arch=('x86_64')
url="https://pypi.python.org/pypi/faulthandler/"
license=('BSD')
depends=('python2')
makedepends=('python2-setuptools')
source=("https://pypi.io/packages/source/f/faulthandler/faulthandler-$pkgver.tar.gz")
sha512sums=('fb133e7c1a8e2c611d9bcfa4d4b6ade92fb638ac60d12701d6bf5320f6a616e3e1b5f3b543f3e3c62f71e26167e2bdd17dfb4741bf758b7052286e5bc19faa59')

build() {
  cd faulthandler-$pkgver
  python2 setup.py build
}

check() {
  cd faulthandler-$pkgver
  echo -e "import site\nsite.addsitedir('$PWD/build/lib.linux-$CARCH-2.7')" > sitecustomize.py
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH" python2 tests.py
  rm sitecustomize.py
}

package() {
  cd faulthandler-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
