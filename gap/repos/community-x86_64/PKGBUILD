# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor:  TDY <tdy@archlinux.info>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgbase=gap
pkgname=(gap gap-doc gap-packages)
pkgver=4.9.3
pkgrel=1
pkgdesc="Groups, Algorithms, Programming: a system for computational discrete algebra"
arch=(x86_64)
url="https://www.gap-system.org/"
license=(GPL)
source=("https://www.gap-system.org/pub/gap/gap-${pkgver%.*}/tar.gz/gap-$pkgver.tar.gz" gap-no-packages-by-default.patch
        gap-polymake-3.1.patch)
sha256sums=('e502941e52352285e87faaf11e7f0e810eab8e38849d869fc6b9714d6cf7fe7c'
            'e8f19968d998172b7c289d0499b8b8a05bc31119a1ce073181d3842ece6fe3a2'
            '6d8d57a9947575a3f95e682172040618be3d6a144424496229ae4ec50c0ab4e9')
makedepends=(libxaw givaro mpfi normaliz boost c-xsc zeromq fplll polymake wget)

prepare() {
  cd gap-$pkgver

# Use system normaliz
  sed -e '/build-normaliz.sh/d' -i bin/BuildPackages.sh
# Fix build of guava package
  sed -e '/cd leon make/d' -i pkg/guava-*/src/Makefile
  sed -e '/AM_INIT_AUTOMAKE/d' -i pkg/guava-*/src/leon/configure.ac
# Don't load any packages by default
  patch -p1 -i ../gap-no-packages-by-default.patch
# Fix build of PolymakeInterface with polymake 3.1
  patch -p1 -i ../gap-polymake-3.1.patch
# Fix https://bugs.archlinux.org/task/55174
  sed -e '/xgap/d' -i pkg/sonata/PackageInfo.g
# Disable anupq package, it's i686 only
  rm -r pkg/anupq-*
}

build() {
  cd gap-$pkgver
  ./configure --prefix=/usr --with-gmp=system
  make

  cd pkg
  export MAKEFLAGS='-j1' # Fix build of guava package
  ../bin/BuildPackages.sh
# These packages fail to build:
# linboxing (doesn't support givaro 4)
# PolymakeInterface (T_POLYMAKE removed from GAP)
}

package_gap() {
  depends=(gmp)
  optdepends=('gap-packages: extra packages' 'gap-doc: documentation')
  replaces=(gap-data)
  cd gap-$pkgver

  mkdir -p "$pkgdir"/usr/{bin,lib/gap/pkg}
  cp -r grp lib "$pkgdir"/usr/lib/gap
  cp -r pkg/{GAPDoc-*,primgrp-*,SmallGrp-*,transgrp} "$pkgdir"/usr/lib/gap/pkg
  install -m755 gap -t "$pkgdir"/usr/lib/gap
  sed -e "s|/build/gap/src/gap-$pkgver|/usr/lib/gap|g" bin/gap.sh > "$pkgdir"/usr/bin/gap
  chmod 755 "$pkgdir"/usr/bin/gap
}

package_gap-doc() {
  depends=(gap)
  pkgdesc="Documentation for GAP"
  cd gap-$pkgver

  mkdir -p "$pkgdir"/usr/lib/gap
  cp -r doc "$pkgdir"/usr/lib/gap
}

package_gap-packages() {
  depends=(gap)
  optdepends=('normaliz: Normaliz interface package' 'libxaw: xgap package' 'c-xsc: float package' 'mpfi: float package'
              'libmpc: float package' 'fplll: float package' 'zeromq: ZeroMQ interface package') # 'polymake: Polymake interface package'
  pkgdesc="Extra packages for GAP"
  cd gap-$pkgver

  mkdir -p "$pkgdir"/usr/lib/gap
  cp -r pkg "$pkgdir"/usr/lib/gap

# provided by main gap package
  rm -r "$pkgdir"/usr/lib/gap/pkg/{GAPDoc-*,primgrp-*,SmallGrp-*,transgrp}
}
