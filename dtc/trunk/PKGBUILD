# $Id$
# Maintainer: Anatol Pomozov
# Contributor: Frederic Bezies <fredbezies at gmail dot com>

pkgname=dtc
pkgver=1.4.7
pkgrel=1
pkgdesc='Device Tree Compiler'
url='http://www.devicetree.org/Device_Tree_Compiler'
arch=(x86_64)
license=(GPL2)
makedepends=(swig python2)
checkdepends=(valgrind)
source=(https://www.kernel.org/pub/software/utils/dtc/dtc-$pkgver.tar.xz)
sha256sums=('6643e8f00ff86350f465bb54b2185058b5b1b7bac01a0842c81a52b86589cde7')

prepare() {
  cd dtc-$pkgver
  sed -i 's/-Werror//' Makefile
  sed -i 's/python\b/python2/' tests/run_tests.sh
}

build() {
  cd dtc-$pkgver
  make
}

check() {
  cd dtc-$pkgver
  make check
}

package() {
  cd dtc-$pkgver
  make DESTDIR="$pkgdir" SETUP_PREFIX="$pkgdir/usr" PREFIX=/usr install
}

