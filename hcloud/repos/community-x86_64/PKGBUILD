# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Konrad Tegtmeier <konrad.tegtmeier+aur@gmail.com>

pkgname='hcloud'
pkgver='1.3.0'
pkgrel='1'
pkgdesc="CLI for Hetzner Cloud"
arch=('x86_64')
url='https://github.com/hetznercloud/cli'
license=('MIT')
optdepends=(
    'bash-completion: tab auto-completion'
    'zsh: tab auto-completion')
makedepends=('go' 'git')
provides=('hcloud')
_hcloud_commit='4c412e3e22e604bc53d93cf937817f5d74c16267'
source=("git+https://github.com/hetznercloud/cli#commit=${_hcloud_commit}")
sha512sums=('SKIP')

prepare(){
  export GOPATH="${srcdir}"
  export PATH="$PATH:$GOPATH/bin"
  mkdir -p src/github.com/hetznercloud
  mv cli src/github.com/hetznercloud
}

build(){
  cd src/github.com/hetznercloud/cli
  go build \
  -ldflags "-w -X github.com/hetznercloud/cli.Version=${pkgver}" \
  github.com/hetznercloud/cli/cmd/hcloud
  chmod +x ./hcloud
  ./hcloud completion bash > "$pkgname-completion.bash"
}

check(){
  cd src/github.com/hetznercloud/cli
  go test -v -x
}

package(){
  cd src/github.com/hetznercloud/cli
  install -Dsm755 hcloud "${pkgdir}/usr/bin/hcloud"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "${pkgname}-completion.bash" "$pkgdir/usr/share/bash-completion/completions/$pkgname"
}
