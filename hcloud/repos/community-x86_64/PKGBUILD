# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Konrad Tegtmeier <konrad.tegtmeier+aur@gmail.com>

pkgname=hcloud
pkgver=1.6.1
pkgrel=1
pkgdesc="CLI for Hetzner Cloud"
arch=('x86_64')
url='https://github.com/hetznercloud/cli'
license=('MIT')
optdepends=(
    'bash-completion: tab auto-completion'
    'zsh: tab auto-completion')
makedepends=('go-pie' 'git')
depends=('glibc')
provides=('hcloud')
_hcloud_commit='a80f24289dfd290c50b3b9984d75e5bfe48aa16f'
source=("git+https://github.com/hetznercloud/cli#commit=${_hcloud_commit}"
        _hcloud.zsh)
sha512sums=('SKIP'
            '1964c7445200327ee71974591516bdcc3a00751ff3407891a7401ad486b4a0ee51bfc8c0e8d0e074b788936f365a435322bd4c9b2b527f6621b0965b60477897')

prepare(){
  export GOPATH="${srcdir}"
  export PATH="$PATH:$GOPATH/bin"
  mkdir -p src/github.com/hetznercloud
  mv cli src/github.com/hetznercloud
}

build(){
  cd src/github.com/hetznercloud/cli/cli/
  go build  \
  -ldflags "-w -X github.com/hetznercloud/cli/cli.Version=${pkgver}" \
  github.com/hetznercloud/cli/cmd/hcloud
  chmod +x ./hcloud
  ./hcloud completion bash > "$pkgname-completion.bash"
}

check(){
  cd src/github.com/hetznercloud/cli/cli
  go test -v -x
}

package(){
  cd src/github.com/hetznercloud/cli
  install -Dsm755 cli/hcloud "${pkgdir}/usr/bin/hcloud"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "cli/${pkgname}-completion.bash" "${pkgdir}/usr/share/bash-completion/completions/$pkgname"
  install -Dm644 "${srcdir}"/_hcloud.zsh "${pkgdir}/usr/share/zsh/site-functions/_hcloud"
}
