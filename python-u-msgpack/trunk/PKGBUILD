# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=u-msgpack-python
pkgbase='python-u-msgpack'
pkgname=('python-u-msgpack' 'python2-u-msgpack')
pkgver=2.4.1
pkgrel=1
arch=('any')
url="https://github.com/vsergeev/${_pkgname}"
license=('MIT')
makedepends=('python' 'python2')
checkdepends=('python-pytest' 'python2-pytest')
source=("https://pypi.io/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('2f31fc1b6dd8632c230ea7012470e40f77119f20999274dda6f1afa9c305e590')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    python setup.py build
    python2 setup.py build
}

check() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    pytest
    pytest2
}

package_python-u-msgpack() {
    pkgdesc="portable, lightweight MessagePack serializer and deserializer for Python"
    depends=('python')

    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-u-msgpack() {
    pkgdesc="portable, lightweight MessagePack serializer and deserializer for Python2"
    depends=('python2')

    cd "${srcdir}/${_pkgname}-${pkgver}"
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
