# $Id$
# Maintainer: schuay <jakob.gruber@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jeremy Newton (Mystro256) <alexjnewt@gmail.com>

pkgname=dolphin-emu
pkgver=5.0.r7309.10d230a512
pkgrel=5
epoch=1
pkgdesc='A Gamecube / Wii / Triforce emulator'
arch=('x86_64')
url='http://dolphin-emu.org'
license=('GPL')
depends=('alsa-lib' 'bluez-libs' 'enet' 'gcc-libs' 'glibc' 'hidapi' 'libevdev'
         'libgl' 'libpng' 'libpulse' 'libx11' 'libxi' 'libxrandr' 'lzo'
         'mbedtls' 'qt5-base' 'sfml' 'zlib'
         'libavcodec.so' 'libavformat.so' 'libavutil.so' 'libcurl.so'
         'libminiupnpc.so' 'libswscale.so' 'libudev.so' 'libusb-1.0.so')
makedepends=('cmake' 'git')
optdepends=('pulseaudio: PulseAudio backend')
options=('!emptydirs')
_commit='10d230a512b99aee66f7343481ed13d83557de22'
source=("dolphin-emu::git+https://github.com/dolphin-emu/dolphin.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd dolphin-emu

  git describe | sed 's/-/.r/; s/-g/./'
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  cmake ../dolphin-emu \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DENABLE_QT2='ON' \
    -DENABLE_WX='OFF' \
    -DUSE_SHARED_ENET='ON' \
    -DDISTRIBUTOR='archlinux.org'
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install

  install -Dm 644 ../dolphin-emu/Data/51-usb-device.rules -t "${pkgdir}"/usr/lib/udev/rules.d/
}

# vim: ts=2 sw=2 et:
