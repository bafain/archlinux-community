# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-softlayer
pkgname=('python-softlayer' 'python2-softlayer')
pkgver=5.4.1
pkgrel=1
pkgdesc="A library for SoftLayer's API"
arch=('any')
license=('MIT')
url='https://github.com/softlayer/softlayer-python'
makedepends=('python-setuptools' 'python2-setuptools' 'python-prettytable' 'python2-prettytable'
             'python-click' 'python2-click' 'python-requests' 'python2-requests'
             'python-prompt_toolkit' 'python2-prompt_toolkit' 'python-pygments' 'python2-pygments')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock'
              'python-testtools' 'python2-testtools')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/softlayer/softlayer-python/archive/v$pkgver.tar.gz")
sha512sums=('fbc2d55d812e4ba4e3d8df3e3e3260747c68e399d681491ef227e0e1e606948e1b14f86c1f33637d3642f473c71aa70ddd8b519e024fcb26fd63c07af65f1069')

prepare() {
  cp -a softlayer-python-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/softlayer-python-$pkgver
  python setup.py build

  cd "$srcdir"/softlayer-python-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/softlayer-python-$pkgver
  python setup.py pytest

  cd "$srcdir"/softlayer-python-$pkgver-py2
  python2 setup.py pytest
}

package_python-softlayer() {
  depends=('python-setuptools' 'python-prettytable' 'python-click' 'python-requests'
           'python-prompt_toolkit' 'python-pygments')

  cd softlayer-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-softlayer() {
  depends=('python2-setuptools' 'python2-prettytable' 'python2-click' 'python2-requests'
           'python2-prompt_toolkit' 'python2-pygments')

  cd softlayer-python-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/sl{,2}
  mv "$pkgdir"/usr/bin/slcli{,2}
}

# vim:set ts=2 sw=2 et:
