# $Id$
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Clint Valentine <valentine.clint@gmail.com>

pkgbase=python-datrie
_pkgname=${pkgbase#python-}
pkgname=(python-$_pkgname python2-$_pkgname)
pkgver=0.7.1
pkgrel=2
pkgdesc="Super-fast, efficiently stored Trie for Python"
arch=('x86_64')
url="https://github.com/pytries/datrie"
license=('LGPL')
makedepends=('python-setuptools' 'python2-setuptools' 'cython')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-hypothesis' 'python2-hypothesis')
options=(!emptydirs)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('dd81fa726b601a5c38b7c838111996852c2c3fae3ddab90f00ac37c62bb7dbcd3fa55aa7e57007b157daddc8d597f4298424503233b50dadbcb484916b961e07')

prepare() {
  rm -rf $_pkgname-$pkgver-py2 # Tests fail if we keep the cache
  cp -a $_pkgname-$pkgver{,-py2}
}

build() {
  echo "Building python-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver
  ./update_c.sh
  python setup.py build

  echo "Building python2-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver-py2
  ./update_c.sh
  python2 setup.py build
}

check() {
  echo "Running tests on python-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver
  python setup.py pytest

  echo "Running tests on python2-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver-py2
  python2 setup.py pytest
}

package_python-datrie() {
  depends=('python')

  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-datrie() {
  depends=('python2')

  cd $_pkgname-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

