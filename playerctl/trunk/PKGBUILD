# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor: Ian D. Scott <ian@perebruin.com>
# Contributor: Tony Crisci <tony@dubstepdish.com>
pkgname=playerctl
pkgver=0.6.1
pkgrel=1
pkgdesc="mpris media player controller and lib for spotify, vlc, audacious, bmp, xmms2, and others."
arch=('x86_64')
url="https://github.com/acrisci/playerctl"
license=('LGPL3')
depends=('glib2')
makedepends=('gobject-introspection' 'gtk-doc')
source=("playerctl-${pkgver}.tar.gz::https://github.com/acrisci/playerctl/archive/v${pkgver}.tar.gz")
sha512sums=('07c302eb06a96bf3b8aa180e68e85ef6f9fdb5933b80fbce8ee5f9a36c1d961eaf5638e319ff0b26475984acc57a447a4aa8e705667f77e4bd393364cdbfb284')

prepare() {
    cd "${srcdir}/playerctl-${pkgver}"
    NOCONFIGURE=true ./autogen.sh
}

build() {
    cd "${srcdir}/playerctl-${pkgver}"
    ./configure --prefix=/usr --enable-gtk-doc
}

package() {
    cd "${srcdir}/playerctl-${pkgver}"
    make DESTDIR="${pkgdir}/" install
}
