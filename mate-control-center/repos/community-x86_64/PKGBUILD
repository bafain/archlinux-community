# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-control-center
pkgver=1.20.3
pkgrel=1
pkgdesc="The Control Center for MATE"
url="http://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('mate-menus' 'mate-settings-daemon' 'marco' 'libxss')
makedepends=('intltool' 'itstool')
groups=('mate')
conflicts=('mate-control-center-gtk3')
replaces=('mate-control-center-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('6d7e4c23cbfee2ec3682b6b07c0226742775c669f6e47862691d84ffc742f172')

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --localstatedir=/var \
        --disable-update-mimedb

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    rm "${pkgdir}/usr/share/applications/mimeinfo.cache"
}
