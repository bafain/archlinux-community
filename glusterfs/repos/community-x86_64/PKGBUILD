# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributors:
#   Andrei Antoukh - niwi@niwi.be - http://www.niwi.be
#   henning mueller <henning@orgizm.net>

pkgname=glusterfs
epoch=1
pkgver=4.1.2
pkgrel=2
pkgdesc='Is a cluster file-system capable of scaling to several peta-bytes.'
arch=(x86_64)
url='http://www.gluster.org/'
license=(GPL2 LGPL3)
install=glusterfs.install
backup=('etc/glusterfs/glusterd.vol'
	'etc/glusterfs/eventsconfig.json'
	'etc/glusterfs/gluster-rsyslog-5.8.conf'
	'etc/glusterfs/gluster-rsyslog-7.2.conf'
	'etc/glusterfs/glusterd.vol'
	'etc/glusterfs/glusterfs-georep-logrotate'
	'etc/glusterfs/glusterfs-logrotate')
depends=(fuse python2 libxml2 libaio liburcu attr which rpcbind)
makedepends=(rpcsvc-proto)
optdepends=('glib2: qemu-block')
#source=($pkgname-$pkgver.tar.gz::https://github.com/gluster/glusterfs/archive/v$pkgver.tar.gz)
source=(https://bits.gluster.org/pub/gluster/glusterfs/src/glusterfs-$pkgver.tar.gz
        glusterfs.sysusers)
sha256sums=('6f0b01c082fec65134eea43b2e4df8d4b55269f43b7d330e81eaad920d7c63e5'
            'a470d07aedb2674adfb295b4775e490b48816b87974946da4dcf62aa0d72108a')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  find -type f -name '*.py' -exec sed -i \
    -e 's:env\ python$:env\ python2:' \
    -e 's:/usr/bin/python$:/usr/bin/python2:' {} +
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --with-mountutildir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --mandir=/usr/share/man \
    --libexecdir=/usr/lib/$pkgname \
    --with-systemddir=/usr/lib/systemd/system \
    --with-tmpfilesdir=/usr/lib/tmpfiles.d \
    --enable-gnfs \
    PYTHON=python2 LEXLIB=
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  make -j1 DESTDIR="$pkgdir" install

  # https://bugzilla.redhat.com/show_bug.cgi?id=1598900
  install -Dm644 "$srcdir"/glusterfs.sysusers \
    "$pkgdir"/usr/lib/sysusers.d/glusterfs.conf

  install -D -m 644 \
    "$srcdir"/$pkgname-$pkgver/{README.md,INSTALL,COPYING*} \
    "$pkgdir"/usr/share/doc/glusterfs/

  cp -rf \
    "$srcdir"/$pkgname-$pkgver/doc/* \
    "$pkgdir"/usr/share/doc/glusterfs/
  rm -rf "$pkgdir"/var/run
}

# vim:set ts=2 sw=2 et:
