# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: unikum <unikum.pm@gmail.com>
# Contributor: speed145a <jonathan@tagchapter.com>

pkgname=firewalld
pkgver=0.6.1
pkgrel=3
pkgdesc='Firewall daemon with D-Bus interface'
arch=('any')
url='https://github.com/t-woerner/firewalld'
license=('GPL2')
depends=(
  'dconf' 'ebtables' 'glib2' 'hicolor-icon-theme' 'ipset' 'iptables' 'nftables'
  'python-decorator' 'python-gobject' 'python-slip'
)
makedepends=('docbook-xsl' 'git' 'intltool')
optdepends=('bash-completion: bash completion'
            'gtk3: firewall-config'
            'libnm-glib: firewall-config and firewall-applet'
            'libnotify: firewall-applet'
            'python-pyqt5: firewall-applet')
backup=('etc/conf.d/firewalld'
        'etc/firewalld/firewalld.conf')
source=("git+https://github.com/firewalld/firewalld.git#tag=v${pkgver}"
        'firewalld-sysconfigdir.patch')
sha256sums=('SKIP'
            'cf7d655230c43acf10a0f97dffdbcba136729967c8b9a25a930871d54a589834')

prepare() {
  cd firewalld

  git cherry-pick -n 5a59a90f449a8bf836e62e2d9ad486301b1aa2bb # FS#59661

  patch -Np1 -i ../firewalld-sysconfigdir.patch

  NOCONFIGURE='true' ./autogen.sh
}

build() {
  cd firewalld

  ./configure \
    --prefix='/usr' \
    --localstatedir='/var' \
    --sbindir='/usr/bin' \
    --sysconfdir='/etc' \
    --disable-schemas-compile \
    --disable-sysconfig
  make
}

package() {
  cd firewalld

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
