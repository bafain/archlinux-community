# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Chris Severance aur.severach AatT spamgourmet.com

pkgbase=python-s3transfer
pkgname=('python-s3transfer' 'python2-s3transfer')
pkgver=0.1.13
pkgrel=4
pkgdesc='Amazon S3 Transfer Manager for Python'
arch=('any')
url="https://github.com/boto/s3transfer"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
source=($pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz)
md5sums=('7bad7a38ca2442d650455fadeb3578b0')

prepare() {
  cp -r "s3transfer-$pkgver" "python-s3transfer-$pkgver"
  cp -r "s3transfer-$pkgver" "python2-s3transfer-$pkgver"
}

build() {
  cd "$srcdir/python-s3transfer-$pkgver"
  python setup.py build

  cd "$srcdir/python2-s3transfer-$pkgver"
  python2 setup.py build
}

package_python-s3transfer() {
  depends=('python-botocore')
  cd python-s3transfer-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-s3transfer() {
  depends=('python2-botocore' 'python2-futures')
  cd python2-s3transfer-$pkgver

  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
