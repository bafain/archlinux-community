# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Alexsandr Pavlov <kidoz at mail dot ru>

_gemname=rack
pkgname=ruby-rack
pkgver=2.0.4
pkgrel=1
pkgdesc='Minimal, modular, and adaptable interface for developing web applications'
url='https://rack.github.io/'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-rdoc')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/rack/rack/archive/${pkgver}.tar.gz)
sha256sums=('4bebeacd4966b8693b35dd5ffe1ef56dc68c4d693d963e71c89e0aaac520af62')
sha512sums=('c78e734cc628a57405bdf21b9cda3b618204a517e07d53aabdcca101a14ff267691332d037c366515f4a7ef607928420d132d6bd844bb94a3c3e59f2e2c44ae4')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.rdoc NEWS.md HISTORY.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 example/* -t "${pkgdir}/usr/share/doc/${pkgname}/example"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
