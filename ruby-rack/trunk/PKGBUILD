# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Alexsandr Pavlov <kidoz at mail dot ru>

_gemname=rack
pkgname=ruby-rack
pkgver=2.0.5
pkgrel=1
pkgdesc='Minimal, modular, and adaptable interface for developing web applications'
url='https://rack.github.io/'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-rdoc')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/rack/rack/archive/${pkgver}.tar.gz)
sha256sums=('2bbe5edd09c5b4e6b1940b16932d96df573bae90a8f06b48efcb5010262733cc')
sha512sums=('cbfd837a685ef04f4732cb234d7b8d5740ceab826d88bee0505d7e27981843f080e82d022f8bb138be9efd86aca90b27244b9269c6ad69846eddcca1c43d1f7e')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.rdoc NEWS.md HISTORY.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 example/* -t "${pkgdir}/usr/share/doc/${pkgname}/example"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
