# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Husam Bilal <husam212 AT gmail DOT com>

_name=orderedmultidict
pkgbase=python-orderedmultidict
pkgname=('python-orderedmultidict' 'python2-orderedmultidict')
pkgver=1.0
pkgrel=2
pkgdesc="Ordered multivalue dictionary that retains method parity with dict."
url="https://github.com/gruns/orderedmultidict"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pycodestyle' 'python2-pycodestyle')
license=('custom')
arch=('any')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('7f731bde1f9cdd3e0490d3f590f2bfefa21935155d309d7460e45e11a640272de15a192200cb5110cf45876b7d6f0b6c7eac635fd122f6225b25aca90a4d6d36')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py test

  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py test
}

package_python-orderedmultidict() {
  depends=('python-six')
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -t "${pkgdir}/usr/share/doc/${pkgname}" -vDm 644 {API,README}.md
}

package_python2-orderedmultidict() {
  depends=('python2-six')
  cd "${pkgname}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -t "${pkgdir}/usr/share/doc/${pkgname}" -vDm 644 {API,README}.md
}
