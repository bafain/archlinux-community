# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=nginx-mod-brotli
epoch=1
pkgver=0.1.1
pkgrel=1

_modname="ngx_${pkgname#nginx-mod-}"
_nginxver=1.14.0

# https://github.com/eustas/ngx_brotli/tree/v$pkgver/deps
_brotliver=c6333e1e79fb62ea088443f192293f964409b04e

pkgdesc="Brotli compression filter module for nginx"
arch=('x86_64')
depends=("nginx=$_nginxver")
url="https://github.com/google/ngx_brotli"
license=('CUSTOM')

source=(
	https://nginx.org/download/nginx-$_nginxver.tar.gz{,.asc}
	https://github.com/eustas/$_modname/archive/v$pkgver/$_modname-$pkgver.tar.gz
	https://github.com/google/brotli/archive/$_brotliver/brotli-$_brotliver.tar.gz
)
validpgpkeys=(B0F4253373F8F6F510D42178520A9993A1C052F8) # Maxim Dounin <mdounin@mdounin.ru>
sha256sums=('5d15becbf69aba1fe33f8d416d97edd95ea8919ea9ac519eff9bafebb6022cb5'
            'SKIP'
            'c3f063e32bdb779e18b82a9cdb9f7c92293e72d71ba417e06e031805e099a555'
            '298fdfb8bc14430bd0c7fbbf73f6d9d1a5ad1cd33c3f4f2679a6afd85a71adb5')

prepare() {
	cd "$srcdir"/$_modname-$pkgver/deps
	rm -rf brotli
	ln -s ../../brotli-$_brotliver brotli
}

build() {
	cd "$srcdir"/nginx-$_nginxver
	./configure --with-compat --add-dynamic-module=../$_modname-$pkgver
	make modules
}

package() {
	install -Dm644 "$srcdir"/$_modname-$pkgver/LICENSE \
	               "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

	cd "$srcdir"/nginx-$_nginxver/objs
	for mod in ngx_*.so; do
		install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
	done
}
