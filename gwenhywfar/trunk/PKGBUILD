# $Id$
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=gwenhywfar
pkgver=4.20.0
_dnrel=208
pkgrel=2
pkgdesc="OS abstraction functions for various projects"
arch=(x86_64)
url="https://www.aquamaniac.de/"
license=(LGPL)
depends=('openssl' 'gnutls' 'libgcrypt' 'libice' 'libsm')
makedepends=('gtk3' 'qt5-base')
optdepends=('gtk3: for the GTK3 UI' 'qt5-base: for the Qt5 UI')
source=(gwenhywfar-$pkgver.tar.gz::"https://www.aquamaniac.de/sites/download/download.php?package=01&release=${_dnrel}&file=02&dummy=gwenhywfar-$pkgver.tar.gz"
  $pkgname-$pkgver.tar.gz.asc::"https://www.aquamaniac.de/sites/download/download.php?package=01&release=${_dnrel}&file=01&dummy=gwenhywfar-$pkgver.tar.gz.asc"
  gtk3-fixes.patch)
sha256sums=('5a88daabba1388f9528590aab5de527a12dd44a7da4572ce48469a29911b0fb0'
            'SKIP'
            'd76b62a725482b6a87d88c21b69e96c512d636157cfc8ad67be4bfec5fa2b402')
validpgpkeys=(42400AF5EB2A17F0A69BB551E9899D784A977416) # AqBanking Package Key <packages@aqbanking.de>

prepare() {
  cd $pkgname-$pkgver
  # Apply upstream fixes for the GTK3 UI
  patch -Np1 -i ../gtk3-fixes.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --enable-ssl --enable-system-certs --with-guis="gtk3 qt5"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
