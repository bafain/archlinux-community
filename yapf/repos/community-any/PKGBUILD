# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=yapf
pkgname=("yapf" "python2-yapf")
pkgver=0.24.0
pkgrel=1
pkgdesc="Python style guide checker"
arch=('any')
url="https://github.com/google/yapf"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/google/yapf/archive/v$pkgver.tar.gz")
sha512sums=('daa61faaa2877af8bc8579b1f6a8e9cf177d1caf27e0430cc508b86b42d907186a0fe14913530368097b888f12c19fc601fbcc1c4e605341ca328e8bf40ea399')

prepare() {
  cp -a yapf-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/yapf-$pkgver
  python setup.py build

  cd "$srcdir"/yapf-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/yapf-$pkgver
  nosetests3 || warning "https://github.com/google/yapf/issues/586"

  cd "$srcdir"/yapf-$pkgver-py2
  nosetests2
}

package_yapf() {
  depends=('python-setuptools')

  cd yapf-$pkgver
  python setup.py install -O1 --root="$pkgdir"
}

package_python2-yapf() {
  depends=('python2-setuptools')

  cd yapf-$pkgver-py2
  python2 setup.py install -O1 --root="$pkgdir"
  mv "$pkgdir"/usr/bin/yapf{,2}
}

# vim:set ts=2 sw=2 et:
