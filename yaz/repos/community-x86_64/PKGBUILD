# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Robert Emil Berge <filoktetes@linuxophic.org>

pkgname=yaz
pkgver=5.26.1
pkgrel=2
pkgdesc="A toolkit supporting the development of Z39.50/SRW/SRU clients and servers"
arch=('x86_64')
url="http://www.indexdata.dk/yaz"
license=('BSD')
depends=('gnutls' 'libxslt' 'icu')
makedepends=('automake')
changelog=$pkgname.changelog
source=(http://ftp.indexdata.dk/pub/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('ceb4367e3098bce8e09734f09c0410bd679fdc3f9423bb28d70cc15cc95b66cc')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  automake
  ./configure --prefix=/usr --enable-shared=yaz
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

#License
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
