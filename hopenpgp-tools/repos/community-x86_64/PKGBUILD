# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hopenpgp-tools
pkgver=0.21.2
pkgrel=23
pkgdesc="hOpenPGP-based command-line tools"
url="http://floss.scru.org/hopenpgp-tools"
license=("AGPL3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-attoparsec' 'haskell-base16-bytestring'
         'haskell-binary-conduit' 'haskell-conduit' 'haskell-conduit-extra' 'haskell-crypto-pubkey'
         'haskell-cryptohash' 'haskell-errors' 'haskell-fgl' 'haskell-graphviz' 'haskell-hopenpgp'
         'haskell-http-client' 'haskell-http-client-tls' 'haskell-http-types' 'haskell-ixset-typed'
         'haskell-lens' 'haskell-monad-loops' 'haskell-openpgp-asciiarmor'
         'haskell-optparse-applicative' 'haskell-prettyprinter'
         'haskell-prettyprinter-ansi-terminal' 'haskell-prettyprinter-convert-ansi-wl-pprint'
         'haskell-resourcet' 'haskell-time-locale-compat' 'haskell-yaml')
makedepends=('alex' 'happy' 'ghc')
source=(https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('2a5e1487670b9616a5546ec0cd2671e712aeef8017daf20ce78eebd569445355025f9b39a897747dcf9ededf0dba713319bb5bfa02349fd38a69af65962264c5')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    runhaskell Setup configure -O --prefix=/usr --enable-executable-dynamic --docdir="/usr/share/doc/${pkgname}"
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"

    rm "$pkgdir/usr/share/doc/hopenpgp-tools/LICENSE"
    rmdir "$pkgdir/usr/share/doc/hopenpgp-tools" "$pkgdir/usr/share/doc" "$pkgdir/usr/share"
}
