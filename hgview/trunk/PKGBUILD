# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: TDY <tdy@gmx.com>

pkgname=hgview
pkgver=1.10.2
pkgrel=3
pkgdesc="A fast Mercurial log navigator"
arch=('any')
url="http://www.logilab.org/project/hgview/"
license=('GPL')
depends=('mercurial>=1.6' 'python2-urwid' 'python2-pyinotify' 'python2-pygments')
makedepends=('asciidoc' 'xmlto')
source=(http://download.logilab.org/pub/$pkgname/$pkgname-$pkgver.tar.gz
        hg47.patch)
md5sums=('1d68a8de8652a3ca411a049579a694e4'
         'be6e50bed82d8424c223433fe216dfaa')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 < ../hg47.patch

  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir"/ --optimize=1

  # fix man page placement
  install -d "$pkgdir"/usr/share
  mv "$pkgdir"/usr/man "$pkgdir"/usr/share/man

  # fix FS#59623
  rm "$pkgdir"/usr/lib/python2.7/site-packages/hgext3rd/__init__.*
}

