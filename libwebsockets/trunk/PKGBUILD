# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Jingbei Li <i@jingbei.li>

pkgname=libwebsockets
pkgver=3.0.0
pkgrel=1
pkgdesc="C library for websocket clients and servers"
arch=('x86_64')
url="https://libwebsockets.org"
depends=('openssl')
makedepends=('cmake')
license=('LGPL')
source=("https://github.com/warmcat/libwebsockets/archive/v$pkgver.tar.gz")
sha256sums=('a6b611c212c52f161f70556339fdaa199b7e9b6a167c4638e086d19db75d6290')

build() {
  cd "$pkgname-$pkgver"

  mkdir build && cd build
  # http2 support -DLWS_WITH_HTTP2=1
  cmake -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX:PATH=/usr \
        -DLWS_WITH_STATIC=OFF \
        -DLWS_LINK_TESTAPPS_DYNAMIC=ON \
        ..
#-DLWS_WITHOUT_TEST_SERVER=ON \  # Disable certificate generation
  make
}

package() {
  cd "$pkgname-$pkgver/build"
  make DESTDIR="${pkgdir}" install

  cd ..

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
