# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Donald Webster <fryfrog@gmail.com>
pkgbase='python-logutils'
_pkgbase="${pkgbase#python-}"
pkgname=('python-logutils' 'python2-logutils')
pkgver=0.3.5
pkgrel=3
pkgdesc='Python module to parse metadata in video files.'
arch=('any')
url='https://bitbucket.org/vinay.sajip/logutils/'
license=('LGPL')
# makedepends=('python2-distribute' 'python2-setuptools')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/l/logutils/logutils-${pkgver}.tar.gz")
sha512sums=('0194bf4f01e7a29d4211495dc5fead9d4bd5bc1b791d25ff38ad28960a229a9bcdbaa7e6d3b5a4105eef6c9aa159459d6529c2615df113e7ec1ae93aac5cce6c')

prepare(){
  cp -r "${srcdir}/${_pkgbase}-${pkgver}" "${srcdir}/${_pkgbase}-${pkgver}-py2"
}

check() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python ./setup.py test
  cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
  python2 ./setup.py test
}

package_python-logutils() {
  depends=('python')
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python ./setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-logutils() {
  depends=('python2')
  cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
  python2 ./setup.py install --root="$pkgdir/" --optimize=1
}
