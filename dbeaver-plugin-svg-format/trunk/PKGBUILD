# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=dbeaver-plugin-svg-format
pkgver=1.0.13
pkgrel=1
pkgdesc='DBeaver plugin to save diagrams in SVG format'
arch=('any')
url='http://dbeaver.jkiss.org/'
license=('Apache')
makedepends=('unzip')
depends=('dbeaver>=4.2.5' 'dbeaver-plugin-batik')
source=("https://dbeaver.jkiss.org/update/svg/latest/plugins/org.jkiss.dbeaver.ext.ui.svg_${pkgver}.jar"
        "https://dbeaver.jkiss.org/update/svg/latest/features/org.jkiss.dbeaver.ext.ui.svg.feature_${pkgver}.jar"
        "${pkgname}.info")
sha256sums=('51a692f45ae8f1e91fce6b8dfbd53bc239c18e507eeeb3f189b9212c73209cbb'
            'c6697b14da2ce34df612223ad596f1d14b44199828cc0e7173c48148e66190e6'
            'b9d285178b258be783c7ffe160a90768b38244be113d09f712d2746e6f11d1d7')
noextract=("org.jkiss.dbeaver.ext.ui.svg_${pkgver}.jar"
           "ui.svg.feature_${pkgver}.jar")

package() {
  # Install plugin
  install -m 755 -d "${pkgdir}/usr/lib/dbeaver/plugins"
  install -m 644 -t "${pkgdir}/usr/lib/dbeaver/plugins" \
    "org.jkiss.dbeaver.ext.ui.svg_${pkgver}.jar"

  # Extract feature
  install -m 755 -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.ext.ui.svg.feature_${pkgver}"
  unzip -q "${srcdir}/org.jkiss.dbeaver.ext.ui.svg.feature_${pkgver}.jar" \
        -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.ext.ui.svg.feature_${pkgver}"

  # Add configuration file (handled by the hook)
  install -m 755 -d "${pkgdir}/etc/dbeaver/bundles.d"
  install -m 644 "${srcdir}/${pkgname}.info" "${pkgdir}/etc/dbeaver/bundles.d/20-${pkgname}.info"
}
