# $Id:
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Sandy Carter <bwrsandman@gmail.com>
pkgname=openmw
pkgver=0.44.0
pkgrel=1
pkgdesc="Open-source engine reimplementation for the role-playing game Morrowind"
arch=('x86_64')
url="http://www.openmw.org"
license=('GPL3' 'MIT' 'custom')
depends=('openal' 'openscenegraph' 'mygui' 'bullet' 'qt5-base' 'ffmpeg' 'sdl2' 'unshield' 'libxt')
makedepends=('cmake' 'boost' 'doxygen' 'ninja')
source=("https://github.com/OpenMW/openmw/archive/openmw-${pkgver}.tar.gz")
sha512sums=('1b178891274361b7821ac1d3d66976ed5d443545531499d98f7ed30823f3891172cc5dcb3f7382ba0f561774a92b9ad8805e0c9582b78d2f010e4ed661219017')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake "../$pkgname-$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DDESIRED_QT_VERSION=5 \
    -GNinja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# vim: ts=2 sw=2 et:
