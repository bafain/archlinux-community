# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
_name=zita-jacktools
pkgname=python-zita-jacktools
pkgver=1.0.0
pkgrel=3
pkgdesc="Audio tools running as a Jack client"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio"
license=('LGPL')
depends=('python-zita-audiotools' 'zita-convolver' 'zita-jclient')
makedepends=('python-setuptools')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${_name}-${pkgver}.tar.bz2")
sha512sums=('f91fad1e9cd3cd0f6868ab655f0eebfc9cb5e93036e8a939e1b34e55b4465a4b7d9ea89348b0c539d366c490a5a466a1a9a5c492c106a9fbde78a9da8af19c62')
validpgpkeys=()

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 755 examples/*.py \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
  install -vDm 755 examples/utils/*.py \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples/utils"
}
