# $Id$
# Maintainer  : Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer  : Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor : Daniel Micay <danielmicay@gmail.com>
pkgname=openvas-scanner
pkgver=5.1.2
pkgrel=1
pkgdesc='The OpenVAS scanning Daemon'
arch=('x86_64')
url='http://www.openvas.org/'
license=('GPL')
depends=('openvas-libraries' 'redis')
makedepends=('cmake' 'doxygen')
groups=('openvas')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/greenbone/openvas-scanner/archive/v${pkgver}.tar.gz"
        "https://github.com/greenbone/openvas-scanner/releases/download/v${pkgver}/openvas-scanner-${pkgver}.tar.gz.sig"
        "${pkgname}.service")
sha512sums=('b9acaff071ccbfa29b62943fae18cb4ace81526afd6ae354aa7f7acb17f5ca91416f8b12646214f6b5aa993dd1348d385be34420e3a5228d3a8e449b9977c507'
            'SKIP'
            '574a9125928e672a9a22a00cab9c635897eedad0eaae550295c21d45cabd52d1d1cd376f441665d37fd1bb9fa48360039f553c300eb15dd9b83cf14df7f37de5')
validpgpkeys=(
              'C3B468D2288C68B9D526452248479FF648DB4530' # OpenVAS Transfer Integrity
)

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_BUILD_TYPE=Release -DSBINDIR=/usr/bin \
    -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var .
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  install -Dm644 ../"${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
}
