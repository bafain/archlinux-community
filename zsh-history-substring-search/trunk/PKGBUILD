# Maintainer: David Runge <dave@sleepmap.de>
pkgname=zsh-history-substring-search
pkgver=1.0.1
pkgrel=2
pkgdesc="ZSH port of Fish history search (up arrow)"
arch=('any')
url="https://github.com/zsh-users/zsh-history-substring-search"
license=('BSD')
depends=('zsh')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zsh-users/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('0795befa068cedd46578960cf4102baafd182b69db598700c6361aebf17f64615972a981a5c4aad48318c45e9e9cd623d53956c704baa1b024cef9df83b138eb')

prepare() {
  cd "$pkgname-$pkgver"
  sed -ne 4,37p ${pkgname}.zsh > LICENSE
  sed -e 's/^# //g' -e 's/^#//g' -i LICENSE
}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 644 "${pkgname}.zsh" \
    -t "${pkgdir}/usr/share/zsh/plugins/${pkgname}/"
  # docs
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
