# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-xpybutil
pkgname=(python-xpybutil python2-xpybutil)
pkgver=0.0.5
pkgrel=2
pkgdesc="An incomplete xcb-util port plus some extras"
arch=('any')
url="https://github.com/BurntSushi/xpybutil"
license=('custom:WTFPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-xcffib' 'python2-xcffib')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/BurntSushi/xpybutil/archive/$pkgver.tar.gz")
sha512sums=('fdae21af40737715722e0650f436329358c475a6d2fee5b260b0655f71234e8c9639516927e78836ed54651780a6998593535680bda6e7feef38295f6f4a0442')

prepare() {
  cp -a xpybutil-$pkgver{,-py2}
}

build() {
  cd "$srcdir/xpybutil-$pkgver"
  python setup.py build

  cd "$srcdir/xpybutil-$pkgver-py2"
  python2 setup.py build
}

package_python-xpybutil() {
  depends=('python-xcffib')

  cd "${srcdir}/xpybutil-$pkgver"
  python setup.py install --root="${pkgdir}/" --optimize=1

  install -Dm664 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  mv "$pkgdir/usr/share/doc/"{,python-}xpybutil
}

package_python2-xpybutil() {
  depends=('python2-xcffib')

  cd "${srcdir}/xpybutil-$pkgver-py2"
  python2 setup.py install --root="${pkgdir}/" --optimize=1

  install -Dm664 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  mv "$pkgdir/usr/share/doc/"{,python2-}xpybutil
}
