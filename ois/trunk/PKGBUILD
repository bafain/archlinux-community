# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: scippio <scippio@berounet.cz>
pkgname=ois
pkgver=1.4
pkgrel=1
pkgdesc="Object Oriented Input System"
arch=('x86_64')
url="http://sourceforge.net/projects/wgois"
license=('custom:zlib')
makedepends=('cmake' 'gcc' 'libxaw')
source=("https://github.com/ogre3d/OIS/archive/3b4c2d6ceaa14f38a371b92de61d2603d2522b71.zip")
sha512sums=('b1c69f3f2a5be22adc88a886f97dc27501e80b96de377ca588662fc03206deb6f885895f618db6e99541cbb97a564afeb84c5ee97709873eaf060c0062d1240b')

build() {
  cd OIS-*/

  mkdir -p build && cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd OIS-*/build

  make DESTDIR=${pkgdir} install

  install -Dm644 ../LICENSE.md ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
