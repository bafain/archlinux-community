# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Nicky726 <nicky726@gmail.com>
# Contributor: Tom Killian <tom@archlinux.org>
# Contributor: RÃ©my Oudompheng <remyoudompheng@gmail.com>

pkgname=lib32-libtheora
pkgver=1.1.1
pkgrel=11
pkgdesc='An open video codec developed by the Xiph.org'
arch=('x86_64')
url='http://www.xiph.org'
license=('BSD')
depends=('lib32-libogg' 'libtheora')
makedepends=('gcc-multilib' 'lib32-libvorbis')
source=("http://downloads.xiph.org/releases/theora/libtheora-${pkgver}.tar.bz2")
sha512sums=('9ab9b3af1c35d16a7d6d84f61f59ef3180132e30c27bdd7c0fa2683e0d00e2c791accbc7fd2c90718cc947d8bd10ee4a5940fb55f90f1fd7b0ed30583a47dbbd')

prepare() {
  cd libtheora-${pkgver}

  sed -i 's|AC_DEFINE(\[OC_X86_64_ASM\]|#AC_DEFINE(\[OC_X86_64_ASM\]|
          s|AM_CONFIG_HEADER|AC_CONFIG_HEADERS|' configure.ac
}

build() {
  cd libtheora-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./autogen.sh \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --with-pic \
    --disable-examples \
    --disable-static
  make
}

package() {
  cd libtheora-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share}

# License
  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libtheora "${pkgdir}"/usr/share/licenses/lib32-libtheora
}

# vim: ts=2 sw=2 et:
