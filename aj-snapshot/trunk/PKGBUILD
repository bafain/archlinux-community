# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=aj-snapshot
pkgver=0.9.8
pkgrel=1
pkgdesc="Command line utility to store/restore ALSA and/or JACK connections to/from an XML file."
arch=('x86_64')
url="http://aj-snapshot.sourceforge.net/"
license=('GPL3')
groups=('pro-audio')
depends=('jack' 'mxml')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('a69e4130eef2125052b7bba65279c2c3997935c4583478abaecbd4e18974e9708cf5aa34c51468e97af3c21b445beeeea29e66f6979238a309c1f39953e19299')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm644 {AUTHORS,ChangeLog,README}
}

# vim:set ts=2 sw=2 et:
