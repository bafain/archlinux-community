# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=aj-snapshot
pkgver=0.9.7
pkgrel=3
pkgdesc="Command line utility to store/restore ALSA and/or JACK connections to/from an XML file."
arch=('x86_64')
url="http://aj-snapshot.sourceforge.net/"
license=('GPL3')
depends=('jack' 'mxml')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('b7582e07ba3f7567e656406ce9be1265e565d5194ced4e8f5009eb06509d2b69a95d7b90a0ef230587e2852d5713d67e87b53fa7f8defe1741b9d861e964c115')

prepare() {
  cd "${pkgname}-${pkgver}"
  # does resolve depends with '--as-needed'
  # https://sourceforge.net/p/aj-snapshot/bugs/4/
  export LDFLAGS="${LDFLAGS//,--as-needed}"
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm644 {AUTHORS,ChangeLog,README}
}

# vim:set ts=2 sw=2 et:

