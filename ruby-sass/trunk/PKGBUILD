# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=sass
pkgname=ruby-sass
pkgver=3.5.6
pkgrel=1
pkgdesc='Sass makes CSS fun again'
url='http://sass-lang.com/'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-sass-listen' 'ruby-yard' 'ruby-maruku' 'ruby-rdoc')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/sass/sass/archive/${pkgver}.tar.gz)
sha256sums=('7ba95dbb21806ad086e32a863b7f2d5033c9d5f49f0de65bb25b4ce223b0e63d')
sha512sums=('0c38b3a208b83c880d496c3ca2978eff469507a22ca6f9f76fe28a4de6bbd452659d8effc25d46701e2b597f4494d8a780b59d22c82dde31c190318fd84d25fa')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 MIT-LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
