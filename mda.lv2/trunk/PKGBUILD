# Maintainer: David Runge <dave@sleepmap.de>
pkgname=mda.lv2
pkgver=1.2.2
pkgrel=3
pkgdesc="A port of the MDA VST plugins to LV2"
arch=('x86_64')
url="https://git.drobilla.net/cgit.cgi/mda.lv2.git/about/"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio')
depends=('gcc-libs')
makedepends=('lv2' 'python')
source=("https://git.drobilla.net/cgit.cgi/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('5fa9a9a27e62b007b9703055b869bc47ad02ba122ba04abf8a1de420b695d9e0c6dc9a1633777b8d380e1452b3d75b1c0eaffb1c703a962814281e2d79236179')

build() {
  cd "$pkgname-$pkgver"
  ./waf configure --prefix=/usr
  ./waf
}

package() {
  cd "$pkgname-$pkgver"
  ./waf install --destdir="$pkgdir/"
  install -vDm 644 {NEWS,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
