# Maintainer: David Runge <dave@sleepmap.de>
pkgname=moony.lv2
pkgver=0.22.0
pkgrel=2
pkgdesc="Easily add realtime programmable logic glue in LV2 plugin graphs"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/moony/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('libgl')
makedepends=('cmake' 'lv2')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz")
sha512sums=('a6bc08698a97c18ee5f349c5d64635822f8ecc6727f7118a2335856cc2a146a7309d6707710acd10462e0d4682070c48d364a52c2524780f57292df2e6fc2536')

prepare() {
  cd "$pkgname-$pkgver"
  mkdir -v build
}

build() {
  cd "$pkgname-$pkgver/build"
  cmake -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        ..
  make
}

check() {
  cd "$pkgname-$pkgver/build"
  ARGS='-VV' make test
}

package() {
  cd "$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir/" install
  # docs
  install -vDm 644 ../README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
