# Maintainer: David Runge <dave@sleepmap.de>
pkgname=dpf-plugins
pkgver=1.1
pkgrel=2
pkgdesc="Collection of DPF-based plugins"
arch=('x86_64')
url="https://github.com/DISTRHO/DPF-Plugins"
license=('custom:ISC' 'GPL2' 'GPL3' 'LGPL3' 'MIT')
groups=('ladspa-plugins' 'lv2-plugins' 'pro-audio' 'vst-plugins')
depends=('liblo' 'projectm')
makedepends=('lv2')
source=("$pkgname-$pkgver.tar.gz::https://github.com/DISTRHO/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('b624693ed5d20018352506f159c15188f32a6c2d2fd3996055efa2606ccbb02a8a8116713efe6480c2686bb8e1d318c40c4c5217d101c0d897a90d623845feee')

prepare() {
  mv -v "DPF-Plugins-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  make BUILD_VST=true \
       BUILD_DSSI=true \
       BUILD_LV2=true \
       BUILD_JACK=true
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" \
       PREFIX=/usr \
       install
  # docs
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # licenses
  install -vDm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -vDm 644 dpf/LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname}/dpf/LICENSE"
  install -vDm 644 plugins/Kars/LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname}/Kars/LICENSE"
  install -vDm 644 plugins/AmplitudeImposer/LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname}/AmplitudeImposer/LICENSE"
  install -vDm 644 plugins/CycleShifter/LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname}/CycleShifter/LICENSE"
  install -vDm 644 plugins/SoulForce/LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname}/SoulForce/LICENSE"
}
