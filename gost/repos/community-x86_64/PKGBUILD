# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=gost
pkgver=2.5
pkgrel=1
pkgdesc="GO Simple Tunnel"
arch=('x86_64')
url="https://github.com/ginuerzh/gost"
license=('MIT')
depends=('glibc')
makedepends=('go-pie')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ginuerzh/gost/archive/v$pkgver.tar.gz"
        LICENSE)
sha512sums=('d4e76b2c1bfc8ecfc6b53c5b63caa5776974c8a266a2e04ef6d6eea9e7e752672e488e3b711deeda9dcfa9606a2a6e7811791e3f1720912c1f16013e23f8275f'
            '8815c27a49d747e90fbcbf03c6011a76b95bc72a6c963ee8df9490e6aba9a3f4f9895e6a7759988aef123160ef8a6d2de85e8fce01c5980f8744e212792ad07c')

prepare() {
  mkdir .gopath
  ln -sf "$PWD/gost-$pkgver/vendor" .gopath/src
  ln -s "$PWD/gost-$pkgver" .gopath/src/github.com/ginuerzh/gost
  export GOPATH="$srcdir/.gopath"
}

build() {
  cd gost-$pkgver/cmd/gost
  go build # -gccgoflags "$CFLAGS $LDFLAGS"
}

package() {
  cd gost-$pkgver
  install -Dm755 cmd/gost/gost "$pkgdir/usr/bin/gost"
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
