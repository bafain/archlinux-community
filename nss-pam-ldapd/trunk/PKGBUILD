# $Id$
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Federico Cinelli <cinelli@aur.archlinux.org>

pkgname=nss-pam-ldapd
pkgver=0.9.10
pkgrel=1

pkgdesc='LDAP module for NSS and PAM'
url="https://arthurdejong.org/nss-pam-ldapd/"
arch=('x86_64')
license=('LGPL2.1')

depends=('pam' 'python2')
conflicts=('pam_ldap')

provides=('nslcd')

backup=('etc/nslcd.conf')

options=('!emptydirs')


source=("https://arthurdejong.org/nss-pam-ldapd/nss-pam-ldapd-$pkgver.tar.gz"{,.sig}
        "nslcd.service"
        "nslcd.sysusers"
        "nslcd.tmpfiles")

md5sums=('0aeeba1882c5d77108eab24ca6995b8d'
         'SKIP'
         'c0d4921737bdbb38924e6774cd1d3bb1'
         'f7a52c4e0eae1a5d525d0e2f374f9114'
         'd78a702ae9e81af03ae0a85a6af4d2f3')

validpgpkeys=('452EC2CB65CF68C2A1ADBF5F2A8B746810E0AFC1')

prepare() {
  cd nss-pam-ldapd-$pkgver
  autoreconf -fi
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-pam-seclib-dir=/usr/lib/security \
    CFLAGS="$CFLAGS" LDFLAGS="$LDFLAGS" PYTHON=python2

  sed -i 's/env python$/env python2/' utils/chsh.py utils/getent.py
}

build() {
  cd nss-pam-ldapd-$pkgver
  make
}

check() {
  cd nss-pam-ldapd-$pkgver
  make check
}

package() {
  cd nss-pam-ldapd-$pkgver

  install -Dm644 "$srcdir/nslcd.sysusers" "$pkgdir/usr/lib/sysusers.d/nslcd.conf"
  install -Dm644 "$srcdir/nslcd.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/nslcd.conf"
  install -Dm644 "$srcdir/nslcd.service" "$pkgdir/usr/lib/systemd/system/nslcd.service"
  install -Dm644 "$srcdir/$pkgname-$pkgver/nslcd.conf" "$pkgdir/etc/nslcd.conf"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
