# Maintainer : Eli Schwartz <eschwartz@archlinux.org>

pkgname=(python-genshi python2-genshi)
pkgver=0.7.1
pkgrel=1
pkgdesc="Python toolkit for stream-based generation of output for the web."
url="https://genshi.edgewall.org/"
license=("BSD")
arch=('x86_64')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/edgewall/genshi/archive/${pkgver}.tar.gz")
sha256sums=('72246b63cf71853b76c53b530059690eede9f85105da3e33842fdcf391a34e61')

prepare() {
    # uses 2to3 so need to build python2/python3 in separate dirs
    cp -a "${srcdir}"/genshi-${pkgver}{,-py2}
}

build() {
    cd "${srcdir}"/genshi-${pkgver}
    python setup.py build

    cd "${srcdir}"/genshi-${pkgver}-py2
    python2 setup.py build
}

check() {
    cd "${srcdir}"/genshi-${pkgver}
    python setup.py test

    cd "${srcdir}"/genshi-${pkgver}-py2
    python2 setup.py test
}

package_python-genshi() {
    depends=('python')

    cd "${srcdir}"/genshi-${pkgver}
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}

package_python2-genshi() {
    depends=('python2')

    cd "${srcdir}"/genshi-${pkgver}-py2
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}
