# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=prettier
pkgver=1.14.2
pkgrel=1
pkgdesc="An opinionated code formatter for JS, JSON, CSS, YAML and much more"
arch=("any")
url="https://prettier.io/"
license=("MIT")
depends=("nodejs")
makedepends=("yarn")
source=("https://github.com/$pkgname/$pkgname/archive/$pkgver.tar.gz")
sha256sums=("9c3a504a59a83b29c169c409713e8f9040212d0da85df3649ec84986879fd189")


build() {
	cd "$srcdir/$pkgname-$pkgver"
	yarn
	yarn build
}


package() {
	install -d "$pkgdir/usr/lib/$pkgname"
	cd "$pkgdir/usr/lib/$pkgname"
	cp -a "$srcdir/$pkgname-$pkgver/dist/"* .
	install -d "$pkgdir/usr/bin"
	ln -s "/usr/lib/$pkgname/bin-$pkgname.js" "$pkgdir/usr/bin/$pkgname"
}
