# $Id$
# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=('python-pyblake2' 'python2-pyblake2')
pkgver=1.1.2
pkgrel=2
pkgdesc='BLAKE2 hash function for Python'
arch=('x86_64')
url='https://pythonhosted.org/pyblake2'
license=('custom')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/a6/ea/559658f48713567276cabe1344a9ef918adcb34a9da417dbf0a2f7477d8e/pyblake2-${pkgver}.tar.gz")
sha256sums=('5ccc7eb02edb82fafb8adbb90746af71460fbc29aa0f822526fc976dff83e93f')

prepare() {
  cp -a pyblake2-${pkgver} python2-pyblake2-${pkgver}
}

build() {
  cd pyblake2-${pkgver}
  python setup.py build

  cd ../python2-pyblake2-${pkgver}
  python2 setup.py build
}

package_python-pyblake2() {
  depends=('python')

  cd pyblake2-${pkgver}

  python setup.py install --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 -t "${pkgdir}/usr/share/licenses/python-pyblake2" COPYING
}

package_python2-pyblake2() {
  depends=('python2')

  cd python2-pyblake2-${pkgver}

  python2 setup.py install --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 -t "${pkgdir}/usr/share/licenses/python2-pyblake2" COPYING
}
