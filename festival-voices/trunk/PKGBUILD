# $Id$
# Maintainer: Alexander RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=festival-voices
pkgname=('festival-us' 'festival-english')
pkgver=2.4
pkgrel=2
arch=('any')
url='http://www.cstr.ed.ac.uk/projects/festival/'
license=('custom')
depends=('festival')
source=("http://www.cstr.ed.ac.uk/downloads/festival/$pkgver/voices/festvox_cmu_us_awb_cg.tar.gz"
	"http://www.cstr.ed.ac.uk/downloads/festival/$pkgver/voices/festvox_cmu_us_rms_cg.tar.gz"
	"http://www.cstr.ed.ac.uk/downloads/festival/$pkgver/voices/festvox_cmu_us_slt_cg.tar.gz"
	"http://www.cstr.ed.ac.uk/downloads/festival/$pkgver/voices/festvox_kallpc16k.tar.gz"
	"http://www.cstr.ed.ac.uk/downloads/festival/$pkgver/voices/festvox_rablpc16k.tar.gz"
	'LICENSE')
sha512sums=('edb4008606afc72802b265a503d1f6a37fc8f99bd21f6aaa72a06a970f86858da17c0857ce423356f0a432b19ada87778e0a88f6ecb9aeb66ae42a41c06e0494'
            'b0a519a654e349b49b627bb35384dadde8e44c4af43a4df7f8126c1535fe5838d8d2cd13fc9813cad7ec4dd92ae018c6090ca453d0e6a1c470ccf839faf21125'
            'b02da2a5a17e0341cbd0d692021b8cb44cd3bfd3e6a60a06c5a22004ce2ad135efba64df75f4c8479a4cd2da15fd733165bce89972ae206c3e11b543150a69e4'
            'e7155a8df1305fc80a0b7f97e07b74f2d687ce93f89dc74b4ab3653a369629b956491381a1f4613e531570cc688480b151b2e60fd5b8f9755c641648dc72d3bc'
            'a8962a8187e83c4419414e7ef5953a8a1ed1b65a7d5a9dea3722185a4fb660727897e7dca9f6ccddf4fefc85d9d757dcb444a6ac08328b7dc94624ffff24f5a6'
            '98832eb38246e38baa5db77b065c0dd45b1b447226dc2f900c4341ff0309f39147e3fb325b7ca0af2cf2c5f38e5d33cefec7d96f823a0bda5120cf5241cb355f')

package_festival-us() {
  pkgdesc='American Male/Female and Scottish English Male speaker'
  replaces=('festival-awb-arctic')

  install -d "$pkgdir/usr/share/festival/voices/"
  cp -rf festival/lib/voices/us "$pkgdir/usr/share/festival/voices/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/festival-us/LICENSE"
}

package_festival-english() {
  pkgdesc='British and American English Male speaker'
  replaces=('festival-kallpc16k' 'festival-rablpc16k')

  install -d "$pkgdir/usr/share/festival/voices/"
  cp -rf festival/lib/voices/english "$pkgdir/usr/share/festival/voices/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/festival-english/LICENSE"
}

# vim:set ts=2 sw=2 et:
