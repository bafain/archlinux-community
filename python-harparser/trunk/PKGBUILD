# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-harparser
pkgname=('python-harparser' 'python2-harparser')
pkgver=0.4
pkgrel=2
pkgdesc="Python HAR Parser Utility"
arch=('any')
license=('MIT')
url="https://github.com/JustusW/harparser"
makedepends=('python' 'python2')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/JustusW/harparser/archive/$pkgver.tar.gz")
sha512sums=('1aef9ae75b9446d487310a2e4ae4ee8a2aff10c022f68c316307335a5df4346defd73e0d12d0bd4d1a08eeabc90ade9f8d043ee981467bb4fe633184e9f815d9')

prepare() {
  cp -a harparser-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/harparser-$pkgver
  python setup.py build

  cd "$srcdir"/harparser-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/harparser-$pkgver
  python -m unittest test.log

  cd "$srcdir"/harparser-$pkgver-py2
  python2 -m unittest test.log
}

package_python-harparser() {
  depends=('python')

  cd harparser-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-harparser() {
  depends=('python2')

  cd harparser-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
