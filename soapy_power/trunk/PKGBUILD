# $Id$
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Michal Krenek (Mikos) <m.krenek@gmail.com>

pkgname=soapy_power
pkgver=1.6.1
pkgrel=2
pkgdesc="Obtain power spectrum from SoapySDR devices"
arch=('any')
url="https://github.com/xmikos/soapy_power"
license=('MIT')
depends=('python' 'python-numpy' 'simplesoapy>=1.5.0' 'simplespectral')
makedepends=('python-setuptools')
optdepends=('soapyrtlsdr: Support for RTL-SDR dongles'
	    'soapyairspy: Support for Airspy R2 and Airspy Mini'
	    'soapysdrplay: Support for SDRplay RSP'
	    'soapyhackrf: Support for HackRF'
	    'soapybladerf: Support for Nuand bladeRF'
	    'soapyuhd: Support for Ettus USRP'
	    'soapylms7: Support for LimeSDR and other LMS7002M based Myriad RF boards'
	    'soapyredpitaya: Support for Red Pitaya'
	    'soapyosmo: Support for MiriSDR and RFSpace'
	    'soapyremote: Use any SoapySDR device remotely over network'
	    'python-pyfftw: Fastest FFT calculations with FFTW library'
	    'python-scipy: Faster FFT calculations with scipy.fftpack library')
source=("$pkgname-$pkgver.tar.gz::https://github.com/xmikos/soapy_power/archive/v$pkgver.tar.gz")
sha256sums=('fcf80fb3eca178ff8ea2ce87bc5cc390fa2b17f8dc72fa9e1213ca4022f469e4')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py build
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

