# $Id$
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=piper
pkgver=0.2.902
pkgrel=1
pkgdesc='GTK application to configure gaming mice'
arch=('any')
url='https://github.com/libratbag/piper'
license=('GPL')
depends=('python' 'python-gobject' 'libratbag>=0.9.900' 'python-evdev' 'python-lxml' 'python-cairo')
makedepends=('meson')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('2b6733a539de6b46eb43fd830b59d9ac02778d54ccb0e6f219962d8b69061f2259e6f8afa6c78aa89c4543a4b7009ae84322e8e417374f778086d1cf6e2b8d6e')

prepare() {
  cd $pkgname-$pkgver

  # Remove install script
  # This is handled by pacman hooks
  sed -i "/meson.add_install_script('meson_install.sh')/d" meson.build
}

build() {
  mkdir $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  arch-meson ..

  ninja
}

package() {
  cd $pkgname-$pkgver/build

  DESTDIR="$pkgdir" ninja install
}
