# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=processing
pkgver=3.3.7
pkgrel=1
arch=('x86_64')
pkgdesc='Programming environment for creating images, animations and interactions'
url='http://www.processing.org/'
license=('GPL' 'LGPL')
makedepends=('gendesk' 'java-openjfx' 'java-environment=8' 'apache-ant' 'rsync')
depends=('libgl' 'java-runtime=8')
options=('!strip')
# ex: 3.3.7 -> 0264 | update the expression when the 3.3. series version scheme changes
source=("https://github.com/$pkgname/$pkgname/archive/$pkgname-0$((257+${pkgver##3.3.}))-$pkgver.tar.gz")
sha256sums=('8d5bc6559c40f2427ffb91747323281a69e7d24523ee1fce91093b98bd6465e1')

prepare() {
  gendesk -f -n --pkgname="$pkgname" --pkgdesc="$pkgdesc"

  # Symbolic link for not having to repeat the revision number
  ln -sf "$pkgname-$pkgname-"*"-$pkgver" "$pkgname"

  # Use the font's built-in hinting instructions
  sed 's|  java|  _JAVA_OPTIONS="-Dawt.useSystemAAFontSettings=gasp" java|g' \
    -i "$pkgname/build/linux/processing"
}

build() {
  cd "$pkgname/build"

  ant run
}

package() {
  cd "$pkgname"

  install -d "$pkgdir/usr/"{bin/,share/$pkgname/}
  cp -r build/linux/work/* "$pkgdir/usr/share/$pkgname/"

  # Desktop shortcut
  install -Dm644 "build/shared/lib/icons/pde-256.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # Symbolic links in /usr/bin
  ln -s "/usr/share/$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"
  ln -s "/usr/share/$pkgname/$pkgname-java" "$pkgdir/usr/bin/$pkgname-java"
}

# getver: raw.githubusercontent.com/processing/processing/master/build/shared/revisions.txt
# vim: ts=2 sw=2 et:
