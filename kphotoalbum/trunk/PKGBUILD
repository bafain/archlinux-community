# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb ][ gmail ? com>
# Contributor: Jonathan Schaeffer <joschaeffer ][ gmail ? com>
# Contributor: chfoxli <lg ][ romandie ? com>

pkgname=kphotoalbum
pkgver=5.3
pkgrel=1
pkgdesc="KDE Photo Album, picture collection manager"
url="https://www.kphotoalbum.org/"
arch=('x86_64')
license=('GPL')
depends=('libkipi' 'libkdcraw' 'libkgeomap' 'exiv2')
makedepends=('extra-cmake-modules' 'plasma-framework' 'phonon-qt5' 'kdoctools' 'kparts' 'libkexiv2')
optdepends=()
source=("https://download.kde.org/stable/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.sig}
        kphotoalbum-qt-5.11.patch::"https://github.com/KDE/kphotoalbum/commit/47f4c00a.patch")
sha256sums=('c1175489c70902511d7fa7c261a65d4dd19a3a651a1811edea3034e36fda88d1'
            'SKIP'
            '78fd0e12826c9fa8c36f3be5bf8a7048ed1382be1a813d985bc65eba9b52ba66')
validpgpkeys=('8A53F0CB9F752E6F4F72FEC6F8DECD0DDF0F74A8') # Tobias Leupold <tobias.leupold@gmx.de>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kphotoalbum-qt-5.11.patch # Fix build with Qt 5.11
}

build() {
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd "${srcdir}"/build

  make DESTDIR="${pkgdir}" install
}
