# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Karol "Kenji Takahashi" Wo≈∫niak <kenji.sx>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Allen Li <darkfeline at abagofapples.com>

pkgbase=flake8
pkgname=(flake8 python2-flake8)
pkgver=3.5.0
pkgrel=4
epoch=1
pkgdesc="The modular source code checker: pep8, pyflakes and co"
arch=('any')
url="http://flake8.pycqa.org"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pyflakes' 'python2-pyflakes'
             'python-pycodestyle' 'python2-pycodestyle' 'python-mccabe' 'python2-mccabe'
             'python2-enum34' 'python2-configparser' 'python-pytest-runner' 'python2-pytest-runner')
checkdepends=('python-mock' 'python2-mock')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/PyCQA/flake8/archive/$pkgver.tar.gz"
        "https://gitlab.com/pycqa/flake8/commit/4fcbcccf381ce0987faa297173e4008b0490918f.patch"
        "flake8-pycodestyle2.4-compat.patch::https://gitlab.com/pycqa/flake8/merge_requests/230.patch"
        "flake8-pycodestyle2.4-new-checks.patch::https://gitlab.com/pycqa/flake8/merge_requests/231.patch"
        "https://gitlab.com/pycqa/flake8/commit/527af5c214ef0eccfde3dd58d7ea15e09c483bd3.patch")
sha512sums=('36c6bd0166a0e8f9b8c99cfd7d92f21a4b8d761727185bbd0dff231001dbb495ed4128e2c60ab16bf3920a8a8394efb0127d9233962940333cfc346766736639'
            '97d49780cd297091e6f556fc0bdaa5d508cbd591debdc17916932a9548418c861df733e1f9ca9d19d8e09ab006cd7a430290569b67382b1ee20072a985799ca1'
            '6793f1ac9340d4866f162848fd0beec78796a1f9865fb9bb8051f3e52ed5528c30f7dd54ec7aea0e830cfa6baf6f5eab9b32e716ffe849611a7da048e68d602a'
            '912d29dad70000fb6f7b94c32956d69cb39c883df6100776b0c529fcef8f42ca8614497bd1b3f842b4f237e361c2483db1fb1c634930fd21e0cb1e583296afb5'
            '0b826ab9a3257a7acfc260e8762e40ba2855914c0009c223fce2076d4ef92cc32956222ad0b9ccb6f62b3fb3dfe78ceaad6c389525795d74484a79afa7c0bb92')

prepare() {
  # update versions to what the next patch expects
  patch -p1 -d flake8-$pkgver < 4fcbcccf381ce0987faa297173e4008b0490918f.patch
  # fix renamed checks in newer pycodestyle versions
  patch -p1 -d flake8-$pkgver < flake8-pycodestyle2.4-compat.patch
  # add new pycodestyle 2.4.0 checks
  patch -p1 -d flake8-$pkgver < flake8-pycodestyle2.4-new-checks.patch
  # fix pyflakes 2.0 compat
  patch -p1 -d flake8-$pkgver < 527af5c214ef0eccfde3dd58d7ea15e09c483bd3.patch
  # disable overly restrictive dependency pinning
  sed -i -e 's/, *< *[0-9=.]*//' flake8-$pkgver/setup.py
  cp -a flake8-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/flake8-$pkgver
  python setup.py build

  cd "$srcdir"/flake8-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/flake8-$pkgver
  python setup.py pytest

  cd "$srcdir"/flake8-$pkgver-py2
  python2 setup.py pytest
}

package_flake8() {
  depends=('python-pyflakes' 'python-mccabe' 'python-pycodestyle')
  provides=('flake8-python3')
  replaces=('flake8-python3')
  conflicts=('flake8-python3')

  cd "$srcdir"/flake8-$pkgver
  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-flake8() {
  depends=('python2-pyflakes' 'python2-mccabe' 'python2-pycodestyle' 'python2-enum34'
           'python2-configparser')
  provides=('flake8-python2')
  replaces=('flake8-python2')
  conflicts=('flake8-python2')

  cd "$srcdir"/flake8-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
  mv "$pkgdir"/usr/bin/flake8{,-python2}
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
