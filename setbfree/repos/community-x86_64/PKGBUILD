# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Johannes Maibaum <jmaibaum at gmail dot com>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>
_name=setBfree
pkgname=setbfree
pkgver=0.8.8
pkgrel=3
pkgdesc="DSP tonewheel organ"
arch=('x86_64')
url="http://setbfree.org"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('ftgl' 'jack' 'liblo' 'pango')
makedepends=('gendesk' 'lv2' 'ttf-dejavu')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pantherb/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('b4a1031c9e133275d4ce1d7a40f872606ee3862e10443e1e4cdd6e526e36c77f5ed64a84f66639370fa61dd2ecaf9b355fea3f5a8b6161c573fb176c6d5c835e')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  export FONTFILE=/usr/share/fonts/TTF/DejaVuSans-Bold.ttf
  # XDG desktop files
  gendesk -n \
          --pkgname "${_name}" \
          --pkgdesc "${pkgdesc}" \
          --exec "${_name}UI" \
          --name "${_name}" \
          --categories "AudioVideo;Audio"
  gendesk -n \
          --pkgname x42-whirl \
          --pkgdesc "Leslie speaker emulation as JACK client" \
          --name x42-whirl \
          --categories "AudioVideo;Audio"
}

build() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr
}

package() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
  # xdg desktop files
  install -vDm 644 ../x42-whirl.desktop -t "${pkgdir}/usr/share/applications"
  install -vDm 644 ../${_name}.desktop \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  # icons
  install -vDm 644 doc/{${_name},x42-whirl}.png \
    -t "${pkgdir}/usr/share/pixmaps/"
  # man
  install -vDm 644 doc/*.1 -t "${pkgdir}/usr/share/man/man1/"
  # docs
  install -vDm 644 {AUTHORS,ChangeLog,README.md} doc/*.png \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
