# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
pkgname='radcli'
pkgver=1.2.9
pkgrel=1
pkgdesc='Client library for writing Radius clients in C or C++'
arch=('x86_64')
url='http://radcli.github.io/radcli/'
license=('BSD')
depends=('nettle' 'gnutls')
source=("https://github.com/${pkgname}/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('5edf75dbfe5830d69aac2ba1e48ca7b96fc3a9ae7b297befa4c78d6ceb526b2257ea98c1199a6f78d90f7dd2b6a4c12cd0c095056b0aadf451470717237e1c1e'
            'SKIP')
validpgpkeys=('1F42418905D8206AA754CCDC29EE58B996865171') # Nikos Mavrogiannopoulos <nmav@gnutls.org>

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --prefix='/usr' \
    --sysconfdir='/etc'
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
