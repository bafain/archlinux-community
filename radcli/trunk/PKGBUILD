# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
pkgname='radcli'
pkgver=1.2.10
pkgrel=1
pkgdesc='Client library for writing Radius clients in C or C++'
arch=('x86_64')
url='http://radcli.github.io/radcli/'
license=('BSD')
depends=('nettle' 'gnutls')
source=("https://github.com/${pkgname}/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('51671c1324bc846b861942b58727b1c0f98b16a2e3cf95a360e66e648ef34d579034da2768e1ff65ede773c8a170a35a45c92a0be8899939431edc3360d4e8bd'
            'SKIP')
validpgpkeys=('1F42418905D8206AA754CCDC29EE58B996865171') # Nikos Mavrogiannopoulos <nmav@gnutls.org>

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --prefix='/usr' \
    --sysconfdir='/etc'
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
