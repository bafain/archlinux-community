# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Pavol (Lopo) Hluchy <lopo AT losys DOT eu>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Jonas Heinrich <onny@project-insnaity.org>
# Contributor: Lee Watson <aur@revthefox.co.uk>
# Contributor: Tobias Hunger <tobias DOT hunger AT gmail DOT com>
# Contributor: Stefan Tatschner <stefan@sevenbyte.org>

pkgname=gitlab-shell
pkgver=8.1.1
pkgrel=1
epoch=2
pkgdesc="Git management software"
arch=('x86_64')
url="https://gitlab.com/gitlab-org/gitlab-shell/"
license=('MIT')
depends=('ruby2.3' 'redis' 'git')
makedepends=('go-pie')
options=('!strip')
install=gitlab-shell.install
backup=("etc/webapps/${pkgname}/config.yml"
        "etc/webapps/${pkgname}/secret"
        "etc/webapps/${pkgname}/gitconfig"
        "etc/webapps/${pkgname}/authorized_keys"
        "etc/webapps/${pkgname}/")
source=("${pkgname}-${pkgver}.tar.gz::https://gitlab.com/api/v4/projects/gitlab-org%2Fgitlab-shell/repository/archive?sha=v${pkgver}"
        "gitlab-shell.gitconfig"
        "gitlab-shell.sysusers")
sha512sums=('a9846bddc81792fab71bffbae3c36e6e1330fab556f72d393257b39749bce63d6829fd76db756236170123953da2629c79a024ba59928b1e76c250db7f7f13cd'
            '5caafe5300621ced58479e724b39656afc17f32a64ecde5dfa4919683ef37d9121552c78f066678472acb56e0a34b5c3cf34d9b44414d1ed5d91f725d180fa43'
            '616188911fa7b7c569bc2d5e2024177e67366c48b4ffef1f7ad4dc8e4e9e2cdb421b687f45a1e9bdec8bf2736a157ca9f1e0ba24edae2f073b8c7b5ac42745e8')

_datadir="/usr/share/webapps/${pkgname}"
_etcdir="/etc/webapps/${pkgname}"
_homedir="/var/lib/gitlab"
_logdir="/var/log/gitlab"
_srcdir="gitlab-shell-v${pkgver}"

prepare() {
  cd "${srcdir}/${_srcdir}"-*

  sed -e "s|user: git|user: gitlab|" \
      -e "s|/home/git|${_homedir}|" \
      -e "s|# log_file: .*|log_file: \"${_logdir}/gitlab-shell.log\"|" \
      -e "s|# host:|host:|" \
      -e "s|# port:|port:|" \
      -e "s|socket:|# socket:|" \
      config.yml.example > config.yml

  for file in {bin,hooks}/*; do
    sed -i s/ruby/ruby-2.3/g ${file}
  done
}

build() {
  cd "${srcdir}/${_srcdir}"-*

  ./bin/compile
}

package() {
  cd "${srcdir}/${_srcdir}"-*

  install -dm755 -o root -g root "${pkgdir}${_datadir}"
  install -dm750 -o 105 -g 105 "${pkgdir}${_homedir}"
  install -dm750 -o 105 -g 105 "${pkgdir}${_homedir}/satellites"
  install -dm700 -o 105 -g 105 "${pkgdir}${_homedir}/.ssh"
  install -dm750 -o 105 -g 105 "${pkgdir}${_etcdir}"
  install -dm770 -o 105 -g root "${pkgdir}${_logdir}"

  # sysusers
  install -Dm644 "${srcdir}/gitlab-shell.sysusers" "${pkgdir}/usr/lib/sysusers.d/gitlab-shell.conf"

  # .gitconfig
  install -Dm644 -o 105 -g 105 "${srcdir}/gitlab-shell.gitconfig" "${pkgdir}${_etcdir}/.gitconfig"
  ln -fs "${_etcdir}/.gitconfig" "${pkgdir}${_homedir}/.gitconfig"

  # .gitlab_shell_secret
  touch "${pkgdir}${_etcdir}/secret"
  chmod 640 "${pkgdir}${_etcdir}/secret"
  chown root:105 "${pkgdir}${_etcdir}/secret"
  ln -fs "${_etcdir}/secret" "${pkgdir}${_datadir}/.gitlab_shell_secret"

  # config.yml
  mv config.yml "${pkgdir}${_etcdir}"
  ln -fs "${_etcdir}/config.yml" "${pkgdir}${_datadir}/config.yml"

  cp -r VERSION bin hooks lib spec support "${pkgdir}${_datadir}"
  ln -fs "${_datadir}" "${pkgdir}/${_homedir}/"

  install -dm2770 -o 105 -g 105 "${pkgdir}${_homedir}/repositories"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim:set ts=2 sw=2 et:
