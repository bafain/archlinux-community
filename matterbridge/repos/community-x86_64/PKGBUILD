# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=matterbridge
pkgver=1.11.1
pkgrel=1
pkgdesc="Multi-protocols (IRC/XMPP/Mattermost/Slack/Matrix/etc) bridge"
arch=('x86_64')
_url="github.com/42wim/matterbridge"
url="https://${_url}"
license=('Apache')
depends=('glibc')
makedepends=('go-pie')
backup=('etc/matterbridge.toml')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/v${pkgver}.tar.gz"
        ${pkgname}-${pkgver}.tar.gz.asc::"${url}/releases/download/v${pkgver}/v${pkgver}.tar.gz.asc"
        'matterbridge.service')
sha256sums=('1ee04e8da50e1274ee5d3886ead67d1a9b0b5429568ce1f0285b2b40c47a2774'
            'SKIP'
            'afb424b918f7108f5efae39afbed5d23d9a2c36710c6e0adbb96c7e5c7835aa3')
validpgpkeys=('CC7D978417C1AEA1E4CDD7240E41AB4BF4C610B4') # wim <wim@42.be>

prepare() {
  export GOPATH="${srcdir}"/gopath
  mkdir -p "$GOPATH"/src/${_url}
  cp -r ${pkgname}-${pkgver}/* "$GOPATH"/src/${_url}
}

build() {
  export GOPATH="${srcdir}"/gopath
  go install -v ${_url}
}

package() {
  install -Dm755 gopath/bin/matterbridge -t "${pkgdir}"/usr/bin/
  install -Dm644 ${pkgname}-${pkgver}/matterbridge.toml.sample "${pkgdir}"/etc/matterbridge.toml
  install -Dm644 ${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
}
