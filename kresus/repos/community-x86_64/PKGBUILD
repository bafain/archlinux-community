# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <bruno.n.pagani@gmail.com>

pkgname=kresus
pkgver=0.13.3
_commit=c6a2355c8d31be99d1a9d8ab53d3f3e58c57cf1b
pkgrel=1
pkgdesc="Self-hosted personal finance manager"
arch=('x86_64')
url="https://kresus.org"
license=('MIT')
backup=('etc/webapps/kresus/config.ini')
depends=('nodejs' 'weboob-headless')
makedepends=('npm' 'libpng' 'python2')
optdepends=('python2-pdfminer: For IBAN extraction from PDF RIB')
source=(${pkgname}-${pkgver}.tar.bz2::"https://framagit.org/bnjbvr/kresus/repository/${pkgver}/archive.tar.bz2"
        'config.ini'
        "${pkgname}.service"
        "${pkgname}.sysusers"
        "${pkgname}.tmpfiles")
sha256sums=('a1df0997f46e5e9d4745fc1e9f4e875ffa3c99ec32848fd78fb9872092281cc2'
            'adc91cd0cef6b546d482ebe1e9de85a451105166c15c190caa8c6a86c023b07a'
            '5ea65d143558e50a47a65daa6a363b876e0d369a162ba88e3070f685a9ac8de9'
            'd9d30f5470c7165e4917487b69d7ab82e463da4e1355056e1035ee501d3f1adc'
            'ba8ad7d9eb5d2b47fde5f6a3ab98596e5c679141b78d76d54b44830604b67632')

build() {
    cd ${pkgname}-${pkgver}-${_commit}
    npm install && npm run build:prod
}

package() {
    cd ${pkgname}-${pkgver}-${_commit}
    make DESTDIR="${pkgdir}" install
    # Fix python2 name
    sed -i 's:#!/usr/bin/env python:#!/usr/bin/env python2:' "${pkgdir}"/usr/lib/node_modules/kresus/server/weboob/main.py
    sed -i 's:#!/usr/bin/env python:#!/usr/bin/env python2:' "${pkgdir}"/usr/lib/node_modules/kresus/build/server/weboob/main.py
    # Fix npm crazyness
    chmod -R go-w "${pkgdir}"/usr
    install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/

    cd ..
    install -Dm600 config.ini -t "${pkgdir}"/etc/webapps/kresus/
    install -Dm644 ${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm644 ${pkgname}.sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
    install -Dm644 ${pkgname}.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/${pkgname}.conf
}
