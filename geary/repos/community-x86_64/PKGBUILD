# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: sebikul <sebikul@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=geary
pkgver=0.12.4
pkgrel=1
pkgdesc='A lightweight email client for the GNOME desktop'
arch=('x86_64')
url='http://www.yorba.org/projects/geary/'
license=('GPL3')
depends=(
  'cairo' 'enchant' 'gcr' 'gdk-pixbuf2' 'glib2' 'gmime' 'gnome-keyring' 'gtk3'
  'iso-codes' 'libcanberra' 'libgee' 'libnotify' 'libsecret' 'libsoup'
  'libxml2' 'pango' 'sqlite' 'webkit2gtk'
)
makedepends=(
  'cmake' 'git' 'gnome-doc-utils' 'gobject-introspection' 'intltool' 'vala'
)
source=("git+https://gitlab.gnome.org/GNOME/geary.git#tag=geary-${pkgver}"
        'geary-enchant2.patch')
sha256sums=('SKIP'
            'd90243e5ede36399562b5c2e53350e8f0f1a6a56f8a062910a4d4e83b006acb7')

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build

  cd geary

  patch -Np1 -i ../geary-enchant2.patch
}

build() {
  cd build

  cmake ../geary \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DDESKTOP_UPDATE='FALSE' \
    -DICON_UPDATE='FALSE' \
    -DGSETTINGS_COMPILE='FALSE' \
    -DGSETTINGS_COMPILE_IN_PLACE='FALSE'
  make
}

package() {
  make DESTDIR="${pkgdir}" -C build install
}

# vim: ts=2 sw=2 et:
