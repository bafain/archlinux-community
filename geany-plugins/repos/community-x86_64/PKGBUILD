# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel 'angvp' Velasquez <angvp@archlinux.org>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@gmail.com>
# Contributor: Patrick Melo <patrick@patrickmelo.eti.br>

pkgname=geany-plugins
pkgver=1.33
pkgrel=1
pkgdesc='Plugins for Geany'
arch=('x86_64')
url='https://plugins.geany.org/'
license=('GPL')
depends=("geany>=$pkgver" 'ctpl' 'gpgme' 'gtkspell3' 'libgit2' 'lua51')
makedepends=('cppcheck' 'intltool')
source=("https://plugins.geany.org/$pkgname/$pkgname-$pkgver.tar.bz2")
sha256sums=('12825739e2c3caefbc1ad57e62f1e4f691af8db8368f173aa84c43dffb4503a2')

prepare() {
  cd $pkgname-$pkgver

  autoreconf -fi
}

build() {
  cd "$pkgname-$pkgver"

  # Looking forward to Python 3 being supported
  export PYTHON=/usr/bin/python2

  ./configure --prefix=/usr --libexecdir=/usr/lib
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# getver: plugins.geany.org/downloads.html
# vim: ts=2 sw=2 et:
