# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel 'angvp' Velasquez <angvp@archlinux.org>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@gmail.com>
# Contributor: Patrick Melo <patrick@patrickmelo.eti.br>

pkgname=geany-plugins
pkgver=1.33
pkgrel=3
pkgdesc='Plugins for Geany'
arch=('x86_64')
url='https://plugins.geany.org/'
license=('GPL')
depends=('ctpl' "geany>=$pkgver" 'gpgme' 'gtkspell3' 'libgit2' 'lua51' 'python')
makedepends=('cppcheck' 'intltool')
source=("https://plugins.geany.org/$pkgname/$pkgname-$pkgver.tar.bz2"{,.sig}
        "https://github.com/geany/geany-plugins/commit/3d02ec2a2b0d3ccfeb28503534b95e60861e6fe8.patch")
sha256sums=('12825739e2c3caefbc1ad57e62f1e4f691af8db8368f173aa84c43dffb4503a2'
            'SKIP'
            '1e71e3aec1acbfb9a1e5bb803504bf8cc6feaf999dc24fb1e4d30760e3aacb99')
validpgpkeys=('CD38C008828C2EF1B63A8ED1B5688B006F78F01E') # Frank Lanitz <frank@geany.org>

prepare() {
  cd $pkgname-$pkgver

  # backport fix for https://github.com/geany/geany-plugins/pull/729
  patch -p1 < ../3d02ec2a2b0d3ccfeb28503534b95e60861e6fe8.patch

  autoreconf -fi
}

build() {
  cd "$pkgname-$pkgver"

  export PYTHON=/usr/bin/python
  ./configure --prefix=/usr --libexecdir=/usr/lib
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# getver: plugins.geany.org/downloads.html
# vim: ts=2 sw=2 et:
