# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Baptiste Grenier <baptiste@bapt.name>

pkgname=rubber
pkgver=1.4
pkgrel=2
pkgdesc="A wrapper for LaTeX and friends"
arch=('any')
url="https://launchpad.net/rubber/"
license=('GPL')
depends=('python2')
makedepends=('texinfo' 'texlive-plainextra')
source=("https://launchpad.net/rubber/trunk/$pkgver/+download/rubber-$pkgver.tar.gz")
sha512sums=('8b4a9f689702505e19543e5295375c14ae93fb9d6b89d1234fda093d4161551bd58c2485b53678f141d52a140002df08856cfa059f6287721402b74b0ef666f7')

build() {
  cd "${srcdir}/rubber-$pkgver"
  python2 setup.py build
}

package() {
  cd "${srcdir}/rubber-$pkgver"
  python2 setup.py install --root="$pkgdir" --mandir=/usr/share/man --infodir=/usr/share/info

  mkdir -p "$pkgdir"/usr/share/bash-completion/completions/
  echo "complete -f -X '!*.tex' rubber" >"$pkgdir"/usr/share/bash-completion/completions/rubber
}
