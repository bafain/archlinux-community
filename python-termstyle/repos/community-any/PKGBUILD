# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-termstyle
pkgname=(python-termstyle python2-termstyle)
pkgver=0.1.11
_commit=ed18c5ed4bfee21267843f758094a5620d5f7929
pkgrel=2
pkgdesc="Console colouring for python"
url="https://github.com/timbertson/termstyle"
license=('BSD')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
source=("$pkgbase-$_commit.tar.gz::https://github.com/timbertson/termstyle/archive/$_commit.tar.gz")
sha512sums=('02849055614caf37d3b836bef52b6c5ad281570ad5577aa253e079ecd3d82ed1f2168394edbf4760e1a9ba16e7f23aecd7191c39d3dc1de2aeb4ab9599b0717a')

prepare() {
  mv termstyle-{$_commit,$pkgver}
  cp -a termstyle-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/termstyle-$pkgver
  python setup.py build

  cd "$srcdir"/termstyle-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/termstyle-$pkgver
  python test3.py

  cd "$srcdir"/termstyle-$pkgver-py2
  PYTHONIOENCODING=UTF-8 python2 test2.py
}

package_python-termstyle() {
  depends=('python-setuptools')

  cd termstyle-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-termstyle() {
  depends=('python2-setuptools')

  cd termstyle-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
