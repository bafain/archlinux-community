# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=calc
pkgver=2.12.6.7
pkgrel=1
pkgdesc="Arbitrary precision console calculator"
arch=('x86_64')
url="http://www.isthe.com/chongo/tech/comp/calc/"
license=('GPL')
depends=('readline')
makedepends=('chrpath')
#source=("http://switch.dl.sf.net/sourceforge/calc/calc-$pkgver.tar.bz2")
source=("http://www.isthe.com/chongo/src/calc/calc-$pkgver.tar.bz2")
sha256sums=('3a4f1acde15941048214f393beb97f9e12fc1ef2585fe0ab026e93ebcd19dd46')

build() {
  cd "$srcdir/calc-$pkgver"
  make USE_READLINE="-DUSE_READLINE" READLINE_LIB="-lreadline" READLINE_EXTRAS="-lhistory -lncurses" -j1
}

package() {
  cd "$srcdir/calc-$pkgver"
  make T="$pkgdir" install
  mv "$pkgdir/usr/bin/cscript" "$pkgdir/usr/share/calc/"
  chrpath -d "$pkgdir/usr/bin/calc"
}
