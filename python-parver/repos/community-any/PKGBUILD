# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-parver
pkgver=0.1.1
pkgrel=1
pkgdesc="Parse and manipulate version numbers"
url="https://github.com/RazerM/parver"
license=('MIT')
arch=('any')
depends=('python-arpeggio' 'python-attrs' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-hypothesis' 'python-pretend')
source=("$pkgname-$pkgver.tar.gz::https://github.com/RazerM/parver/archive/$pkgver.tar.gz")
sha512sums=('d79a4981f3ad77180c5a9c5972410714a1be075308c933bde6af499c25aaa2f82034afb27c0b4c4d7ca269d645285a953a91ea6f945fcac663250ed06ae3accb')

build() {
  cd parver-$pkgver
  python setup.py build
}

check() {
  cd parver-$pkgver
  python setup.py pytest
}

package() {
  cd parver-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
