# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: stonecrest <stonecrest[at]gmail[dot]com>

pkgname=mac
pkgver='3.99u4b5'
_pkgver='3.99-u4-b5-s7'
pkgrel=9
pkgdesc='APE codec and decompressor'
arch=('x86_64')
url='http://etree.org/shnutils/shntool/'
license=('custom')
depends=('gcc-libs')
makedepends=('yasm')
_baseurl='http://etree.org/shnutils/shntool/support/formats/ape/unix'
source=("${_baseurl}/${_pkgver}/${pkgname}-${_pkgver}.tar.gz" mac-gcc6.patch)
md5sums=('74df3c96476636387ddd12255b03bebe'
         'd3e9814f519b523a5dd86f8f86e3a69c')

prepare() {
  cd $pkgname-$_pkgver
  patch -p1 -i ../mac-gcc6.patch # Fix build with GCC 6 (Gentoo)
}

build() {
  cd "${srcdir}"/${pkgname}-${_pkgver}

  CXXFLAGS="-DSHNTOOL" ./configure --prefix=/usr --enable-assembly=yes 
  CXXFLAGS="-DSHNTOOL" make
}
package() {
  cd "${srcdir}"/${pkgname}-${_pkgver}

  make DESTDIR="${pkgdir}" install
  install -Dm644 src/License.htm \
      "${pkgdir}"/usr/share/licenses/${pkgname}/license.htm
}
