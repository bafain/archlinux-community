# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

_pkgbasename=openssl
pkgname=lib32-$_pkgbasename
_ver=1.1.0i
# use a pacman compatible version scheme
pkgver=${_ver/[a-z]/.${_ver//[0-9.]/}}
#pkgver=$_ver
pkgrel=1
epoch=1
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security (32-bit)'
arch=('x86_64')
url='https://www.openssl.org'
license=('custom:BSD')
depends=('lib32-glibc' "${_pkgbasename}")
optdepends=('ca-certificates')
source=("https://www.openssl.org/source/${_pkgbasename}-${_ver}.tar.gz"
        "https://www.openssl.org/source/${_pkgbasename}-${_ver}.tar.gz.asc"
        'ca-dir.patch')
validpgpkeys=(8657ABB260F056B1E5190839D9C4D26D0E604491)
md5sums=('9495126aafd2659d357ea66a969c3fe1'
         'SKIP'
         '02b53865fb70faef763e262b4971aa4b')

prepare() {
	cd "$srcdir"/$_pkgbasename-$_ver

	# set ca dir to /etc/ssl by default
	patch -p0 -i "$srcdir"/ca-dir.patch
	# test fails since 1.1.0h
	rm test/recipes/80-test_cms.t
}

build() {
 	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	cd "$srcdir"/$_pkgbasename-$_ver

	# mark stack as non-executable: http://bugs.archlinux.org/task/12434
	./Configure --prefix=/usr --openssldir=/etc/ssl --libdir=lib32 \
		shared no-ssl3-method \
		linux-elf \
		"-Wa,--noexecstack ${CPPFLAGS} ${CFLAGS} ${LDFLAGS}"

	make MAKEDEPPROG="${CC}" depend
	make
}

check() {
	cd "$srcdir"/$_pkgbasename-$_ver
	# the test fails due to missing write permissions in /etc/ssl
	# revert this patch for make test
	patch -p0 -R -i "$srcdir"/ca-dir.patch
	make test
	patch -p0 -i "$srcdir"/ca-dir.patch
}

package() {
	cd "$srcdir"/$_pkgbasename-$_ver
	make DESTDIR="$pkgdir" install_sw

	rm -rf ${pkgdir}/{usr/{include,bin},etc}
	mkdir -p "$pkgdir"/usr/share/licenses
	ln -s $_pkgbasename "$pkgdir"/usr/share/licenses/$pkgname
}
