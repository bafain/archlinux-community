# $Id$
# Maintainer:m Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=lib32-at-spi2-core
pkgver=2.28.0
pkgrel=1
pkgdesc='Protocol definitions and daemon for D-Bus at-spi'
arch=('x86_64')
url='https://www.gnome.org'
license=('GPL2')
depends=('at-spi2-core' 'lib32-dbus' 'lib32-glib2' 'lib32-libxtst')
makedepends=(intltool gobject-introspection git gtk-doc meson)
_commit=c210da7e817f36643fb6b3fb3410103f9887dcf8  # tags/AT_SPI2_CORE_2_28_0^0
source=("git+https://git.gnome.org/browse/at-spi2-core#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd at-spi2-core
  git describe --tags | sed 's/^AT_SPI2_CORE_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd at-spi2-core
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  arch-meson at-spi2-core build -D enable_docs=false --libdir=/usr/lib32
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  rm -r "$pkgdir"/{etc,usr/{include,lib,share}}
}

# vim:set ts=2 sw=2 et:
