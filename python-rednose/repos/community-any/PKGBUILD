# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-rednose
pkgname=(python-rednose python2-rednose)
pkgver=1.3.0
pkgrel=2
pkgdesc="Coloured output for nosetests"
url="https://github.com/JBKahn/rednose"
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-termstyle' 'python2-termstyle'
             'python-colorama' 'python2-colorama' 'python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/JBKahn/rednose/archive/$pkgver.tar.gz")
sha512sums=('15a58e659a8b5a467eed095adfdb6d1c6e7f660b37290d6ab15795888e3ba099b6c299cfc1d6caa7e9f8ba71e4260e05c87b8c72d06103ba1ff9a3a3f455c325')

prepare() {
  sed -i 's/==1.10.0//' rednose-$pkgver/setup.py
  cp -a rednose-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/rednose-$pkgver
  python setup.py build

  cd "$srcdir"/rednose-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/rednose-$pkgver
  python setup.py test

  cd "$srcdir"/rednose-$pkgver-py2
  # Stuck. https://github.com/JBKahn/rednose/issues/23
  # python2 setup.py test
}

package_python-rednose() {
  depends=('python-setuptools' 'python-termstyle' 'python-colorama' 'python-nose')

  cd rednose-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}

package_python2-rednose() {
  depends=('python2-setuptools' 'python2-termstyle' 'python2-colorama' 'python2-nose')

  cd rednose-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}
