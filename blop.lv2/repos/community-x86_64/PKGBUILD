# Maintainer: David Runge <dave@sleepmap.de>
pkgname=blop.lv2
pkgver=1.0.0
pkgrel=3
pkgdesc="A port of the BLOP LADSPA plugins by Mike Rawes to LV2."
arch=('x86_64')
url="https://git.drobilla.net/cgit.cgi/blop.lv2.git/about/"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio')
depends=('gcc-libs')
makedepends=('lv2' 'python2')
source=("https://git.drobilla.net/cgit.cgi/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('f1e6a1aadf733f86faa57e827002dcab6afea428258ba20bc8ea62f5f53ce4a1341e3e534b9b3f169df1de1b39f992f40f30d18447dc344e593af6a733da013a')

build() {
  cd "$pkgname-$pkgver"
  python2 waf configure --prefix=/usr
  python2 waf
}

package() {
  cd "$pkgname-$pkgver"
  python2 waf install --destdir="$pkgdir/"
  install -vDm 644 {AUTHORS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
