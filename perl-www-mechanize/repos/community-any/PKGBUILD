# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Francois Charette <firmicus@gmx.net>

pkgname=perl-www-mechanize
pkgver=1.87
pkgrel=1
pkgdesc="Automates web page form & link interaction"
arch=(any)
url="http://search.cpan.org/dist/WWW-Mechanize"
license=('GPL' 'PerlArtistic')
depends=('perl-libwww' 'perl-html-parser' 'perl-uri' 'perl-http-response-encoding'
	 'perl-http-server-simple' 'perl-test-warn' 'perl-html-form')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/O/OA/OALDERS/WWW-Mechanize-$pkgver.tar.gz)
sha256sums=('819342a55cdaaf742e8511ecf72c7764b71789f4d1ef0eb6f4b4db4361f3b9cf')

build() {
  cd  "$srcdir"/WWW-Mechanize-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  "$srcdir"/WWW-Mechanize-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
