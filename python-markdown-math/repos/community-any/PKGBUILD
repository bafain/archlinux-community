# $Id$
# Maintainer: Jiachen YANG <farseerfc@gmail.com>
# AUR Maintainer: Marcus Scheunemann

pkgbase=python-markdown-math
pkgname=('python-markdown-math' 'python2-markdown-math')
pkgver=0.6
pkgrel=2
pkgdesc="Math extension for Python-Markdown"
arch=('any')
url="https://github.com/mitya57/python-markdown-math"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-markdown' 'python2-markdown')
source=("https://files.pythonhosted.org/packages/source/${pkgbase:0:1}/${pkgbase}/${pkgbase}-${pkgver}.tar.gz"{,.asc})
sha256sums=('c68d8cb9695cb7b435484403dc18941d1bad0ff148e4166d9417046a0d5d3022'
            'SKIP')
validpgpkeys=('F24299FF1BBC9018B906A4CB6026936D2F1C8AE0') # Dmitry Shachnev <mitya57@debian.org>

build() {
    cd "python-markdown-math-${pkgver}"

    python setup.py build
    python2 setup.py build
}

check() {
    cd "python-markdown-math-${pkgver}"

    python setup.py test
    python2 setup.py test
}

package_python-markdown-math() {
    depends=('python-markdown')

    cd "python-markdown-math-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-markdown-math() {
    depends=('python2-markdown')

    cd "python-markdown-math-${pkgver}"
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
