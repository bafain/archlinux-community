# $Id$
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: razer <razer@neuf.fr>

pkgbase=python-serpent
pkgname=("$pkgbase" "${pkgbase/-/2-}")
pkgver=1.27
pkgrel=2
pkgdesc='Serializer for literal Python expressions'
url='https://github.com/irmen/Serpent'
arch=('any')
license=('MIT')
makedepends=('git')
depends=('python' 'python2')
source=("git+https://github.com/irmen/Serpent.git#tag=serpent-$pkgver")
md5sums=('SKIP')

package_python-serpent() {
  depends=('python')

  cd Serpent
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-serpent() {
  depends=('python2')

  cd Serpent
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: -u 2 anaconda.org/conda-forge/serpent
# vim: ts=2 sw=2 et:
