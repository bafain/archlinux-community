# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Corey Mwamba <contact.me@coreymwamba.co.uk>
# Contributor: Corey Mwamba <contact.me@coreymwamba.co.uk>

pkgname=rtirq
pkgver=20150216
pkgrel=2
pkgdesc="Realtime IRQ thread system tuning."
arch=('any')
url="https://www.rncbc.org/jack/"
license=('GPL2')
depends=('bash')
backup=('etc/conf.d/rtirq')
source=("https://www.rncbc.org/jack/${pkgname}-${pkgver}.tar.gz")
sha512sums=('4cb4e3a8e314cec238c2a21c975bec258f229b660e0994893b73aa0b16f6041179ff4e220532fde237d70e15fd5420e22ee5dbf84168148060e760d76617b59c')

prepare() {
  cd "${pkgname}-${pkgver}"
  sed -e "s|etc/sysconfig/rtirq|etc/conf.d/rtirq|" \
      -e "s|IRQ\.|irq/|" \
      -i "${pkgname}.sh"
  sed -e "s|etc/init.d|usr/bin|" -i "${pkgname}.service"
}

package() {
  cd "${pkgname}-${pkgver}"
  install -vDm755 "${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  install -vDm644 "${pkgname}.conf" "${pkgdir}/etc/conf.d/${pkgname}"
  install -vDm644 "${pkgname}.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"

}

