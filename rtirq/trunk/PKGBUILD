# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Corey Mwamba <contact.me@coreymwamba.co.uk>
# Contributor: Corey Mwamba <contact.me@coreymwamba.co.uk>

pkgname=rtirq
pkgver=20180209
pkgrel=3
pkgdesc="Realtime IRQ thread system tuning."
arch=('any')
url="https://www.rncbc.org/archive/#rtirq"
license=('GPL2')
groups=('realtime')
depends=('bash')
backup=('etc/rtirq.conf')
source=("https://www.rncbc.org/archive/${pkgname}-${pkgver}.tar.gz")
sha512sums=('9de924e627b91d6df162b6c8c0c3421fbab11f08162abe98b0fac8742ea924a7e0a18aab23d828e19fe81dfdb70f2f6814c7195acbcbc93af9a339dc549190b2')

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix executable location in systemd service
  sed -e "s|etc/init.d|usr/bin|" -i "${pkgname}.service"
}

package() {
  cd "${pkgname}-${pkgver}"
  install -vDm 755 "${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  install -vDm 644 "${pkgname}.conf" "${pkgdir}/etc/${pkgname}.conf"
  install -vDm 644 "${pkgname}.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
}

