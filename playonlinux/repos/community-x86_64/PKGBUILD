# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: K. Hampf <khampf@users.sourceforge.net>
# Contributor: Skunnyk <skunnyk@archlinux.fr>

pkgname=playonlinux
pkgver=4.2.12
pkgrel=2
pkgdesc="GUI for managing Windows programs under linux"
url="http://www.playonlinux.com/"
license=('GPL')
depends=('wine' 'imagemagick' 'xterm' 'wxpython' 'cabextract' 'unzip' 'mesa-demos' 'gnupg'
         'icoutils' 'xdg-user-dirs' 'libxmu' 'netcat' 'wget' 'p7zip')
arch=('x86_64')
source=(http://www.playonlinux.com/script_files/PlayOnLinux/${pkgver/.0/}/PlayOnLinux_${pkgver/.0/}.tar.gz
        PlayOnLinuxUrlHandler.desktop)
options=(!strip)
sha256sums=('38cbdc85cca83b72ed0e54ee3a768f939fefe29d3670fc5d76b0303b5bda680e'
            '304d8e998d271383c44acdf386c4664cd65463d5f7f5e3c1c7563fbd8f71a6a8')

package() {
  cd "$srcdir/$pkgname"

  sed -i "s/libexec/bin/g" Makefile

  make PREFIX=/usr
  make DESTDIR="${pkgdir}" install

  install -d ${pkgdir}/usr/share/playonlinux/lang
  mv -v ${pkgdir}/usr/share/locale ${pkgdir}/usr/share/playonlinux/lang/locale
  chmod 755 ${pkgdir}/usr/share/playonlinux/lang

  install -m755 ${srcdir}/PlayOnLinuxUrlHandler.desktop ${pkgdir}/usr/share/applications/PlayOnLinuxUrlHandler.desktop
  install -m755 playonlinux-url_handler ${pkgdir}/usr/bin/playonlinux-url_handler
  sed -i "s/python /python2 /g" $pkgdir/usr/{bin,share/playonlinux}/playonlinux-url_handler

#  sed -i "s/ %F//g" $pkgdir/usr/share/applications/playonlinux.desktop
}
