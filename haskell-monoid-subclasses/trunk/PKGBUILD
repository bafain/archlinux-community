# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

_hkgname=monoid-subclasses
pkgname=haskell-monoid-subclasses
pkgver=0.4.6
pkgrel=5
pkgdesc="Subclasses of Monoid"
url="https://github.com/blamario/monoid-subclasses/"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-primes' 'haskell-vector')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('25c21b9ecd75ef1918d407163618e49817d01e8617a9ee4aa613ce3a9f6daac1f5bc278bf47672568c85a122c46319ebf92042d6cb7072037b5f2ef0ea010fa8')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "BSD3-LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/BSD3-LICENSE.txt"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/BSD3-LICENSE.txt"
}
