# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

_gemname=yard
pkgname=ruby-yard
pkgver=0.9.14
pkgrel=1
pkgdesc='YARD is a Ruby Documentation tool. The Y stands for "Yay!"'
url='https://yardoc.org/'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-rdoc')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/lsegal/yard/archive/v${pkgver}.tar.gz)
sha256sums=('ce7933c4545fc2fd6ca176913befaa7a8a69ff545f21c6b883b066af83f600bb')
sha512sums=('7974d431b2c1ef4830f225fd2012592405e7fb63546aeb52b74f4ad8b498926be979f3010bd315877bb4f738f6aa6c31f54b71832b461ebd3fa54fe1a1fccf5f')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a docs/* "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
