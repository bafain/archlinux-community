# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=alembic
pkgver=1.7.5
pkgrel=2
pkgdesc="A open framework for storing and sharing scene data"
arch=('x86_64')
url="http://www.alembic.io/"
license=('BSD3')
depends=('openexr' 'boost-libs' 'hdf5' 'zlib')
makedepends=('cmake' 'boost')
source=(https://github.com/alembic/alembic/archive/${pkgver}.tar.gz)
sha512sums=('9e8c9a29661d4cec16dbc77014188a07112c78b011c48fb5e9c2107512926e2cdd43f65e732c456574e07947afc4e53e200753ca59430f011e6fc8ea31041fc0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  mkdir build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_HDF5=OFF
    # Temporarily disable HDF5 until upstream fixes the linking or until we make a patch.

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  cd build
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
