# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gordian Edenhofer <gordian.edenhofer[at]yahoo[dot]de>

pkgname=certbot-nginx
pkgver=0.21.1
pkgrel=1
pkgdesc="Nginx plugin for Letâ€™s Encrypt client"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
depends=("certbot=$pkgver" "python-acme=$pkgver" 'python-mock' 'python-pyopenssl' 'python-pyparsing'
         'python-setuptools' 'python-zope-interface')
replaces=("letsencrypt-nginx")
source=("https://pypi.io/packages/source/c/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('e0366a13dcd7763b7972438b31931d07545a82745d7f6a9f23f07006bd1d4458a67facbdd178b4c9c1341f90f14d0aff313c2a93dde26af1fff1287a48644ceb'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py test
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py install --root="$pkgdir"
}
