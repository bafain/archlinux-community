# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=merlin
pkgver=3.1.0
pkgrel=1
pkgdesc="Context sensitive completion for OCaml in Vim and Emacs"
arch=('x86_64')
url="https://github.com/ocaml/merlin"
license=('MIT')
options=('!strip')
depends=('glibc')
makedepends=('ocaml-findlib' 'ocaml-yojson' 'emacs')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/v${pkgver}.tar.gz")
sha256sums=('5deb4e3d9f1eaf283e17ec7dca2f17ce768a229e1da0170844d99860f84b8bc6')

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix "/usr" \
        --vimdir "/usr/share/vim/vimfiles" \
        --enable-compiled-emacs-mode
    make
}

check() {
    cd ${pkgname}-${pkgver}
    make test || warning "Tests failed"
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install

    install -Dm644 LICENSE_MIT.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
}
