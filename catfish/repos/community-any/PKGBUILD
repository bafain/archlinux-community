# $Id$
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alexander Fehr <pizzapunk@gmail.com>
# Contributor: Pantelis Panayiotou <p.panayiotou@gmail.com>

pkgname=catfish
pkgver=1.4.6
pkgrel=2
pkgdesc='Versatile file searching tool'
arch=('any')
url='https://launchpad.net/catfish-search'
license=('GPL')
install="$pkgname.install"
depends=('gtk3' 'python-cairo' 'python-gobject' 'python-pexpect'
         'python-ptyprocess' 'python-xdg' 'xdg-utils')
makedepends=('git' 'intltool' 'python-distutils-extra')
optdepends=('mlocate: filename search'
            'zeitgeist: integration with zeitgeist')
source=("git+https://git.launchpad.net/catfish-search#tag=catfish-$pkgver")
md5sums=('SKIP')

build() {
  cd "$pkgname-search"

  python setup.py build
}

package () {
  cd "$pkgname-search"

  python setup.py install --root="$pkgdir" --optimize=1

  install -d "$pkgdir/usr/share/pixmaps"
  ln -s "/usr/share/icons/hicolor/scalable/apps/$pkgname.svg" \
    "$pkgdir/usr/share/pixmaps/$pkgname.svg"
}

# getver: git.launchpad.net/catfish-search/plain/catfish.1
# vim: ts=2 sw=2 et:
