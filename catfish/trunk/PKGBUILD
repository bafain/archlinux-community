# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alexander Fehr <pizzapunk@gmail.com>
# Contributor: Pantelis Panayiotou <p.panayiotou@gmail.com>

pkgname=catfish
pkgver=1.4.4
pkgrel=1
pkgdesc='Versatile file searching tool'
arch=('any')
url='https://launchpad.net/catfish-search'
license=('GPL')
install="$pkgname.install"
depends=('python-gobject' 'python-xdg' 'python-pexpect' 'python-cairo' 'python-ptyprocess' 'gtk3' 'xdg-utils')
makedepends=('intltool' 'python-distutils-extra')
optdepends=('zeitgeist: integration with zeitgeist'
            'mlocate: filename search')
source=("https://launchpad.net/catfish-search/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz")
sha256sums=('a2d452780bf51f80afe7621e040fe77725021c24a0fe4a9744c89ba88dbf87d7')

build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package () {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1

  install -d "$pkgdir/usr/share/pixmaps"
  ln -s "/usr/share/icons/hicolor/scalable/apps/$pkgname.svg" \
    "$pkgdir/usr/share/pixmaps/$pkgname.svg"
}

# getver: launchpad.net/catfish-search/+download
# vim: ts=2 sw=2 et:
