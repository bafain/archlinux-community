# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=chafa
pkgver=0.9.0
pkgrel=1
pkgdesc="Image-to-text converter supporting a wide range of symbols and palettes, transparency, animations, etc."
arch=("x86_64")
url="https://hpjansson.org/chafa/"
depends=('libmagick' 'libxslt')
makedepends=('gtk-doc')
license=("LGPL")
source=(https://github.com/hpjansson/chafa/releases/download/$pkgver/chafa-$pkgver.tar.xz{,.sig})
sha512sums=('37c9050709d6ec2ac8f6c2202e172df0ac8aa85ea398277d4e680412b9671cd117aba55befe60ade6365f94d909fe47a8f93795bf6d6308e7a25f15e2536fbd2'
            'SKIP')
validpgpkeys=('C01EDE5BB0D91E26D003662EC76BB9FEEAD12EA7')  # Hans Petter Jansson

prepare() {
  cd $pkgname-$pkgver
  sed -i -e 's|wand/MagickWand.h|MagickWand/MagickWand.h|' \
         -e 's|$LDFLAGS $MAGICKWAND_LIBS|$MAGICKWAND_LIBS $LDFLAGS|' \
         configure chafa/chafa.c
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --enable-man
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
