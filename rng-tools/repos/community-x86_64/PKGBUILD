# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Alasdair Haswell <ali at arhaswell dot co dot uk>

pkgname=rng-tools
pkgver=6.4
pkgrel=1
pkgdesc="Random number generator related utilities"
arch=('x86_64')
url="https://github.com/nhorman/rng-tools"
license=('GPL')
depends=('libgcrypt' 'curl' 'libxml2' 'sysfsutils')
backup=(etc/conf.d/rngd)
source=(https://github.com/nhorman/rng-tools/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        rngd.conf
        rngd.service)
sha256sums=('8a5b8b262b4fb430fcb2d12c9d3d3af83c1fe855a8db700b3de34744f5ccb300'
            'f4fd197dd682cdab07ddeabe8d3b73b1bc2e1cf322237be238a26b49a7dcb047'
            '653224f7a3294618069c78b9d87e8cdeb0f6d00a97f180fba8849a499fdbe726')

prepare() {
	cd "$srcdir"/$pkgname-$pkgver
	mv README{.md,}

	libtoolize --force
	aclocal
	autoheader
	automake --force-missing --add-missing
	autoconf
}

build() {
	cd "$srcdir"/$pkgname-$pkgver
	./configure --prefix=/usr --sbindir=/usr/bin
	make
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make DESTDIR="$pkgdir" install
	install -Dm0644 "$srcdir"/rngd.service "$pkgdir"/usr/lib/systemd/system/rngd.service
	install -Dm0644 "$srcdir"/rngd.conf "$pkgdir"/etc/conf.d/rngd
}
