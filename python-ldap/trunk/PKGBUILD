# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Maikel Wever <maikelwever@gmail.com>

pkgbase=python-ldap
pkgname=(python-ldap python2-ldap)
pkgver=3.0.0
pkgrel=1
pkgdesc="LDAP client API for Python"
url="http://python-ldap.readthedocs.io/"
arch=('x86_64')
license=('MIT')
makedepends=('python' 'python-setuptools'
             'python2' 'python2-setuptools'
	     'python-coverage' 'python2-coverage')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/python-ldap/python-ldap/archive/${pkgname}-${pkgver}.tar.gz")
sha256sums=('c5ae7cb9575b59eb45df51dc77e755f2d584ee1ee8992b5a003dbb8732a922c3')

prepare() {
  cp -a ${pkgbase}-${pkgbase}-$pkgver{,-py2}
}

build(){
  cd "$srcdir/$pkgbase-$pkgbase-$pkgver"
  python setup.py build

  cd "$srcdir/$pkgbase-$pkgbase-$pkgver-py2"
  python2 setup.py build
}

# TODO: Next release
#check(){
#  cd "$srcdir/$pkgbase-$pkgbase-$pkgver"
#  python -m coverage run --parallel setup.py test
#
#  cd "$srcdir/$pkgbase-$pkgbase-$pkgver-py2"
#  python2 -m coverage run --parallel setup.py test
#}

package_python2-ldap() {
  depends=('python2' 'python2-pyasn1' 'python2-pyasn1-modules' 'libldap')
  cd "$srcdir/$pkgbase-$pkgbase-$pkgver-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm 644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

}

package_python-ldap() {
  depends=('python' 'python-pyasn1' 'python-pyasn1-modules' 'libldap')
  cd "$pkgbase-$pkgbase-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm 644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

