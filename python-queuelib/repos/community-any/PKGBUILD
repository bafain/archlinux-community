# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: PyroDevil <p dot devil at gmail dot com>

pkgbase=python-queuelib
pkgname=('python2-queuelib' 'python-queuelib')
pkgver=1.5.0
pkgrel=2
pkgdesc="Collection of persistent (disk-based) queues."
arch=('any')
license=('BSD')
url="http://scrapy.org"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/scrapy/queuelib/archive/v$pkgver.tar.gz")
sha512sums=('8217dfa82c4a7c2449277d81f553b1dd5b82fa34ba0c69ed98bcc146940e8afb79251715be9ec61c962a78292f14d83dac862ba021485573c9069e85710d5998')

prepare() {
  cp -a queuelib-$pkgver{,-py2}
}

check() {
  cd "$srcdir"/queuelib-$pkgver
  py.test

  cd "$srcdir"/queuelib-$pkgver-py2
  py.test2
}

package_python-queuelib() {
  depends=('python')

  cd queuelib-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -D -m644 "README.rst" "$pkgdir/usr/share/doc/$pkgname/README.rst"
}

package_python2-queuelib() {
  depends=('python2')

  cd queuelib-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -D -m644 "README.rst" "$pkgdir/usr/share/doc/$pkgname/README.rst"
}
