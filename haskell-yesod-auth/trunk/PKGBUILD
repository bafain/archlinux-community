# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=yesod-auth
pkgname=haskell-yesod-auth
pkgver=1.6.3
pkgrel=98
pkgdesc="Authentication for Yesod."
url="http://www.yesodweb.com/"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-authenticate' 'haskell-base16-bytestring'
         'haskell-base64-bytestring' 'haskell-blaze-builder' 'haskell-blaze-html'
         'haskell-blaze-markup' 'haskell-byteable' 'haskell-conduit' 'haskell-conduit-extra'
         'haskell-cryptonite' 'haskell-data-default' 'haskell-email-validate'
         'haskell-file-embed' 'haskell-http-client' 'haskell-http-client-tls' 'haskell-http-conduit'
         'haskell-http-types' 'haskell-memory' 'haskell-mime-mail' 'haskell-network-uri'
         'haskell-nonce' 'haskell-persistent' 'haskell-persistent-template' 'haskell-random'
         'haskell-resourcet' 'haskell-safe' 'haskell-shakespeare' 'haskell-unliftio'
         'haskell-unliftio-core' 'haskell-unordered-containers' 'haskell-wai' 'haskell-yesod-core'
         'haskell-yesod-form' 'haskell-yesod-persistent')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('67c23def147cf2cc174475d193e97afdf2b6fc4121a9fec7e0894562e626774bee9114325c26f172e58686119a3508ed2365ece419a10721873d43bf7684854a')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fnetwork-uri
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
