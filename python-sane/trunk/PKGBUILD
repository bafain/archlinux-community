# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgbase=python-sane
pkgname=(python-sane python2-sane)
pkgver=2.8.3
pkgrel=2
pkgdesc="Python interface to the SANE scanner and frame grabber."
arch=('x86_64')
url="https://python-sane.readthedocs.io/en/latest/"
license=('MIT')
depends=('sane')
makedepends=('python-setuptools' 'python2-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/python-pillow/Sane/archive/v${pkgver}.tar.gz")
sha256sums=('1c3aa1c246007e9e6f7b11ca2c98256ebefed66f4ff13dcf38179c721677022f')

build() {
  cd "${srcdir}"/Sane-${pkgver}
  python setup.py build
  python2 setup.py build
}

package_python-sane() {
  depends+=('python')
  cd "${srcdir}"/Sane-${pkgver}
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_python2-sane() {
  depends+=('python2')
  cd "${srcdir}"/Sane-${pkgver}
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
