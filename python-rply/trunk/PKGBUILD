# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-rply
pkgname=('python-rply' 'python2-rply')
pkgver=0.7.6
pkgrel=2
pkgdesc="A pure Python Lex/Yacc that works with RPython"
arch=('any')
license=('BSD')
url="https://rply.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools' 'python-appdirs' 'python2-appdirs')
checkdepends=('python-pytest' 'python2-pytest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/alex/rply/archive/v$pkgver.tar.gz")
sha512sums=('b0edcff1ca32c465b404a0b2f548f90d312bcb98d3f4aa4a61fb44986c8aa7ecb6f046e72112e2c1f5654173bcfcc96ab4e0625632e908ccf17b43cdb3d8d99d')

prepare() {
  cp -a rply-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/rply-$pkgver
  python setup.py build

  cd "$srcdir"/rply-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/rply-$pkgver
  py.test

  cd "$srcdir"/rply-$pkgver-py2
  py.test2
}

package_python-rply() {
  depends=('python-appdirs')

  cd rply-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-rply() {
  depends=('python2-appdirs')

  cd rply-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

