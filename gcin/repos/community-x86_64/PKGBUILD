# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=gcin
pkgver=2.8.6
pkgrel=1
pkgdesc='Input method server supporting various input methods'
url='http://hyperrate.com/dir.php?eid=67'
license=('LGPL')
arch=('x86_64')
depends=('gtk2' 'libxtst')
makedepends=('qt5-base' 'qt4' 'gtk3' 'anthy' 'libchewing')
optdepends=('qt5-base: support for qt5 input method'
            'qt4: support for qt4 input method'
            'gtk3: support for gtk3 input method'
            'anthy: support for anthy input method'
            'libchewing: support for chewing input method')
source=("http://hyperrate.com/gcin-source/${pkgname}-${pkgver}.tar.xz"
        qt-5.5.patch)
sha512sums=('9aded8f4cf73965f506b36c2a0bd1716cf72d0e17a862a23a0a6f83423505b8b7a13d9f399dedaf975d38ba96e5f672d7e502cb38f85b7fb2eb0f7ffa0a86dbb'
            'cefe7fcd1009ac90a735a9b28ce7749a549a707c97019d54e2227b8e248118b21641878cfcc20cfc781693d4eff1e9fc6ff85742dabe38507505ff2d3cec57c0')

install=install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed 's/lib64/lib/g' -i configure
    sed '/include suffixes-rule/a \
        CFLAGS+='"${CFLAGS}"' \
        LDFLAGS+='"${LDFLAGS}"' \
        OPTFLAGS=' \
        -i Makefile
    sed \
        -e 's:usr/include/qt5:usr/include/qt:g' \
        -e 's:QT=qt5:QT=qt:' \
        -e '/^MODVERSION=/a INCS+=-I/usr/include/qt/QtGui/$(MODVERSION) -I/usr/include/qt/QtCore/$(MODVERSION)' \
        -i qt5-im/Makefile59

    # FS#45732
    patch -p1 -i ../qt-5.5.patch

    # Qt 5.6 doesn't provide Qt5PlatformSupport.pc anymore
    sed -i 's/Qt5PlatformSupport//' qt5-im/Makefile
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr --use_i18n=Y
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
