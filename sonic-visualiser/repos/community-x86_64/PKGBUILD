# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Orivej Desh <masecretaire@gmx.fr>

_download_id=2391
pkgname=sonic-visualiser
pkgver=3.1.1
pkgrel=2
pkgdesc="A viewer and analyser of music audio files."
arch=('x86_64')
url="https://www.sonicvisualiser.org/"
license=('GPL2')
groups=('pro-audio')
depends=('capnproto' 'desktop-file-utils' 'hicolor-icon-theme' 'libfishsound'
'libid3tag' 'liblo' 'liblrdf' 'libmad' 'liboggz' 'libpulse' 'portaudio'
'qt5-svg' 'rubberband' 'sord')
source=("https://code.soundsoftware.ac.uk/attachments/download/${_download_id}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('e83efffb62179e67a0e5fd7352f63f5a0210102a7fb38c5ad486479a3749f68a44fec2d1843d1c1ca826ba0004bf421811a270a0a50e3f89f71d956dc7a76ae7')

prepare() {
  cd "${pkgname}-${pkgver}"
  # build using c++14: https://sourceforge.net/p/sv1/bugs/261/
  sed -e 's/c++11/c++14/g' -i *.pr* configure.ac* svcore/svcore.pro \
  svapp/svapp.pro checker/*.pr* svgui/svgui.pro {bqaudioio,piper-cpp}/Makefile \
  piper-cpp/vamp-client/qt/test.pro piper-cpp/ext/json11/Makefile \
  dataquay/{lib.pro,tests/tests.pro}

  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  # DESTDIR still ignored
  # https://sourceforge.net/p/sv1/bugs/254
  export INSTALL_ROOT="${pkgdir}/"
  ./configure --prefix=/usr
  qmake-qt5 ${pkgname}.pro
  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make install
  # install some things separately, because build system is broken:
  # https://sourceforge.net/p/sv1/bugs/247/

  # desktop files
  install -t "${pkgdir}/usr/share/applications/" -vDm 644 "${pkgname}.desktop"

  # samples
  install -t "${pkgdir}/usr/share/${pkgname}/samples" -vDm 644 samples/*

  # templates
  install -t "${pkgdir}/usr/share/${pkgname}/templates" -vDm 644 templates/*

  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {CHANGELOG,CITATION,README.{md,OSC}}
}
