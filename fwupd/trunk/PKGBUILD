# $Id$
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Mirco Tischler <mt-ml at gmx dot de>

pkgname=fwupd
pkgver=1.0.4
pkgrel=1
pkgdesc='A simple daemon to allow session software to update firmware'
arch=('x86_64')
url='https://github.com/hughsie/fwupd'
license=('GPL2')
depends=('appstream-glib' 'fwupdate' 'colord' 'python')
makedepends=('meson' 'valgrind' 'docbook-utils' 'docbook-sgml' 'perl-sgmls' 'help2man'
             'gobject-introspection' 'gtk-doc' 'python-pillow' 'python-cairo'
             'ttf-dejavu' 'adobe-source-han-sans-cn-fonts' 'python-gobject')
checkdepends=('umockdev')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/${pkgver}.tar.gz")
sha256sums=('925a8e37400e6327f0b6ca5861b6d633b97b168a2b86c63d6a5071f7ad5b08b2')

build() {
    cd ${pkgname}-${pkgver}

    arch-meson -D b_lto=false ../build

    ninja -v -C ../build
}

check() {
    cd build
    # https://github.com/hughsie/fwupd/issues/370
    meson test || echo "WARNING: Tests failed"
}

package() {
    DESTDIR="${pkgdir}" ninja -C build install
    # Fixup mode to match polkit
    install -d -o root -g 102 -m 750 "${pkgdir}/usr/share/polkit-1/rules.d"
    # Remove the tests
    rm -r ${pkgdir}/usr/share/installed-tests/
}
