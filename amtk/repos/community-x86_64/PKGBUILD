# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=amtk
pkgver=5.0.0
pkgrel=1
pkgdesc="Actions, Menus and Toolbars Kit for GTK+ applications"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/Amtk"
license=(LGPL2.1)
depends=(gtk3)
makedepends=(autoconf-archive git gobject-introspection gtk-doc)
_commit=6b465f9628f6107f73bdd5faef77c60391713aa0  # tags/5.0.0^0
source=("git+https://gitlab.gnome.org/GNOME/amtk.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-gtk-doc --disable-Werror
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
