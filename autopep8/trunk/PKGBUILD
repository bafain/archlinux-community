# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ismael Carnales <icarnales at gmail dot com>
# Contributor: Sergey Mastykov <smastykov[at]gmail[dot]com>

pkgbase=autopep8
pkgname=(autopep8 python2-autopep8)
pkgver=1.3.5
pkgrel=2
epoch=1
pkgdesc="A tool that automatically formats Python code to conform to the PEP 8 style guide"
arch=('any')
url="https://github.com/hhatto/autopep8"
license=('MIT')
makedepends=('python-pycodestyle' 'python2-pycodestyle')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/hhatto/autopep8/archive/v$pkgver.tar.gz")
sha512sums=('7ed6d0c1f234b4da7412dbc9e1dea5e3260b6c1c857262c283fc304670388a57ec0de2298b89a7fab27d2dc18796ba15c75353e86b5639093429de8501ec91e3')

prepare() {
  cp -a autopep8-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/autopep8-$pkgver
  python setup.py build

  cd "$srcdir"/autopep8-$pkgver-py2
  python2 setup.py build
}

check() {
  export LC_CTYPE=en_US.UTF-8

  cd "$srcdir"/autopep8-$pkgver
  python setup.py test

  cd "$srcdir"/autopep8-$pkgver-py2
  python2 setup.py test
}

package_autopep8() {
  depends=('python-pycodestyle')

  cd autopep8-$pkgver
  python3 setup.py install -O1 --prefix=/usr --root="$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-autopep8() {
  depends=('python2-pycodestyle')

  cd autopep8-$pkgver-py2
  python2 setup.py install -O1 --prefix=/usr --root="$pkgdir"
  mv "$pkgdir"/usr/bin/autopep8{,-python2}

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
