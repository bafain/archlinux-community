# $Id$
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Nezmer <Nezmer@gmail.com>

_pkgbasename=fribidi
pkgname=lib32-$_pkgbasename
pkgver=1.0.1+8+gdf7252b
pkgrel=1
pkgdesc="A Free Implementation of the Unicode Bidirectional Algorithm (32-bit)"
arch=('x86_64')
license=('LGPL')
url="https://github.com/fribidi/fribidi/"
makedepends=(git lib32-glib2)
depends=('lib32-glibc' $_pkgbasename)
options=('!makeflags')
_commit=df7252be3fd94ceae8141a09ee86421fdd8b3b7e  # master
source=("git+https://github.com/fribidi/fribidi#commit=$_commit")
md5sums=('SKIP')

pkgver() {
  cd $_pkgbasename
  git describe --tags | sed -e 's/-/+/g' -e 's/^v//'
}


prepare() {
  cd $_pkgbasename
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/$_pkgbasename"
  ./configure --prefix=/usr --libdir=/usr/lib32
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd "${srcdir}/$_pkgbasename"

  make DESTDIR="${pkgdir}" install
  rm -rf "$pkgdir"/usr/{include,share,bin}
}

check() {
  cd "${srcdir}/$_pkgbasename"
  make check
}
