# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Jonathan Liu <net147@gmail.com>

pkgbase=python-suds
pkgname=(python-suds python2-suds)
pkgver=0.6
pkgrel=3
pkgdesc="A lightweight SOAP python client"
arch=('any')
url="https://bitbucket.org/jurko/suds"
license=('LGPL')
makedepends=('python-setuptools' 'python2-setuptools')
source=("$pkgbase-$pkgver.tar.gz::https://bitbucket.org/jurko/suds/get/release-$pkgver.tar.gz")
sha512sums=('9930c4e023e937e0f5323bcaae7017af6c4462c240d90cb77ec403da6f4f0ac1304013bdc30d9c0f2f778510b0762f9b771333f75e3dfb5a79dc24961a15c9be')

prepare() {
  mv jurko-suds-* $pkgbase-$pkgver
  cp -a $pkgbase-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/$pkgbase-$pkgver
  python setup.py build

  cd "$srcdir"/$pkgbase-$pkgver-py2
  python2 setup.py build
}

package_python-suds() {
  depends=('python')

  cd $pkgbase-$pkgver
  python setup.py install --root="$pkgdir" -O1
  # https://bugs.archlinux.org/task/57562
  local _py3_ver=$(python --version | cut -d " " -f2)
  rm -rf "${pkgdir}/usr/lib/python${_py3_ver%"."*}/site-packages/tests"
}

package_python2-suds() {
  depends=('python2')

  cd $pkgbase-$pkgver-py2
  python2 setup.py install --root="$pkgdir" -O1
  # https://bugs.archlinux.org/task/57562
  local _py2_ver=$(python2 -c "import sys; print(str(sys.version_info.major)+'.'+str(sys.version_info.minor))")
  rm -rf "${pkgdir}/usr/lib/python${_py2_ver}/site-packages/tests"
}

# vim:set ts=2 sw=2 et:
