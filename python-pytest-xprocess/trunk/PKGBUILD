# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Neil Santos <nsantos16+aur@gmail.com>

_name=pytest-xprocess
pkgbase=python-${_name}
pkgname=('python-pytest-xprocess' 'python2-pytest-xprocess')
pkgver=0.8
pkgrel=2
pkgdesc='Pytest plugin to manage external processes across test runs.'
arch=('any')
url='https://bitbucket.org/hpk42/pytest-xprocess/'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-cache' 'python2-pytest-cache')
source=("https://pypi.python.org/packages/source/p/${_name}/${_name}-$pkgver.tar.gz")
sha512sums=('c93bfb6bf18b946f47c69f45e25d8bf64defd1a5795b98689a55227e017db77220e9ae0ede684262d2ffb0f535bd204565748216c1f964570ec8c5353aab2842')

prepare(){
  mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py test
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py test
}

package_python-pytest-xprocess() {
  depends=('python-pytest')
  cd "${pkgname[0]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname[0]}/LICENSE"
  install -t "${pkgdir}/usr/share/${pkgname[0]}" -vDm644 {CHANGELOG,README.rst}
}

package_python2-pytest-xprocess() {
  depends=('python2-pytest')
  cd "${pkgname[0]}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname[1]}/LICENSE"
  install -t "${pkgdir}/usr/share/${pkgname[1]}" -vDm644 {CHANGELOG,README.rst}
}
