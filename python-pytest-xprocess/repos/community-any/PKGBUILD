# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Neil Santos <nsantos16+aur@gmail.com>

_name=pytest-xprocess
pkgbase=python-${_name}
pkgname=('python-pytest-xprocess' 'python2-pytest-xprocess')
pkgver=0.12.1
pkgrel=1
pkgdesc='Pytest plugin to manage external processes across test runs.'
arch=('any')
url='https://github.com/pytest-dev/pytest-xprocess'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python-pytest-cache' 'python2-pytest' 'python2-pytest-cache')
source=("${pkgname[0]}-${pkgver}.tar.gz::https://github.com/pytest-dev/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('e6deebf6d82999d38fe771cb0e1f52a87b33fac720046e63156ff176c004ea9ec4bd48990ea1fde9c3f1e8fa0217bc153dda4c6407c89cb254f4aa8e6acea378')

prepare(){
  mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

# checks disabled for now: https://github.com/pytest-dev/pytest-xprocess/issues/10
#check() {
#  cd "${pkgname[0]}-${pkgver}"
#  export PYTHONPATH="${srcdir}/${pkgname[0]}-${pkgver}/build/":${PYTHONPATH}
#  py.test
#  cd ../"${pkgname[1]}-${pkgver}"
#  export PYTHONPATH="${srcdir}/${pkgname[1]}-${pkgver}/build/":${PYTHONPATH}
#  py.test2
#}

package_python-pytest-xprocess() {
  depends=('python-psutil' 'python-pytest')
  cd "${pkgname[0]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname[0]}/LICENSE"
  install -t "${pkgdir}/usr/share/${pkgname[0]}" -vDm644 {CHANGELOG,README.rst}
}

package_python2-pytest-xprocess() {
  depends=('python2-psutil' 'python2-pytest')
  cd "${pkgname[0]}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname[1]}/LICENSE"
  install -t "${pkgdir}/usr/share/${pkgname[1]}" -vDm644 {CHANGELOG,README.rst}
}
