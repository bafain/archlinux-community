# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=Type-Tiny
pkgname=perl-${_pkg,,}
pkgver=1.002001
pkgrel=1
pkgdesc="tiny, yet Moo(se)-compatible type constraint"
arch=('any')
url="https://metacpan.org/release/${_pkg}"
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl' 'perl-exporter-tiny')
checkdepends=('perl-test-simple')
source=("https://cpan.metacpan.org/authors/id/T/TO/TOBYINK/${_pkg}-${pkgver}.tar.gz")
sha512sums=('e499c4f113e5a327294012daac9f0cd60a42a6c40b9d0c9c523a229f574a7dd0f672f209d0596d25edcbc39b79cd2ea65791b6a5146345e75fdd1d84ee1cd161')

build() {
    cd ${_pkg}-${pkgver}
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
    perl Makefile.PL
    make
}

check() {
    cd ${_pkg}-${pkgver}
    make test
}

package() {
    cd ${_pkg}-${pkgver}
    make INSTALLDIRS=vendor DESTDIR="$pkgdir" install
}
