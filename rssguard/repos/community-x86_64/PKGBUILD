# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: Alois Nespor <info@aloisnespor.info>
# Contributor: speps <speps dot aur dot archlinux dot org>

pkgbase=rssguard
pkgname=(rssguard{,-nowebengine})
pkgver=3.5.6
pkgrel=2
pkgdesc='Simple (yet powerful) Qt5 feed reader'
arch=(x86_64)
url='https://github.com/martinrotter/rssguard'
license=(GPL3)
depends=(qt5-webengine)
makedepends=(qt5-tools)
source=("$pkgbase-$pkgver.tar.gz::https://github.com/martinrotter/$pkgbase/archive/$pkgver.tar.gz")
sha256sums=('8429b20e44fd394d6d053ed87b7e71ced9795f0d1b448621c475421f0e2daafa')

prepare() {
  mkdir build{,-nowebengine}
}

build() {
  cd build
  qmake ../$pkgname-$pkgver -r CONFIG+=release PREFIX=/usr
  make

  cd ../build-nowebengine
  qmake ../$pkgname-$pkgver -r CONFIG+=release PREFIX=/usr USE_WEBENGINE=false
  make
}

package_rssguard() {
  depends=(qt5-webengine)

  cd build
  make INSTALL_ROOT="$pkgdir" install
}

package_rssguard-nowebengine() {
  pkgdesc+=' without WebEngine support'
  depends=(qt5-base)
  conflicts=($pkgbase)
  provides=($pkgbase=$pkgver)

  cd build-nowebengine
  make INSTALL_ROOT="$pkgdir" install
}
