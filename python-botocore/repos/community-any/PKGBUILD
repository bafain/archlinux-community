# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Chris Severance aur.severach AatT spamgourmet.com
# Contributor: Ainola
# Contributor: Chris Fordham

pkgbase=python-botocore
pkgname=('python-botocore' 'python2-botocore')
pkgver=1.11.6
pkgrel=1
pkgdesc='A low-level interface to a growing number of Amazon Web Services'
arch=('any')
url="https://github.com/boto/botocore"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
source=($pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz)
md5sums=('352895bcffde3978f411bc1126c62126')

prepare() {
  cp -r "botocore-$pkgver" "python-botocore-$pkgver"
  cp -r "botocore-$pkgver" "python2-botocore-$pkgver"
}

build_python-botocore() {
  cd "python-botocore-$pkgver"

  python setup.py build
}

build_python2-botocore() {
  cd "python2-botocore-$pkgver"

  python2 setup.py build
}

package_python-botocore() {
  depends=('python-dateutil' 'python-jmespath' 'python-docutils')

  cd "python-botocore-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-botocore() {
  depends=('python2-dateutil' 'python2-jmespath' 'python2-docutils')

  cd "python2-botocore-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
