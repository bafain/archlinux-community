# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Ivan Bobrov <ibobrik at gmail dot com>
# Contributor: Black_Mage <vleon1 at gmail dot com>

pkgname=astromenace
pkgver=1.3.2
pkgrel=3
pkgdesc="Hardcore 3D space shooter with spaceship upgrade possibilities"
arch=('x86_64')
url="http://www.viewizard.com/astromenace/index_linux.php"
license=('GPL3')
depends=('sdl' 'freealut' 'libjpeg' 'libvorbis' 'glu' 'freetype2' 'libxinerama')
makedepends=('cmake' 'mesa')
source=(https://downloads.sourceforge.net/openastromenace/astromenace-src-$pkgver.tar.bz2
        astromenace.desktop)
sha512sums=('0a06278a8a7fdaaf5c730590fcc1f8f59503fe4c94dcbb6dd6b6ee638048f9bebf7c49c75cd32f26e4cd08cfbe01f5082002860901b7196f08a55712bb3ed5d4'
            'cc63f110bfec4f5c6b2cb1e5e940f23ceb9f75ca984278537b3bf4c041db401a46c10f54f91db8d42e1d25403c8d2d1bd106756d180383c91ac5abd2d847fbec')

build() {
    cd $srcdir/AstroMenace

    [[ -d build ]] && rm -r build
    mkdir build && cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DDATADIR=/usr/share/astromenace
    make
}

package() {
    cd $srcdir/AstroMenace

    build/AstroMenace --pack --rawdata=./RAW_VFS_DATA --dir=build

    install -Dm755 build/AstroMenace $pkgdir/usr/bin/astromenace
    install -Dm644 build/gamedata.vfs $pkgdir/usr/share/astromenace/gamedata.vfs

    install -Dm 644 astromenace_128.png $pkgdir/usr/share/pixmaps/astromenace.png
    install -Dm 644 $srcdir/astromenace.desktop $pkgdir/usr/share/applications/astromenace.desktop
}
