# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
pkgname=qmidictl
pkgver=0.5.2
pkgrel=1
pkgdesc="MIDI remote controller application that sends MIDI data over the network, using UDP/IP multicast"
arch=('x86_64')
url="https://qmidictl.sourceforge.io/"
license=('GPL2')
groups=('pro-audio')
depends=('hicolor-icon-theme' 'qt5-base')
makedepends=('qt5-tools')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('d3475da2187e0ca4e717f8b808c1d376629233ed9c7fa849bfea4df6aab152896336e5c1eefb9d67ee291deb199acdd773d589d307a13a738b836a0386ac76d6')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 {AUTHORS,ChangeLog,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
