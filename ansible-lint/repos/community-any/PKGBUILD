# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Sander Boom <sanderboom@gmail.com>

pkgname=ansible-lint
pkgver=3.4.23
pkgrel=2
pkgdesc="Checks playbooks for practices and behaviour that could potentially be improved."
arch=('any')
url="https://github.com/willthames/ansible-lint"
license=('MIT')
depends=('python' 'ansible')
makedepends=('python-distribute')
checkdepends=('python-nose')
source=("https://github.com/willthames/ansible-lint/archive/v${pkgver}.tar.gz")
sha256sums=('c5e1c6500d65fa9655ef155fecd67b775701b5e427d5d978a677e5d53d584ef4')

build() {
    cd "${pkgname}-${pkgver}"
    python3 setup.py build
}

package() {
    cd "${pkgname}-${pkgver}"
    python3 setup.py install --root="$pkgdir" --optimize=1
    install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
