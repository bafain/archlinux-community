# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Wes Brewer <brewerw@gmail.com>
# Contributor: VuDu <vudu.curse@gmail.com>

pkgname=nfoview
pkgver=1.24
pkgrel=2
pkgdesc="Simple NFO file viewer in GTK+ 3"
arch=('any')
url="https://otsaloma.io/nfoview/"
license=('GPL3')
depends=('gtk3' 'python-gobject')
makedepends=('python-setuptools')
optdepends=('terminus-font: recommended font')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/otsaloma/${pkgname}/archive/${pkgver}.tar.gz"
        'x-nfo.desktop')
sha512sums=('91ad1f9f972b7681655b429c0910475012bd2231e51c6db5659b19fb869779d1de10dadc1d6461fa7d577c051af9b74f1d463543a429e1dca438a4ff17ac2293'
            'a800af55df103ee12ce7a8815780e15896d7ad3eee947760ff4486ffb57fea8cf4105b5ffc61dc81e849ddf03eeac77f593a4ffe4fd73b5ea026672c72dff108')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"

  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"

  # docs
  install -vDm 644 {AUTHORS,NEWS,README,TODO}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"

  install -vDm 644 "${srcdir}/x-nfo.desktop" \
    "${pkgdir}/usr/share/mimelnk/text/x-nfo.desktop"
}

# vim:set ts=2 sw=2 et:
