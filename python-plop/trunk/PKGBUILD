# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-plop
pkgname=(python-plop python2-plop)
pkgver=0.3.0
pkgrel=3
pkgdesc="Python Low-Overhead Profiler"
arch=('any')
license=('MIT')
url="https://github.com/bdarnell/plop"
makedepends=('python-tornado' 'python2-tornado' 'python-six' 'python2-six'
             'python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/p/plop/plop-${pkgver}.tar.gz"
        disable-flaky-threaded-test.patch)
md5sums=('4a313d6a8dd59ac48cfa3af426d197fc'
         '1d9dc194b65a08f877506b5c5098c251')

prepare() {
  patch -d plop-$pkgver -Np1 < disable-flaky-threaded-test.patch
  # tornado.testing.LogTrapTestCase was removed in Tornado 5.0
  sed -i 's/LogTrapTestCase/ExpectLog/' plop-$pkgver/plop/test/callgraph_test.py
  cp -a plop-$pkgver{,-py2}
}

build() {
  cd "$srcdir/plop-${pkgver}"
  python setup.py build

  cd "$srcdir/plop-${pkgver}-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/plop-${pkgver}"
  python -m plop.test.runtests

  cd "$srcdir/plop-${pkgver}-py2"
  python2 -m plop.test.runtests
}

package_python-plop() {
  depends=('python-six' 'python-tornado')

  cd "${srcdir}/plop-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_python2-plop() {
  depends=('python2-six' 'python2-tornado')

  cd "${srcdir}/plop-${pkgver}-py2"
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
