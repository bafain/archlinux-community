# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=nimble
pkgver=0.8.10
pkgrel=1
epoch=1
pkgdesc='Package manager for the Nim programming language'
url='https://github.com/nim-lang/nimble'
arch=('x86_64')
license=('BSD')
depends=('glibc')
makedepends=('nim')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/nim-lang/nimble/archive/v${pkgver}.tar.gz)
sha256sums=('bb1962a71448945775bba4c1fd63162fa9bb2afbaf6d6f61a5012c1c3fee59f9')
sha512sums=('18128cb28c6f1b1d1bf2dddb287e376404ddf842a3f665ae3dfddb39ad5dc08e7ec0509d6e823cec3eaad3263ec4a5e72d513aae173a50451799a85ef79453b3')

build() {
  cd ${pkgname}-${pkgver}
  nim c -d:release src/nimble
}

package() {
  cd ${pkgname}-${pkgver}

  install -Dm 755 src/nimble -t "${pkgdir}/usr/bin"
  install -Dm 644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Nimble looks for nimscriptapi.nim in /usr/bin/nimblepkg/, of all places.
  cp -r src/nimblepkg "${pkgdir}/usr/share/${pkgname}"
  ln -s /usr/share/${pkgname} "${pkgdir}/usr/bin/nimblepkg"
}

# vim: ts=2 sw=2 et:
