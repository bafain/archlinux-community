# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Ben <contrasutra@myrealbox.com>

pkgname=xpad
pkgver=5.1.0
pkgrel=1
pkgdesc="Stickies-like notes application"
arch=('x86_64')
depends=('gtksourceview3' 'libsm')
makedepends=('intltool')
url="https://launchpad.net/xpad"
license=('GPL3')
source=(https://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.bz2)
# Unknown key
#validpgpkeys=('3930646349B9DD3886D9087392BF2FCCA6F2322B'
#	      '8450BEE19B722715FFDB18C6683EAA45951FC552')
sha256sums=('83567c9ad4790fba4cd67552dd369ba778e6b5941963ab67719037f3502f0f50')

prepare() {
  cd $pkgname-$pkgver
  ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
