# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=terraform
pkgver=0.11.4
pkgrel=1
pkgdesc="Tool for building, changing, and versioning infrastructure safely and efficiently"
url="http://www.terraform.io/"
arch=("x86_64")
license=("MPL")
makedepends=("go-pie")
source=("$pkgname-$pkgver.tar.gz::https://github.com/hashicorp/terraform/archive/v$pkgver.tar.gz")
sha512sums=('f8c75b792d7d31bac34b3cd3fbdd647bdc9fff94496cf955ce4b83e5e4da662b29cd6551a530cbbf917fcc11f111038470a3415ff2fade2d5e6fc52bfe93aa90')


prepare() {
  export GOPATH="${srcdir}"
  export PATH="$PATH:$GOPATH/bin"
  mkdir -p src/github.com/hashicorp/
  mv "${pkgname}-${pkgver}" src/github.com/hashicorp/${pkgname}
}


build() {
  cd src/github.com/hashicorp/"${pkgname}"
  go build -o terraform-binary
}


package() {
  cd src/github.com/hashicorp/"${pkgname}"
	install -Dm755 terraform-binary "$pkgdir/usr/bin/terraform"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	# Zsh completion
	install -D contrib/zsh-completion/_terraform "$pkgdir/usr/share/zsh/site-functions/_terraform"
}
