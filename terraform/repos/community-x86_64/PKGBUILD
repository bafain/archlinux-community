# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=terraform
pkgver=0.11.8
pkgrel=1
pkgdesc="Tool for building, changing, and versioning infrastructure safely and efficiently"
url="http://www.terraform.io/"
arch=("x86_64")
license=("MPL")
makedepends=("go-pie")
depends=('glibc')
source=("$pkgname-$pkgver.tar.gz::https://github.com/hashicorp/terraform/archive/v$pkgver.tar.gz")
sha512sums=('a82be5a2000b5adb417451d87def4ad93a86f63f1a72774f6b181b93e4d95e339eb13d6f30bd887bb89e40fa3f5a859d98303dca9332238450fa18ca8f4e3e99')


prepare() {
  export GOPATH="${srcdir}"
  export PATH="$PATH:$GOPATH/bin"
  mkdir -p src/github.com/hashicorp/
  mv "${pkgname}-${pkgver}" src/github.com/hashicorp/${pkgname}
}


build() {
  cd src/github.com/hashicorp/"${pkgname}"
  go build -o terraform-binary
}


package() {
  cd src/github.com/hashicorp/"${pkgname}"
	install -Dm755 terraform-binary "$pkgdir/usr/bin/terraform"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
