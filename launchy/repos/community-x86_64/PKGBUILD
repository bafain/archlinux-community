# $Id$
# Contributor: Andrew Burkett <burkett.andrew@gmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=launchy
pkgver=2.5
pkgrel=6
pkgdesc="Indexes the programs in your start menu and can launch your documents, project files, folders, and bookmarks with just a few keystrokes!"
arch=('x86_64')
url="http://www.launchy.net/"
license=('GPL')
depends=('qt4' 'xdg-utils')
makedepends=('gcc' 'boost')
source=("http://downloads.sourceforge.net/launchy/launchy-${pkgver}.tar.gz"
	fix-linking.patch)
sha512sums=('31cf591184c81c5c44c174f12ffdec4fd15eb2f78af3195afceb9b11253d4bb17edee77b9316c8275828431b2ece5bfbc69f50b390eec27295828e80965c0930'
            '40351c33e41f423801c98685dbb7274d668ca1678b8d13aa6396600bf7052a7cef3c1421b428740c50e36c6ba8b1428182cdb54c59af54456799b7334b9ba053')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # fix linking against libX11
  patch -Np1 -i ../fix-linking.patch

  qmake-qt4 -r Launchy.pro
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make INSTALL_ROOT=${pkgdir} install
}
