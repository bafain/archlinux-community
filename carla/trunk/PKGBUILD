# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Llewelyn Trahaearn <WoefulDerelict [at] GMail [dot] com>
# Contributor: falkTX <falktx [at] gmail [dot] com>

_name=Carla
pkgname=carla
pkgver=1.9.9
pkgrel=3
pkgdesc="Audio Plugin Host"
arch=('x86_64')
url="https://kxstudio.linuxaudio.org/Applications:Carla"
license=('GPL2')
groups=('pro-audio')
depends=('file' 'fluidsynth' 'gtk2' 'gtk3' 'linuxsampler' 'python-pyqt5'
'python-pyliblo' 'python-rdflib' 'qt5-svg')
makedepends=('qt5-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/falkTX/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('74f5880c60ca95b0173f2a408ae00235e1f422a8672436ab925caf9c1fd718da90182f092ad304f578082119381ef100ec210b6f3556a58a3cd1b28f50977a2e')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  make features
  make DEFAULT_QT=5
}

package() {
  cd "${pkgname}-${pkgver}"
  make DEFAULT_QT=5 \
       DESTDIR="${pkgdir}/" \
       PREFIX=/usr \
       install
  install -vDm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  # removing unneeded integration files:
  # https://github.com/falkTX/Carla/issues/678
  rm -rv "${pkgdir}/usr/lib/python3/"
}

# vim:set ts=2 sw=2 et:
