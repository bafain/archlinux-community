# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: 3ED <krzysztof1987 at gmail dot com>

pkgname=perl-parallel-forkmanager
pkgver=1.20
pkgrel=1
pkgdesc="A simple parallel processing fork manager"
arch=('any')
url="https://metacpan.org/release/Parallel-ForkManager"
license=('PerlArtistic' 'GPL')
depends=('perl')
makedepends=('perl-extutils-makemaker')
checkdepends=('perl-test-warn')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/Y/YA/YANICK/Parallel-ForkManager-$pkgver.tar.gz)
sha256sums=('7cc4c1c3b0e676b61ffa90f82f4128e8057327449ca86a9beb2f39217023f289')

build() {
  cd Parallel-ForkManager-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}
check() {
  cd Parallel-ForkManager-$pkgver
  make test
}
package() {
  cd Parallel-ForkManager-$pkgver
  make install DESTDIR="$pkgdir"
}
