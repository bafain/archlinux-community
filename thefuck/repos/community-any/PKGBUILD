# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=thefuck
pkgver=3.27
pkgrel=2
pkgdesc="Magnificent app which corrects your previous console command"
arch=('any')
url="https://github.com/nvbn/thefuck"
license=('MIT')
depends=('python-setuptools' 'python-psutil' 'python-six' 'python-colorama' 'python-decorator'
         'python-pyte')
makedepends=('python-pip')
checkdepends=('python-pytest-mock' 'python-mock' 'python-pexpect')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nvbn/thefuck/archive/$pkgver.tar.gz")
sha512sums=('766c589f0e99a9773e74094e91c6a74d00e5be8b7241680ad664ecc3f549b597416bc7a4d289f399ed3a0d9ccad629ed78bb983edc809664c303c687ea1eec1a')

build() {
  cd thefuck-$pkgver
  python setup.py build
}

check() {
  cd thefuck-$pkgver

  # Hack distribution test by installing it and set PYTHONPATH afterwards
  python setup.py install --root="$PWD/tmp_install" --optimize=1

  # Hack $HOME as it's / in the chroot and not writable
  cp tests/test_conf.py "$srcdir/test_conf.py.bak"
  sed -i "s|data = {}|data = {'HOME': '$srcdir'}|" tests/test_conf.py
  LC_CTYPE=en_US.utf8 PYTHONPATH="$PWD/tmp_install/usr/lib/python3.7/site-packages:$PYTHONPATH" py.test tests
  mv "$srcdir/test_conf.py.bak" tests/test_conf.py
}

package() {
  cd thefuck-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}

# vim:set ts=2 sw=2 et:
