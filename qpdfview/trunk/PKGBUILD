# $Id$
# Maintainer: Alad Wenter <alad@mailbox.org>
# Contributor: Adam Reichold <adam.reichold@t-online.de>

pkgname=qpdfview
pkgver=0.4.17beta1
pkgrel=7
pkgdesc='A tabbed PDF viewer using the poppler library.'
url='https://launchpad.net/qpdfview'
arch=('x86_64')
license=('GPL2')
depends=('libcups' 'libsynctex' 'poppler-qt5' 'qt5-svg')
makedepends=('qt5-tools' 'libspectre' 'djvulibre')
optdepends=('libspectre: for PostScript support'
            'djvulibre: for DjVu support')
source=("https://launchpad.net/qpdfview/trunk/$pkgver/+download/qpdfview-$pkgver.tar.gz"
        "https://launchpad.net/qpdfview/trunk/$pkgver/+download/qpdfview-$pkgver.tar.gz.asc"
        qpdfview-synctex-2018.patch::"https://bazaar.launchpad.net/~adamreichold/qpdfview/trunk/diff/2070?context=3")
sha256sums=('60206c5f76410a35098e3c965552cec08cd85941776ea6f6b207d7ea63ce940f'
            'SKIP'
            '83d92853e410400b36f90e123647f01b2393c2ebb457a1e4c41ddb1877bb5234')
# Adam Reichold <adam.reichold@t-online.de>
validpgpkeys=('1F521FF0F87E9E1CDE46B8A9F4928C4DD24D4DF8')

prepare() {
  cd $pkgname-$pkgver
  patch -p0 -i ../qpdfview-synctex-2018.patch # Fix build with recent synctex
  sed -e 's|2.0.0|1.21.0|' -i application.pro
}

build() {
    cd "$pkgname-$pkgver"
    lrelease-qt5 qpdfview.pro
    qmake-qt5 qpdfview.pro
    make
}

package() {
    cd "$pkgname-$pkgver"
    make INSTALL_ROOT="$pkgdir" install
}

