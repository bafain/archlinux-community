# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Tim Meusel <tim@bastelfreak.de>

_gemname='nats-pure'
pkgname="ruby-${_gemname}"
pkgver=0.4.0
pkgrel=1
pkgdesc="Ruby client for the NATS messaging system"
url="https://nats.io"
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=("https://rubygems.org/gems/${_gemname}-${pkgver}.gem"
        'LICENSE')
noextract=("${_gemname}-${pkgver}.gem")
sha512sums=('198a07de4bebb2735748038d688c2ff1f1843a6329619be1144105bb8780918048c024cc3c0595ea7761d6bc3bbc031efcc627303f4c885961fb28655169109f'
            'ec05133944c3dc861ae437ce8681f54a871b614ff020fd4db1b3b3eb75f9e612222b682d1cceb5a3335e479e3541072abd4abc8f272d2e4d8803f97fe9f58b08')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --ignore-dependencies \
    --no-user-install \
    -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" \
    ${_gemname}-${pkgver}.gem

  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"

  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
