# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Tim Meusel <tim@bastelfreak.de>

_gemname='nats-pure'
pkgname="ruby-${_gemname}"
pkgver=0.5.0
pkgrel=1
pkgdesc="Ruby client for the NATS messaging system"
url="https://nats.io"
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=("https://rubygems.org/gems/${_gemname}-${pkgver}.gem"
        'LICENSE')
noextract=("${_gemname}-${pkgver}.gem")
sha512sums=('eb39f1477c90b35b4852c1946fb690e1d3c6a4fa8e4b0816539f40cedd37f726c38edf3acbe127e949eeaabdadc26a78f4024923aaa66c0dbfbc781f1272d5a5'
            'ec05133944c3dc861ae437ce8681f54a871b614ff020fd4db1b3b3eb75f9e612222b682d1cceb5a3335e479e3541072abd4abc8f272d2e4d8803f97fe9f58b08')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --ignore-dependencies \
    --no-user-install \
    -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" \
    ${_gemname}-${pkgver}.gem

  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"

  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
