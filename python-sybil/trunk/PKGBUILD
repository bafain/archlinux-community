# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-sybil
pkgname=('python-sybil' 'python2-sybil')
pkgver=1.0.9
pkgrel=1
pkgdesc='Automated testing for the examples in your documentation.'
arch=('any')
license=('MIT')
url='https://github.com/cjw296/sybil'
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/cjw296/sybil/archive/$pkgver.tar.gz")
sha512sums=('f084150cd1c87912c1843fc3e4205d5a3d7edaa5fb6f0ea9db87a70f1e6899eb5113d3ba8e56edd8e2498dfdf3e73d22d36de0528ebf690a4be01224f2ec256c')

prepare() {
  cp -a sybil-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/sybil-$pkgver
  python setup.py build

  cd "$srcdir"/sybil-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/sybil-$pkgver
  python setup.py pytest

  cd "$srcdir"/sybil-$pkgver-py2
  python2 setup.py pytest
}

package_python-sybil() {
  depends=('python')

  cd sybil-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-sybil() {
  depends=('python2')

  cd sybil-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
