# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=hcxkeys
_gitcommit=451c639d5fc099acb3eb86f647450650452cba2e
pkgver=4.0.1+4+g451c639
pkgrel=1
pkgdesc='Set of tools to generate plainmasterkeys (rainbowtables) and hashes for hashcat and John the Ripper'
url='https://github.com/ZerBea/hcxkeys'
arch=('x86_64')
license=('MIT')
depends=('openssl' 'opencl-driver' 'opencl-icd-loader')
makedepends=('git' 'opencl-headers')
source=("${pkgname}::git+https://github.com/ZerBea/hcxkeys#commit=${_gitcommit}")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  git describe --tags|sed -r 's|v?(.+)|\1|'|sed 's|-|+|g'
}

build() {
  cd ${pkgname}
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" PREFIX=/usr install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
