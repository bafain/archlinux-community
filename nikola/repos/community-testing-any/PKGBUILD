# Maintainer: David Runge <dave@sleepmap.de>
_name=Nikola
pkgname=nikola
pkgver=8.0.0rc1
pkgrel=3
pkgdesc="A static site and blog generator"
arch=('any')
url="https://getnikola.com/"
license=('MIT')
depends=('python-babel' 'python-blinker' 'python-doit' 'python-dateutil'
'python-docutils' 'python-logbook' 'python-lxml' 'python-mako'
'python-markdown' 'python-natsort' 'python-piexif' 'python-pillow'
'python-pygments' 'python-pyrss2gen' 'python-requests' 'python-setuptools'
'python-unidecode' 'python-yapsy')
makedepends=('python-jinja' 'python-pyphen' 'python-husl' 'jupyter-notebook'
'python-ipykernel' 'python-pyaml' 'python-toml' 'python-micawber'
'python-ghp-import' 'python-phpserialize' 'python-pygal' 'python-typogrify'
'python-watchdog')
checkdepends=('python-coverage' 'python-freezegun' 'python-pytest-cov')
optdepends=('jupyter-notebook: .ipynb support'
            'python-aiohttp: for `nikola auto`'
            'python-ghp-import: for upload to Github pages'
            'python-husl: optional color support'
            'python-ipykernel: .ipynb support'
            'python-jinja: jinja2 based themes'
            'python-micawber: for embedding media in posts'
            'python-phpserialize: for Wordpress import'
            'python-pygal: for SVG graph plotting'
            'python-pyphen: hyphenation support'
            'python-pyaml: for something'
            'python-toml: .toml support'
            'python-typogrify: for typographical enhancements'
            'python-watchdog: for `nikola auto`')
# TODO: remove with next minor version
replaces=('python-nikola' 'python2-nikola' 'python-nikola-doc')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('f9decb28940ae2d258f0283365050448b915332f2d3dc44a87ebc5624f2e5d267f4ca65e96b2bba6a52df4b4b011cfbcd9d2e296b4b2eb369831372b8f12d80b')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH=build:${PYTHONPATH}
  py.test
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE.txt \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -vDm 644 {AUTHORS,CHANGES}.txt {CONTRIBUTING,README}.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  # shell completions
  export PYTHONPATH="${pkgdir}/usr/lib/python3.7/site-packages:${PYTHONPATH}"
  "${pkgdir}/usr/bin/${pkgname}" tabcompletion \
    --shell bash \
    --hardcode-tasks \
    > "${pkgname}_bash"
  install -vDm 644 ${pkgname}_bash \
    "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" tabcompletion \
    --shell zsh \
    --hardcode-tasks \
    > "${pkgname}_zsh"
  install -vDm 644 ${pkgname}_zsh \
    "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
}
