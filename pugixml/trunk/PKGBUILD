# $Id: PKGBUILD 149592 2015-12-07 05:31:05Z bpiotrowski $
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=pugixml
pkgver=1.9
pkgrel=1
pkgdesc="Light-weight, simple and fast XML parser for C++ with XPath support"
arch=('x86_64')
url="https://pugixml.org"
license=('MIT')
depends=('gcc-libs')
makedepends=('cmake')
source=("https://github.com/zeux/pugixml/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        'pkg-config.patch'
        'LICENSE')
sha256sums=('d156d35b83f680e40fd6412c4455fdd03544339779134617b9b28d19e11fdba6'
            'a0e47aed9f28c061b889d05b34d30c29cb06eeb262f5c306ef3f80c81412ee43'
            'bb5e0eca3fdd4f841ae7e06d867a0f4883ae1b9f6f37be201cf0b152a98c818e')

prepare() {
    mkdir -p build
    cd ${pkgname}-${pkgver}
    patch -p1 -i ../pkg-config.patch
}

build() {
    cd build
    cmake ../${pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DBUILD_SHARED_LIBS=ON
    make
}

package() {
    make -C build DESTDIR="${pkgdir}" install
    install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
