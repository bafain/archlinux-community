# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=lib32-libepoxy
pkgver=1.5.0
pkgrel=1
pkgdesc='Library handling OpenGL function pointer management'
arch=('x86_64')
url='https://github.com/anholt/libepoxy'
license=('MIT')
depends=('lib32-glibc')
makedepends=('gcc-multilib' 'lib32-libgl' 'lib32-systemd' 'meson' 'python')
source=("https://github.com/anholt/libepoxy/releases/download/${pkgver}/libepoxy-${pkgver}.tar.xz")
sha256sums=('4c94995398a6ebf691600dda2e9685a0cac261414175c2adf4645cdfab42a5d5')

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  export CC='gcc -m32'
  export CXX='/bin/false'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  arch-meson ../libepoxy-${pkgver} \
    --libdir='/usr/lib32'
  ninja
}

package() {
  cd build

  DESTDIR="${pkgdir}" ninja install
  rm -rf "${pkgdir}"/usr/include

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libepoxy "${pkgdir}"/usr/share/licenses/lib32-libepoxy
}

# vim: ts=2 sw=2 et:
