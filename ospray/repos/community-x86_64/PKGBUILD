# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=ospray
pkgver=1.5.0
pkgrel=1
pkgdesc="A Ray Tracing Based Rendering Engine for High-Fidelity Visualization"
arch=('x86_64')
url="http://www.ospray.org/"
license=('Apache')
depends=('intel-tbb' 'embree' 'ispc' 'libgl' 'glfw-x11' 'glu' 'openmpi')
makedepends=('cmake')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/ospray/OSPRay/archive/v${pkgver}.tar.gz")
sha256sums=('c6708bea857d20102c29968601ddb52d2cd341b91dae6e74d4a1eddb788d6e03')

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DOSPRAY_BUILD_ISA=ALL \
        -DOSPRAY_ENABLE_TESTING=ON \
        -DOSPRAY_MODULE_BILINEAR_PATCH=ON \
        -DOSPRAY_MODULE_MPI=ON
    make
}

check() {
    cd build
    make test
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
    # Remove tests
    rm -f "${pkgdir}"/usr/bin/test_*
}
