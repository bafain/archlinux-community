# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=ospray
pkgver=1.6.0
pkgrel=1
pkgdesc="A Ray Tracing Based Rendering Engine for High-Fidelity Visualization"
arch=('x86_64')
url="http://www.ospray.org/"
license=('Apache')
depends=('intel-tbb' 'embree' 'ispc' 'libgl' 'glfw-x11' 'glu' 'openmpi')
makedepends=('cmake')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/ospray/OSPRay/archive/v${pkgver}.tar.gz")
sha256sums=('4b296fba03ab7e125cd9d9bdf59355f2567d09af6ae958a6cd1e4c05fff030b7')

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DOSPRAY_BUILD_ISA=ALL \
        -DOSPRAY_ENABLE_TESTING=ON \
        -DOSPRAY_MODULE_MPI=ON
    make
}

check() {
    cd build
    make test
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
    # Remove tests
    rm "${pkgdir}"/usr/bin/test_*
    rm -r "${pkgdir}"/usr/scripts
}
