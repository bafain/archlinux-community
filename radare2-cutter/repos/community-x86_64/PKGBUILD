# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Aaron McDaniel (mcd1992) <'aur' at the domain 'fgthou.se'>

pkgname=radare2-cutter
_gitcommit=d510897ad10fdddc5198465ba1c1d80b590cb04a
pkgver=1.7.1
pkgrel=1
pkgdesc='Qt and C++ GUI for radare2 reverse engineering framework'
url='https://github.com/radareorg/cutter'
arch=('x86_64')
license=('GPL3')
depends=('radare2' 'capstone' 'qt5-base' 'qt5-svg' 'qt5-webengine' 'icu' 'python')
makedepends=('git' 'cmake')
source=(${pkgname}::"git+https://github.com/radareorg/cutter#commit=${_gitcommit}")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  # remove 'v' prefix on tags; prefix revision with 'r'; replace all '-' with '.'
  git describe --always --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd ${pkgname}
  install -d build
}

build() {
  cd ${pkgname}/build
  qmake ../src/Cutter.pro
  make
}

package() {
  cd ${pkgname}
  install -Dm 755 build/Cutter -t "${pkgdir}/usr/bin"
  install -Dm 644 src/Cutter.desktop -t "${pkgdir}/usr/share/applications"
  install -Dm 644 src/img/cutter.svg -t "${pkgdir}/usr/share/icons/hicolor/scalable/apps"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a docs/* "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
