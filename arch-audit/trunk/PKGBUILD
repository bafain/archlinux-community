# $Id$
# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=arch-audit
pkgver=0.1.10
pkgrel=1
pkgdesc='An utility like pkg-audit based on Arch CVE Monitoring Team data'
url='https://github.com/ilpianista/arch-audit'
depends=('curl' 'openssl')
makedepends=('cargo' 'pkg-config')
arch=('x86_64')
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ilpianista/arch-audit/archive/${pkgver}.tar.gz")
sha512sums=('78d437c5124e76e9f821f7fcabe845d551f69c1c19f554202d90f330b4ade355ae45b9e0aab22a147a45a1579d15c4798014c86df9d02468d1e2574701164eae')

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -Dm644 doc/arch-audit.1 "${pkgdir}/usr/share/man/man1/arch-audit.1"

  install -Dm644 systemd/arch-audit.service \
    "${pkgdir}/usr/share/${pkgname}/arch-audit.service"
  install -Dm644 systemd/arch-audit.timer \
    "${pkgdir}/usr/share/${pkgname}/arch-audit.timer"

  install -Dm644 completions/zsh/_arch-audit \
    "${pkgdir}"/usr/share/zsh/site-functions/_arch-audit
}
