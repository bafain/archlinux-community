# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=gajim
pkgver=1.0.3
pkgrel=3
pkgdesc="Full featured and easy to use XMPP (Jabber) client"
arch=('any')
url="https://www.gajim.org/"
license=('GPL3')
depends=('gtk3' 'python-cairo' 'python-dbus' 'python-gobject' 'python-keyring' 'python-nbxmpp'
         'python-pyasn1' 'python-pyopenssl')
makedepends=('intltool')
optdepends=('alsa-utils: play notification sounds'
            'avahi: serverless chatting with autodetected clients in a local network'
            'farstream: start audio and video chat'
            'geoclue2: share current location'
            'gnome-keyring: password storage'
            'gspell: spellchecking of composed messages'
            'gupnp-igd: request your router to forward port for file transfer'
            'libxss: measure idle time, in order to set auto status'
            'python-crypto: encrypting chat messages'
            'python-docutils: generate XHTML output from RST code'
            'python-gnupg: encrypting chat messages with OpenPGP'
            'python-pillow: support of WebP avatars')
source=(https://www.gajim.org/downloads/${pkgver%.*}/gajim-${pkgver}.tar.bz2)
sha1sums=('82e103e35ce29ea7b587ef55b8d8da1c013ddfcb')

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --root="$pkgdir" --optimize=1
}
