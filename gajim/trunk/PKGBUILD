# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=gajim
pkgver=1.0.0
pkgrel=1
pkgdesc="A full featured and easy to use Jabber client"
arch=('any')
url="https://www.gajim.org/"
license=('GPL3')
depends=('python-gobject' 'python-cairo' 'python-pyopenssl' 'python-pyasn1' 'python-nbxmpp'
         'ca-certificates' 'bind-tools' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=('python-dbus: dbus support'
            'gtkspell: for spelling support'
            'libxss: for idle module'
            'notification-daemon: for desktop notification'
            'python-keyring: password storage'
            'python-crypto: support for E2E encryption'
            'python-docutils: for RST generator support'
            'gupnp-igd: for UPnP-IGD support'
            'python-gnupg: for PGP encryption')
source=(https://www.gajim.org/downloads/${pkgver%.*}/gajim-${pkgver}.tar.bz2)
sha1sums=('05d5177af4a09b041aa03d488524c6859fa51130')

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --root="$pkgdir" --optimize=1
}
