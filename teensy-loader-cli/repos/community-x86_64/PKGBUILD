# Maintainer: Kyle Keen <keenerd@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Creator: Peter Feigl <craven@gmx.net>
# Modified: Josh Kropf <josh@slashdev.ca>

pkgname=teensy-loader-cli
pkgver=2.1
pkgrel=2
pkgdesc="Command line loader for the teensy microprocessor boards"
arch=('x86_64')
url="https://www.pjrc.com/teensy/loader_cli.html"
license=('GPL2')
depends=('libusb-compat')
source=(
  "https://www.pjrc.com/teensy/${pkgname//-/_}.${pkgver}.zip"
  "https://www.pjrc.com/teensy/49-teensy.rules")
sha256sums=('dafd040d6748b52e0d4a01846d4136f3354ca27ddc36a55ed00d0a0af0902d46'
            '031de0b26991b5a3b19c497d9c0a17f86c40c55d925b9d07d19ab89f2286469d')

build() {
  cd ${pkgname//-/_}
  make
}

package() {
  cd ${pkgname//-/_}
  install -Dm755 teensy_loader_cli "${pkgdir}"/usr/bin/teensy-loader-cli
  install -Dm644 "${srcdir}"/49-teensy.rules -t "${pkgdir}"/usr/lib/udev/rules.d/
}
