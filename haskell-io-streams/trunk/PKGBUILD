# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=io-streams
pkgname=haskell-io-streams
pkgver=1.5.0.1
pkgrel=26
pkgdesc="Simple, composable, and easy-to-use stream I/O"
url="https://github.com/snapframework/io-streams"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-network' 'haskell-primitive' 'haskell-vector'
         'haskell-zlib-bindings')
makedepends=('ghc' 'haskell-hunit' 'haskell-quickcheck' 'haskell-test-framework'
             'haskell-test-framework-hunit' 'haskell-test-framework-quickcheck2' 'haskell-zlib')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('610a149870732b04c59a5fb91b92e7b9a7f7c53e89ab29dc9249fca9e6a9a54ab97c7986b0bde23a98e5301b3d6bd7537f3ca6c7023574bf4a8b9ca7292e2489')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e '/bytestring-builder/d' -e 's/<.*2.7/<3/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
