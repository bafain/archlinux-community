# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgbase=snes9x
pkgname=('snes9x' 'snes9x-gtk')
pkgver=1.56.2
pkgrel=1
pkgdesc='Portable Super Nintendo Entertainment System (TM) emulator'
arch=('x86_64')
url='http://www.snes9x.com/'
license=('custom')
makedepends=(
  'alsa-lib' 'cairo' 'gdk-pixbuf2' 'glib2' 'gtk3' 'intltool' 'libepoxy'
  'libpng' 'libpulse' 'libx11' 'libxext' 'libxml2' 'libxrandr' 'libxv'
  'minizip' 'nasm' 'sdl2' 'zlib'
)
source=("https://github.com/snes9xgit/snes9x/archive/${pkgver}.tar.gz")
sha1sums=('761af9b038d8856af67aa6683f9f25865ab57283')

prepare() {
  cd snes9x-${pkgver}/gtk

  autoreconf -vif
}


build() {
  cd snes9x-${pkgver}/unix

  ./configure \
    --prefix='/usr' \
    --enable-netplay
  make

  cd ../gtk
  ./autogen.sh
  ./configure \
    --prefix='/usr' \
    --with-netplay \
    --with-opengl
  make
}

package_snes9x() {
  depends=('libpng' 'libx11' 'libxext' 'libxinerama' 'libxv' 'minizip' 'zlib')

  cd snes9x-${pkgver}/unix

  install -Dm 755 snes9x -t "${pkgdir}"/usr/bin/
  install -Dm 644 ../docs/{snes9x.conf.default,control-inputs.txt,controls.txt,snapshots.txt} -t "${pkgdir}"/usr/share/doc/snes9x
  install -Dm 644 ../LICENSE "${pkgdir}"/usr/share/licenses/snes9x/LICENSE

}

package_snes9x-gtk() {
  depends=(
    'alsa-lib' 'cairo' 'gdk-pixbuf2' 'glib2' 'gtk3' 'hicolor-icon-theme'
    'libepoxy' 'libpng' 'libpulse' 'libx11' 'libxext' 'libxml2' 'libxrandr'
    'libxv' 'minizip' 'sdl2' 'zlib'
  )

  cd snes9x-${pkgver}/gtk

  make DESTDIR="${pkgdir}" install
  install -Dm 644 ../docs/{snes9x.conf.default,control-inputs.txt,controls.txt,snapshots.txt} -t "${pkgdir}"/usr/share/doc/snes9x-gtk
  install -Dm 644 ../LICENSE "${pkgdir}"/usr/share/licenses/snes9x-gtk/LICENSE
}

# vim: ts=2 sw=2 et:
