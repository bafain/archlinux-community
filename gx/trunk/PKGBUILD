# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Jakub "Kubuxu" Sztandera  <kubuxu@protonmail.ch>

pkgname=gx
pkgver=0.13.0
pkgrel=2

pkgdesc="Packaging tool built around the distributed, content addressed filesystem IPFS"
url="https://github.com/whyrusleeping/gx"
arch=('x86_64')
license=('MIT')

makedepends=('git' 'go-pie')
optdepends=('gx-go: gx packaing module for go')

source=("git+https://github.com/whyrusleeping/gx.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  mkdir -p "$srcdir"/src/github.com/whyrusleeping/
  ln -fs "$srcdir/gx" "$srcdir"/src/github.com/whyrusleeping/gx
}

build() {
  # Required for go get
  export GOPATH="$srcdir"
  export GOBIN="$GOPATH/bin"
  cd "$srcdir"/src/github.com/whyrusleeping/gx

  msg2 'Installing dependencies...'
  go get -v

  msg2 'Building binary...'
  go install -v
}

package() {
  msg2 'Packaging binary...'
  install -Dm 755 bin/gx "${pkgdir}/usr/bin/gx"

  msg2 'Packaging auxiliary files...'
  cd gx
  install -Dm 644 -t "${pkgdir}/usr/share/licenses/gx" LICENSE
}
