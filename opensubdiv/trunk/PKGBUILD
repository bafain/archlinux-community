# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=opensubdiv
pkgver=3.3.3
pkgrel=1
pkgdesc="An Open-Source subdivision surface library"
arch=(x86_64)
url="http://graphics.pixar.com/opensubdiv"
license=('apache')
depends=(zlib ptex intel-tbb libxcursor xorg-xrandr libxinerama)
makedepends=(cmake doxygen glfw glew python2 python-pygments python-docutils opencl-headers)
#makedepends_x86_64=(cuda) Broken currently :/
source=("https://github.com/PixarAnimationStudios/OpenSubdiv/archive/v${pkgver//./_}.tar.gz")
sha512sums=('65cd72b4240eeb3fdb6d52497a2daa2fc68e10c25c41ffa0cf81449ebcb860e303539043c672265916267a0115f250566570b5705fd5da54f46abe7b9f7baf81')

prepare() {
    cd "OpenSubdiv-${pkgver//./_}"

    find . -name \*.py | xargs sed -i "s/env python/env python2/g"
}

build() {
    cd "OpenSubdiv-${pkgver//./_}"

    rm -rf build
    mkdir build && cd build

    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr
}

package() {
    cd "OpenSubdiv-${pkgver//./_}"/build

    make DESTDIR="$pkgdir/" install

    rm -rf ${pkgdir}/usr/bin
}
