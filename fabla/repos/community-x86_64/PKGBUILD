# Maintainer: David Runge <dave@sleepmap.de>
pkgname=fabla
pkgver=1.3.2
pkgrel=2
pkgdesc="An open-source LV2 drum sampler plugin instrument"
arch=('x86_64')
url="http://openavproductions.com/fabla/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('libsndfile' 'ntk')
makedepends=('cairomm' 'cmake' 'lv2' 'mesa')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openAVproductions/openAV-${pkgname}/archive/release-${pkgver}.tar.gz")
sha512sums=('009aa11b50743fe3a8f401657b0c67eb9e8b757f35d713d07e763a23f5e3e5be398858a766e6b116be85fae11969baddafd5d98623858cf0ebedc2a8e3b02f66')

prepare() {
  mv -v "openAV-Fabla-release-$pkgver" "${pkgname}-${pkgver}"
  cd "$pkgname-$pkgver"
  mkdir -v build
}

build() {
  cd "$pkgname-$pkgver/build"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        ..
  make
}

package() {
  cd "$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 ../{CHANGELOG,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
