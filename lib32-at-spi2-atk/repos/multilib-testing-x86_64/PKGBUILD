# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor:Ionut Biru <ibiru@archlinux.org> 

pkgname=lib32-at-spi2-atk
pkgver=2.26.2
pkgrel=1
pkgdesc='A GTK+ module that bridges ATK to D-Bus at-spi'
arch=('x86_64')
url='https://wiki.gnome.org/Accessibility'
license=('GPL2')
depends=('at-spi2-atk' 'lib32-at-spi2-core' 'lib32-atk' 'lib32-libx11' 'lib32-libxml2')
makedepends=(git meson)
_commit=196498d7f02e4e5662941213e852ef1ca899cf79  # tags/AT_SPI2_ATK_2_26_2^0
source=("git+https://git.gnome.org/browse/at-spi2-atk#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd at-spi2-atk
  git describe --tags | sed 's/^AT_SPI2_ATK_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd at-spi2-atk
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  arch-meson at-spi2-atk build --libdir=/usr/lib32
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  rm -r "$pkgdir"/usr/{include,lib32/gnome-settings-daemon-3.0}
}

# vim:set ts=2 sw=2 et:
