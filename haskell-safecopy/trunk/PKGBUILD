# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=safecopy
pkgname=haskell-safecopy
pkgver=0.9.4.1
pkgrel=6
pkgdesc="Binary serialization with version control."
url="http://acid-state.seize.it/safecopy"
license=('custom:PublicDomain')
arch=('x86_64')
depends=('ghc-libs' 'haskell-cereal' 'haskell-old-time' 'haskell-vector')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('7c645cd3e216040e155e814eae72c3f584523f77f1d81835f7a1fb9c5bd63ebd51f746a49584be8a6822866b48119be289f3e74514a054c2f65e6443304e6967')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
}
