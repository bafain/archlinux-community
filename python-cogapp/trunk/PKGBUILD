# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cogapp
pkgver=2.5.1
pkgrel=1
pkgdesc="A code generator for executing Python snippets in source files"
url="http://nedbatchelder.com/code/cog"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://pypi.io/packages/source/c/cogapp/cogapp-$pkgver.tar.gz")
sha512sums=('91eec0ef2ee0f4bac50c90c46ed8cbe2f97688ea2e783eae6a99686d8d7e2634ff53b3d634cbfb8ddc65caf41321b9fb2c05641891c34dd0b7abaaa47a4be731')

build() {
  cd cogapp-$pkgver
  python setup.py build
}

check() {
  cd cogapp-$pkgver
  python setup.py pytest
}

package() {
  cd cogapp-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
