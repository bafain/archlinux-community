# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Philipp Sieweck

pkgname=cvc4
pkgver=1.6
pkgrel=1
pkgdesc="An automatic theorem prover"
arch=('x86_64')
url="http://cvc4.cs.nyu.edu/web/"
depends=('boost' 'libantlr3c' 'cln')
makedepends=('python')
optdepends=('readline: for an improved interaction support')
license=('GPL' 'BSD' 'MIT' 'custom:HPND')
source=("http://cvc4.cs.stanford.edu/downloads/builds/src/cvc4-$pkgver.tar.gz" cvc4-gcc7.patch)
sha512sums=('0887b3f74a4b9e51e634591c7cf39d730110ca5d930149bab4816a49e383eeea8ccadf8474d22f5529cc03ddd045acacf8a2b92434b882adf352f4de4075fcd4'
            '5b05fff54c1f610b4e1d9884812980529512a0656da66cf5f67a407eaf28e10da59fd06e9b34ee0200751a2e796df37e37df9dda406cc3a5ceaaf518c357a5d7')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../cvc4-gcc7.patch # Fix build with GCC 7
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-doxygen-doc --with-cln --enable-gpl
  make
}

package() {
  cd $pkgname-$pkgver
  make prefix="$pkgdir"/usr install
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/cvc4/LICENSE
}
