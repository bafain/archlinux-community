# Maintainer: Baptiste Jonglez <archlinux at bitsofnetworks dot org>

pkgbase=python-sphinx-testing
pkgname=(python-sphinx-testing python2-sphinx-testing)
pkgver=0.7.2
pkgrel=2
pkgdesc="Testing utility classes and functions for Sphinx extensions"
arch=('any')
url="https://github.com/sphinx-doc/sphinx-testing"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-mock' 'python-sphinx' 'python2-mock' 'python2-sphinx')
source=("https://pypi.io/packages/source/s/sphinx-testing/sphinx-testing-$pkgver.tar.gz"
        "0001-Skip-checking-cleanup-is-called.patch")
sha256sums=('9d30f93007620e137b33edf19f52a7225eab853546b7e588ef09d1342e821e94'
            '21f729deaa27b8476858f0bfa123d83fcc0583fa9fe2b3e22131fc3882f26821')

prepare() {
  cd "$srcdir/sphinx-testing-$pkgver"
  # Fix test failure
  patch -p1 < "$srcdir/0001-Skip-checking-cleanup-is-called.patch"
  cp -a "$srcdir"/sphinx-testing-$pkgver{,-py2}
}

build() {
  cd "$srcdir/sphinx-testing-$pkgver"
  python setup.py build

  cd "$srcdir/sphinx-testing-$pkgver-py2"
  python2 setup.py build
}

check() {
  export PYTHONPATH="$srcdir/sphinx-testing-$pkgver/src"
  cd "$srcdir/sphinx-testing-$pkgver/tests"
  #python setup.py test
  python -m unittest discover

  export PYTHONPATH="$srcdir/sphinx-testing-$pkgver-py2/src"
  cd "$srcdir/sphinx-testing-$pkgver-py2/tests"
  #python2 setup.py test
  python2 -m unittest discover
}

package_python-sphinx-testing() {
  depends=('python-six' 'python-sphinx')

  cd "$srcdir/sphinx-testing-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-sphinx-testing() {
  depends=('python2-six' 'python2-sphinx')

  cd "$srcdir/sphinx-testing-$pkgver-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
