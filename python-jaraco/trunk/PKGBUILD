# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgbase=python-jaraco
pkgname=('python-jaraco' 'python2-jaraco')
pkgver=2018.09.08
pkgrel=1
pkgdesc='A gaggle of idiosyncratic and questionable wrappers for the stdlib.'
arch=('any')
url='https://github.com/jaraco?page=1&tab=repositories&utf8=%E2%9C%93&q=jaraco'
license=('MIT')
depends=('python-six' 'python-pytz' 'python-more-itertools')
makedepends=('python-setuptools' 'python2-setuptools'
    'python2-backports.functools_lru_cache' 'python2-more-itertools' 'python2-pytz')

# These should not exist and do not deserve 16 packages.
# Furthermore, try to make a replacement for python-irc so we can nuke it all.
# And now cherrypy depends on this too :-(
_text=1.10.1
_functools=1.20
_collections=1.5.3
_classes=1.5
_stream=1.2
_logging=1.5.2
_itertools=2.5
_pyhost="https://files.pythonhosted.org/packages/source"

source=("$_pyhost/j/jaraco.text/jaraco.text-$_text.tar.gz"
        "$_pyhost/j/jaraco.functools/jaraco.functools-$_functools.tar.gz"
        "$_pyhost/j/jaraco.collections/jaraco.collections-$_collections.tar.gz"
        "$_pyhost/j/jaraco.classes/jaraco.classes-$_classes.tar.gz"
        "$_pyhost/j/jaraco.stream/jaraco.stream-$_stream.tar.gz"
        "$_pyhost/j/jaraco.logging/jaraco.logging-$_logging.tar.gz"
        "$_pyhost/j/jaraco.itertools/jaraco.itertools-$_itertools.tar.gz")
sha512sums=('5c09b90f586d5ddf7419013f7e36814598337c257e6bd1c1eec993d7df021440798ec9300a6d45c960c5458c4197b458d1863f478b60a9a003241e98a644f28e'
            'ba17b567dfcadb0d8ff1cc7a6ed07f2a23abfd42e73f3d815a6fa19d9be97d6af0c90bff7fb5e7ef67012ea5d931e9babc963da8a7105ba6a92d5c43c7126e04'
            '6e1fa88b468b9d971a7a302ebfdf8b9762404c2fdc1b003eee6653704b9f385f0f2e0c84ac075bdc81f8693195781cc5532c4adb31bcabd87974624a14e287ed'
            '8fe3d40863424547e5396fd6a2b4ef3f27a92185ca82206bb88de2716419a9c2dc917fc945c732d5dab1a1795727a12014e0bbaceee7834896351de05491f99c'
            '47a96c1de12eb64e250c85d97429218606dc9bb212cf7b97e579763dd901ee063711c00307b7442be400856d3fd4e5994afbe1cb91a99e233541ffd7ff4024b9'
            '3299adc9c9ca6113198cddbc34147afe1bf453edee16a2d6bfcce64e63deeaff7f87beb74ff44f092435f3433a921b19167fb45778dce444512807357bce1661'
            '9e56282073e7a09bcbe114c863c27d4a8233dc5b2805b17f174afcaba8643e6f8405c26f492713d0970975535102b188a4bad246b747a5718362c81a845cb494')

prepare() {
  # remove inflect dep
  cd "$srcdir/jaraco.itertools-$_itertools"
  sed -i "s/'inflect',//" setup.py
  sed -i "s/inflect//"    jaraco.itertools.egg-info/requires.txt
  sed -i 's/import inflect//' jaraco/itertools.py
  sed -i 's/ord = .*$/ord = str(self.sliceArgs[2])+"th"/' jaraco/itertools.py

  cd "$srcdir"
  cp -R "jaraco.text-$_text"               "py2-jaraco.text-$_text"
  cp -R "jaraco.functools-$_functools"     "py2-jaraco.functools-$_functools"
  cp -R "jaraco.collections-$_collections" "py2-jaraco.collections-$_collections"
  cp -R "jaraco.classes-$_classes"         "py2-jaraco.classes-$_classes"
  cp -R "jaraco.stream-$_stream"           "py2-jaraco.stream-$_stream"
  cp -R "jaraco.logging-$_logging"         "py2-jaraco.logging-$_logging"
  cp -R "jaraco.itertools-$_itertools"     "py2-jaraco.itertools-$_itertools"
}

package_python-jaraco() {
  cd "$srcdir/jaraco.text-$_text"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.functools-$_functools"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.collections-$_collections"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.classes-$_classes"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.stream-$_stream"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.logging-$_logging"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.itertools-$_itertools"
  python3 setup.py install --root="$pkgdir/" --optimize=0

  #rm "$pkgdir/usr/bin/calc-prorate"
  #rmdir "$pkgdir/usr/bin"
  # None of these packages have a license file?  Fix later.
  #install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-jaraco() {
  depends=('python2-six' 'python2-pytz' 'python2-more-itertools' 'python2-backports.functools_lru_cache')
  cd "$srcdir/py2-jaraco.text-$_text"
  python2 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/py2-jaraco.functools-$_functools"
  python2 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/py2-jaraco.collections-$_collections"
  python2 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/py2-jaraco.classes-$_classes"
  python2 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/py2-jaraco.stream-$_stream"
  python2 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/py2-jaraco.logging-$_logging"
  python2 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/py2-jaraco.itertools-$_itertools"
  python2 setup.py install --root="$pkgdir/" --optimize=0

  #rm "$pkgdir/usr/bin/calc-prorate"
  #rmdir "$pkgdir/usr/bin"
  #install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
