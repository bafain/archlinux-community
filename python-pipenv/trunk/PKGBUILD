# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Maikel Wever <maikelwever@gmail.com>

pkgbase=python-pipenv
pkgname=(python-pipenv python2-pipenv)
pkgver=11.0.3
pkgrel=1
pkgdesc="Sacred Marriage of Pipfile, Pip, & Virtualenv."
url="https://docs.pipenv.org"
arch=('any')
license=('MIT')
makedepends=('python' 'python-setuptools'
             'python2' 'python2-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pypa/pipenv/archive/v${pkgver}.tar.gz")
sha256sums=('a57729df9c7d2992757c47a8b926f9eb6038ea9a0a6501b4582b20ddb961de63')

prepare() {
  cp -a pipenv-$pkgver{,-py2}
}

build(){
  cd "$srcdir/pipenv-$pkgver"
  python setup.py build

  cd "$srcdir/pipenv-$pkgver-py2"
  python2 setup.py build
}

package_python2-pipenv() {
  depends=('python2' 'python2-pew' 'python2-virtualenv')
  cd "pipenv-$pkgver-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  mv "${pkgdir}/usr/bin/pipenv" "${pkgdir}/usr/bin/pipenv2"
}

package_python-pipenv() {
  depends=('python' 'python-pew' 'python-virtualenv')
  cd "pipenv-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

