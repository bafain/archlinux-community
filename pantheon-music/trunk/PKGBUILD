# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-music
pkgver=0.4.2.r434.1091a852
pkgrel=1
pkgdesc='The Pantheon Music Player'
arch=('x86_64')
url='https://github.com/elementary/music'
license=('GPL3')
groups=('pantheon')
depends=('cairo' 'gdk-pixbuf2' 'glib2' 'gst-plugins-base-libs' 'gstreamer'
         'gtk3' 'json-glib' 'libaccounts-glib' 'libgda' 'libgee' 'libgpod'
         'libgsignon-glib' 'libpeas' 'libsoup' 'pango' 'taglib' 'zeitgeist'
         'libgranite.so')
makedepends=('git' 'gobject-introspection' 'intltool' 'meson' 'vala')
optdepends=('gst-plugins-base: "Base" plugin libraries'
            'gst-plugins-good: "Good" plugin libraries'
            'gst-plugins-bad: "Bad" plugin libraries'
            'gst-plugins-ugly: "Ugly" plugin libraries')
_commit='1091a85206ad6421add81c8cc6493fae310e6770'
source=("pantheon-music::git+https://github.com/elementary/music.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd pantheon-music

  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson pantheon-music build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
