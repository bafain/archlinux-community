# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=tldr
pkgver=0.4.4
pkgrel=3
pkgdesc="Command line client for tldr, a collection of simplified and community-driven man pages."
arch=('any')
url="https://github.com/tldr-pages/tldr-python-client"
license=('MIT')
depends=('python-colorama' 'python-six' 'python-termcolor')
makedepends=('python-setuptools-markdown' 'python-setuptools-scm')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tldr-pages/tldr-python-client/archive/$pkgver.tar.gz")
sha512sums=('b96fdf8f6168ab35de7c8b68286a2870cc41ac10acbc12fbf9895e002f8f3eb5371e049d096cee179105cc9d5caccb8173b712585caaf433024f40884d223dee')

prepare() {
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd tldr-python-client-$pkgver
  python setup.py build
}

check() {
  cd tldr-python-client-$pkgver
  python setup.py pytest
}

package() {
  cd tldr-python-client-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"
  install -Dm755 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
