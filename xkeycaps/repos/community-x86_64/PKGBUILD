# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Patrick Leslie Polzer <leslie.polzer@gmx.net>

pkgname=xkeycaps
pkgver=2.46
pkgrel=8
arch=('x86_64')
pkgdesc='A graphical front-end to xmodmap'
url="https://www.jwz.org/xkeycaps/"
license=('custom')
depends=('libxaw')
makedepends=('imake' 'xbitmaps')
source=(https://www.jwz.org/$pkgname/$pkgname-$pkgver.tar.Z
	license.txt)
sha512sums=('1bc28b3e69ce03a0b0bf8173c0fa016a683a12c123825d8873eff65f5f02d1b3369096a0c80678383048d93e68e3a607f3bee155f13afe6487a3ffa3dc4f62c5'
            'dc07c0ff12bb103bcb42911977849c586364ba39cf171d3761c37e69131a933545d6b149b1ced8ff9fd9dd0dfc81bad9ac39e26c22c867fca38f85ccdff74220')
DLAGENTS=('https::/usr/bin/curl --user-agent Mozilla\ Firefox -fLC - --retry 3 --retry-delay 3 -o %o %u')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  xmkmf
  make MANPATH=/usr/share/man
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" MANPATH=/usr/share/man install.man
  install -Dm644 ../license.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
