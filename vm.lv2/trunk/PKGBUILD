# Maintainer: David Runge <dave@sleepmap.de>
pkgname=vm.lv2
pkgver=0.4.0
pkgrel=4
pkgdesc="A virtual machine LV2 plugin bundle"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/vm/#"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('libgl')
makedepends=('cmake' 'lv2')
source=("https://git.open-music-kontrollers.ch/lv2//$pkgname/snapshot/$pkgname-$pkgver.tar.xz")
md5sums=('01614eb27309f4fbd0dde02dc74991c0')

prepare() {
  cd "$pkgname-$pkgver"
  # hotfixing undeclared variable:
  # https://gitlab.com/OpenMusicKontrollers/vm.lv2/issues/1
  sed -e 's/HUGE/HUGE_VAL/g' -i vm_ui.c
  mkdir -v build
}

build() {
  cd "$pkgname-$pkgver/build"
  cmake -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        ..
  make
}

package() {
  cd "$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir/" install
  # docs
  install -vDm 644 ../README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
