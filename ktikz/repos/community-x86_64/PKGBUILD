# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgbase=ktikz
pkgname=('ktikz' 'qtikz')
pkgver=0.12
pkgrel=1
pkgdesc="A small application helping you to create TikZ diagrams (from the LaTeX pgf package)"
arch=('x86_64')
url="https://github.com/fhackenberger/ktikz"
license=('GPL')
depends=('poppler-qt5')
makedepends=('cmake' 'extra-cmake-modules' 'qt5-base' 'qt5-tools'
             'kdoctools' 'kxmlgui' 'ktexteditor' 'kparts' 'kiconthemes'
             'poppler' 'poppler-qt5' 'kdelibs4support')
source=(${pkgbase}-${pkgver}.tar.gz::"${url}/archive/${pkgver}.tar.gz")
sha256sums=('ef15fe70f4e057eab74ed738886f8e4a53d5b1bcdf1ee329930bf06e22f56e9d')

prepare() {
    mkdir ${pkgbase}-${pkgver}/{build,buildqt}
}

build() {
    # Build ktikz
    cd ${pkgbase}-${pkgver}/build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None
    make
    # Build qtikz
    cd ../buildqt
    qmake ../qtikz.pro
    make
}

package_ktikz() {
    pkgdesc+=" – KDE version"
    depends+=('kparts')
    cd ${pkgbase}-${pkgver}/build
    make DESTDIR="${pkgdir}" install
}

package_qtikz() {
    pkgdesc+=" – Qt version"
    cd ${pkgbase}-${pkgver}/buildqt
    make INSTALL_ROOT="${pkgdir}" install
    # Fix icons path
    sed -i 's:Icon=/usr/share/qtikz/qtikz-128.png:Icon=qtikz:' "${pkgdir}"/usr/share/applications/qtikz.desktop
    rm "${pkgdir}"/usr/share/qtikz/qtikz-128.png
    install -Dm644 ../app/icons/qtikz.svg "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/qtikz.svg
    install -Dm644 ../app/icons/qtikz-22.png "${pkgdir}"/usr/share/icons/hicolor/22x22/apps/qtikz.png
    install -Dm644 ../app/icons/qtikz-128.png "${pkgdir}"/usr/share/icons/hicolor/128x128/apps/qtikz.png
}
