# $Id$
#Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libxt
pkgname=lib32-$_pkgbasename
pkgver=1.1.5
pkgrel=2
pkgdesc="X11 toolkit intrinsics library (32-bit)"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libsm' 'lib32-libx11' $_pkgbasename)
makedepends=('xorg-util-macros' 'gcc-multilib')
source=(${url}/releases/individual/lib/libXt-${pkgver}.tar.bz2{,.sig})
sha512sums=('29828cfc021b60e0a0b5ff0321801db20f12bd635866cd000b35a23736b5477a3d1e5f936a1124ed3e3f4c0e7a27cb8744dbb5ab4fbcd45c817a13877081e05b'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') #Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd "${srcdir}/libXt-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --sysconfdir=/etc --disable-static --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXt-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname" 
}
