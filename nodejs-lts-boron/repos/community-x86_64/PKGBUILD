# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>

pkgname=nodejs-lts-boron
pkgver=6.14.4
pkgrel=1
pkgdesc='Evented I/O for V8 javascript (LTS release: Boron)'
arch=('x86_64')
url='https://nodejs.org/'
license=('MIT')
depends=('openssl-1.0' 'zlib' 'icu' 'libuv' 'http-parser' 'c-ares')
makedepends=('python2' 'procps-ng')
optdepends=('npm: nodejs package manager')
provides=("nodejs=$pkgver")
conflicts=('nodejs')
source=("${url}/dist/v${pkgver}/node-v${pkgver}.tar.xz")
# https://nodejs.org/download/release/latest-boron/SHASUMS256.txt.asc
sha256sums=('9a4bfc99787f8bdb07d5ae8b1f00ec3757e7b09c99d11f0e8a5e9a16a134ec0f')
validpgpkeys=('C4F0DFFF4E8C1A8236409D08E73BC641CC11F4C8' # Myles Borins <mylesborins@google.com>
              'B9AE9905FFD7803F25714661B63B535A4C206CA9' # Evan Lucas <evanlucas@me.com>
              'DD8F2338BAE7501E3DD5AC78C273792F7D83545D') # Rod Vagg <rod@vagg.org>

prepare() {
  cd node-v${pkgver}

  msg 'Fixing for python2 name'
  find -type f -exec sed \
    -e 's_^#!/usr/bin/env python$_&2_' \
    -e 's_^\(#!/usr/bin/python2\).[45]$_\1_' \
    -e 's_^#!/usr/bin/python$_&2_' \
    -e 's_^\( *exec \+\)python\( \+.*\)$_\1python2\2_'\
    -e 's_^\(.*\)python\( \+-c \+.*\)$_\1python2\2_'\
    -e "s_'python'_'python2'_" -i {} \;
  find test/ -type f -exec sed 's_python _python2 _' -i {} \;
}

build() {
  cd node-v${pkgver}
  export PKG_CONFIG_PATH=/usr/lib/openssl-1.0/pkgconfig
  export PYTHON=python2
  ./configure \
    --prefix=/usr \
    --with-intl=system-icu \
    --without-npm \
    --shared-openssl \
    --shared-zlib \
    --shared-libuv \
    --shared-http-parser \
    --shared-cares

  make
}

check() {
  cd node-v${pkgver}
  # Some network related errors
  make test || warning "Tests failed"
}

package() {
  cd node-v${pkgver}

  make DESTDIR="${pkgdir}" install

  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
