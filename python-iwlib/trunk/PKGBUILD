# $Id$
# Maintainer: David Runge <dave@sleepmap.de>

_name=iwlib
pkgbase=python-iwlib
pkgname=('python-iwlib' 'python2-iwlib')
pkgver=1.6.1
pkgrel=5
pkgdesc='Python module to interface with iwlib'
arch=('x86_64')
url="https://github.com/nathan-hoad/python-iwlib"
license=('GPL2')
makedepends=('python-cffi' 'python-setuptools' 'python2-cffi' 'python2-setuptools' 'wireless_tools')
checkdepends=('python-pytest' 'python2-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('dd6e742c8563408a0aa9c873059705eb92b294d4adc9d40d784eee148b4053b39aab4e41696328b7397ee4587ea1ce05e7dc6577705bb30d0fae2b857db7f95e')

prepare(){
  mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
  python2 setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  local _py3_ver=$(python3 -c "import sys; print(str(sys.version_info.major)+'.'+str(sys.version_info.minor))")
  local _py2_ver=$(python2 -c "import sys; print(str(sys.version_info.major)+'.'+str(sys.version_info.minor))")
  local PYTHON2_PATH="${srcdir}/${pkgbase}-${pkgver}/build/lib.linux-$CARCH-${_py2_ver}/:${PYTHONPATH}"
  local PYTHON3_PATH="${srcdir}/${pkgbase}-${pkgver}/build/lib.linux-$CARCH-${_py3_ver}/:${PYTHONPATH}"
  export PYTHONPATH="${PYTHON3_PATH}"
  py.test
  export PYTHONPATH="${PYTHON2_PATH}"
  py.test2
}

package_python-iwlib() {
  depends=('python-cffi' 'wireless_tools')
  cd "${pkgbase}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,README.rst}
}

package_python2-iwlib() {
  depends=('python2-cffi' 'wireless_tools')
  cd "${pkgbase}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,README.rst}
}
