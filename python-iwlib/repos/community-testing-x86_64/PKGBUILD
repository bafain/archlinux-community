# Maintainer: David Runge <dave@sleepmap.de>

pkgbase=python-iwlib
pkgname=('python-iwlib' 'python2-iwlib')
pkgver=1.5
pkgrel=8
pkgdesc='Python module to interface with iwlib'
arch=('x86_64')
url='https://github.com/nathan-hoad/python-iwlib'
license=('GPL2')
makedepends=('python-setuptools' 'python2-setuptools' 'wireless_tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/nathan-hoad/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('2b79e412949a465d1383620745b6bfbf5924ef8ed47d459882afc5c86e54d10d1b963055df7076a481fb7bd6ade1d318ce35ed36ee5c129c048bbf6b1790f432')

prepare(){
  cp -a "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  python setup.py test
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py test
}

package_python-iwlib() {
  depends=('python' 'wireless_tools')
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"

  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm644 {AUTHORS,README.rst}
}

package_python2-iwlib() {
  depends=('python2' 'wireless_tools')
  cd "${pkgname[1]}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
}
