# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-files
pkgver=0.3.5.r5882.9785e68a
pkgrel=1
pkgdesc='The Pantheon File Manager'
arch=('x86_64')
url='https://launchpad.net/pantheon-files'
license=('GPL3')
groups=('pantheon')
depends=('atk' 'cairo' 'gdk-pixbuf2' 'glib2' 'glibc' 'gtk3' 'libcanberra'
         'libgee' 'libnotify' 'pango' 'sqlite' 'zeitgeist'
         'libgranite.so')
makedepends=('cmake' 'git' 'gnome-common' 'vala')
optdepends=('contractor: Various context menu entries'
            'tumbler: Thumbnails generation')
_commit='9785e68aa828e5a2b400a277dd70cd7eea985512'
source=("pantheon-files::git+https://github.com/elementary/files.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd pantheon-files

  echo "0.3.5.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  cmake ../pantheon-files \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
    -DCMAKE_SKIP_RPATH='TRUE' \
    -DGSETTINGS_COMPILE='FALSE'
  make
}

package() {
  make DESTDIR="${pkgdir}" -C build install
}

# vim: ts=2 sw=2 et:
