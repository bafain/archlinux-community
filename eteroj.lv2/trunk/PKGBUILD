# Maintainer: David Runge <dave@sleepmap.de>
pkgname=eteroj.lv2
pkgver=0.4.0
pkgrel=4
pkgdesc="Open Sound Control for LV2"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/eteroj/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('libuv')
makedepends=('cmake' 'lv2')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz")
sha512sums=('93288eaf6ff1df439f8af920f535f46d2dcdf792b0d737c2c316efe005928e6447dccdbb057594c548943089b2f7883de18ad9f652c4a15f6cad2f5f5de7c5bb')

prepare() {
  cd "$pkgname-$pkgver"
  mkdir -v build
}

build() {
  cd "$pkgname-$pkgver/build"
  cmake -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        ..
  make
}

package() {
  cd "$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir/" install
  # docs
  install -vDm 644 ../README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
