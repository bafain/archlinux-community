# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Omar Sandoval <osandov at cs dot washington dot edu>
# Contributor: Alexander Slesarev <slesarew at gmail dot com>

pkgbase=python-gmpy2
pkgname=(python2-gmpy2 python-gmpy2)
pkgver=2.1.0a2
pkgrel=3
pkgdesc="Provides C-coded Python modules for fast multiple-precision arithmetic."
arch=(x86_64)
url="https://github.com/aleaxit/gmpy"
license=(LGPL2.1)
makedepends=(python2-setuptools python-setuptools libmpc git)
#source=("https://pypi.io/packages/source/g/gmpy2/gmpy2-$pkgver.tar.gz"
source=(git://github.com/aleaxit/gmpy#tag=gmpy2-$pkgver
        gmpy2-cython.patch::"https://github.com/aleaxit/gmpy/commit/cb0d591e.patch")
sha256sums=('SKIP'
            '457e29bc4441a868eabd5a1f2a45316d13b96e949c2616237f2700e5d3d5bf6c')

prepare() {
  mv gmpy gmpy2-$pkgver

  cd gmpy2-$pkgver
   patch -p1 -i ../gmpy2-cython.patch # Fix install of cython headers
  cd ..

  cp -r gmpy2-$pkgver{,-py2}  
}

build() {
  cd gmpy2-$pkgver
  python setup.py build

  cd ../gmpy2-$pkgver-py2
  python2 setup.py build
}

package_python2-gmpy2() {
  depends=(python2 libmpc)

  cd gmpy2-$pkgver-py2
  python2 setup.py install --root=$pkgdir --optimize=1 --skip-build

  install -d -m 755 $pkgdir/usr/share/doc/$pkgname
  install -m 644 -t $pkgdir/usr/share/doc/$pkgname docs/*
}

package_python-gmpy2() {
  depends=(python libmpc)
  
  cd gmpy2-$pkgver 
  python setup.py install --root=$pkgdir --optimize=1 --skip-build
 
  install -d -m 755 $pkgdir/usr/share/doc/$pkgname
  install -m 644 -t $pkgdir/usr/share/doc/$pkgname docs/*
}

