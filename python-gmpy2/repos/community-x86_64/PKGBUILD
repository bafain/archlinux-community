# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Omar Sandoval <osandov at cs dot washington dot edu>
# Contributor: Alexander Slesarev <slesarew at gmail dot com>

pkgbase=python-gmpy2
pkgname=(python2-gmpy2 python-gmpy2)
pkgver=2.1.0a1
pkgrel=1
pkgdesc="Provides C-coded Python modules for fast multiple-precision arithmetic."
arch=(x86_64)
url="https://github.com/aleaxit/gmpy"
license=(LGPL2.1)
makedepends=(python2 python libmpc)
source=("https://pypi.io/packages/source/g/gmpy2/gmpy2-${pkgver}.tar.gz")
sha256sums=('d28fbb0b990233b2339be020418d14d1a132eddfd7a763e56ecef8c87790a881')

package_python2-gmpy2() {
  depends=(python2 libmpc)

  cd gmpy2-$pkgver
  python2 setup.py install --root=$pkgdir --optimize=1

  install -d -m 755 $pkgdir/usr/share/doc/$pkgname
  install -m 644 -t $pkgdir/usr/share/doc/$pkgname docs/*
}

package_python-gmpy2() {
  depends=(python libmpc)
  
  cd gmpy2-$pkgver 
  python setup.py install --root=$pkgdir --optimize=1
 
  install -d -m 755 $pkgdir/usr/share/doc/$pkgname
  install -m 644 -t $pkgdir/usr/share/doc/$pkgname docs/*
}

