# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=singular
_majver=4-1-1
#_patchver=4
#pkgver=${_majver//-/.}.p${_patchver}
pkgver=${_majver//-/.}
pkgrel=3
pkgdesc="Computer Algebra System for polynomial computations"
arch=(x86_64)
url="http://www.singular.uni-kl.de/"
license=(GPL)
depends=(flint cddlib) # polymake
makedepends=(doxygen python2)
optdepends=('python2: Python interface')
#source=("http://www.mathematik.uni-kl.de/ftp/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}p${_patchver}.tar.gz")
source=("http://www.mathematik.uni-kl.de/ftp/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}.tar.gz"
        singular-log2.patch::https://github.com/Singular/Sources/commit/0803a180.patch)
sha256sums=('3792c5707b60c1748298bf47e2277de20303d60563b797372cc0e1eff4bbc583'
            'd985b80da4f0c20eb014093e74f59be7bcb36b25292b8b6c24df180684418489')
options=(!zipman)

prepare() {
  cd singular-${_majver//-/.}
  
# Rename macro that conflicts with PARI
  patch -p1 -i ../singular-log2.patch
}

build() {
  cd singular-${_majver//-/.}
  ./configure --prefix=/usr --libexecdir=/usr/lib
  make
}

package() {
  cd singular-${_majver//-/.}
  make DESTDIR="$pkgdir" install

  # Fix ownership
  chown -R root:root "$pkgdir"/usr/share/singular/html "$pkgdir"/usr/share/info
}
