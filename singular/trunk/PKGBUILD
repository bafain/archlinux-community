# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=singular
_majver=4-1-1
_patchver=3
pkgver=${_majver//-/.}.p${_patchver}
#pkgver=${_majver//-/.}
pkgrel=4
pkgdesc="Computer Algebra System for polynomial computations"
arch=(x86_64)
url="http://www.singular.uni-kl.de/"
license=(GPL)
depends=(flint cddlib) # polymake
makedepends=(doxygen python2)
optdepends=('python2: Python interface')
source=("http://www.mathematik.uni-kl.de/ftp/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}p${_patchver}.tar.gz"
        singular-gcd-Zx.patch)
#source=("http://www.mathematik.uni-kl.de/ftp/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}.tar.gz")
sha256sums=('551b41060c06878e60119f0283edd09ca7a34d47755fe67d04f5cf9bea4a12e3'
            '7c868ff33501b47d2dc0eaab5e8a988f36bb79f744d902a4964f2d45cf8cefbf')
options=(!zipman)

prepare() {
  cd singular-${_majver//-/.}
  patch -p1 -i ../singular-gcd-Zx.patch # Fix gcd in Z[x]
}

build() {
  cd singular-${_majver//-/.}
  ./configure --prefix=/usr --libexecdir=/usr/lib
  make
}

package() {
  cd singular-${_majver//-/.}
  make DESTDIR="$pkgdir" install

  # Fix ownership
  chown -R root:root "$pkgdir"/usr/share/singular/html "$pkgdir"/usr/share/info
  # Don't enforce c++11, it breaks build of polymake
  sed -e 's/-std=c++11//' -i "$pkgdir"/usr/bin/libsingular-config -i "$pkgdir"/usr/lib/pkgconfig/*.pc
}
