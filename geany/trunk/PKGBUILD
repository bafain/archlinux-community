# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Ionut Biru  <ibiru@archlinux.ro>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>

pkgname=geany
pkgver=1.32.0
pkgrel=2
pkgdesc='Fast and lightweight IDE'
arch=('x86_64')
url='https://www.geany.org/'
license=('GPL')
depends=('gtk3')
makedepends=('intltool')
optdepends=('geany-plugins: additional functionality'
            'vte3: embedded terminal support')
source=("https://download.geany.org/$pkgname-${pkgver/.0}.tar.bz2")
sha256sums=('8b7be10b95d0614eb07f845ba2280f7c026eacd5739d8fac4d5d26606f8c3c2d')

build() {
  cd "$pkgname-${pkgver/.0}"

  ./configure --prefix=/usr --enable-gtk3
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  make -C "$pkgname-${pkgver/.0}" DESTDIR="$pkgdir" install
}

# getver: geany.org
# vim: ts=2 sw=2 et:
