# $Id$
# Contributor: SÃ©bastien Luttringer <seblu@archlinux.org>

pkgname=perl-text-template
pkgver=1.51
pkgrel=1
pkgdesc='Expand template text with embedded perl'
arch=('any')
url='http://search.cpan.org/dist/Text-Template'
license=('PerlArtistic' 'GPL')
depends=('perl')
checkdepends=('perl-test-warnings')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/M/MS/MSCHOUT/Text-Template-$pkgver.tar.gz")
sha512sums=('038a810746684b26cd6a763cfb8adff57a2ed8dc57e44b170b6ba6c8271d513289546294439cfe1c8b77f40153751596a023079ee79bfc6b59fa9393cfcc04f4')

build() {
  cd Text-Template-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Text-Template-$pkgver
  make test
}

package() {
  cd Text-Template-$pkgver
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
