# $Id$
# Contributor: SÃ©bastien Luttringer <seblu@archlinux.org>

pkgname=perl-text-template
pkgver=1.53
pkgrel=2
pkgdesc='Expand template text with embedded perl'
arch=('any')
url='http://search.cpan.org/dist/Text-Template'
license=('PerlArtistic' 'GPL')
depends=('perl')
checkdepends=('perl-test-warnings' 'perl-test-more-utf8')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/M/MS/MSCHOUT/Text-Template-$pkgver.tar.gz")
sha512sums=('136860f16f5100b7a672ae798be1e8d2bd1d6091e63248e9dca3bea42566e8289809d863576c7dcd734865eadb79f8d8f6443457cd14012f486ef14c98732228')

build() {
  cd Text-Template-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Text-Template-$pkgver
  make test
}

package() {
  cd Text-Template-$pkgver
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
