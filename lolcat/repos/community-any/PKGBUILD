# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gordin <9ordin @t gmail dot com>

pkgname=lolcat
pkgver=99.9.11
pkgrel=1
pkgdesc='Okay, no unicorns. But rainbows!!'
url='https://github.com/busyloop/lolcat'
arch=('any')
license=('custom')
depends=('ruby' 'ruby-paint' 'ruby-trollop' 'ruby-manpages')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/busyloop/lolcat/archive/v${pkgver}.tar.gz)
sha256sums=('12d6e9d7f17794fe1ea38742e7838a4c23c83ac05e3d7dd042b4edf81ea5065c')
sha512sums=('6af8738bd26a773e1f00758be9a0359cbd1e67b37f4d180221dc0569f1021cc87b28afb623bbed94c5dd4f60574c3fd008b8dcda19e58a8451a0824bbda98df9')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -r 's|~>|>=|g' -i lolcat.gemspec # don't give a fuck about rubys bla bla
  sed 's|git ls-files -- bin/\*|find bin -type f|' -i lolcat.gemspec
  sed 's|git ls-files -- {test,spec,features}/*|echo|' -i lolcat.gemspec
  sed 's|git ls-files|find -type f|' -i lolcat.gemspec
}

build() {
  cd ${pkgname}-${pkgver}
  gem build lolcat.gemspec
}

package() {
  cd ${pkgname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${pkgname}-${pkgver}.gem
  rm "${pkgdir}/${_gemdir}/cache/${pkgname}-${pkgver}.gem" \
    "${pkgdir}/usr/share/man/man6/lolcat.6"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 "${pkgdir}/${_gemdir}/gems/lolcat-${pkgver}/man/lolcat.6" -t "${pkgdir}/usr/share/man/man6"
}

# vim: ts=2 sw=2 et:
