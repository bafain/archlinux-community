# $Id$
# Maintainer: Dan Printzell <arch@vild.io>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Mihails Strasunse <public@dicebot.lv>

pkgname=dtools
pkgver=2.082.0
pkgrel=1
pkgdesc="Ancilliary tools for the D programming language"
license=('Boost')
groups=('dlang')
arch=('x86_64')
url="http://www.dlang.org"
makedepends=('dmd' 'libphobos')
depends=('curl')
source=("$pkgname-$pkgver.tar.gz::https://github.com/dlang/tools/archive/v$pkgver.tar.gz"
        'http://www.boost.org/LICENSE_1_0.txt'
        'autodetect.diff')
sha512sums=('576cb7a1cc0a0406922e08fa1d395dc59e99344f0e790aab663c349e78279f1a75ddda5ac136149718696e2cfcd96f0d19551be2fbb597e5ad9d518a4e8e1afc'
            'd6078467835dba8932314c1c1e945569a64b065474d7aced27c9a7acc391d52e9f234138ed9f1aa9cd576f25f12f557e0b733c14891d42c16ecdc4a7bd4d60b8'
            '3bce3c66cf24837fcb9bf244f2b07ed6116d8d67a75b2319010cf93956e93b43262b7d9613359503e6b0352b82684be8baeeb7b1f50e77accda334ce470bf5e3')

build() {
    DMD=dmd

    # Add $LDFLAGS and $CXXFLAGS to the compilation
    _linkflags=$(echo -ne $LDFLAGS | cut -d\" -f2 | tail -c+4 | sed "s/,/ -L=/g")

    cd tools-$pkgver

    # rdmd is used to build others
    $DMD -od. -ofdtools-rdmd $_linkflags rdmd.d

    for tool in DustMite/dustmite ddemangle catdoc detab tolf dget; do
        ./dtools-rdmd --compiler=$DMD --build-only -od. -ofdtools-`basename $tool` $_linkflags $tool.d
    done
}

package() {
    cd tools-$pkgver

    mkdir -p $pkgdir/usr/bin
    for tool in rdmd ddemangle catdoc detab tolf dget dustmite; do
        install -m755 -t $pkgdir/usr/bin $pkgname-$tool
    done

    cd $pkgdir
    ln -s dtools-rdmd usr/bin/rdmd
    ln -s dtools-dustmite usr/bin/dustmite
    ln -s dtools-ddemangle usr/bin/ddemangle

    install -Dm644 $srcdir/LICENSE_1_0.txt usr/share/licenses/$pkgname/LICENSE
}
