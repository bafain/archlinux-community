# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=bsdiff
pkgver=4.3
pkgrel=9
pkgdesc="bsdiff and bspatch are tools for building and applying patches to binary files."
url="http://www.daemonology.net/bsdiff/"
license=('BSD')
arch=('x86_64')
depends=('bzip2')
source=("http://www.daemonology.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
	Makefile.patch)
md5sums=('e6d812394f0e0ecc8d5df255aa1db22a'
         'f176d14d39c5784a2c824f6797544f4b')

prepare() {
  cd "$srcdir/${pkgname}-${pkgver}"
  patch -p0 < ../Makefile.patch
}

build() {
  cd "$srcdir/${pkgname}-${pkgver}"
  make
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  make PREFIX="$pkgdir/usr" install
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  head -n 26 bsdiff.c >"$pkgdir/usr/share/licenses/$pkgname/license"
}
