# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=esqueleto
pkgname=haskell-esqueleto
pkgver=2.5.3.20180402
_commit=b81e0d951e510ebffca03c5a58658ad884cc6fbd
pkgrel=66
pkgdesc="Type-safe EDSL for SQL queries on persistent backends."
url="https://github.com/bitemyapp/esqueleto"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-blaze-html' 'haskell-conduit' 'haskell-monad-logger'
         'haskell-persistent' 'haskell-resourcet' 'haskell-tagged' 'haskell-unliftio'
         'haskell-unordered-containers')
makedepends=('ghc')
source=("$pkgname-$_commit.tar.gz::https://github.com/bitemyapp/esqueleto/archive/$_commit.tar.gz")
sha512sums=('27e1c7c3dc8598a4b57fa9c30f9121d4858653e64ad1a92fc3d82c487374fe4cc48246924890812198d58c25a3d3f9110a35c0ef27d9040ea15e4559c1c3355f')

prepare() {
    mv $_hkgname-{$_commit,$pkgver}
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-mysql -f-postgresql
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
