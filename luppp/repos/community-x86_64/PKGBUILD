# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
pkgname=luppp
pkgver=1.2.0
pkgrel=1
pkgdesc="A music creation tool, intended for live use"
arch=('x86_64')
url="http://openavproductions.com/luppp/"
license=('GPL2')
groups=('pro-audio')
depends=('jack' 'liblo' 'libsamplerate' 'ntk')
makedepends=('meson')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openAVproductions/openAV-${pkgname}/archive/release-${pkgver}.tar.gz")
sha512sums=('1ff842aea7404e268168265454af12d7d3f3961d0f85228e70e5177ff623f00e2c197ec3b11af3b3a5c5a4040c94b678bbe35e3bbea9008a36c1b96b11c8070e')

prepare() {
  mv -v "openAV-Luppp-release-${pkgver}" "$pkgname-$pkgver"
}

build() {
  arch-meson "$pkgname-$pkgver" build
  ninja -C build
}

package() {
  install -vDm 755 build/${pkgname} -t ${pkgdir}/usr/bin
  cd "$pkgname-$pkgver"
  #docs
  install -vDm 644 {CHANGELOG,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  # XDG integration
  install -vDm 644 "resources/metadata/${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications/"
  install -vDm 644 "resources/metadata/${pkgname}.appdata.xml" \
    -t "${pkgdir}/usr/share/appdata/"
  install -vDm 644 "resources/icons/${pkgname}.png" \
    -t "${pkgdir}/usr/share/pixmaps/"
  # controllers
  install -vDm 644 "resources/controllers/"*.ctlr \
    -t "${pkgdir}/usr/share/${pkgname}/"
}
