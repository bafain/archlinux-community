# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=comonad
pkgname=haskell-comonad
pkgver=5.0.3
pkgrel=2
pkgdesc="Comonads"
url="https://github.com/ekmett/comonad/"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' "haskell-contravariant" "haskell-distributive" "haskell-semigroups"
         "haskell-tagged" "haskell-transformers-compat")
makedepends=('haskell-cabal-doctest' 'ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('797ac24fc07821f84155a7806be0fc812980ded68c17f4fbbab4cc224dba97f0e6d1364a3f55112cf45e9486c4e19f68f5141e80741c4aeabbbc7c31c8468fde')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fdistributive -fcontravariant -fcontainers -ftest-doctests
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
