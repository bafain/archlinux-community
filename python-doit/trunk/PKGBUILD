# Maintainer: David Runge <dave@sleepmap.de>
_name=doit
pkgname=python-doit
pkgver=0.31.1
pkgrel=2
pkgdesc="A task management and automation tool"
arch=('any')
url="http://pydoit.org/"
license=('MIT')
depends=('python-cloudpickle' 'python-pyinotify' 'python-setuptools')
#checkdepends=('flake8'  'python-coverage' 'python-pytest')
optdepends=('bash-completion: autocomplete for bash'
            'zsh-completions: autocomplete for ZSH')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('31235cb46a73adc2d3e726dce24dd9632f5b1ef00d313bf1c42ff895a03eff41be0ec7ae14a004e742d50003c2da17f29bd3392a49d80e73cfb59c12bd152d9d')

prepare() {
  mv -v "$_name-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

# tests are broken:
# https://github.com/pydoit/doit/issues/269
#check() {
#  cd "$pkgname-$pkgver"
#  export PYTHONPATH=build/lib:${PYTHONPATH}
#  py.test
#}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {AUTHORS,CHANGES,CONTRIBUTING.md,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 "bash_completion_${_name}" \
    "${pkgdir}/usr/share/bash-completion/completions/${_name}"
  install -vDm 644 "zsh_completion_${_name}" \
    "${pkgdir}/usr/share/zsh/site-functions/_${_name}"
}
