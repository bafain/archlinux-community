# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=tasty-hedgehog
pkgname=haskell-tasty-hedgehog
pkgver=0.2.0.0
pkgrel=43
pkgdesc="Integration for tasty and hedgehog"
url="https://github.com/qfpl/tasty-hedgehog"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-hedgehog' 'haskell-tagged' 'haskell-tasty')
makedepends=('ghc' 'haskell-tasty-expected-failure')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('ff6eb79e8a1a0d3307b6888fd19fa243591120af3c04ce4318e59846b9165847ebf3162cfca1f80ffbbe532a130235d0a413949c7b2ed0b45838716ded33673b')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e 's/<.*4.11/<5/' -e 's/<.*0.6/<1/' -e 's/<.*1.1/<2/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENCE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENCE"
}
