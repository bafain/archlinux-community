# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=tasty-hedgehog
pkgname=haskell-tasty-hedgehog
pkgver=0.1.0.1
pkgrel=3
pkgdesc="Integrates the hedgehog testing library with the tasty testing framework"
url="https://github.com/qfpl/tasty-hedgehog"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-hedgehog' 'haskell-tagged' 'haskell-tasty')
makedepends=('ghc' 'haskell-tasty-expected-failure')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('987602120f6ba80f0aff7e2b0d8dc32514859066dc08670f3e30a1d00c052002eb627f83b4926b88cdd5f90e428351729b7d1beaf5c7fdd9edd3699923b10cc0')

prepare() {
    sed -i 's/, tasty >= 0.11 && < 0.12/, tasty >= 0.11/' $_hkgname-$pkgver/$_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
