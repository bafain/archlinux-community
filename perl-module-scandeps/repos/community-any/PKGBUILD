# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: xRemaLx <anton.komolov@gmail.com>

pkgname=perl-module-scandeps
_pkgname=Module-ScanDeps
pkgver=1.25
pkgrel=1
pkgdesc="Scan file prerequisites"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
makedepends=('perl-test-requires')
url="http://search.cpan.org/dist/Module-ScanDeps"
source=("http://search.cpan.org/CPAN/authors/id/R/RS/RSCHUPP/Module-ScanDeps-$pkgver.tar.gz")
sha512sums=('1d1d10b55147e68238465fc1e5e9248d926151394034f0bde32c0030ff9bbd685ebb1ed0c6c924769a2e38c2dc15621990b454438db5ef5699aeed378f02aec7')

build() {
  cd Module-ScanDeps-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Module-ScanDeps-$pkgver
  make test
}

package() {
  cd Module-ScanDeps-$pkgver
  make DESTDIR="$pkgdir" install
}
