# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Daniel M. Capella <polyzen@archlinux.info>
# Contributor: Sirat18 <aur@sirat18.de>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

_name=icalendar
pkgbase=python-icalendar
pkgname=('python-icalendar' 'python2-icalendar')
pkgver=4.0.2
pkgrel=2
pkgdesc='A parser/generator of iCalendar files (RFC 2445)'
arch=('any')
url="https://github.com/collective/icalendar"
license=('custom:BSD')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-dateutil' 'python-pytz' 'python2-dateutil' 'python2-pytz')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('6dc2d03209fcdd02b095c305e9cd2a1fe22762b368c459d8fcb6cd5d7657673195dcee3014b3b8f521e0a411f96a5643014bbf7b20065c54146332ce130ba682')

prepare() {
  # Disable failing test: https://github.com/collective/icalendar/issues/260
  sed -e 's/test_create_america_new_york/disabled_&/' \
      -i "${_name}-${pkgver}"/src/icalendar/tests/test_timezoned.py
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${pkgname[0]}-${pkgver}"
  python -m unittest discover src
  cd ../"${pkgname[1]}-${pkgver}"
  python2 -m unittest discover src
}

package_python-icalendar() {
  depends=('python-dateutil' 'python-pytz')
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {CHANGES,CONTRIBUTING,README}.rst
  install -vDm 644 LICENSE.rst \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-icalendar() {
  depends=('python2-dateutil' 'python2-pytz')
  cd "${pkgname}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  mv -v "${pkgdir}/usr/bin/${_name}" "${pkgdir}/usr/bin/${_name}2"
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {CHANGES,CONTRIBUTING,README}.rst
  install -vDm 644 LICENSE.rst \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:

