# $Id$

_pkgbasename=libvorbis
pkgname=lib32-$_pkgbasename
pkgver=1.3.6
pkgrel=1
pkgdesc="Vorbis codec library (32-bit)"
arch=('x86_64')
license=('BSD')
url="https://www.xiph.org/ogg/vorbis/"
depends=('lib32-libogg' $_pkgbasename)
provides=('libvorbis.so' 'libvorbisenc.so' 'libvorbisfile.so')
source=(https://downloads.xiph.org/releases/vorbis/${_pkgbasename}-${pkgver}.tar.gz)
sha256sums=('6ed40e0241089a42c48604dc00e362beee00036af2d8b3f46338031c9e0351cb')

build() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --disable-static --libdir=/usr/lib32
  make
}

check() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  make -j1 check
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  install -d "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
