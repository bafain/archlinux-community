# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Tobias Bohrmann (PlainTextField) <honeypot.carbage@aechelon.de>
# Contributor: Bernd Prünster (JesusMcCloud) <aur@berndpruenster.org>

pkgname=gnome-latex
pkgver=3.30.2
pkgrel=1
pkgdesc="LaTeX editor for the GNOME desktop"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/GNOME-LaTeX"
license=(GPL3)
depends=(gsettings-desktop-schemas gspell libgee tepl)
makedepends=(autoconf-archive appstream-glib git gobject-introspection gtk-doc intltool vala yelp-tools)
optdepends=('texlive-core: build LaTeX documents')
replaces=(latexila)
_commit=6197e6a26028178e13afc77eaa6f9f988b4b22b5  # tags/3.30.2^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-latex.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-gtk-doc --disable-Werror
  make
}

package() {
	cd $pkgname
	make DESTDIR="$pkgdir" install
}
