# Maintainer: David Runge <dave@sleepmap.de>
# Contributors: Dave Reisner <dreisner@archlinux.org>
#               Matthias Blaicher <matthias@blaicher.com>
#               Severen Redwood <severen@shrike.me>

pkgname=capnproto
pkgver=0.7.0
pkgrel=2
pkgdesc="Cap'n Proto serialization/RPC system"
arch=('x86_64')
url='https://capnproto.org/'
license=('MIT')
depends=('openssl')
provides=('capnproto')
source=("https://capnproto.org/${pkgname}-c++-${pkgver}.tar.gz")
sha512sums=('9f8fb5753155798fcf9377a87f984a54d9fc5157c41aa11cd94108a773ca22d6e6952657e2d8079c9806f7de06f316c94957329fa52dbab6207aaa3b52348f04')

build() {
  cd "${pkgname}-c++-${pkgver}"
  export CXXFLAGS+=" -std=c++14"
  ./configure --prefix='/usr'
  make
}

package() {
  cd "${pkgname}-c++-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -vDm 644 LICENSE.txt \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
