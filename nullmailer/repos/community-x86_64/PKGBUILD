# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor : Martin Wimpress <code@flexion.org>
# Contributor: Roberto Alsina <ralsina at kde.org>
# Contributor: Kurt J. Bosch <kjb-temp-2009 at alpenjodel.de>

pkgname=nullmailer
pkgver=2.1
pkgrel=1
pkgdesc='Simple relay-only mail transport agent'
url='https://www.untroubled.org/nullmailer/'
arch=('x86_64')
license=('GPL2')
depends=('gnutls')
provides=('smtp-forwarder')
conflicts=('smtp-forwarder' 'smtp-server')
options=('emptydirs')
source=(https://www.untroubled.org/nullmailer/archive/${pkgname}-${pkgver}.tar.gz
        nullmailer.sysusers
        nullmailer.tmpfiles)
sha512sums=('5219e251f6648258247913fcb7036075a482b0800a98cde0d731aa4a6b514baae4d8a340b6757b8d532c4b8343b718642c0f519292e3660f8c04671ac274944b'
            '004a14e65b7c45264033d1a2fae1cedad056ebde4597cbe711600793c4792715554e94eeee701a3f9618524debd1d8a74f15e297eb5070a45b2195b0b9fd722f'
            'fe05593fac0a5bb0752f2103d8844485d8c39f3a4aed2ab29dc57c1dda0aba1a770e5bf7e6d959db80831f021b0a07c029b844bc79850396b0ea2344af7bd81c')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
      --prefix=/usr \
      --libexecdir=/usr/lib/${pkgname} \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --sbindir=/usr/bin \
      --enable-tls
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm 644 scripts/nullmailer.service -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 ../nullmailer.sysusers "${pkgdir}/usr/lib/sysusers.d/nullmailer.conf"
  install -Dm 644 ../nullmailer.tmpfiles "${pkgdir}/usr/lib/tmpfiles.d/nullmailer.conf"

  # covered by tmpfiles with fixed ownership
  rm -rf "${pkgdir}/var"
}

# vim: ts=2 sw=2 et:
