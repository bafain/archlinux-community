# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Roberto Alsina <ralsina at kde.org>
# Contributor: Kurt J. Bosch <kjb-temp-2009 at alpenjodel.de>

pkgname=nullmailer
pkgver=2.0
pkgrel=3
pkgdesc="Simple relay-only mail transport agent."
arch=('x86_64')
url="https://www.untroubled.org/nullmailer/"
license=("GPL")
provides=('smtp-forwarder')
conflicts=('smtp-forwarder' 'smtp-server')
depends=('gnutls')
options=(emptydirs)
install=nullmailer.install
source=("https://www.untroubled.org/nullmailer/archive/${pkgname}-${pkgver}.tar.gz"
        "nullmailer.sysusers"
        "nullmailer.tmpfiles")
md5sums=('e35e8248056fd21a2e464d34116c44a2'
         'fa9c9f9194d4ffbe0c6cd9e325f0f34a'
         '52e15c34a0a2187688da1f4d1a9cc784')

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --enable-tls
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install

    # covered by tmpfiles with fixed ownership
    rm -rf "${pkgdir}"/var

    install -Dm644 scripts/nullmailer.service "${pkgdir}"/usr/lib/systemd/system/nullmailer.service
    install -Dm644 "${srcdir}"/nullmailer.sysusers "${pkgdir}"/usr/lib/sysusers.d/nullmailer.conf
    install -Dm644 "${srcdir}"/nullmailer.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/nullmailer.conf
}
