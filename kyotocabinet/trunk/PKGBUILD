# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Alexander Duscheleit <jinks@archlinux.us>
# Contributor: Joaquim Pedro (osmano807) <osmano807@gmail.com>

pkgname=kyotocabinet
pkgver=1.2.76
pkgrel=6
pkgdesc="a modern implementation of DBM in C++"
arch=('x86_64')
url="http://fallabs.com/kyotocabinet"
license=('LGPL3')
depends=('zlib' 'gcc-libs')
source=("http://fallabs.com/${pkgname}/pkg/${pkgname}-${pkgver}.tar.gz" kyotocabinet-gcc6.patch)

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../kyotocabinet-gcc6.patch # Fix build with GCC 6 (Debian)
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --disable-opt
  make
}

package() {
  cd $pkgname-$pkgver

  make install DESTDIR="$pkgdir/"
}

sha512sums=('278db7b327eb4c21bf0137d9aa14fb67d74d5ce7ed1cb29fc9120d157a60de165ec0cf842903eb7952e8f998045ae585b958977fa973ba0e0773381de71d9f6a'
            'cd618524e30f7445f32bf33b3cab60f206cb5323898deda2b8ba07a593429f9e3b36f98507792b8fa664eccd581c9e321c52e7b38e878a5e390e41e70edfb1d1')
