# Maintainer: David Runge <dave@sleepmap.de>
pkgname=fomp.lv2
pkgver=1.0.0
pkgrel=3
pkgdesc="An LV2 port of the MCP, VCO, FIL, and WAH plugins by Fons Adriaensen."
arch=('x86_64')
url="https://git.drobilla.net/cgit.cgi/fomp.lv2.git/about/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('gcc-libs')
makedepends=('lv2' 'python2')
source=("https://git.drobilla.net/cgit.cgi/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('3b4a89ac676f475a67c9f4c21433f20e179c7f411e31bf9ce2a8a7fc65055659e28752803eeae9f4e8454ccd83177f40c2a363281db484289c0b9cf7d31b3835')

build() {
  cd "$pkgname-$pkgver"
  python2 waf configure --prefix=/usr
  python2 waf
}

package() {
  cd "$pkgname-$pkgver"
  python2 waf install --destdir="$pkgdir/"
  install -vDm 644 {AUTHORS,NEWS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
