# $Id$
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: ConnorBehan <connor.behan@gmail.com>

pkgname=sk1
pkgver=2.0rc2
pkgrel=2
epoch=1
pkgdesc="Professional quality illustration program"
arch=(x86_64)
url="https://sk1project.net/"
depends=('lcms2' 'libmagick' 'python2-cairo' 'python2-pillow' 'python2-pycups' 'python2-reportlab' 'wxpython')
license=('GPL3')
source=("https://downloads.sk1project.net/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz"
        "imagemagick7.patch")
sha256sums=('ee1f03e6705e2063d0a1f032e475b24868c68fc26410fb1f2a536d3541b88a67'
            'd70fe3ffe32fa0c8ec8bee587b56e5cb08a0981a4bbbe92964bb057b83885c5b')

prepare() {
  cd $pkgname-$pkgver
  # Port to ImageMagick 7
  patch -Np1 -i ../imagemagick7.patch
}

build() {
  cd $pkgname-$pkgver
  python2 setup-sk1.py build
}

package() {
  cd $pkgname-$pkgver
  python2 setup-sk1.py install --root="$pkgdir" --optimize=1
}
