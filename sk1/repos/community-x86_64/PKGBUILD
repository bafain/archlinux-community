# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: ConnorBehan <connor.behan@gmail.com>

pkgname=sk1
pkgver=2.0rc3
pkgrel=2
epoch=1
pkgdesc="Professional quality illustration program"
arch=(x86_64)
url="https://sk1project.net/"
depends=('lcms2' 'libmagick' 'python2-cairo' 'python2-pillow' 'python2-pycups' 'python2-reportlab' 'python2-wxpython3')
license=('GPL3')
source=("https://downloads.sk1project.net/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f3cdba5c764ddc1d4f6052ebce734c77c4c9dff9b5af702f37c8875b57440493ccfffb1ed100c35eda3c16eb5011997d0e42263713f5d1ec6cce32dcfafe4083')

build() {
  cd $pkgname-$pkgver
  python2 setup-sk1.py build
}

package() {
  cd $pkgname-$pkgver
  python2 setup-sk1.py install --root="$pkgdir" --optimize=1
  find "$pkgdir"/usr/{lib,share} -type f | xargs chmod 644
}
