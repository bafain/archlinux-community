# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=async
pkgname=ruby-${_gemname}
pkgver=1.3.0
pkgrel=1
pkgdesc='Composable asynchronous I/O'
url='https://github.com/socketry/async'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-nio4r' 'ruby-timers' 'ruby-rdoc')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/socketry/async/archive/v${pkgver}.tar.gz)
sha256sums=('d3553fb30b341268ff2f3abbd84e75c4df7845e1d1b5da189ac43ce37786d4f0')
sha512sums=('392f1651c2e38e4cc4c5cd8b7378ccec1567332396ac4dc1f26ca512abda5dd52c631804925564ad625f95d57d03a5ffa6211ab48b0d1eb8aafb3760fe423165')

prepare() {
  cd ${_gemname}-${pkgver}
  sed 's|git ls-files|find|' -i ${_gemname}.gemspec
  sed 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm "${pkgdir}"/${_gemdir}/cache/${_gemname}-${pkgver}.gem
}

# vim: ts=2 sw=2 et:
