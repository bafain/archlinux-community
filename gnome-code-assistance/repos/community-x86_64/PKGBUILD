# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-code-assistance
pkgver=3.16.1+12+g5418ece
pkgrel=1
pkgdesc="Code assistance services for GNOME"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/CodeAssistance"
license=(GPL3)
depends=(libgee python-dbus python-gobject python-pylint python-pyflakes python-pycodestyle
         python-lxml python-simplejson ruby-dbus ruby-sass)
makedepends=(intltool gobject-introspection llvm clang gjs vala go-pie gnome-common git)
optdepends=('clang: Assistance for C and C++'
            'gjs: Assistance for JavaScript'
            'vala: Assistance for Vala'
            'go: Assistance for Go')
groups=(gnome-extra)
_commit=5418ece7e268d6a66cad3e9da0659eb26203febc  # master
source=("git+https://git.gnome.org/browse/gnome-code-assistance#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib
  make
}

package(){
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
