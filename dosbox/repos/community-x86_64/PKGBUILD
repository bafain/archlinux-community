# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: James Rayner <james@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=dosbox
pkgver=0.74
pkgrel=8
pkgdesc='Emulator with builtin DOS for running DOS Games'
arch=('x86_64')
url='http://dosbox.sourceforge.net/'
license=('GPL')
depends=('sdl_net' 'zlib' 'sdl_sound' 'libgl' 'libpng' 'alsa-lib' 'gcc-libs' 'glu')
makedepends=('mesa-libgl' 'gendesk')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        dosbox.png
        gcc46.patch)
sha512sums=('4a6ff4c658997a495119d200ec6ad37649da940814a4b14fca0dd29a99142026e324695b7aa9d2946efc2abf9067a819d911e43778efe905ed10ddf9b9f2dd3d'
            'e6f49aadb09fc575442f292d9b318f77f087bc5e39fa8937713b79945233052d954920080678828cdec42065101735057008bf862ab3cace90f8257941aed2ce'
            '94410caf7dbc8a6217eab19cf1df28919ff7f0b9786a20dec9205bc77d826ff0ea68ccaa38ea3319b91cc8da71cbcf3f012cd0e119655615239ee42abd64c6cb')

prepare() {
  cd "$srcdir"

  gendesk --pkgname "$pkgname" --pkgdesc "$pkgdesc"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i "$srcdir/gcc46.patch"
  sed -i 's/png_check_sig/png_sig_cmp/' configure 

  ./configure   --prefix=/usr --sysconfdir=/etc/dosbox  
  make 
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install 

# install docs, make does not install them
  install -Dm644 README "$pkgdir"/usr/share/doc/$pkgname/README
  install -Dm644 docs/README.video "$pkgdir"/usr/share/doc/$pkgname/README.video

  install -Dm644 "$srcdir/$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
