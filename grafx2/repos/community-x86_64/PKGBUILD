# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Simon Parzer <simon.parzer@gmail.com>

pkgname=grafx2
pkgver=2.5
pkgrel=1
pkgdesc='Pixelart-oriented painting program'
arch=('x86_64')
url='https://gitlab.com/GrafX2/grafx2'
license=('GPL2')
depends=('lua' 'fontconfig' 'sdl_image' 'sdl_ttf')
makedepends=('gendesk' 'git')
options=('!emptydirs')
source=("git+https://gitlab.com/GrafX2/grafX2.git#tag=v$pkgver"
        'https://sourceforge.net/projects/recoil/files/recoil/4.2.0/recoil-4.2.0.tar.gz')
sha256sums=('SKIP'
            '4e8c8e3048b143654da49cc1187a0b26679c9f97f841edc0ba9e0b2e2e7f18e5')

prepare() {
  gendesk -f -n \
    --pkgname "$pkgname" \
    --pkgdesc "$pkgdesc" \
    --name 'GrafX2' \
    --genericname 'Drawing program' --exec 'grafx2 %f' \
    --mimetypes 'image/bmp;image/gif;image/png;image/x-tga;image/tiff' \
    --categories 'Graphics;2DGraphics;RasterGraphics'

  mv grafX2 "$pkgname"
  sed 's/-liconv//g' -i "$pkgname/src/Makefile"
  mkdir -p grafx2/3rdparty/archives
  cp -u recoil-4.2.0.tar.gz grafx2/3rdparty/archives/recoil-4.2.0.tar.gz
}

build() {
  CFLAGS="$CFLAGS -w" make -s -C "$pkgname/src" PREFIX=/usr
}

package() {
  install -d "$pkgdir/usr/share/grafx2/scripts/samples_2.4/picture/"{thomson/lib,others-8bit/lib}
  CFLAGS="$CFLAGS -w" make -s -C "$pkgname/src" prefix=/usr DESTDIR="$pkgdir" install
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$pkgname/doc/README.txt" \
    "$pkgdir/usr/share/doc/$pkgname/readme.txt"
}

# vim: ts=2 sw=2 et:
