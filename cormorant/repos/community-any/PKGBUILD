# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgbase=cormorant
pkgname=("otf-${pkgbase}" "ttf-${pkgbase}")
pkgver=3.504
pkgrel=1
pkgdesc="Open-source display font family"
arch=('any')
url="https://github.com/CatharsisFonts/Cormorant"
license=('custom:OFL')
depends=('fontconfig')
source=(${pkgbase}-${pkgver}.tar.gz::"https://github.com/CatharsisFonts/Cormorant/archive/v${pkgver}.tar.gz")
sha256sums=('9477119b6274cd7e8a119eaabe04d5644f4c17abca58c6904992fd1036dbc2c8')

package_otf-cormorant() {
    cd ${pkgbase^}-${pkgver}
    install -Dm644 "2. OpenType Files/"*.otf -t "${pkgdir}"/usr/share/fonts/OTF/
    install -Dm644 OFL.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

package_ttf-cormorant() {
    cd ${pkgbase^}-${pkgver}
    install -Dm644 "1. TrueType Font Files/"*.ttf -t "${pkgdir}"/usr/share/fonts/TTF/
    install -Dm644 OFL.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
