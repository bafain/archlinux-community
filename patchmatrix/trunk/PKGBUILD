# Maintainer: David Runge <dave@sleepmap.de>
pkgname=patchmatrix
pkgver=0.12.0
pkgrel=4
pkgdesc="A JACK patchbay in flow matrix style"
arch=('x86_64')
url="https://git.open-music-kontrollers.ch/lad/patchmatrix/about/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('hicolor-icon-theme' 'jack' 'libgl')
makedepends=('cmake' 'lv2')
source=("https://git.open-music-kontrollers.ch/lad/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz")
sha512sums=('be8c1559aa5c3344a134af974290d5aec0b98e0aa4acb6d7fcfb1c6ee0143cd6863bc094f4037375af0db27bc5a2fb39aa3087ecd14d06143a4fc3ba4bebff20')

prepare() {
  cd "$pkgname-$pkgver"
  mkdir -v build
}

build() {
  cd "$pkgname-$pkgver/build"
  cmake -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        ..
  make
}

package() {
  cd "$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir/" install
  # docs
  install -vDm 644 ../README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
