# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>

pkgname=qjackctl
pkgver=0.5.3
pkgrel=1
pkgdesc="A Qt front-end for the JACK low-latency audio server"
url="https://qjackctl.sourceforge.io"
arch=('x86_64')
license=('GPL')
groups=('pro-audio')
depends=('jack' 'qt5-x11extras' 'hicolor-icon-theme')
optdepends=('portaudio: Portaudio support')
makedepends=('qt5-tools' 'portaudio')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('a57cbf34a3f20a5d5c9078fa59e877d8d2b9b7010afbc79263d36491fbdb288c62ebe9f3a4825fc5abb8d96d9a7a4d3f974723764589df30eb455f4ca4cda32f')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO,TRANSLATORS}
}

# vim:set ts=2 sw=2 et:
