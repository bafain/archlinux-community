# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: earnestly

pkgname=llpp
pkgver=28
# Often breaks with mupdf, which means I'll occasionally need to pin a specific commit.
_commit=6face0c21f4d1ac5d7d4ffd33fefc5ce924d4fc8
pkgrel=1
pkgdesc='opengl accelerated pdf viewer based on mupdf'
arch=('x86_64')
url="https://github.com/moosotc/${pkgname}"
license=('GPL3')
depends=('glu' 'openjpeg2' 'jbig2dec' 'libjpeg-turbo' 'freetype2' 'desktop-file-utils')
optdepends=('djvulibre: llppac djvu conversion'
            'ghostscript: llppac ps, dvi, and djvu conversion'
            'unoconv: llppac office conversion'
            'librsvg: llppac svg conversion'
            'imagemagick: llppac image conversion'
            'inotify-tools: for the llpp.inotify autoreload wrapper')
makedepends=('git' 'asciidoc' 'ocaml' 'ocaml-lablgl' 'libmupdf' 'camlp4')

# llpp seems to go through build systems like fasion, holomorph has provided a
# very nice Makefile for building this software which has served well for
# months.  It was contributed, or offered, to the author malc, but he concluded
# that make was bad and needed constant effort to prevent "bitrot" as
# justification for using a terrible shell script that downloaded arbitrary
# commits of bundled libraries.  Now he's on to shake, a haskell NIH
# implementation of make requiring that you now also have ghc and the shake
# library to build this software.  Before this he used ninja.

# For obvious reasons I'm sticking with the custom makefile which greatly
# reduces maintainence burden and unecessary dependency bloat.
source=("git+${url}#commit=${_commit}"
        'no-quit-on-escape.patch'
        "${url}/pull/85/commits/6e40f7315cb620fef1d96bbe56cfe24dd91e182a.patch"
        'Makefile')
sha256sums=('SKIP'
            '8bba7d57e65679c36f0dde27e38ded5e16f71d3c196d47573faa2c06533b1a73'
            'e6fa1df1e9451d4ffecc416676ad4697da21fc8809e20c78502b97805e4f4321'
            '722b61229adde7e68de375bb713313759be3845715ca12a8fcbe60fe0d79ac45')

pkgver() {
    cd "${srcdir}"/${pkgname}

    git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${srcdir}"/${pkgname}

    # weird tag breaks versioning logic
    git tag -d llipposuction

    # mupdf too new
    git show c4a533126fa20f42088e2d99f416ce3d667f2d13 -- :./link.c | git apply --reverse --index
    # Don't use CPPFLAGS to insert '#include <inttypes.h>'
    patch -Np1 -i "${srcdir}"/6e40f7315cb620fef1d96bbe56cfe24dd91e182a.patch

    patch -Np1 -i "${srcdir}"/no-quit-on-escape.patch
    # apparently the desktop file went "stale"
    git revert --no-commit aad4b1e65e581ff7a096a3c3901b222a9c127a1c
}

build() {
    cd "${srcdir}"/${pkgname}

    make -f "${srcdir}"/Makefile
}

package() {
    cd "${srcdir}"/${pkgname}

    make -f "${srcdir}"/Makefile PREFIX=/usr DESTDIR="${pkgdir}" install
}
