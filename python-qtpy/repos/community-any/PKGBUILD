# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgbase='python-qtpy'
_pkgbase='qtpy'
pkgname=('python-qtpy' 'python2-qtpy')
pkgver=1.4.2
pkgrel=3
pkgdesc='Provides an uniform layer to support PyQt5, PyQt4 and PySide with a single codebase'
makedepends=('python' 'python-setuptools'
             'python2' 'python2-setuptools')
arch=('any')
url="https://github.com/spyder-ide/${_pkgbase}/"
license=('MIT')
source=("${pkgbase}-${pkgver}.tar.gz"::"https://github.com/spyder-ide/${_pkgbase}/archive/v${pkgver}.tar.gz")
sha256sums=('c59690cc70c9c9382ab1d182dc03f0ae77ba4568eab58949a7e2ecd57185e4b1')

package_python2-qtpy() {
  depends=('python2')
  optdepends=('python2-pyqt5: Qt5 Python bindings'
              'python2-pyside2: PySide Qt5 Python bindings')

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python2 setup.py build
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python-qtpy() {
  depends=('python')
  optdepends=('python-pyqt5: Qt5 Python bindings'
              'python-pyside2: PySide Qt5 Python bindings')

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python3 setup.py build
  python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
