# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: M Rawash <mrawash@gmail.com>
# Contributor: János Illés <ijanos@gmail.com>

pkgname=vim-fugitive
pkgver=2.3
pkgrel=1
pkgdesc='Git wrapper so awesome, it should be illegal'
url='https://github.com/tpope/vim-fugitive'
arch=('any')
license=('custom:vim')
depends=('vim' 'git')
groups=('vim-plugins')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/tpope/${pkgname}/archive/v${pkgver}.tar.gz
        license.txt)
sha512sums=('8a176705c0326ac034abaf23423e58d490feb7f11dc45ad23641fafb95279094ed38475a90e7c2e7fc95c615558db75f3587740e2658f3988fa07a1957aa1039'
            'a50e91b1896b0d952008ba2f641a87af2d1a01e4f280f6c914edcd51ae5d1586d5ade71c3609866b501569007bcb7f2494f08280afec170884b90fab36332fac')

package() {
  cd ${pkgname}-${pkgver}
  local installpath="${pkgdir}/usr/share/vim/vimfiles"
  install -Dm 644 doc/* -t "${installpath}/doc"
  install -Dm 644 plugin/* -t "${installpath}/plugin"
  install -Dm 644 ../license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
