# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: M Rawash <mrawash@gmail.com>
# Contributor: János Illés <ijanos@gmail.com>

pkgname=vim-fugitive
pkgver=2.2.r597.b82abd5
pkgrel=1
_gitcommit=b82abd5bd583cfb90be63ae12adc36a84577bd45
pkgdesc='Git wrapper so awesome, it should be illegal'
url='https://github.com/tpope/vim-fugitive'
arch=('any')
license=('custom:vim')
depends=('vim')
groups=('vim-plugins')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/tpope/${pkgname}/archive/${_gitcommit}.tar.gz
        license.txt)
sha512sums=('70852d5b737f262f191d73cc49d1224bd1bcd7f49dadb12082f2ab41e857fb1bc2890849abe4913725b32cceb87decbfe8e52a66d59461acdc28918f89f7e1f0'
            'a50e91b1896b0d952008ba2f641a87af2d1a01e4f280f6c914edcd51ae5d1586d5ade71c3609866b501569007bcb7f2494f08280afec170884b90fab36332fac')

package() {
  cd ${pkgname}-${_gitcommit}
  local installpath="${pkgdir}/usr/share/vim/vimfiles"
  install -Dm 644 doc/* -t "${installpath}/doc"
  install -Dm 644 plugin/* -t "${installpath}/plugin"
  install -Dm 644 ../license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.markdown -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
