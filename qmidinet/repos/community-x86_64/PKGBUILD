# Maintainer: David Runge <dave@sleepmap.de>
pkgname=qmidinet
pkgver=0.5.0
pkgrel=1
pkgdesc="A MIDI Network Gateway via UDP/IP Multicast"
arch=('x86_64')
url="http://qmidinet.sourceforge.net/"
license=('GPL2')
groups=('pro-audio')
depends=('alsa-lib' 'hicolor-icon-theme' 'jack' 'qt5-base')
makedepends=('qt5-tools')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('aadf827dfc98eaebeeadbefe1811e35b26b52d12bd7e90aab6dedef9bf9dd3e65807a98499d5a4962e9f1c28102029013a86742129546fb85c94b4c3dfb926fa')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {AUTHORS,ChangeLog,README,TODO}
}
