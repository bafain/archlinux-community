# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=xjadeo
pkgver=0.8.8
pkgrel=1
pkgdesc="A simple video player that is synchronized to jack transport."
arch=('x86_64')
url="http://xjadeo.sourceforge.net"
license=('GPL2')
depends=('ffmpeg' 'imlib2' 'jack' 'liblo' 'libltc' 'libxpm' 'portmidi')
makedepends=('glu')
backup=('etc/xjadeorc')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.desktop")
sha512sums=('b7e5d4f4f14d1370419e9dadd7c2a7fd6ff7879a86ecca3842e89d22ab36760f0dadb2e3bd6390403ca31061fcade8ccfe8647853cfc620dc3d02353005a54dc'
            '50239162b461feeeb4d9d0d7d947d176d182e279a772977fc7300877565ed6b49244033ab63606d32f6b01f2a38ceecb29ab6da537288974627111306e2dc718')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --sysconfdir=/etc
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  # rc file
  install -vDm 644 doc/xjadeorc "${pkgdir}/etc/xjadeorc"

  # desktop file
  install -vDm 644 "../${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # icon
  install -vDm 644 "src/${pkgname}/icons/xjadeoH128.png" \
    "$pkgdir/usr/share/pixmaps/${pkgname}.png"
  install -vDm 644 {AUTHORS,ChangeLog,NEWS} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
