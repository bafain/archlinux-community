# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: rudy.matela

pkgbase=z3
pkgname=('z3' 'python-z3' 'python2-z3')
pkgver=4.7.1
pkgrel=2
pkgdesc='High-performance theorem prover'
url='https://github.com/Z3Prover/z3'
arch=('x86_64')
license=('MIT')
 # 'java-environment-common' 'mono' 'ocaml' 'ocaml-findlib'
makedepends=('cmake' 'python2' 'python2-setuptools' 'python' 'python-setuptools' 'gcc-libs' 'gmp')
optdepends=('python2-setuptools: bindings for python2')
source=(https://github.com/Z3Prover/z3/archive/${pkgbase}-${pkgver}.tar.gz)
sha512sums=('4c8c856691134298c4b5e465d6fadfe446532dfcd8c92aed7c9a6bbfc8557074a45cd7316dbfa5045824e2504db159afeb8ff676d4bfc942496623cf31d11aa0')

prepare() {
  cd ${pkgbase}-${pkgbase}-${pkgver}
  mkdir -p build
}

build() {
  cd ${pkgbase}-${pkgbase}-${pkgver}/build
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_PYTHON_BINDINGS=1 \
    -DPYTHON_EXECUTABLE=/usr/bin/python \
    -DUSE_LIB_GMP=1
  make
}

package_z3() {
  depends=('gcc-libs' 'gmp')
  provides=('libz3' 'libz3.so')
  cd ${pkgbase}-${pkgbase}-${pkgver}/build
  make DESTDIR="${pkgdir}" install
  install -Dm 644 ../LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm -r "${pkgdir}"/usr/lib/python*
}

package_python-z3() {
  depends=('python' 'libz3')
  cd ${pkgbase}-${pkgbase}-${pkgver}/build
  make DESTDIR="${pkgdir}" install
  install -Dm 644 ../LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  cd "${pkgdir}"
  rm -r usr/include usr/bin usr/lib/cmake usr/lib/lib*
  python -m compileall .
  python -O -m compileall .
}

package_python2-z3() {
  depends=('python2' 'libz3')
  cd ${pkgbase}-${pkgbase}-${pkgver}/build
  make DESTDIR="${pkgdir}" install
  install -Dm 644 ../LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  cd "${pkgdir}"
  rm -r usr/include usr/bin usr/lib/cmake usr/lib/lib*
  mv usr/lib/python* usr/lib/python2.7
  python2 -m compileall .
  python2 -O -m compileall .
}

# vim: ts=2 sw=2 et:
