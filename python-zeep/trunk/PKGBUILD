# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zeep
pkgver=3.1.0
pkgrel=2
pkgdesc="A fast and modern Python SOAP client"
arch=('any')
license=('MIT')
url="http://docs.python-zeep.org"
depends=('python-appdirs' 'python-attrs' 'python-cached-property' 'python-defusedxml'
         'python-isodate' 'python-lxml' 'python-requests' 'python-requests-toolbelt' 'python-six'
         'python-pytz')
checkdepends=('python-pytest-runner' 'python-freezegun' 'python-mock' 'python-pretend'
              'python-pytest-cov' 'python-requests-mock' 'python-pytest-tornado'
              'python-aioresponses' 'python-xmlsec')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mvantellingen/python-zeep/archive/$pkgver.tar.gz")
sha512sums=('b431428c74b784e738ca1fc22226a047c383be1d70beb1e35472a68b5c5f76c12301cd3785124adcc8801c8f6d4dc3e81f321bc42092a9e0dd4c2d325e5867ff')

prepare() {
  sed -i -e 's/==/>=/' -e '/flake8/d' -e '/isort/d' python-zeep-$pkgver/setup.py
}

build() {
  cd python-zeep-$pkgver
  python setup.py build
} 

check() {
  cd python-zeep-$pkgver
  python setup.py pytest || warning "https://github.com/mvantellingen/python-zeep/issues/724"
}

package() {
  cd python-zeep-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
