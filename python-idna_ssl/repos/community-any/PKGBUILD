# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgname=python-idna_ssl
pkgver=1.0.0
pkgrel=2
pkgdesc="Patch ssl.match_hostname for Unicode(idna_ssl) domains support"
arch=('any')
license=('MIT')
url="https://github.com/aio-libs/idna_ssl"
depends=('python' 'python-idna')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.io/packages/source/i/idna_ssl/idna_ssl-$pkgver.tar.gz")
sha512sums=('d1f7479cc3edff164bb26deb05af757906ba91241bc668f1b087560087a89f741a0b36e166bfb8142128ceed6008621ab36a2556ed2304ea6374c19982bc663a')

prepare() {
  sed -i "s/aiohttp.*'/aiohttp==2.3.10'/" idna_ssl-$pkgver/setup.py
}

build() {
  cd "$srcdir"/idna_ssl-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/idna_ssl-$pkgver
  python setup.py test
}

package() {
  cd idna_ssl-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
