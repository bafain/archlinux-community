# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-tenacity
pkgname=('python-tenacity' 'python2-tenacity')
pkgver=5.0.2
pkgrel=1
pkgdesc='Retry code until it succeeeds'
arch=('any')
license=('Apache')
url='https://github.com/jd/tenacity'
makedepends=('python-setuptools' 'python2-setuptools' 'python2-futures' 'python2-monotonic'
             'python-pbr' 'python2-pbr')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-tornado' 'python2-tornado')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/jd/tenacity/archive/$pkgver.tar.gz")
sha512sums=('835faa4593017876d5a9e5247043c1540e75246f86021d2777603f989ef7d2fed8fe437c26172b7c042a0e33308498e771831651676bfd8be69354bb3f19ae5d')

prepare() {
  cp -a tenacity-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/tenacity-$pkgver
  python setup.py build

  cd "$srcdir"/tenacity-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/tenacity-$pkgver
  python setup.py pytest

  cd "$srcdir"/tenacity-$pkgver-py2
  python2 setup.py pytest --addopts '--ignore=tenacity/tests/test_asyncio.py'
}

package_python-tenacity() {
  depends=('python-six')
  optdepends=('python-tornado: for Tornado coroutines support')

  cd tenacity-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-tenacity() {
  depends=('python2-six' 'python2-futures' 'python2-monotonic')
  optdepends=('python2-tornado: for Tornado coroutines support')

  cd tenacity-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
