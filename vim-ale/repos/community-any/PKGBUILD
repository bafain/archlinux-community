# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: David Thurstenson <thurstylark@gmail.com>

pkgname=vim-ale
pkgver=2.1.0
pkgrel=1
pkgdesc='Asynchronous Lint Engine'
url='https://github.com/w0rp/ale'
arch=('any')
license=('BSD')
depends=('vim')
optdepends=('shellcheck: for sh/bash support'
            'flake8: for python support')
groups=('vim-plugins')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/w0rp/ale/archive/v${pkgver}.tar.gz)
sha256sums=('27c6da870601a966f9ee359ea12dca04dd9a3fe5a43f94240c9fd4258ef02dd6')
sha512sums=('29da24fc3a0aca1517aee2c18517ad3b9d7dedb52227b5ca570bb057c671ea27fd4f47766217eb7d3579cdc539eba8f3fe80b89e88371c9cbe5b73854219d435')

package() {
	cd ale-${pkgver}
	install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
	install -dm 755 "${pkgdir}/usr/share/vim/vimfiles"
	cp -dr --no-preserve=ownership {ale_linters,autoload,doc,ftplugin,plugin,syntax} \
		"${pkgdir}/usr/share/vim/vimfiles"
}

# vim: ts=2 sw=2 et:
