# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=nvchecker
pkgver=1.1
pkgrel=2
pkgdesc="New version checker for software"
arch=('any')
url="https://github.com/lilydjwg/nvchecker"
license=('MIT')
depends=('python-setuptools' 'python-structlog' 'python-tornado' 'python-pycurl')
checkdepends=('python-pytest-runner' 'python-pytest-xdist' 'python-pytest-asyncio' 'python-flaky')
source=("$pkgname-$pkgver.tar.gz::https://github.com/lilydjwg/nvchecker/archive/v$pkgver.tar.gz"
        "https://github.com/lilydjwg/nvchecker/commit/6afa9a551aa9d6030accf7f7c46c7184c2193622.patch")
sha512sums=('8680776c22bbb34ad531982027a173744a484a98b9a02fda4068d314bdfadefda29d4542a0dac2981ead26a0040c786c59c9239d5097a8a75aec353ace93b9fd'
            '1169a70ec973360ce56e4ab6fdf8092c26e1e0e53ae54c796f400abb66c7884a998b50bc500ee750a173f347569a416bf6d4b192716e857f2f78e2cb6bb14f6d')

prepare() {
  cd nvchecker-$pkgver
  # fix broken test
  patch -p1 -i ../6afa9a551aa9d6030accf7f7c46c7184c2193622.patch
}

build() {
  cd nvchecker-$pkgver
  python setup.py build
}

check() {
  cd nvchecker-$pkgver
  python setup.py pytest
}

package() {
  cd nvchecker-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
