# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=vty
pkgname=haskell-vty
pkgver=5.20
pkgrel=5
pkgdesc="A simple terminal UI library"
url="https://github.com/jtdaugherty/vty"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-blaze-builder' 'haskell-microlens' 'haskell-microlens-mtl'
         'haskell-microlens-th' 'haskell-hashable' 'haskell-mtl' 'haskell-parallel' 'haskell-parsec'
         'haskell-stm' 'haskell-text' 'haskell-utf8-string' 'haskell-vector')
makedepends=('ghc' 'haskell-hunit' 'haskell-quickcheck' 'haskell-smallcheck'
             'haskell-quickcheck-assertions' 'haskell-test-framework'
             'haskell-test-framework-smallcheck' 'haskell-test-framework-hunit' 'haskell-random'
             'haskell-string-qq')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        https://github.com/dmwit/vty/commit/d589780fe193b5777f45b8d3c5d712a7b4a64437.patch)
sha512sums=('1f44c563ed4eb86d9e1b5f57225b7be1909bc2d27cba9fa89b25ef84ef1ab926c071cdb893e6dfd6f53cc79c35b62a64a7c1dd08a45de39cc23bc6cb8faf1e08'
            '579032c391154d39e1eb2c0e9d99fbf8f1b8c04a8715a1bc915c8f98ed6285b3f89120d8cb0387c23526c9ada17a88cc614ec2d7dad53bf2d92fc6903b9bfe14')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../d589780fe193b5777f45b8d3c5d712a7b4a64437.patch
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    # https://github.com/jtdaugherty/vty/issues/132
    # https://github.com/haskell/cabal/issues/4918
    cd $_hkgname-$pkgver
    # runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
