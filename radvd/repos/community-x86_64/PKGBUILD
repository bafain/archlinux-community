# $Id$
# Maintainer: Sébastien Luttringer
# Maintainer: Thore Bödecker <foxxx0@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Mark Smith <markzzzsmith@yahoo.com.au>

pkgname=radvd
pkgver=2.17
pkgrel=2
pkgdesc='IPv6 Router Advertisement Daemon'
url='http://www.litech.org/radvd/'
license=('custom')
depends=('glibc')
makedepends=('check')
arch=('x86_64')
backup=('etc/radvd.conf')
validpgpkeys=('B11F2EED32FB6728F700337C411FA8C112D91A31') # Reuben Hawkins
source=("http://www.litech.org/radvd/dist/${pkgname}-${pkgver}.tar.xz"{,.asc}
        "$pkgname.service"
        'avoid-premature-warning-about-all-zeros-prefix.patch')
sha512sums=('f62aa8acb423320423637c7908abe1d3b8ec4b32f78a81ba67c1a4ecf9bcaaae4e3671fc0c2c5b5776edfe5e6b5d3bf1daf60e55387dd0c0d7370a86d867873c'
            'SKIP'
            'ff24665937ab2b937d24599e3ae14956d531f969a471b6616775c3d5c48330ae1b22cdd4d569867a92c3d6af1548be75de70e5ead6a9ae4ae45566a6523988dd'
            '413ce0c04f1024d9bfba34911834e9db4a2d30e6c270f989f63c33cd04deabb87627ea2e62a364ac80e44c5501c8ed5afd50f8d7446725240815cae95ae086d5')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # apply patch from the source array (should be a pacman feature)
  local filename
  for filename in "${source[@]}"; do
    if [[ "${filename}" =~ \.patch$ ]]; then
      msg2 "Applying patch ${filename##*/}"
      patch -p1 -N -i "${srcdir}/${filename##*/}"
    fi
  done
  :
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --with-pidfile=/run/radvd.pid \
    --with-check \
    --mandir=/usr/share/man
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # default config
  install -D -m 644 radvd.conf.example "${pkgdir}/etc/radvd.conf"
  # license
  install -D -m 644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"
  # systemd
  install -D -m 644 "${srcdir}/${pkgname}.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
}

# vim:set ts=2 sw=2 et:
