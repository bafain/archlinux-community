# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgbase=python-jupyter_core
pkgname=(python-jupyter_core python2-jupyter_core)
pkgver=4.4.0
pkgrel=4
pkgdesc="Jupyter core package. A base package on which Jupyter projects rely."
arch=('any')
url="https://pypi.python.org/pypi/jupyter_core"
license=('BSD')
depends=('python-traitlets')
makedepends=('python-setuptools' 'python2-setuptools')
source=("$pkgname-$pkgver.tgz::https://github.com/jupyter/jupyter_core/archive/$pkgver.tar.gz"
        "https://github.com/jupyter/jupyter_core/commit/812e613b.diff")
md5sums=('f2bdb8be2959d3043b77508dd6498687'
         'd83097f1ed2655f93dadad873508eb2b')

prepare() {
  cd "$srcdir/jupyter_core-$pkgver"

  # FS#59768, remove on 4.4.1
  patch -Np1 -i ../812e613b.diff

  cd "$srcdir"
  cp -r jupyter_core-$pkgver python2-jupyter_core-$pkgver

  cd python2-jupyter_core-$pkgver
  sed -i 's/env python$/&2/' jupyter_core/troubleshoot.py
}

build() {
  cd "$srcdir"
}

package_python-jupyter_core() {
  cd "$srcdir/jupyter_core-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 examples/jupyter-completion.bash "$pkgdir/usr/share/bash-completion/completions/jupyter"
  install -Dm644 examples/completions-zsh "$pkgdir/usr/share/zsh/site-functions/_jupyter"
}

package_python2-jupyter_core() {
  # todo: figure out how to remove this, it probably shouldn't be used
  depends=('python2-traitlets')

  cd "$srcdir/python2-jupyter_core-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "$pkgdir/usr"
  rm bin/jupyter
  rm bin/jupyter-migrate
  rmdir bin
}
