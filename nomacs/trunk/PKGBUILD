# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

_plugins_pkgver=3.8.1
pkgname=nomacs
pkgver=3.10.2
pkgrel=2
pkgdesc="A Qt image viewer"
arch=('x86_64')
url="https://nomacs.org/"
license=('GPL3')
depends=('qt5-svg' 'exiv2' 'libraw' 'opencv')
optdepends=('qt5-imageformats: support additional image formats')
makedepends=('cmake' 'qt5-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz"
        "${pkgname}-plugins-${_plugins_pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}-plugins/archive/${_plugins_pkgver}.tar.gz")
sha512sums=('977dbe24e09b00001dd8a33b18c1dbbb3a59106715746b07892f289918205818664fd7b4213028d4288009bcf14a0195759ac1eafca076f81ee72a1e2ae027dd'
            '86bc4db30b0815fd2efa272b14d3917f0e88fa95284f0afb55acca87b77b3db7668238fb3023cc9b21fe8c87cad72319d45d48633a646bf5b6acb80a6537b8c8')

prepare() {
  cd "${pkgname}-${pkgver}"
  # move plugins
  mv "${srcdir}/${pkgname}-plugins-${_plugins_pkgver}" "ImageLounge/plugins"
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake ./ImageLounge -DCMAKE_INSTALL_PREFIX=/usr \
                      -DENABLE_TRANSLATIONS=true
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
