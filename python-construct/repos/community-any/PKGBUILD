# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-construct
pkgname=('python-construct' 'python2-construct')
pkgver=2.9.45
pkgrel=2
pkgdesc="A powerful declarative parser/builder for binary data"
arch=('any')
license=('MIT')
url="https://construct.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-arrow' 'python2-arrow' 'python-numpy' 'python2-numpy' 'python-pytest-runner'
              'python2-pytest-runner' 'python-pytest-benchmark' 'python2-pytest-benchmark'
              'python2-enum34' )
source=("$pkgbase-$pkgver.tar.gz::https://github.com/construct/construct/archive/v$pkgver.tar.gz")
sha512sums=('78d64f3e567e6c9cef842d3e15f416d48856d229a8c6f21e282d3123a86fdd243b20dba5040c6e1d28b67422881351b303262132263efdf94bb73adde0d5dfc1')

prepare() {
  cp -a construct-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/construct-$pkgver
  python setup.py build

  cd "$srcdir"/construct-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/construct-$pkgver
  python setup.py pytest

  cd "$srcdir"/construct-$pkgver-py2
  python2 setup.py pytest
}

package_python-construct() {
  depends=('python')
  optdepends=('python-arrow: for Timestamp'
              'python-numpy: for array serialization with Numpy protocol')

  cd construct-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-construct() {
  depends=('python2')
  optdepends=('python2-arrow: for Timestamp'
              'python2-enum34: for Enum / EnumFlags'
              'python2-numpy: for array serialization with Numpy protocol')

  cd construct-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
