# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-calc
pkgver=1.20.2
pkgrel=1
pkgdesc="Calculator for the MATE desktop environment"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('gtk3')
makedepends=('intltool' 'itstool')
groups=('mate-extra')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('a0536986f9c1adf04a1e59110c29277caedb06e27ac7876096f9399ae4001fbe')

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
