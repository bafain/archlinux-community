# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-openstackclient
pkgname=(python-openstackclient python2-openstackclient)
pkgver=3.16.1
pkgrel=1
pkgdesc="OpenStack Command-line Client"
arch=('any')
url="https://docs.openstack.org/python-openstackclient/latest/"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-babel'
             'python2-babel' 'python-cliff' 'python2-cliff' 'python-keystoneclient'
             'python2-keystoneclient' 'python-keystoneauth1' 'python2-keystoneauth1'
             'python-osc-lib' 'python2-osc-lib' 'python-oslo-utils' 'python2-oslo-utils'
             'python-oslo-i18n' 'python2-oslo-i18n' 'python-openstacksdk' 'python2-openstacksdk'
             'python-glanceclient' 'python2-glanceclient' 'python-novaclient' 'python2-novaclient'
             'python-cinderclient' 'python2-cinderclient')
checkdepends=('python-oslotest' 'python2-oslotest' 'python-requests-mock' 'python2-requests-mock'
              'python-testrepository' 'python2-testrepository')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/python-openstackclient/archive/$pkgver.tar.gz")
sha512sums=('99ec061699e06a2f796a94860e065f3ad0135869803116571af88d4388993058a524c7fb7f00f9a551d782972564bd922f0a4c8b3168785002c3c1355a113e91')

export PBR_VERSION=$pkgver

prepare() {
  cp -a python-openstackclient-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-openstackclient-$pkgver
  python setup.py build

  cd "$srcdir"/python-openstackclient-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-openstackclient-$pkgver
  python setup.py testr || warning "Tests failed: https://storyboard.openstack.org/#!/story/2003143"

  cd "$srcdir"/python-openstackclient-$pkgver-py2
  PYTHON=python2 python2 setup.py testr || warning "Tests failed: https://storyboard.openstack.org/#!/story/2003143"
}

package_python-openstackclient() {
  depends=('python-six' 'python-pbr' 'python-babel' 'python-cliff' 'python-keystoneclient'
           'python-keystoneauth1' 'python-osc-lib' 'python-oslo-utils' 'python-oslo-i18n'
           'python-openstacksdk' 'python-glanceclient' 'python-novaclient' 'python-cinderclient')

  cd "$srcdir"/python-openstackclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-openstackclient() {
  depends=('python2-six' 'python2-pbr' 'python2-babel' 'python2-cliff' 'python2-keystoneclient'
           'python2-keystoneauth1' 'python2-osc-lib' 'python2-oslo-utils' 'python2-oslo-i18n'
           'python2-openstacksdk' 'python2-glanceclient' 'python2-novaclient'
           'python2-cinderclient')

  cd "$srcdir"/python-openstackclient-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/openstack{,2}
}

# vim:set ts=2 sw=2 et:
