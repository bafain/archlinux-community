# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=balsa
pkgver=2.5.6
pkgrel=1
pkgdesc="An e-mail client for GNOME"
arch=('x86_64')
url='http://pawsa.fedorapeople.org/balsa/'
license=('GPL')
depends=('gmime' 'webkit2gtk' 'libesmtp' 'libnotify'
         'gpgme' 'gtksourceview3' 'gspell' 'desktop-file-utils')
makedepends=('yelp-tools' 'intltool')
changelog=$pkgname.changelog
source=(https://pawsa.fedorapeople.org/balsa/$pkgname-$pkgver.tar.bz2)
sha256sums=('7d5aa5a12e54d385da77660dc5908299146ba4ab108e3c353b713e4435e3669e')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --with-gpgme=gpgme-config \
      --with-gss \
      --with-ldap \
      --with-gtksourceview \
      --with-spell-checker=gspell \
      --with-rubrica \
      --with-sqlite \
      --with-gnome \
      --with-html-widget=webkit2
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make GTK_UPDATE_ICON_CACHE=/bin/true DESTDIR="${pkgdir}" install
}
