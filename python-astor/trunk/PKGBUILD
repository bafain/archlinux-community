# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-astor
pkgname=('python-astor' 'python2-astor')
pkgver=0.7.1
pkgrel=1
pkgdesc="Read/rewrite/write Python ASTs"
arch=('any')
license=('BSD')
url="http://astor.rtfd.org/"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose' 'python2-unittest2')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/berkerpeksag/astor/archive/$pkgver.tar.gz")
sha512sums=('02764e5751e4c4b0ffa83da262b87e0a6bf027461529a99d3ca01a415db0896754f2b3f278e8a28f9bce4972ee7a75eec4eec5ac47d1064e6d6656a007b38a64')

prepare() {
  cp -a astor-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/astor-$pkgver
  python setup.py build

  cd "$srcdir"/astor-$pkgver-py2
  python2 setup.py build
}

check() {
  # https://github.com/berkerpeksag/astor/issues/89
  cd "$srcdir"/astor-$pkgver
  nosetests3 || warning "Tests failed"

  cd "$srcdir"/astor-$pkgver-py2
  nosetests2
}

package_python-astor() {
  depends=('python')

  cd astor-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-astor() {
  depends=('python2')

  cd astor-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
