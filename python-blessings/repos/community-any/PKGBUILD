# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Andrey Mikhaylenko <neithere at gmail dot com>

pkgbase=python-blessings
pkgname=(python-blessings python2-blessings)
_pkgname=blessings
pkgver=1.6.1
pkgrel=1
pkgdesc="A thin, practical wrapper around terminal coloring, styling, and positioning"
url="https://github.com/erikrose/blessings"
arch=('any')
license=('MIT')
depends=('python')
makedepends=('python-distribute' 'python2-distribute')
#options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/b/$_pkgname/$_pkgname-$pkgver.tar.gz")
md5sums=('548a69d43c991fa85fdaa24806884e7b')

prepare() {
  cd "$srcdir"
  cp -r blessings-$pkgver blessings2-$pkgver
}

package_python-blessings() {
  cd "$srcdir/blessings-$pkgver"
  python3 setup.py install --root="$pkgdir/" --prefix=/usr --optimize=0
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/python-blessings/LICENSE"
}

package_python2-blessings() {
  depends=('python2')
  cd "$srcdir/blessings2-$pkgver"
  python2 setup.py install --root="$pkgdir/" --prefix=/usr --optimize=0
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/python2-blessings/LICENSE"
}

