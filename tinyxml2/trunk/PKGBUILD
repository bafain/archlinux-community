# $Id: PKGBUILD 148562 2015-12-05 17:09:20Z foutrelis $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: dhead666 <myfoolishgames@gmail.com>

pkgname=tinyxml2
pkgver=6.2.0
pkgrel=1
pkgdesc='Simple XML parser made for easy integration'
# no working https page
url='http://www.grinninglizard.com/tinyxml2'
arch=('x86_64')
license=('zlib')
makedepends=('cmake' 'git' 'ninja')
source=("git+https://github.com/leethomason/tinyxml2#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  grep License -A20 "$pkgname/readme.md" > LICENSE
}

build() {
  mkdir -p build
  cd build
  cmake "../$pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -G Ninja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  cat LICENSE
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/leethomason/tinyxml2
# vim: ts=2 sw=2 et:
