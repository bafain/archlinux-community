# Maintainer: David Runge <dave@sleepmap.de>
_name=pytest-rerunfailures
pkgbase=python-pytest-rerunfailures
pkgname=('python-pytest-rerunfailures' 'python2-pytest-rerunfailures')
pkgver=4.0
pkgrel=2
pkgdesc="A py.test plugin that re-runs failed tests up to -n times to eliminate flakey failures"
arch=('any')
url="https://github.com/pytest-dev/pytest-rerunfailures/"
license=('MPL2')
makedepends=('python-setuptools' 'python2-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('6692662ae721e7e42d5459cea6eb0d4b2640e4c98879fe512cb8af0b1e44fc5ecf1c749ff3f60a3803f492f5bbcc602a46a3534abce92c94c5a45a2e4be4c65c')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

package_python-pytest-rerunfailures() {
  depends=('python-pytest')
  cd "${pkgname[0]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname[0]}" \
    -vDm644 {CHANGES.rst,README.rst}
}

package_python2-pytest-rerunfailures() {
  depends=('python2-pytest')
  cd "${pkgname[1]}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname[1]}" \
    -vDm644 {CHANGES.rst,README.rst}
}

# vim:set ts=2 sw=2 et:
