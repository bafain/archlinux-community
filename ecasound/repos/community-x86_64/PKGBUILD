# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ecasound
pkgver=2.9.1
pkgrel=11
pkgdesc="Command-line multitrack audio processor"
arch=('x86_64')
url="https://ecasound.seul.org/ecasound/"
license=('GPL' 'LGPL')
groups=('pro-audio')
depends=('audiofile' 'liblo>=0.28' 'lilv')
makedepends=('python2' 'ruby')
optdepends=('python2: ecamonitor, ECI API'
            'ruby: ECI API'
            'mpg123: MP3 decoding'
            'lame: MP3 encoding'
            'vorbis-tools: Ogg Vorbis decoding/encoding'
            'faad2: AAC decoding'
            'faac: AAC encoding'
            'timidity++: MIDI file input'
            'libmikmod: tracker module')
source=("https://ecasound.seul.org/download/$pkgname-$pkgver.tar.gz"
        'ruby2.patch')
options=('staticlibs')
sha512sums=('b91fd68cb4809fdf62b25323fbf52c9bb7de6663287d5aa2576cc4fb28e322850b7020df2718ed6087f5d9f5f96692a476287378ea65853d01834523e8eb7033'
            'aa1553e81acc41917cc8e62b7e2569106183b4500d687fc17e0b56c8f70e78ae63cd65c4940ef83a13dbb0b320ba51573b6eb43da54b141f846623962838d639')

prepare() {
  cd "$pkgname-$pkgver"

  # fix python2 shebang
  sed -e 's:bin/env python:bin/env python2:' \
      -i "ecatools/ecamonitor"

  # fix ruby 2.0 incompatibility + use vendor_ruby for ruby files
  patch -Np1 -i "$srcdir/ruby2.patch"
}
build() {
  cd "$pkgname-$pkgver"

  PYTHONPATH=/usr/bin/python2 ./configure \
    --prefix=/usr \
    --with-python-includes=/usr/include/python2.7 \
    --with-python-modules=/usr/lib/python2.7 \
    --enable-liboil=no

  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  # docs
  install -vDm 644 {AUTHORS,BUGS,NEWS,README,RELNOTES,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}

# vim:set ts=2 sw=2 et:
