# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=qcef
pkgver=1.1.4.4
_cefcommit=854e4930be007f08c62a46c0a5a259c0cfb83569
pkgrel=1
pkgdesc="Qt5 binding of CEF"
arch=('x86_64')
url="https://github.com/linuxdeepin/qcef"
license=('GPL')
depends=('gconf' 'libxss' 'nss' 'qt5-webchannel' 'qt5-x11extras')
makedepends=('cmake' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qcef/archive/$pkgver.tar.gz"
        "cef-$_cefcommit.tar.gz::https://github.com/linuxdeepin/cef-binary/archive/$_cefcommit.tar.gz")
        # http://opensource.spotify.com/cefbuilds/cef_binary_${_cefver}_linux64_minimal.tar.bz2
        # https://raw.githubusercontent.com/cztomczak/cefpython/master/patches/issue231.patch)
sha512sums=('18dbecb57d0c09314bf8a7f411e90da1f5a2aa5786f7ef8d5e0972bea184d9ebe2646236ac9607b12d0bc6daf34a0d6453beb400fd36bf917b642d097aa7ea54'
            'b17dff043819ac5a506dc698b899c3d3eb82dadfde87c4f7e158409dfadb7e25cc6aa4a0fadf586dd1990187229d16e793f3acdcfe4b231cda4afe79354c67cc')

prepare() {
  mkdir -p build

  # TODO: build cef instead of using binary distribution
  rmdir qcef-$pkgver/cef
  mv cef-binary-$_cefcommit qcef-$pkgver/cef

  # mv cef_binary_${_cefver}_linux64_minimal qcef-$pkgver/cef
  # cd qcef-$pkgver/cef
  # tar czvf cef.x86_64.tar.gz Release Resources
}

build() {
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release ../qcef-$pkgver
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  chmod +s "$pkgdir"/usr/lib/qcef/chrome-sandbox
  rm -rf "$pkgdir"/build
}
