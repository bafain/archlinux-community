# Maintainer: Baptiste Jonglez <archlinux at bitsofnetworks.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Kritoke <kritoke@gamebox.net>
# Contributor: jlvsimoes <jlvsimoes@oninet.pt>

pkgname=crypto++
pkgver=6.1.0
_srcver=${pkgver//./}
pkgrel=1
pkgdesc="A free C++ class library of cryptographic schemes"
arch=('x86_64')
url="https://www.cryptopp.com/"
license=('custom')
depends=('gcc-libs')
makedepends=('unzip')
# Fix https://bugs.archlinux.org/task/56689
install="crypto++.install"
source=("https://www.cryptopp.com/cryptopp${_srcver}.zip"
        'libcrypto++.pc')
# Checksums from https://www.cryptopp.com/release600.html
sha1sums=('c4e7c14ad17b6fae6a1b84d0824b4365f2a779e6'
          'f776881de464d591b33eec88b76039bc5da8c1c8')
sha256sums=('21289d2511101a9350c87c8eb1f4982d4a266e8037b19dab79a32cc13ea108c7'
            '86370744f3191eaba9f334c293fdf1670188a4462edef98ace9389462664b01b')
sha512sums=('cb01330ffb2f34bf51ca5a9d530b0be987c7cf36ab2be0e562a61bd39748f9fa5533be43050c7505f8be22b03a9dece829c1b27987b9c9c5cb7ecf51d75bf6a3'
            'e15a5d1e2c3d9e0fd87028848e7174fc0589a1f492d5acc95c1b1d7073bccdfa325d515b6ef3c4aed850b48302b876a754add1a73123e4bc5416dbf0586258a1')

build() {
  CXXFLAGS+=" -DNDEBUG -fPIC" make dynamic cryptest.exe
}

check() {
  make test
}

package() {
  make install DESTDIR="$pkgdir" PREFIX="/usr"
  # Install pkgconfig file
  install -d "${pkgdir}/usr/lib/pkgconfig"
  install -m644 "${srcdir}/libcrypto++.pc" "${pkgdir}/usr/lib/pkgconfig/libcrypto++.pc"
  # Remove cryptest.exe and test files, only needed for check() and bloats the package
  # because cryptest.exe is linked statically.
  rm "${pkgdir}/usr/bin/cryptest.exe"
  rmdir "${pkgdir}/usr/bin/"
  rm -r "${pkgdir}/usr/share/cryptopp/"
  # Install license
  install -D -m644 License.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
