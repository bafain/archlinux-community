# Maintainer: Baptiste Jonglez <archlinux at bitsofnetworks.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Kritoke <kritoke@gamebox.net>
# Contributor: jlvsimoes <jlvsimoes@oninet.pt>

pkgname=crypto++
pkgver=7.0.0
_srcver=${pkgver//./}
pkgrel=1
pkgdesc="A free C++ class library of cryptographic schemes"
arch=('x86_64')
url="https://www.cryptopp.com/"
license=('custom')
depends=('gcc-libs')
makedepends=('unzip')
# Fix https://bugs.archlinux.org/task/56689
install="crypto++.install"
source=("https://www.cryptopp.com/cryptopp${_srcver}.zip"
        'libcrypto++.pc')
# Checksums from https://www.cryptopp.com/release600.html
sha1sums=('1288682ae2f3062ea3a82ca2e90bd06d147568a4'
          'ef530175d27101dcb23a3f92d3c80a529f1d7b02')
sha256sums=('a4bc939910edd3d29fb819a6fc0dfdc293f686fa62326f61c56d72d0a366ceb0'
            '8722862336f9fe0181734619c197bf4248f0e07b93bdcd693709f57b2f6aa9e6')
sha512sums=('8fd8d680e5ceaf10b3c32cb1504bda4b32abc9fcaa2c6e61e3b5e67aca805c792579884bc6a749c29a99105304c421d03d7fe7909fc58f750b3155c7c5cec8ec'
            '3be1569e81af1f9b35e944faae3e9962ee2e492fb38e94fe7f847b85da033a79bbfeff193e0edb2d69f2d893f6e8279be144b9395653db67374300f7feb23276')

build() {
  CXXFLAGS+=" -DNDEBUG -fPIC" make dynamic cryptest.exe
}

check() {
  make test
}

package() {
  make install DESTDIR="$pkgdir" PREFIX="/usr"
  # Install pkgconfig file
  install -d "${pkgdir}/usr/lib/pkgconfig"
  install -m644 "${srcdir}/libcrypto++.pc" "${pkgdir}/usr/lib/pkgconfig/libcrypto++.pc"
  # Remove cryptest.exe and test files, only needed for check() and bloats the package
  # because cryptest.exe is linked statically.
  rm "${pkgdir}/usr/bin/cryptest.exe"
  rmdir "${pkgdir}/usr/bin/"
  rm -r "${pkgdir}/usr/share/cryptopp/"
  # Install license
  install -D -m644 License.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
