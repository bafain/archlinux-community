# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=cabal-install
pkgver=2.2.0.0
pkgrel=69
pkgdesc="The command-line interface for Cabal and Hackage."
url="https://hackage.haskell.org/package/cabal-install"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-base16-bytestring' 'haskell-cryptohash-sha256'
         'haskell-echo' 'haskell-edit-distance' 'haskell-hackage-security' 'haskell-hashable'
         'haskell-http' 'haskell-network' 'haskell-network-uri' 'haskell-random' 'haskell-resolv'
         'haskell-tar' 'haskell-zlib')
makedepends=('ghc' 'haskell-pretty-show' 'haskell-quickcheck' 'haskell-tagged' 'haskell-tasty'
             'haskell-tasty-hunit' 'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/package/cabal-install-${pkgver}/cabal-install-${pkgver}.tar.gz")
sha512sums=('1b8c31dee09332808bc727322e1e0ca6aaa8c818c5172f412a9d56e8474c93cc814cab6d0cb776a5d0bb94155c023a098872fb91329ff6377278f891f06686f2')

prepare() {
    cd $pkgname-$pkgver
    sed -i 's/<.*2.7/<3/' $pkgname.cabal
}

build() {
  cd "${srcdir}/$pkgname-$pkgver"

  runhaskell Setup configure -O --prefix=/usr --enable-executable-dynamic --disable-library-vanilla \
    --docdir="/usr/share/doc/${pkgname}" --enable-tests \
    -fnetwork-uri -fnative-dns -f-debug-expensive-assertions -f-debug-conflict-sets \
    -f-debug-tracetree -f-monolithic -f-lib
  runhaskell Setup build
}

check() {
  cd $pkgname-$pkgver
  runhaskell Setup test || warning "TODO: pass -dynamic somewhere"
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  runhaskell Setup copy --destdir="${pkgdir}"

  install -Dm644 LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -Dm644 bash-completion/cabal \
    "${pkgdir}/usr/share/bash-completion/completions/cabal"
}
