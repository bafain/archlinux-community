# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Roberto Alsina <ralsina@kde.org>
# Contributor: Julien Duponchelle <julien@gns3.net>

pkgbase=python-cx_freeze
pkgname=('python-cx_freeze' 'python2-cx_freeze')
pkgver=5.1.1
pkgrel=2
pkgdesc='Utilities for freezing Python scripts into executables'
arch=('x86_64')
url='http://cx-freeze.sourceforge.net/'
license=('PSF')
makedepends=('python' 'python2')
source=("https://pypi.io/packages/source/c/cx_Freeze/cx_Freeze-$pkgver.tar.gz")
sha512sums=('64f9c3b7f6c3003c1dc439fc2da7d0c13c5f146834e3ab8ece3921f1eb30c6e3eda480ccc84e61cc061597521b360cf8f5ef8e0bbcce1437157360c3d9c1adbc')

package_python-cx_freeze() {
  depends=('python')
  conflicts=('cx_freeze' 'python2-cx_freeze')

  cd "cx_Freeze-$pkgver"
  python setup.py install --root "$pkgdir" --optimize 1
}

package_python2-cx_freeze() {
  depends=('python2')
  conflicts=('cx_freeze' 'python-cx_freeze')
  replaces=('cx_freeze')

  cd "cx_Freeze-$pkgver"
  python2 setup.py install --root "$pkgdir" --optimize 1

  # Python 2 fix
  find "$pkgdir" -name qotd.py -exec sed -i '0,/on/s//on2/' {} \;
}
