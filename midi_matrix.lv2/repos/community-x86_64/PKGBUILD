# Maintainer: David Runge <dave@sleepmap.de>
pkgname=midi_matrix.lv2
pkgver=0.20.0
pkgrel=2
pkgdesc="A 3-in-1 filter plugin with a simple UI enabling you to easily accomplish MIDI channel filtering, multiplication and rerouting"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/midi_matrix/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('libgl')
makedepends=('cmake' 'lv2')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz")
md5sums=('96b74972b0ab0b0a1ab5629c234fe70f')

prepare() {
  cd "$pkgname-$pkgver"
  mkdir -v build
}

build() {
  cd "$pkgname-$pkgver/build"
  cmake -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        ..
  make
}

package() {
  cd "$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir/" install
  # docs
  install -vDm 644 ../README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
