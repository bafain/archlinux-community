# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=pcmanfm-qt
pkgver=0.13.0
pkgrel=1
pkgdesc="The LXQt file manager, Qt port of PCManFM"
arch=("x86_64")
groups=("lxqt")
url="http://lxqt.org"
license=("GPL2")
depends=("desktop-file-utils" "libfm-qt" "lxmenu-data")
makedepends=("lxqt-build-tools")
optdepends=(
	"gvfs: for trash support, mounting with udisks and remote filesystems"
	"gvfs-mtp: MTP support (Media Transfer Protocol)"
	"xarchiver: archive management"
)
source=(
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha256sums=(
	"bf961a6d91a50a3e7e69ae4b249d41b11f01d70142759a3019f30a33e7cc5722"
	"SKIP"
)
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
)


build() {
	mkdir -p build
	cd build

	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
