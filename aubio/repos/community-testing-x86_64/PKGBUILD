# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Tobias Kieslich tobias [funnychar] archlinux org

pkgbase=aubio
pkgname=('aubio' 'python-aubio' 'python2-aubio')
pkgver=0.4.6
pkgrel=2
pkgdesc="A tool for extracting annotations from audio signals"
arch=('x86_64')
url="https://aubio.org/"
license=('GPL3')
makedepends=('doxygen' 'ffmpeg' 'fftw' 'jack' 'libsamplerate' 'libsndfile'
'python-numpy' 'python-setuptools' 'python-sphinx' 'python2-numpy'
'python2-setuptools' 'python2-sphinx' 'txt2man')
checkdepends=('python-nose2' 'python2-nose2')
source=("https://${pkgname}.org/pub/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('0e5cab0c88d51f2fb70c0d85e1c067a7b3758fbffdbd5026686726379aa36203adb63ed3f8e40c83ea02930e0ffc4e6c246803f1d518d448d47a01984678e77d')

prepare() {
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[2]}-${pkgver}"
  cd "${pkgname[2]}-${pkgver}"
  # python2 shebang
  sed -e '1s/python/&2/' -i "python/lib/${pkgname}/"{__init__,cmd,cut}.py -i "python/tests/"*.py
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python2 waf configure --prefix=/usr \
    --enable-fftw3 \
    --htmldir=/usr/share/doc/${pkgname} \
    --docdir=/usr/share/doc/${pkgname} \
    --libdir=/usr/lib
  python2 waf build
  cd ../"${pkgname[1]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[2]}-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${pkgname[1]}-${pkgver}"
  export PYTHONPATH="${srcdir}/${pkgname[1]}-${pkgver}/build/lib.linux-${arch}-3.6":${PYTHONPATH}
  python setup.py test

  cd ../"${pkgname[2]}-${pkgver}"
  export PYTHONPATH="${srcdir}/${pkgname[2]}-${pkgver}/build/lib.linux-${arch}-2.7":${PYTHONPATH}
  python2 setup.py test
}

package_aubio() {
  depends=('ffmpeg' 'fftw' 'jack' 'libsamplerate' 'libsndfile')
  optdepends=('python2-numpy: Python 2.x interface'
              'python-numpy: Python 3.x interface')
  cd "${pkgname[0]}-${pkgver}"
  python2 waf --destdir="${pkgdir}" install
  # move doc, as waf configure doesn't honor the htmldir and docdir settings
  mv -v "${pkgdir}/usr/share/doc/lib${pkgname}-doc" \
    "${pkgdir}/usr/share/doc/${pkgname}"
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm644 {AUTHORS,ChangeLog,README.md}
}

package_python-aubio() {
  depends=('aubio' 'python-numpy')
  cd "${pkgname[1]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm644 {AUTHORS,ChangeLog,README.md}
}

package_python2-aubio() {
  depends=('aubio' 'python2-numpy')
  cd "${pkgname[2]}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm644 {AUTHORS,ChangeLog,README.md}
  # move duplicates
  mv -v ${pkgdir}/usr/bin/aubio ${pkgdir}/usr/bin/aubio2
  mv -v ${pkgdir}/usr/bin/aubiocut ${pkgdir}/usr/bin/aubiocut2
}

# vim:set ts=2 sw=2 et:
