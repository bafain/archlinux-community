# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=tftpy
pkgname=('python2-tftpy')
pkgver=0.7.0
pkgrel=1
pkgdesc='Pure Python TFTP library'
url='https://github.com/msoulier/tftpy'
arch=('any')
license=('MIT')
makedepends=('python2' 'python2-setuptools')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/msoulier/tftpy/archive/${pkgver}.tar.gz)
sha256sums=('92163d4596c07790e5b4daaccf93e147163aeb961647fae99ae245156e2f1741')
sha512sums=('4f7d5d5f5331c832ae9bd04e467fdc2dc31cb7b898146fc4331c87817ff03c8dfd9e13bfea7331a7be2073d22ed8a1fa9f22179be55531225f1995c9b31be5a0')

build() {
  cd ${pkgbase}-${pkgver}
  python2 setup.py build
}

check() {
  cd ${pkgbase}-${pkgver}
  make test PY=python2
}

package_python2-tftpy() {
  depends=('python2')
  cd ${pkgbase}-${pkgver}
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README ChangeLog  -t "${pkgdir}/usr/share/doc/${pkgname}"
  mv "${pkgdir}/usr/bin/tftpy_client"{.py,}
  mv "${pkgdir}/usr/bin/tftpy_server"{.py,}
}

# vim: ts=2 sw=2 et:
