# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-oslo-log
pkgname=(python-oslo-log python2-oslo-log)
pkgver=3.39.0
pkgrel=1
pkgdesc="Oslo Logging Library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.log/$pkgver"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-oslo-config'
             'python2-oslo-config' 'python-oslo-context' 'python2-oslo-context' 'python-oslo-i18n'
             'python2-oslo-i18n' 'python-oslo-utils' 'python2-oslo-utils' 'python-oslo-serialization'
             'python2-oslo-serialization' 'python-debtcollector' 'python2-debtcollector'
             'python-pyinotify' 'python2-pyinotify' 'python-dateutil' 'python2-dateutil'
             'python-monotonic' 'python2-monotonic')
checkdepends=('python-mock' 'python2-mock' 'python-oslotest' 'python2-oslotest'
              'python-testrepository' 'python2-testrepository')
options=('!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/oslo.log/archive/$pkgver.tar.gz")
sha512sums=('ce8451d857ce3e85dbb61fccad6a9a96cc5e99d0fdc34f7d1126d04fade65dace1c78104996caf485d381b375a6799a51bcc760da351cf0176b30d3c79d7c506')

prepare() {
  sed -i 's/unittest2/unittest/g' oslo.log-$pkgver/oslo_log/tests/unit/*.py
  cp -a oslo.log-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/oslo.log-$pkgver
  python setup.py build

  cd "$srcdir"/oslo.log-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/oslo.log-$pkgver
  python setup.py testr || warning "Tests failed"

  cd "$srcdir"/oslo.log-$pkgver-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-oslo-log() {
  depends=('python-six' 'python-pbr' 'python-oslo-config' 'python-oslo-context' 'python-oslo-i18n'
           'python-oslo-utils' 'python-oslo-serialization' 'python-debtcollector' 'python-pyinotify'
           'python-dateutil' 'python-monotonic')

  cd "$srcdir"/oslo.log-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-oslo-log() {
  depends=('python2-six' 'python2-pbr' 'python2-oslo-config' 'python2-oslo-context' 'python2-oslo-i18n'
           'python2-oslo-utils' 'python2-oslo-serialization' 'python2-debtcollector' 'python2-pyinotify'
           'python2-dateutil' 'python2-monotonic')

  cd "$srcdir"/oslo.log-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/convert-json{,2}
}

# vim:set ts=2 sw=2 et:
