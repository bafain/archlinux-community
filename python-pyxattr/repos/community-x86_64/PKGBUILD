# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Jon Bergli Heier <snakebite@jvnv.net>

pkgname=('python2-pyxattr' 'python-pyxattr')
pkgbase=python-pyxattr
_pkgname=pyxattr
pkgver=0.6.0
pkgrel=2
pkgdesc='A python extension module that allows you to manipulate the extended attributes.'
arch=('x86_64')
url='https://pyxattr.k1024.org/'
license=('GPL')
makedepends=('python2' 'python' 'python2-setuptools' 'python-setuptools')
source=("https://pyxattr.k1024.org/downloads/pyxattr-$pkgver.tar.gz"{,.asc}
        "pyxattr-xattr-2.4.48.patch::https://github.com/iustin/pyxattr/pull/15.patch")
md5sums=('3e2f9dd92459a0fe0d02861dff4c2f4d'
         'SKIP'
         '00dbdc4c8ca4638356e57751ad164591')
validpgpkeys=('CB94E3AA3B1755D61EBB19A5F66E3E419F84F4DE') # Iustin Pop <iustin@k1024.org>

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  # https://github.com/iustin/pyxattr/pull/15
  patch -p1 -i ../pyxattr-xattr-2.4.48.patch
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  python setup.py build
  python2 setup.py build
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  python setup.py test
  python2 setup.py test
}

package_python2-pyxattr() {
  depends=('python2')
  provides=('pyxattr')
  conflicts=('pyxattr')
  replaces=('pyxattr')

  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}"
}

package_python-pyxattr() {
  depends=('python')

  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}"
}
