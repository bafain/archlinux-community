# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Neil Santos <nsantos16+aur@gmail.com>

_name=wsgi_intercept
pkgbase=python-wsgi-intercept
pkgname=('python-wsgi-intercept' 'python2-wsgi-intercept')
pkgver=1.8.0
pkgrel=3
pkgdesc='Intercept socket connection to wsgi applications for testing.'
arch=('any')
url="https://github.com/cdent/wsgi-intercept"
license=('MIT')
makedepends=('python-setuptools' 'python-sphinx' 'python2-setuptools' 'python2-sphinx')
checkdepends=('python-httplib2' 'python-pytest' 'python-requests'
'python-urllib3' 'python2-httplib2' 'python2-pytest' 'python2-requests'
'python2-urllib3')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('980fd8d2c5b5fe1cd320995c4ee9c7c406fd0aedc5328647430cd1dd0aabd465761749804f7d73697ae3ac8284771632f9bce8d0a4c95d4153c21cabd0f4a590')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd docs
  make man
  cd ../../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
  cd docs
  make man
}

check() {
  cd "${pkgname[0]}-${pkgver}"
  py.test
  cd ../"${pkgname[1]}-${pkgver}"
  py.test2
}

package_python-wsgi-intercept() {
  depends=('python-six')
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -vDm 644 README -t "${pkgdir}/usr/share/${pkgname}/"
  install -vDm 644 docs/_build/man/${_name}.1 \
    -t "${pkgdir}/usr/share/man/man1/"
}

package_python2-wsgi-intercept() {
  depends=('python2-six')
  cd "${pkgname}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -vDm 644 README -t "${pkgdir}/usr/share/${pkgname}/"
  install -vDm 644 docs/_build/man/${_name}.1 \
    "${pkgdir}/usr/share/man/man1/${_name}2.1"
}

