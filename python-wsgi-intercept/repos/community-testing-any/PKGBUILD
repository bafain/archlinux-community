# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Neil Santos <nsantos16+aur@gmail.com>

_name=wsgi-intercept
pkgbase=python-wsgi-intercept
pkgname=('python-wsgi-intercept' 'python2-wsgi-intercept')
pkgver=1.6.0
pkgrel=1
pkgdesc='Intercept socket connection to wsgi applications for testing.'
arch=('any')
url="https://github.com/cdent/wsgi-intercept"
license=('MIT')
makedepends=('python-setuptools' 'python-sphinx' 'python2-setuptools' 'python2-sphinx')
checkdepends=('python-httplib2' 'python-pytest' 'python-requests'
'python-urllib3' 'python2-httplib2' 'python2-pytest' 'python2-requests'
'python2-urllib3')
source=("${_name}-${pkgver}.tar.gz::https://github.com/cdent/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('7a2278b1956cb1618365a2226b09ae4ad1d4a3cbe96bb0fbd905e2c9bd6374e03d5fb92d6b2ccf4fc210d8c8764c2f452b10a0c213a9deacf0a62b368ac75ebd')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd docs
  make man
  cd ../../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
  cd docs
  make man
}

check() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py test
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py test
}

package_python-wsgi-intercept() {
  depends=('python-six')
  cd "${pkgname[0]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname[0]}/LICENSE"
  install -vDm644 README.rst "${pkgdir}/usr/share/${pkgname[0]}/README.rst"
  install -vDm644 docs/_build/man/wsgi_intercept.1 "${pkgdir}/usr/share/man/man1/wsgi_intercept.1"
}

package_python2-wsgi-intercept() {
  depends=('python2-six')
  cd "${pkgname[1]}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname[1]}/LICENSE"
  install -vDm644 README.rst "${pkgdir}/usr/share/${pkgname[1]}/README.rst"
  install -vDm644 docs/_build/man/wsgi_intercept.1 "${pkgdir}/usr/share/man/man1/wsgi_intercept2.1"
}

