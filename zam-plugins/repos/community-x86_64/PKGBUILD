# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
pkgname=zam-plugins
pkgver=3.10
pkgrel=5
pkgdesc="Collection of LADSPA/LV2/VST/JACK audio plugins for high-quality processing"
arch=('x86_64')
url="https://github.com/zamaudio/zam-plugins"
license=('GPL2')
groups=('ladspa-plugins' 'lv2-plugins' 'pro-audio' 'vst-plugins')
depends=('jack' 'libglvnd' 'liblo' 'zita-convolver')
makedepends=('git' 'ladspa' 'libx11' 'libgl' 'lv2')
source=("$pkgname-$pkgver::git+https://github.com/zamaudio/${pkgname}.git#tag=${pkgver}"
        "git+https://github.com/distrho/dpf.git"
        'zita-convolver4.patch'
        'system-zita-convolver.patch')
sha512sums=('SKIP'
            'SKIP'
            '21bba1704ddf912146e3a6ce58752236f40695f592cffd50814cbc8083e8d97d8ed4791362990883a59f98b538bde32c320059f5db58061dbbcfddad9003ee02'
            'bb3f3f2f0c5ce0c80755016566ffc2ea1436646ab69cd6f8cd82ea1b221d67490599264964edca062a9e3c19861f99d7c8f12cf51829e0432a9658c8adcf63f3')

prepare() {
  cd "$pkgname-$pkgver"
  git submodule init
  git config submodule.dpf.url "${srcdir}/dpf"
  git submodule update
  patch -Np1 -i ../zita-convolver4.patch
  patch -Np1 -i ../system-zita-convolver.patch
}

build() {
  cd "$pkgname-$pkgver"
  export USE_SYSTEM_LIBS=1
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" PREFIX='/usr' install
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {README.md,changelog}
}
