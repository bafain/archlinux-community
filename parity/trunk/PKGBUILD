# $Id$
# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=parity
pkgver=1.11.10
_commit=31720e6151d4fb6c0fce4aa670fb543e669a11ab
pkgrel=1
pkgdesc='Fast, light, robust Ethereum implementation'
arch=('x86_64')
url='https://parity.io/'
license=('GPL3')
depends=('libsystemd')
makedepends=('cmake' 'git' 'rust' 'systemd')
source=("git+https://github.com/paritytech/parity.git#commit=${_commit}"
        'parity.service')
sha256sums=('SKIP'
            '2b3714df9417fa11b7636114b8a5f126c2f6fd3934e74d81bdc85061e8d6e737')

build() {
    cd ${pkgname}

    cargo build --release --features final
    cargo build --release -p evmbin
    cargo build --release -p ethstore-cli
    cargo build --release -p ethkey-cli
}

package() {
    cd ${pkgname}

    install -Dm755 -t "${pkgdir}"/usr/bin \
        target/release/{ethstore,parity,parity-evm}
    install -Dm755 target/release/ethkey "${pkgdir}"/usr/bin/parity-ethkey
    install -Dm644 -t "${pkgdir}"/usr/lib/systemd/user ../parity.service
}
