# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=treefrog-framework
pkgver=1.20.0
pkgrel=1
pkgdesc="High-speed C++ MVC Framework for Web Application"
arch=('x86_64')
url="https://github.com/treefrogframework/treefrog-framework"
license=('BSD')
depends=('qt5-declarative' 'libmongoc')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/treefrogframework/treefrog-framework/archive/v$pkgver.tar.gz")
sha512sums=('3232816c0a2c1a65d0ed23777004f0109c88eea98b8ce25136596dfa1012f76964f8ccfce8a23a0152e20d209ddb98684d5724a5947d9bbdcaa712fb795ccfc7')

prepare() {
  cd $pkgname-$pkgver
  rm -r 3rdparty

  sed -i 's|LIBS += ../3rdparty/mongo-c-driver/libmongoc.a|LIBS += -lmongoc-1.0\nINCLUDEPATH += /usr/include/libbson-1.0 /usr/include/libmongoc-1.0|' src/corelib.pro
  sed -i 's|exit $RET||' configure
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  
  cd src
  make
}

package() {
  cd $pkgname-$pkgver/src
  make INSTALL_ROOT="$pkgdir" install

  install -Dm644 ../copyright "$pkgdir"/usr/share/licenses/$pkgname/copyright
}
