# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Mort Yao <soi@mort.ninja>
# Contributor: Alex Merry <dev@randomguy3.me.uk>

pkgname=polyml
pkgver=5.7.1
pkgrel=2
pkgdesc='Standard ML compiler originally written in an experimental language called Poly'
arch=('x86_64')
url='https://polyml.org/'
license=('LGPL')
conflicts=('mesa-demos') # /usr/bin/poly
options=('!libtool')
depends=('gmp' 'libffi')
install=polyml.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('d18dd824b426edaed1cec71dded354b57df9ebdbd38863bc7540a60bd0667028')

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --enable-shared \
    --with-system-libffi \
    --with-gmp
  make
}

check() {
  make -C "$pkgname-$pkgver" -k check
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# getver: polyml.org
# vim: ts=2 sw=2 et:
