# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jim Bridgewater <jwbwater at gmail dot com>
# Contributor: delta48 <dark.magician.48[at]gmail[dot]com>

pkgbase=python-pyperclip
pkgname=('python-pyperclip' 'python2-pyperclip')
pkgver=1.6.4
_commit=16b1f5b7ece36a08adc5ddec1ef6b1b0aa9de77f
pkgrel=2
pkgdesc="A cross-platform clipboard module for Python"
arch=('any')
url="https://github.com/asweigart/pyperclip"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
# GTK/Qt tests crash xvfb, and Klipper tests require a running klipper
checkdepends=('xorg-server-xvfb' 'python-pytest' 'python2-pytest' 'xsel' 'xclip')
source=("$pkgbase-$_commit.tar.gz::https://github.com/asweigart/pyperclip/archive/$_commit.tar.gz")
sha512sums=('2e8213503d83e9e4e676eefd2336cfa2d92b8c7e28c4d0100361ae9814d1378f70c2b6e9112fb29ed68aa7af95b7fc0784f2c927304bde175b21cbfdc94f8bd6')

prepare() {
  mv pyperclip-{$_commit,$pkgver}
  cp -a pyperclip-$pkgver{,-py2}
}

check() {
  cd "$srcdir"/pyperclip-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" xvfb-run py.test

  cd "$srcdir"/pyperclip-$pkgver-py2
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" xvfb-run py.test2 || warning "https://github.com/asweigart/pyperclip/issues/129"
}

package_python-pyperclip() {
  depends=('python')
  optdepends=('xclip: xclip backend'
              'xsel: xsel backend'
              'plasma-workspace: klipper backend')

  cd "$srcdir"/pyperclip-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-pyperclip() {
  depends=('python2')
  optdepends=('xclip: xclip backend'
              'xsel: xsel backend'
              'plasma-workspace: klipper backend'
              'pygtk: gtk backend')

  cd "$srcdir"/pyperclip-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

# vim:set ts=2 sw=2 et:
