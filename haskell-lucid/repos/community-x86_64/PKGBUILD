# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=lucid
pkgname=haskell-lucid
pkgver=2.9.10
pkgrel=30
pkgdesc="Clear to write, read and edit DSL for HTML"
url="https://github.com/chrisdone/lucid"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-blaze-builder' 'haskell-hashable' 'haskell-mmorph'
         'haskell-unordered-containers')
makedepends=('ghc' 'haskell-bifunctors' 'haskell-hspec' 'haskell-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('0c1a98f9c78d860bcd34e627d7cf0b32692258addebf4b3f90b3a7031ba5386cf58f012b3505aade0bf1d1913efc86990fd45f329c8ae8ab83f8a53215aa8209')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
