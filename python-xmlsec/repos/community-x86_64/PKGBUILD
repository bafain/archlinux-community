# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-xmlsec
pkgver=1.3.3
pkgrel=1
pkgdesc="Python bindings for the XML Security Library"
url="https://github.com/mehcode/python-xmlsec"
license=('MIT')
arch=('x86_64')
depends=('python-pkgconfig' 'python-lxml' 'xmlsec')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mehcode/python-xmlsec/archive/$pkgver.tar.gz")
sha512sums=('501c1a5e7e7d28f9838914bacc77765346dfa4a97ad1772a4d0c62882796da37c819d329c46fcf16c1f9d64c62e175368208e51d74f267bf5764b70f724ecea2')

build() {
  cd python-xmlsec-$pkgver
  python setup.py build
}

check() {
  cd python-xmlsec-$pkgver
  python setup.py pytest || warning "https://github.com/mehcode/python-xmlsec/issues/84"
}

package() {
  cd python-xmlsec-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
