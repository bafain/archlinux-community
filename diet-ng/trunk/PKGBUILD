# $Id$
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=diet-ng
pkgver=1.5.0
pkgrel=2
pkgdesc='Generic compile-time template system based on an XML-like structure for D'
arch=('x86_64')
url='https://github.com/rejectedsoftware/diet-ng'
license=('MIT')
depends=('liblphobos')
makedepends=('meson' 'ldc')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('ffeedbf50c693dc4d9df04070d9528cec173e6351e542ef0ce411845b595713f071f0af7c97517af566a251f37bf43c065340ce39a9d6c0606555dd67536f961')

build() {
  mkdir -p $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  export DC=ldc

  arch-meson ..

  ninja
}

check() {
  cd $pkgname-$pkgver/build

  ninja test
}

package() {
  cd $pkgname-$pkgver/build

  DESTDIR="$pkgdir" ninja install

  install -Dm 644 ../LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

