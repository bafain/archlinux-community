# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Sebastien Luttringer <seblu+arch@seblu.net>
# Contributor: Jochen Immend≈ìrfer <jochen.immendoerfer@gmail.com>

pkgname=alsa-tools
pkgver=1.1.6
pkgrel=3
pkgdesc="Advanced tools for certain sound cards"
arch=('x86_64')
url="https://alsa-project.org/"
license=('GPL2')
depends=('alsa-lib' 'gcc-libs')
makedepends=('fltk' 'gtk2' 'gtk3' 'python2-pyalsa')
optdepends=('fltk: hdspconf, hdspmixer'
            'gtk2: envy24control, echomixer, rmeddigicontrol'
            'gtk3: hdajackretask'
            'python2-pyalsa: hwmixvolume'
            'pygtk: hwmixvolume'
            'python2-gobject2: hwmixvolume')
provides=("$pkgname-emu10k1" "$pkgname-ice1712")
source=("ftp://ftp.alsa-project.org/pub/tools/$pkgname-$pkgver.tar.bz2")
sha512sums=('33617ec6726ca11f66849c893a8dd9bf9faa8832374788819d9280159ac358c83443331faac24881df593cfd0b24b22769a41cbcb0af3c1fd781baab0c802d17')

# qlo10k1 is removed, because it's unable to build
_tools=( 'as10k1' 'envy24control' 'hdsploader' 'hdspconf' 'hdspmixer'
'mixartloader' 'pcxhrloader' 'rmedigicontrol' 'sb16_csp' 'seq/sbiload'
'sscape_ctl' 'us428control usx2yloader' 'vxloader' 'echomixer' 'ld10k1'
'hwmixvolume' 'hdajackretask' 'hda-verb' 'hdajacksensetest')

prepare() {
  cd "$pkgname-$pkgver"
  for tool in ${_tools[@]}; do
    (
    cd $tool
    autoreconf -vfi
    )
  done
}

build() {
  cd "$pkgname-$pkgver"
  # not using upstream's global makefile as it's trying to be too clever
  for tool in ${_tools[@]}; do
    (
    cd $tool
    ./configure --prefix=/usr --sbindir=/usr/bin
    make
    )
  done
}

package() {
  cd "$pkgname-$pkgver"
  for tool in ${_tools[@]}; do
    (
    cd $tool
    make DESTDIR="$pkgdir" install
    )
  done
}

# vim:set ts=2 sw=2 et:
