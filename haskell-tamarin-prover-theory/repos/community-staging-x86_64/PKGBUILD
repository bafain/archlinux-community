# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=tamarin-prover-theory
pkgname=haskell-tamarin-prover-theory
pkgver=1.4.0
pkgrel=34
pkgdesc="Security protocol types and constraint solver library for the tamarin prover"
url="http://www.infsec.ethz.ch/research/software/tamarin"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-aeson-pretty' 'haskell-dlist' 'haskell-fclabels'
         'haskell-parallel' 'haskell-safe'
         'haskell-uniplate' 'haskell-tamarin-prover-utils' 'haskell-tamarin-prover-term')
makedepends=('ghc')
source=("tamarin-prover-$pkgver.tar.gz::https://github.com/tamarin-prover/tamarin-prover/archive/$pkgver.tar.gz"
        ghc-8.4.patch)
sha512sums=('7c1afe6a53b596c2ce01e9ad7a7f464af1f4efbc5f8edc13d5ec8bc32ce4e91ddde91dff6ab8e01cf3cf30a37a3a18953d937debc36c9df664f718d968e2ae74'
            '5e18015155a1ac940591d7c28f3741cf56d9dd36f530895b3d9fec7865563db5c10524252d496b3d4a59c9f1ad2a9db4e2a56f31b73cf91ce1a2e698c3d6dadf')

prepare() {
    cd tamarin-prover-$pkgver
    patch -p1 -i ../ghc-8.4.patch
}

build() {
    cd "${srcdir}/tamarin-prover-${pkgver}/lib/theory"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    # haddock failed to generate
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/tamarin-prover-${pkgver}/lib/theory"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
