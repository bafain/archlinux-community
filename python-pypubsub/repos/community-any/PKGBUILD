# $Id$
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

pkgname=python-pypubsub
_pkgname=${pkgname#python-}
pkgver=4.0.0
pkgrel=1
pkgdesc="Python publish-subcribe library"
arch=('any')
url="https://github.com/schollii/pypubsub"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-asyncio')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('d5759d0b359dac66819251be61c58958fcfc9948e68faedb5f7f2e5a5919bcbbabf5556867c881c3db5f9069b6d6b9b6827e6331dd5f900c329e1e80d1f0044d')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver/tests/suite

  PYTHONPATH=$PWD/../../src python -m pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir"  --optimize=1 --skip-build

  install -Dm 644 src/pubsub/LICENSE_BSD_Simple.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
