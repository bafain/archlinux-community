# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-vcrpy
pkgname=('python-vcrpy' 'python2-vcrpy')
pkgver=1.13.0
pkgrel=1
pkgdesc='Automatically mock your HTTP interactions to simplify and speed up testing'
arch=('any')
license=('MIT')
url='https://github.com/kevin1024/vcrpy'
makedepends=('python-setuptools' 'python2-setuptools' 'python-yaml' 'python2-yaml' 'python-wrapt'
             'python2-wrapt' 'python-six' 'python2-six' 'python-yarl' 'python2-contextlib2'
             'python2-mock')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pytest-httpbin'
              'python2-pytest-httpbin' 'python-mock')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/kevin1024/vcrpy/archive/v$pkgver.tar.gz"
        python3.7.patch)
sha512sums=('dda875faa6c5e29c26c774b45b27e046437cf9add0b7e73bd5be545062dbde46b91e90fbc0dafe1efd5c76e447068fabd3018c6ff35db1e8bb009e50a72bd341'
            '369676b91ec3f0118112391d6b30bf4787211ec8fe9e35b79eb32b8179b02f70495c6b7d58e4d756c8530038a3fc7fc288cc636cd4a78193ecc8798b28d238dc')

prepare() {
  patch -d vcrpy-$pkgver -p1 -i "$srcdir"/python3.7.patch
  cp -a vcrpy-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/vcrpy-$pkgver
  python setup.py build

  cd "$srcdir"/vcrpy-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/vcrpy-$pkgver
  python setup.py pytest

  cd "$srcdir"/vcrpy-$pkgver-py2
  python2 setup.py pytest
}

package_python-vcrpy() {
  depends=('python-yaml' 'python-wrapt' 'python-six' 'python-yarl')

  cd vcrpy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-vcrpy() {
  depends=('python2-yaml' 'python2-wrapt' 'python2-six' 'python2-contextlib2' 'python2-mock')

  cd vcrpy-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

# vim:set ts=2 sw=2 et:
