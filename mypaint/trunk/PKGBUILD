# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jon Nordby <jononor@gmail.com>

pkgname=mypaint
pkgver=1.2.1
pkgrel=6
pkgdesc="Fast and easy painting application for digital painters, with brush dynamics"
arch=('x86_64')
url="http://mypaint.org/"
license=('GPL')
depends=('gtk3' 'python2-cairo' 'python2-gobject' 'python2-numpy')
makedepends=('scons' 'swig')
source=(https://github.com/mypaint/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz{,.asc}
        fix-wayland.patch)
validpgpkeys=('C02391F4BBA4F0E2B27C6BFF6E3037E12878B299') # Andrew Chadwick
sha512sums=('1783765e495b9535669a83a29d27ba684e6201fdfe0875bdd2c167b1dab8af05993b19025767fe0b2770ed9e578e4709f75d8a4820ac945812cba85609f9eb9b'
            'SKIP'
            '8aa0b865c577400039eeba5f7b54d8db4e4baa6e395691db080d43ca803a793e549cb7f5396a2f6a58fa60de8614f9d84cd1b0f55dc71699b685daf2beb2ef9c')

prepare() {
  cd $pkgname-$pkgver
  # Fix run under wayland
  # https://github.com/mypaint/mypaint/issues/791
  patch -Np1 -i ../fix-wayland.patch
}

build() {
  cd $pkgname-$pkgver
  scons
}

package() {
  cd $pkgname-$pkgver
  scons prefix="$pkgdir"/usr install
}
