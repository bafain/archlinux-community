# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=blinksocks
pkgver=3.3.6
pkgrel=1
pkgdesc='A framework for building composable proxy protocol stack.'
arch=('any')
url='https://github.com/blinksocks/blinksocks'
license=('Apache')
depends=('nodejs' 'semver')
makedepends=('npm')
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz
        blinksocks@.service)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('b62f4fc576b238d05983e1b1c9f1710f0aa3b0978e0a8cf71ff66ee9d024243ac74445b88c72bfc4390fdf4b77cf927c3e291988d20740117e1fecb8f8c5dbc1'
            '709153bb95492cf0d6fa070ad8eef16220a97896731f688ec79e03cfddf112b4475fff589490740a8dae33b7bc4c1a331a4baf0d190eaf219f193fbb8128abeb')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Why 777? :/
  chmod -R u=rwX,go=rX "$pkgdir"

  rm -r "$pkgdir"/usr/lib/node_modules/blinksocks/node_modules/{,.bin/}semver

  install -Dm644 "$srcdir/blinksocks@.service" "$pkgdir/usr/lib/systemd/system/blinksocks@.service"
}
