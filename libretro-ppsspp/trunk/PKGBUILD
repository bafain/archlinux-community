# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Duck Hunt <vaporeon@tfwno.gf>

pkgname=libretro-ppsspp
pkgver=24309
pkgrel=1
pkgdesc='Sony PlayStation Portable core'
arch=('x86_64')
url='https://github.com/libretro/libretro-ppsspp'
license=('GPL2')
groups=('libretro')
depends=('libgl' 'libretro-core-info' 'sdl2' 'snappy' 'zlib')
makedepends=('cmake' 'git')
_commit='ad1b75292f9fd4a3a868d16dc25ce357e31cc177'
source=("git+https://github.com/hrydgard/ppsspp.git#commit=${_commit}"
        'git+https://github.com/hrydgard/ppsspp-ffmpeg.git'
        'ppsspp-glslang::git+https://github.com/hrydgard/glslang.git'
        'git+https://github.com/hrydgard/ppsspp-lang.git'
        'git+https://github.com/Kingcom/armips.git'
        'git+https://github.com/KhronosGroup/SPIRV-Cross.git')
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd ppsspp

  git rev-list --count HEAD
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build

  cd ppsspp

  for submodule in assets/lang ext/glslang ffmpeg; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../ppsspp-${submodule#*/}
    git submodule update ${submodule}
  done
  for submodule in ext/{SPIRV-Cross,armips}; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../${submodule#*/}
    git submodule update ${submodule}
  done
}

build() {
  cd build

  cmake ../ppsspp \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_SKIP_RPATH='ON' \
    -DLIBRETRO='ON'
  make
}

package() {
  cd build

  install -Dm 644 lib/ppsspp_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 ../ppsspp/LICENSE.TXT -t "${pkgdir}"/usr/share/licenses/libretro-ppsspp-git/
}

# vim: ts=2 sw=2 et:
