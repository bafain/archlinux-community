# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gordian Edenhofer <gordian.edenhofer[at]yahoo[dot]de>

pkgname=(python-pyrfc3339 python2-pyrfc3339)
pkgver=1.1
pkgrel=2
pkgdesc="Parses and generates RFC 3339-compliant timestamps using Python datetime.datetime objects."
arch=('any')
license=('MIT')
url="https://pypi.python.org/pypi/pyRFC3339"
makedepends=('python-setuptools' 'python2-setuptools' 'python-pytz' 'python2-pytz')
checkdepends=('python-nose' 'python2-nose')
source=("https://pypi.python.org/packages/source/p/pyRFC3339/pyRFC3339-${pkgver}.tar.gz"
        LICENSE.txt)
sha512sums=('958b7761fab590aa42bb57a955c5d834441f717796a452b60df21663099dcf2fc046afe60f8157fd0f1edfd95c5e9c9c5349ab10ca4078d210fc63d848496a2f'
            '73c58b87e14593ee283cc323a93820c18a00e9af4e5027687fc8f6cd5735c98f341c6ac1aa90c040d0899766cabb976a85201b171874b4b81934ff02a9728f2d')

prepare() {
  cp -a pyRFC3339-$pkgver{,-py2}
}

build() {
  cd "$srcdir/pyRFC3339-$pkgver"
  python setup.py build

  cd "$srcdir/pyRFC3339-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/pyRFC3339-$pkgver"
  python setup.py test

  cd "$srcdir/pyRFC3339-$pkgver-py2"
  python2 setup.py test
}

package_python-pyrfc3339() {
  depends=('python-pytz')

  cd "$srcdir/pyRFC3339-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
  install -D -m644 ../LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_python2-pyrfc3339() {
  depends=('python2-pytz')

  cd "$srcdir/pyRFC3339-$pkgver-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -D -m644 ../LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
