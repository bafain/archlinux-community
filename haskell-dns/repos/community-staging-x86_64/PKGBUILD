# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=dns
pkgname=haskell-dns
pkgver=3.0.4
pkgrel=26
pkgdesc="DNS library in Haskell"
url="https://hackage.haskell.org/package/${_hkgname}"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-auto-update' 'haskell-attoparsec'
         'haskell-base64-bytestring' 'haskell-cryptonite' 'haskell-iproute'
         'haskell-network' 'haskell-psqueues' 'haskell-safe')
makedepends=('ghc' 'haskell-doctest' 'haskell-hspec' 'haskell-word8' 'haskell-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('41cfecf8701d82ac3895ba03b53225b3273f166289472cde3a0a399384d05e83bfef5efba1b2ad8cfe2f180a895b144fd275b514430cc219a906053999e8d806')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    # https://github.com/kazu-yamamoto/dns/issues/102
    runhaskell Setup test || warning "Tests failed"
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
