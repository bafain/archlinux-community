# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgname=matrix-synapse
pkgver=0.33.4
pkgrel=1

pkgdesc="Matrix reference homeserver"
url="https://github.com/matrix-org/synapse"
arch=('any')
license=('Apache')

depends=('python2-jsonschema' 'python2-twisted' 'python2-service-identity'
         'python2-pyopenssl' 'python2-yaml' 'python2-pyasn1' 'python2-pynacl'
         'python2-daemonize' 'python2-bcrypt' 'python2-frozendict'
         'python2-pillow' 'python2-pydenticon' 'python2-ujson'
         'python2-pysaml2' 'python2-setuptools'
         'python2-systemd' 'python2-unpaddedbase64' 'python2-canonicaljson'
         'python2-signedjson' 'python2-pymacaroons-pynacl'
         'python2-service-identity' 'python2-msgpack'
         'python2-phonenumbers' 'python2-prometheus_client'
         'python2-attrs' 'python2-netaddr' 'python2-sortedcontainers'
         'systemd')
makedepends=('python2-mock')
checkdepends=('python2-lxml')
optdepends=('python2-psycopg2: PostgreSQL support'
            'python2-lxml: URL previewing'
            'python2-jinja: e-mail notifications'
            'python2-bleach: e-mail notifications'
            'python2-psutil: metrics'
            'python2-matrix-angular-sdk: built-in web client')

source=("synapse-$pkgver.tar.gz::https://github.com/matrix-org/synapse/archive/v$pkgver.tar.gz"
        'sysusers-synapse.conf')

md5sums=('e812301aa398114f9e06004f7639c8ee'
         'ecd9f66fb57fe1a2e1e2df07a460a35b')

backup=('etc/synapse/log_config.yaml')
install=synapse.install

build() {
	cd synapse-$pkgver
	python2 setup.py build
}

check() {
	cd synapse-$pkgver
	PYTHONPATH=. trial tests
}

package() {
	cd synapse-$pkgver
	python2 setup.py install --root "$pkgdir" --optimize=1 --skip-build

	install -dm755 -o 198 -g 198 "$pkgdir"/etc/synapse
	install -Dm644 contrib/systemd/log_config.yaml "$pkgdir"/etc/synapse/log_config.yaml
	install -Dm644 contrib/systemd/synapse.service "$pkgdir"/usr/lib/systemd/system/synapse.service
	install -Dm644 "$srcdir"/sysusers-synapse.conf "$pkgdir"/usr/lib/sysusers.d/synapse.conf
}
