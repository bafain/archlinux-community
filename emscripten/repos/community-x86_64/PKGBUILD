# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Vlad Kolotvin <vlad.kolotvin@gmail.com>

pkgname=emscripten
pkgver=1.37.34
pkgrel=1
pkgdesc="LLVM-based project that compiles C and C++ into highly-optimizable JavaScript for the web"
arch=('x86_64')
url="http://emscripten.org"
license=('custom')
depends=('nodejs' 'python' 'python2')
makedepends=('cmake' 'libxml2')
optdepends=('java-environment: for using clojure'
            'ruby: for using websockify addon')
install=$pkgname.install
source=(emscripten-$pkgver.tar.gz::"https://github.com/kripken/emscripten/archive/$pkgver.tar.gz"
        emscripten-fastcomp-$pkgver.tar.gz::"https://github.com/kripken/emscripten-fastcomp/archive/$pkgver.tar.gz"
        emscripten-fastcomp-clang-$pkgver.tar.gz::"https://github.com/kripken/emscripten-fastcomp-clang/archive/$pkgver.tar.gz"
        "emscripten.sh")
sha512sums=('afd0adf606f4ab2a3ea27dce2d9fee644bb20e779ea2013c13c26751ec023acbe26583e3bbef595c7c514181a59cebd5dd766165fffc40445cd28ea9b4aab74b'
            '4711c499b7d4e7f5db4d17ca688e9f9bcd892c3178cc867c5e2e75780fc5dd3673bfbb830c1df9fe94a35d27273ecf377b49fd1ccf9ee6447f8902e1f785f81c'
            '3b03fd26a2aa7840531f74ea5fb6821e4f3ddeb43b18be50a497339e42c6a9d8b9e57c9433a4de16d669b9175dd925d4d745618a5bcb9023e29ef2f7f3ded825'
            '52007717c21c22f5d9f027268de2516e969f2ffb8e37121f75ca2697b7ddcde6c0be9636d19df7b3c2e89bc769f5361750c62f9fcb84d224cde3c8626bc9c93b')

prepare() {
  cd emscripten-fastcomp-$pkgver

  # reset folder for out-of-source build
  rm -rf build
  mkdir build

  # put clang source into the right place (http://git.io/i1GBkg)
  rm -rf tools/clang
  ln -s "$srcdir"/emscripten-fastcomp-clang-$pkgver tools/clang

  # python2 shebang fixes
  cd ../emscripten-$pkgver
  sed '1s|python$|python2|' -i $(find third_party tools -name \*.py) emrun

  # adapt config file template to use our custom environment variable and path
  sed -e "s|getenv('LLVM')|getenv('EMSCRIPTEN_FASTCOMP')|" \
    -e 's|{{{ LLVM_ROOT }}}|/usr/lib/emscripten-fastcomp|' \
    -i tools/settings_template_readonly.py
}

build() {
  cd emscripten-fastcomp-$pkgver/build

  CC=gcc CXX=g++ cmake .. -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=YES \
    -DLLVM_TARGETS_TO_BUILD="X86;JSBackend" \
    -DLLVM_BUILD_RUNTIME=OFF \
    -DLLVM_INCLUDE_EXAMPLES=OFF \
    -DLLVM_INCLUDE_TESTS=OFF \
    -DCLANG_INCLUDE_TESTS=OFF
  make
}

package() {
  # exported variables
  install -Dm755 "$srcdir"/emscripten.sh "$pkgdir"/etc/profile.d/emscripten.sh

  # LLVM-backend, TODO: include only needed tools
  cd "$srcdir"/emscripten-fastcomp-$pkgver
  install -Dm644 emscripten-version.txt "$pkgdir"/usr/lib/emscripten-fastcomp/emscripten-version.txt
  install -m755 build/bin/* "$pkgdir"/usr/lib/emscripten-fastcomp

  # copy structure
  cd "$srcdir"/emscripten-$pkgver
  install -d "$pkgdir"/usr/lib/emscripten
  cp -rup em* cmake site src system third_party tools "$pkgdir"/usr/lib/emscripten

  # remove clutter
  rm "$pkgdir"/usr/lib/emscripten-fastcomp/{*-test,llvm-lit}
  rm "$pkgdir"/usr/lib/emscripten/*.bat

  # docs
  install -d "$pkgdir"/usr/share/doc
  ln -s /usr/lib/emscripten/site/source/docs "$pkgdir"/usr/share/doc/$pkgname

  # license
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
