# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=dune
pkgver=1.0b17
pkgrel=1
pkgdesc="A composable build system for OCaml (formerly jbuilder)"
arch=('x86_64')
url="https://github.com/ocaml/dune"
license=('Apache')
depends=('glibc')
makedepends=('ocaml-findlib' 'opam')
provides=("jbuilder=${pkgver/b/+beta}")
conflicts=('jbuilder')
replaces=('jbuilder')
source=(${pkgname}-${pkgver/b/+beta}.tar.gz::"${url}/archive/${pkgver/b/+beta}.tar.gz")
sha256sums=('62d24f73101979c0680ad4093f65860792e8600090d04d7132446ab6ae269bec')

build() {
    cd ${pkgname}-${pkgver/b/-beta}
    make release
}

package() {
    cd ${pkgname}-${pkgver/b/-beta}

    # Initialize OPAM
    export OPAMROOT="${srcdir}"/opam
    opam init -n

    # Work around the install command
    export OCAMLFIND_DESTDIR="${pkgdir}$(ocamlfind printconf destdir)"
    install -dm755 ${OCAMLFIND_DESTDIR}
    make INSTALL_ARGS="--prefix='${pkgdir}'/usr --libdir='${OCAMLFIND_DESTDIR}'" install

    # Fix doc and man install
    install -dm755 "${pkgdir}"/usr/share
    mv "${pkgdir}"/usr/{doc,share/}
    mv "${pkgdir}"/usr/{man,share/}
}
