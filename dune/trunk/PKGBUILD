# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Jakob Gahde <j5lx@fmail.co.uk>

pkgname=dune
pkgver=1.0.0
pkgrel=2
pkgdesc="A composable build system for OCaml (formerly jbuilder)"
arch=('x86_64')
url="https://github.com/ocaml/dune"
license=('Apache')
#Note: opam should be removed/moved to makedepends after https://github.com/ocaml/dune/issues/372 is fixed
depends=('glibc' 'ocaml' 'ocaml-findlib' 'opam')
provides=('jbuilder')
conflicts=('jbuilder')
replaces=('jbuilder')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/${pkgver}.tar.gz")
sha256sums=('1bba92d9f1a397dd315a461fd5d3917cba84ec86a2dd164cef112851c80c2e4f')

build() {
    cd ${pkgname}-${pkgver}
    make release
}

# Tests requires a bunch of (currently) unpackaged dependencies
#check() {
#    cd ${pkgname}-${pkgver/b/-beta}
#    make test
#}

package() {
    cd ${pkgname}-${pkgver}

    # Initialize OPAM
    export OPAMROOT="${srcdir}"/opam
    opam init -n

    # Work around the install command
    export OCAMLFIND_DESTDIR="${pkgdir}$(ocamlfind printconf destdir)"
    install -dm755 ${OCAMLFIND_DESTDIR}
    make INSTALL_ARGS="--prefix='${pkgdir}'/usr --libdir='${OCAMLFIND_DESTDIR}'" install

    # Fix doc and man install
    install -dm755 "${pkgdir}"/usr/share
    mv "${pkgdir}"/usr/{doc,share/}
    mv "${pkgdir}"/usr/{man,share/}
}
