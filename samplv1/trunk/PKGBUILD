# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=samplv1
pkgver=0.9.0
pkgrel=1
pkgdesc="Polyphonic sampler synthesizer with stereo fx"
arch=('x86_64')
url="https://samplv1.sourceforge.io/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio' 'vee-one')
depends=('jack' 'liblo' 'lv2' 'qt5-base' 'desktop-file-utils'
'hicolor-icon-theme' 'shared-mime-info')
makedepends=('qt5-tools')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('13d9a06828673cb7f3097892898d7caa900cd7a42dbb3444658809dfa1fa18ce039f0b786fc5f3db3cbde1864460ff56708662c17d0487610b761c76605cd541')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {AUTHORS,ChangeLog,README,TODO}
}

# vim:set ts=2 sw=2 et:
