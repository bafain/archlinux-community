# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jonathan Liu <net147@gmail.com>

pkgbase=python-pyicu
pkgname=('python-pyicu' 'python2-pyicu')
pkgver=2.0.6
pkgrel=1
pkgdesc="Python binding for ICU"
arch=('x86_64')
license=('MIT')
url="http://pyicu.osafoundation.org/"
makedepends=('python-setuptools' 'python2-setuptools' 'icu')
checkdepends=('python-fonttools' 'python2-fonttools' 'python-pytest' 'python2-pytest')
source=("https://pypi.io/packages/source/P/PyICU/PyICU-$pkgver.tar.gz")
sha512sums=('e423f41e17bef12a48551ace8a25c1381bff8a6973d228a59ba985b62c3244228e06275e26dbe5cc67044d0ab67ce65e32d1e49f6d3e13b436636970096f8699')

prepare() {
  cp -a PyICU-$pkgver{,-py2}
}

build() {
  cd "$srcdir/"PyICU-$pkgver
  python setup.py build

  cd "$srcdir/"PyICU-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir/"PyICU-$pkgver
  python setup.py test

  cd "$srcdir/"PyICU-$pkgver-py2
  python2 setup.py test || true # https://github.com/ovalhub/pyicu/issues/61
}

package_python-pyicu() {
  depends=('python' 'icu')

  cd PyICU-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pyicu() {
  depends=('python2' 'icu')

  cd PyICU-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
