# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Python Shell <pythonshell@yeah.net>
# Contributor: paraxor <paraxor5344@gmail.com>
# Contributor: Pranay Kanwar <pranay.kanwar@gmail.com>

pkgname=flawfinder
pkgver=2.0.6
pkgrel=1
pkgdesc='Searches through source code for potential security flaws'
url='http://www.dwheeler.com/flawfinder'
arch=('any')
license=('GPL2')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python2')
source=(http://www.dwheeler.com/flawfinder/flawfinder-${pkgver}.tar.gz)
sha256sums=('d33caeb94fc7ab80b75d2a7a871cb6e3f70e50fb835984e8b4d56e19ede143fc')
sha512sums=('e9597fd8afb9e6209e6a65357466860966cfde863027401c4d14257f192c466dc7472e3a759b32091bab7cc8906506a6b6fad6e8153f823b8d18f032183b84b9')

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's|env python.*|env python|' -i flawfinder
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" prefix=/usr install
  install -Dm 644 README ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
