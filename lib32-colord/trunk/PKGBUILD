# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=lib32-colord
pkgver=1.4.2
pkgrel=1
pkgdesc='System daemon for managing color devices'
arch=('x86_64')
url='https://www.freedesktop.org/software/colord'
license=('GPL2')
depends=('colord' 'lib32-dconf' 'lib32-libgudev' 'lib32-libgusb' 'lib32-lcms2'
         'lib32-polkit' 'lib32-sqlite' 'lib32-dbus')
makedepends=('argyllcms' 'gcc-multilib' 'gobject-introspection' 'intltool'
             'meson' 'python2' 'sane' 'vala' 'git')
options=(!emptydirs)
_commit=5b9aa8de432579a2636f13ad6895928f42511081  # tags/1.4.2^0
source=("git+https://github.com/hughsie/colord#commit=$_commit"
        0001-Make-cd_color_get_blackbody_rgb_full-safer.patch)
sha256sums=('SKIP'
            '73689805d46705c8680d9ae979a2f5203f661741e56aa07dc627b36059ab404b')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17')  # Richard Hughes

pkgver() {
  cd colord
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd colord
  patch -Np1 -i ../0001-Make-cd_color_get_blackbody_rgb_full-safer.patch
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  arch-meson colord build \
    --libdir=/usr/lib32 \
    -Denable-bash-completion=false \
    -Denable-docs=false \
    -Denable-man=false \
    -Denable-tests=false \
    -Denable-libcolordcompat=true \
    -Denable-sane=false \
    -Denable-vala=true \
    -Denable-print-profiles=false \
    -Dwith-daemon-user=colord
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  rm -r "$pkgdir"/usr/{bin,include,lib,share}
}

# vim:set ts=2 sw=2 et:
