# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=spring
pkgver=104.0
pkgrel=3
pkgdesc='A free 3D real-time-strategy (RTS) game engine'
arch=('x86_64')
url="http://springrts.com/"
license=('GPL')
depends=('openal' 'glew' 'boost-libs' 'freetype2' 'devil' 'libvorbis' 'sdl2' 'libunwind'
         'libxcursor' 'curl' 'shared-mime-info' 'desktop-file-utils' 'libx11')
makedepends=('boost' 'cmake' 'zip' 'xz' 'p7zip' 'python2' 'jdk8-openjdk' 'mesa')
optdepends=('python2: python-based bots'
            'java-runtime: java-based bots')
source=(https://downloads.sourceforge.net/sourceforge/springrts/${pkgname}_${pkgver}_src.tar.lzma
        fix-reinterpret_cast-in-constexpr.patch)
sha512sums=('be8922e798d7c959c248501494ab61cff625154c109debd324875a4e2a7149748224b6fcb428948f2b4d66cc9e484bc002a63810f55c5f25a499c34a01dfc225'
            'ba8a6f3031c7507983f2ae10de9f19b27434cd9159df6ce431d1f83b21b37974e8add4bea8bc0d738f88f5bfebdbeeacaeb3964b9db31b5181d12bed66ff2109')

prepare() {
  cd spring_$pkgver

  # https://springrts.com/mantis/view.php?id=5781
  # https://github.com/spring/spring/commit/6104061fe3
  patch -Np1 -i ../fix-reinterpret_cast-in-constexpr.patch
}

build() {
  cd spring_${pkgver}

  cmake	. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDATADIR=share/spring \
    -DJAVA_HOME=/usr/lib/jvm/java-8-openjdk \
    -DCMAKE_SKIP_RPATH=ON
  make
}

package() {
  cd spring_${pkgver}

  make DESTDIR="$pkgdir" install

  install -d "$pkgdir/etc/spring"
  echo '$HOME/.spring' > "$pkgdir/etc/spring/datadir"
}

# vim sw=2:ts=2 et:
