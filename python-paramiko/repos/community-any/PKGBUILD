# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Mario Danic <mario.danic@gmail.com>

pkgbase=python-paramiko
pkgname=(python-paramiko python2-paramiko)
pkgver=2.4.1
pkgrel=2
pkgdesc="Python module that implements the SSH2 protocol"
url="https://github.com/paramiko/paramiko/"
license=('LGPL')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-bcrypt' 'python2-bcrypt'
             'python-cryptography' 'python2-cryptography' 'python-pyasn1' 'python2-pyasn1'
             'python-pynacl' 'python2-pynacl')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pytest-relaxed'
              'python2-pytest-relaxed')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/paramiko/paramiko/archive/$pkgver.tar.gz")
sha512sums=('5a580d25e7911937f228ef4a2b89b5a1efd771f576c6c68cf1c36e1abbc38adbc6ad5dc91400de7112238fcf7803f5f26c13fe10be4af8a2a86af9398da7a8de')

prepare() {
  cp -a paramiko-$pkgver{,-py2}

  find paramiko-$pkgver-py2 -name '*.py' -print0 | xargs -0 \
    sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env python2,' \
        -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'
}

check() {
  cd "$srcdir"/paramiko-$pkgver
  LANG=en_US.UTF-8 python setup.py pytest

  cd "$srcdir"/paramiko-$pkgver-py2
  LANG=en_US.UTF-8 python2 setup.py pytest
}

package_python-paramiko() {
  depends=('python-bcrypt' 'python-cryptography' 'python-pyasn1' 'python-pynacl')

  cd paramiko-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1
  install -dm755 "$pkgdir"/usr/share/doc/$pkgname/demos
  install -m644 demos/* "$pkgdir"/usr/share/doc/$pkgname/demos
  chmod 755 "$pkgdir"/usr/share/doc/$pkgname/demos/*.py
}

package_python2-paramiko() {
  depends=('python2-bcrypt' 'python2-cryptography' 'python2-pyasn1' 'python2-pynacl')

  cd paramiko-$pkgver-py2

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -dm755 "$pkgdir"/usr/share/doc/$pkgname/demos
  install -m644 demos/* "$pkgdir"/usr/share/doc/$pkgname/demos
  chmod 755 "$pkgdir"/usr/share/doc/$pkgname/demos/*.py
}
