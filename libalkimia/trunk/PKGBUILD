# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: gumper <gumper1034@gmail.com>

pkgname=libalkimia
pkgver=7.0.2
pkgrel=1
pkgdesc="A library with common classes and functionality used by finance applications for the KDE SC"
arch=('x86_64')
url="https://community.kde.org/Alkimia/libalkimia"
license=('LGPL')
depends=('qt5-base')
makedepends=('cmake' 'doxygen' 'extra-cmake-modules')
source=(https://ftp.acc.umu.se/mirror/kde.org/ftp/stable/alkimia/$pkgver/alkimia-$pkgver.tar.xz{,.sig})
validpgpkeys=('D69A745A55331F44F404D8258D4DE062AA2EB01C') #Thomas Baumgart <thb@net-bembel.de>
sha256sums=('59e6b10d819479bc8dde53a8b10e6ec05e9d465c5e035528a5c0b036456a2454'
            'SKIP')

build() {
  cd "${srcdir}"

  mkdir build
  cd build

  cmake ../alkimia-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -Wno-dev
  make
}

package() {
  cd "${srcdir}"/build

  make DESTDIR="${pkgdir}" install
}
