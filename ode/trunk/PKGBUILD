# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Adam Griffiths <adam_griffithsAATTdart.net.au>

pkgname=ode
pkgver=0.15.2
pkgrel=1
pkgdesc="An open source, high performance library for simulating rigid body dynamics"
arch=('x86_64')
url="https://bitbucket.org/odedevs/ode/"
license=('LGPL' 'BSD')
depends=('gcc-libs' 'bash')
source=("https://bitbucket.org/odedevs/${pkgname}/downloads/${pkgname}-${pkgver}.tar.gz")
sha256sums=('2eaebb9f8b7642815e46227956ca223806f666acd11e31708bd030028cf72bac')

build() {
  cd ${pkgname}-${pkgver}

  ./bootstrap
  ./configure --prefix=/usr \
              --enable-shared \
              --enable-libccd \
              --enable-double-precision
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir}/ install

  install -Dm0644 ${srcdir}/${pkgname}-${pkgver}/ou/LICENSE-BSD.TXT \
                  ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-BSD.TXT
  install -Dm0644 ${srcdir}/${pkgname}-${pkgver}/GIMPACT/GIMPACT-LICENSE-BSD.TXT \
                  ${pkgdir}/usr/share/licenses/${pkgname}/GIMPACT-LICENSE-BSD.TXT
  install -Dm0644 ${srcdir}/${pkgname}-${pkgver}/libccd/BSD-LICENSE \
                  ${pkgdir}/usr/share/licenses/${pkgname}/BSD-LICENSE
}
