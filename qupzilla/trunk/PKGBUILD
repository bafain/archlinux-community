# $Id$
# Maintainer: speps <speps at aur dot archlinux dot org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=qupzilla
pkgver=2.2.5
pkgrel=2
pkgdesc="Cross-platform QtWebEngine browser"
arch=(x86_64)
url="https://www.qupzilla.com/"
license=(GPL3)
depends=(qt5-webengine qt5-x11extras qt5-svg)
makedepends=(qt5-tools kwallet libgnome-keyring)
optdepends=('kwallet: kf5 kwallet integration' 'libgnome-keyring: gnome keyring integration')
source=(https://github.com/QupZilla/qupzilla/releases/download/v$pkgver/QupZilla-$pkgver.tar.xz
        https://github.com/QupZilla/qupzilla/commit/f3b770d2.patch)
sha256sums=('b1b5bf6b3061a76d1add3c072753cc088140eb0fafcf24b9a0c4951b4d8948f6'
            '497268175711ff833451943a40803892c18388ca7ab8fb86c8277c26961c678b')

prepare() {
  cd QupZilla-$pkgver
  patch -p1 -i ../f3b770d2.patch # https://github.com/QupZilla/qupzilla/issues/2578
}

build() {
  cd QupZilla-$pkgver
  export USE_WEBGL=true \
    QUPZILLA_PREFIX=/usr/ \
    KDE_INTEGRATION=true \
    GNOME_INTEGRATION=true
  qmake
  make
}

package() {
  cd QupZilla-$pkgver
  make INSTALL_ROOT="$pkgdir/" install

  # zsh completion
  install -Dm644 linux/completion/_$pkgbase \
    "$pkgdir/usr/share/zsh/site-functions/_$pkgbase"
}
