# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test-mockmodule
pkgver=0.170.0
pkgrel=1
pkgdesc="Override subroutines in a module for unit testing"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-super')
makedepends=('perl-module-build')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage' 'perl-test-warnings')
url='http://search.cpan.org/dist/Test-MockModule'
source=("http://search.cpan.org/CPAN/authors/id/G/GF/GFRANKS/Test-MockModule-v$pkgver.tar.gz")
sha512sums=('e62f79eb5c737838a0276bb1de5301e3f2eceef4f9b80fde0678e16490419f2d02afd24ab292e938b923a7d214e3aa60e73baf0d17da7047ca37064111c3ff79')

build() {
  cd "$srcdir/Test-MockModule-v$pkgver"
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd "$srcdir/Test-MockModule-v$pkgver"
  perl Build test
}

package() {
  cd "$srcdir/Test-MockModule-v$pkgver"
  perl Build install destdir="$pkgdir"
}

# vim:set ts=2 sw=2 et:
