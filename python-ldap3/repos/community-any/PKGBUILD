# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgbase=python-ldap3
pkgname=(python-ldap3 python2-ldap3)
pkgver=2.5.1
pkgrel=1
pkgdesc="A strictly RFC 4510 conforming LDAP V3 pure Python client library"
url="https://github.com/cannatag/ldap3"
license=('LGPL')
arch=('any')
makedepends=('python-pyasn1' 'python2-pyasn1' 'python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/cannatag/ldap3/archive/$pkgver.tar.gz")
sha512sums=('15b49efee10ae34d7a0a4ca156bbed1bec1e86cfd918b1e6853bee0fd3cb2c82246b04328964079c1681a9a8a2fd212208250007a68b10abbd4f11c84a140a11')

prepare() {
  cp -a ldap3-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/ldap3-$pkgver
  python setup.py build

  cd "$srcdir"/ldap3-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/ldap3-$pkgver
  nosetests3 -s test || warning "Tests require a running LDAP server"

  cd "$srcdir"/ldap3-$pkgver-py2
  nosetests2 -s test || warning "Tests require a running LDAP server"
}

package_python-ldap3() {
  depends=('python-pyasn1')

  cd ldap3-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-ldap3() {
  depends=('python2-pyasn1')

  cd ldap3-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
