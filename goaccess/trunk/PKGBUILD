# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josh VanderLinden <arch@cloudlery.com>
# Contributor: Nicolas Storme <nicolas@atelier-web.com>

pkgname=goaccess
pkgver=1.2
pkgrel=3
pkgdesc="An open source real-time web log analyzer and interactive viewer"
arch=('x86_64')
url='http://goaccess.io'
license=('GPL')
depends=('ncurses' 'libmaxminddb' 'openssl-1.0')
optdepends=('geoip2-database: for geoip support')
backup=('etc/goaccess.conf')
source=("http://tar.goaccess.io/$pkgname-$pkgver.tar.gz"
        https://github.com/xobs/goaccess/commit/4152916ca78f3fddf418ecc833f0c91050bbb4a2.patch)
sha512sums=('bc31ef1e4c92608635856fa73e69b58267c161a7e3c6f2e3ca8860b862ed1c47575b93943dd975a5f8503774ee90ee0649d380563639e30908d876416638f889'
            'cf7fe6757a01f882dff8c70dc9b81c2dcf1fa0831e871ad87cb9c853dd29db7a7ac893712c49a6cb9e22d94620e79e06ec76fcf680e269f3870f4a4169a57617')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../4152916ca78f3fddf418ecc833f0c91050bbb4a2.patch

  export CFLAGS="$CFLAGS -I/usr/include/openssl-1.0"
  export LDFLAGS="$LDFLAGS -L/usr/lib/openssl-1.0"
}

build() {
  cd $pkgname-$pkgver
  ./configure --enable-geoip=mmdb --enable-utf8 --with-openssl
  make prefix=/usr sysconfdir=/etc
}

package() {
  cd $pkgname-$pkgver
  make prefix="$pkgdir/usr" sysconfdir="$pkgdir/etc" install
}
