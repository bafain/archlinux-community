# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: David Keogh <davekeogh@archlinux.us>

pkgname=pycharm-community-edition
pkgver=2018.2.2
_build=182.4129.34
pkgrel=1
pkgdesc='Python IDE for Professional Developers'
arch=('x86_64')
url='http://www.jetbrains.com/pycharm/'
license=('Apache')
depends=('giflib' 'glibc' 'java-runtime=8' 'python' 'sh' 'ttf-font')
optdepends=('ipython2: IPython integration for Python 2'
            'ipython: IPython integration for Python 3'
            'python2: Python 2 support')
makedepends=('apache-ant' 'java-environment=8' 'java-openjfx'
             'python2-setuptools' 'python-setuptools')
source=("pycharm-${_build}.tar.gz::https://github.com/JetBrains/intellij-community/archive/pycharm/${_build}.tar.gz"
        'pycharm-build.patch'
        'pycharm.desktop'
        'pycharm.sh')
sha256sums=('2a8890f8f17de42066a4decc69fae87fcf25d1eda4b19ba3083faaf5417c702c'
            '3793e8125abb05b1580919017469ada2563a2e5972a8d74666557df60d270cfd'
            '28e0d3200c721e61831d8246eba8f72485d3c8379d10bfa75ccb5331b6749480'
            '448f90f35f1ae1de784b663e9fd539baf494a7c08a8a7e2efbd5e75c801da12e')

prepare() {
  cd intellij-community-pycharm-${_build}

  patch -Np1 -i ../pycharm-build.patch
  sed "s/SNAPSHOT/${_build}/" -i python/build.xml
  sed '/intellij\.markdown/d' -i python/{build/plugin-list.txt,intellij.pycharm.community.main.iml}
}

build() {
  cd intellij-community-pycharm-${_build}/python

  unset _JAVA_OPTIONS

  ant build
  tar -xf ../out/pycharm-ce/artifacts/pycharmPC-${_build}-no-jdk.tar.gz -C "${srcdir}"

  cd ../../pycharm-community-${pkgver}

  python2 helpers/pydev/setup_cython.py build_ext --build-temp build --build-lib .
  python3 helpers/pydev/setup_cython.py build_ext --build-temp build --build-lib .
}

package() {
  cd pycharm-community-${pkgver}

  # workaround FS#40934
  sed -i 's/lcd/on/' bin/*.vmoptions

  rm -rf bin/fsnotifier{,-arm} lib/libpty/linux/x86

  install -dm 755 "${pkgdir}"/usr/share/{licenses,pixmaps,pycharm}
  cp -dr --no-preserve='ownership' bin helpers lib plugins "${pkgdir}"/usr/share/pycharm/
  cp -dr --no-preserve='ownership' license "${pkgdir}"/usr/share/licenses/pycharm/
  ln -s /usr/share/pycharm/bin/pycharm.png "${pkgdir}"/usr/share/pixmaps/
  install -Dm 644 ../pycharm.desktop -t "${pkgdir}"/usr/share/applications/
  install -Dm 755 ../pycharm.sh "${pkgdir}"/usr/bin/pycharm
}

# vim: ts=2 sw=2 et:
