# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>

pkgname=otf-overpass
pkgver=3.0.3
pkgrel=1
pkgdesc="An open source font family inspired by Highway Gothic esp. suited for signs, lettering and web sites"
arch=('any')
url="http://overpassfont.org/"
license=('custom:SIL Open Font License 1.1' 'LGPL2.1')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-font-utils')
replaces=('ttf-overpass')
source=($pkgname-$pkgver.tar.gz::https://github.com/RedHatBrand/overpass/archive/$pkgver.tar.gz)
sha256sums=('5575965097b1682330a9a4d36e4090bfe53f6cf97f72351adbf44e05e008a8aa')

package() {
  cd "${srcdir}/Overpass-$pkgver"

  install -d "${pkgdir}"/usr/share/{fonts/OTF,doc/$pkgname}

  install -m644 "desktop-fonts/overpass/"*.otf "${pkgdir}"/usr/share/fonts/OTF
  install -m644 "desktop-fonts/overpass-mono/"*.otf "${pkgdir}"/usr/share/fonts/OTF

#license and doc files
  install -Dm644 LICENSE.md "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE.md
  install -m644 overpass-specimen.pdf overpass-mono-specimen.pdf README.md \
    "${pkgdir}"/usr/share/doc/$pkgname
}
