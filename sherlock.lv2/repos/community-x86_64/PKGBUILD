# Maintainer: David Runge <dave@sleepmap.de>
pkgname=sherlock.lv2
pkgver=0.14.0
pkgrel=4
pkgdesc="An investigative plugin bundle"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/sherlock/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('libgl' 'sratom')
makedepends=('cmake' 'flex' 'lv2')
source=("https://git.open-music-kontrollers.ch/lv2/$pkgname/snapshot/$pkgname-$pkgver.tar.xz")
sha512sums=('fff8aad4bf1570d3919320d0adc2dcbc984d90e0b133f7062d492c1a14540c41cbe02601e490c40c715aa629688d81e39ec2f73906c52c5d4a581a6871783c10')

prepare() {
  cd "$pkgname-$pkgver"
  mkdir -v build
}

build() {
  cd "$pkgname-$pkgver/build"
  cmake -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        ..
  make
}

package() {
  cd "$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir/" install
  # docs
  install -vDm 644 ../README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
