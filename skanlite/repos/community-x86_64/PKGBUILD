# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: birdflesh <antkoul at gmail dot com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=skanlite
pkgver=2.1.0.1
pkgrel=1
pkgdesc="Image Scanning Application for KDE"
arch=(x86_64)
url='https://www.kde.org/applications/graphics/skanlite'
license=(GPL)
depends=(libksane kio)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/$pkgname/${pkgver%.*.*}/$pkgname-$pkgver.tar.xz") # {,.sig} - No public key available
sha256sums=('b4733e55b706eaf749522aa81ec47bfd65a95c2fec994c1c47e0c4b628e70442')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
