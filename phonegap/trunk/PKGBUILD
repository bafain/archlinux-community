# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=phonegap
pkgver=8.0.0
pkgrel=1
pkgdesc="PhoneGap command-line interface and node.js library"
arch=('x86_64')
url="https://github.com/phonegap/phonegap-cli"
license=('Apache')
depends=('acorn' 'semver' 'cordova' 'browserify')
makedepends=('npm' 'python2')
install=${pkgname}.install
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('603acfa6d0813c26ce2161f2da4184364b57d99a486006911c35378b5488931213a8a372b356be7bfc3086a06f974af42df59a200dc831d69cbe2f993cf2db5f')

package() {
  PYTHON=python2 npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in acorn semver cordova browserify; do
    rm -r $dep;
  done
}

# vim:set ts=2 sw=2 et:
