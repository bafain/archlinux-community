# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgbase=python-pytools
pkgname=('python2-pytools' 'python-pytools')
pkgver=2018.3
pkgrel=1
pkgdesc="A collection of tools for Python"
arch=('any')
url="http://mathema.tician.de/software/pytools"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-decorator' 'python2-decorator'
             'python-six' 'python2-six' 'python-appdirs' 'python2-appdirs' 'python-numpy'
             'python2-numpy')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("https://pypi.io/packages/source/p/pytools/pytools-$pkgver.tar.gz")
sha512sums=('4dd61f7e5372793ad3a27f2cd05e984b1f297a1b1a1f251f4e1468a5267b0c79ef06aadf7593d8cbfdca6f57f445cf4c0fc0ce1a616d1a6535f5da683c319c50')

prepare() {
  cp -a pytools-$pkgver{,-python2}
}

build() {
  cd "$srcdir"/pytools-$pkgver
  python setup.py build

  cd "$srcdir"/pytools-$pkgver-python2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pytools-$pkgver
  python setup.py pytest

  cd "$srcdir"/pytools-$pkgver-python2
  python2 setup.py pytest
}

package_python-pytools(){
  depends=('python-six' 'python-decorator' 'python-appdirs' 'python-numpy')

  cd pytools-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}

package_python2-pytools() {
  depends=('python2-six' 'python2-decorator' 'python2-appdirs' 'python2-numpy')

  cd pytools-$pkgver-python2
  python2 setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}
