# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: hexchain <i at hexchain.org>

pkgname=range-v3
pkgver=0.3.6
pkgrel=2
pkgdesc="Experimental range library for C++11/14/17"
arch=('any')
url='https://github.com/ericniebler/range-v3'
license=('custom')
makedepends=('cmake')
source=("https://github.com/ericniebler/range-v3/archive/${pkgver}.tar.gz")
sha512sums=('218cb179023980a3c13678da24638a10030e1c957c711117ebdd7d895f6480644b598686bc1a03d0460889ac058c0f6f9b2d9fcafaae0588a6e2cc1752be1db0')

prepare() {
    cd "${srcdir}/range-v3-${pkgver}"
    rm -rf build && mkdir build
}

build() {
    cd "${srcdir}/range-v3-${pkgver}"

    cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DRANGE_V3_TESTS=OFF \
        -DRANGE_V3_HEADER_CHECKS=OFF \
        -DRANGE_V3_EXAMPLES=OFF \
        -DRANGE_V3_PERF=OFF
    make
}

package() {
    cd "${srcdir}/range-v3-${pkgver}"
    install -Dm644 "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    cd build
    make DESTDIR="${pkgdir}/" install
}
