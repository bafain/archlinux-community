# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-google-auth-httplib2
pkgname=(python-google-auth-httplib2 python2-google-auth-httplib2)
pkgver=0.0.3
pkgrel=2
pkgdesc="Google Authentication Library: httplib2 transport"
url="https://github.com/GoogleCloudPlatform/google-auth-library-python-httplib2"
license=('Apache')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-google-auth' 'python2-google-auth'
             'python-httplib2' 'python2-httplib2' 'python-six' 'python2-six')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock'
              'python-flask' 'python2-flask' 'python-pytest-localserver'
              'python2-pytest-localserver')
source=("https://pypi.io/packages/source/g/google-auth-httplib2/google-auth-httplib2-$pkgver.tar.gz")
sha512sums=('da9d280f2ab3f9d99e2f011d56d94b09a12e85a5a1e9f78933a93cc57c7778d9002e1a023552a2611283da696334ba795cd7e8003b311db05e9b6b40aca34dc7')

prepare() {
  cp -a google-auth-httplib2-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/google-auth-httplib2-$pkgver
  python setup.py build

  cd "$srcdir"/google-auth-httplib2-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/google-auth-httplib2-$pkgver
  python setup.py pytest

  cd "$srcdir"/google-auth-httplib2-$pkgver-py2
  python2 setup.py pytest
}

package_python-google-auth-httplib2() {
  depends=('python-google-auth' 'python-httplib2' 'python-six')

  cd google-auth-httplib2-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-google-auth-httplib2() {
  depends=('python2-google-auth' 'python2-httplib2' 'python2-six')

  cd google-auth-httplib2-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
