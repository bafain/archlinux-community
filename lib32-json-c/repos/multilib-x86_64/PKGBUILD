# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=lib32-json-c
pkgver=0.13.1
pkgrel=2
pkgdesc="A JSON implementation in C (32-bit)"
url="https://github.com/json-c/json-c/wiki"
license=(MIT)
arch=(x86_64)
depends=(lib32-glibc json-c)
makedepends=(git)
_commit=985c46fec39d1d3043f98e8d8cdb9d040131b3bb  # tags/json-c-0.13.1-20180305^0
source=("git+https://github.com/json-c/json-c#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd json-c
  local tag="$(git describe --tags --abbrev=0)"
  local ver="$(git describe --tags)"
  echo "${tag%-*}${ver#$tag}" | sed 's/^json-c-//;s/-/+/g'
}

prepare() {
  cd json-c
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd json-c

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --disable-static \
    --enable-threading
  make
}

check() {
  cd json-c
  make check
}

package() {
  cd json-c
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir/usr/include"

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s json-c "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 et:
