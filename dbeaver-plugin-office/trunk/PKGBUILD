# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=dbeaver-plugin-office
pkgver=1.1.14
pkgrel=1
pkgdesc='DBeaver plugin to export data to Microsoft Office format'
arch=('any')
url='http://dbeaver.jkiss.org/'
license=('Apache')
makedepends=('unzip')
depends=('dbeaver>=4.2.5' 'dbeaver-plugin-apache-poi')
source=("https://dbeaver.jkiss.org/update/office/latest/plugins/org.jkiss.dbeaver.data.office_${pkgver}.jar"
        "https://dbeaver.jkiss.org/update/office/latest/features/org.jkiss.dbeaver.ext.office.feature_${pkgver}.jar"
        "${pkgname}.info")
sha256sums=('6cd29d8a6deb7a6a6e8eb39a5d7cc6b22f36ab6fd03559b11c26330b28938373'
            '9cecd8c42e38f1d57680d52b165e5b52df91bc1077834cb74e675f3f2000ec04'
            '7bea1ff09361ba07d2243c2222eeb1e4b5fa7983832a53ccbec672f82b2b5055')
noextract=("org.jkiss.dbeaver.data.office_${pkgver}.jar"
           "org.jkiss.dbeaver.ext.office.feature_${pkgver}.jar")

package() {
  # Install plugin
  install -m 755 -d "${pkgdir}/usr/lib/dbeaver/plugins"
  install -m 644 -t "${pkgdir}/usr/lib/dbeaver/plugins" "org.jkiss.dbeaver.data.office_${pkgver}.jar"

  # Extract feature
  install -m 755 -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.ext.office.feature_${pkgver}"
  unzip -q "${srcdir}/org.jkiss.dbeaver.ext.office.feature_${pkgver}.jar" \
        -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.ext.office.feature_${pkgver}"

  # Add configuration file (handled by the hook)
  install -m 755 -d "${pkgdir}/etc/dbeaver/bundles.d"
  install -m 644 "${srcdir}/${pkgname}.info" "${pkgdir}/etc/dbeaver/bundles.d/20-${pkgname}.info"
}
