# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

_pkgbasename=cairo
pkgname=lib32-$_pkgbasename
pkgver=1.15.10
pkgrel=1
pkgdesc="2D graphics library with support for multiple output devices (32-bit)"
url="https://cairographics.org/"
arch=(x86_64)
license=('LGPL' 'MPL')
depends=('lib32-libpng' 'lib32-libxrender' 'lib32-libxext' 'lib32-fontconfig' 'lib32-pixman' 'lib32-glib2' $_pkgbasename)
makedepends=('gcc-multilib' 'lib32-librsvg' 'lib32-gtk2')
source=(https://cairographics.org/snapshots/$_pkgbasename-$pkgver.tar.xz)
sha1sums=('de180498ac563249b93ee5e17ba9aa26f90644b3')

prepare() {
  cd $_pkgbasename-$pkgver
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $_pkgbasename-$pkgver
  ./configure --prefix=/usr \
        --libdir=/usr/lib32 \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --disable-gl \
        --enable-tee \
        --enable-gobject
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/{include,share,bin}
}
