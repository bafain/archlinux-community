# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Bartlomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jason Pierce <pierce.json@gmail.com>
# Contributor: Jeremy Cowgar <jeremy@cowgar.com>
# Contributor: Simon Perry <aur@sanxion.net>

pkgbase=dropbear
pkgname=(dropbear scp)
pkgver=2018.76
pkgrel=1
pkgdesc='Lightweight SSH server'
arch=('x86_64')
url='https://matt.ucc.asn.au/dropbear/dropbear.html'
license=('MIT')
options=(!emptydirs)
validpgpkeys=('F7347EF2EE2E07A267628CA944931494F29C6773')
source=("https://matt.ucc.asn.au/$pkgname/releases/$pkgname-$pkgver.tar.bz2"{,.asc}
        "$pkgname.service")
sha256sums=('f2fb9167eca8cf93456a5fc1d4faf709902a3ab70dd44e352f3acbc3ffdaea65'
            'SKIP'
            '8890a35b6cb14745c1b22f80d3a8532de2a853c78a30eac72b954012e286504a')

prepare() {
  sed -i 's,usr/libexec/sftp,usr/lib/ssh/sftp,;/#define DROPBEAR_SMALL_CODE/d' \
    "$pkgname-$pkgver/options.h"
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --bindir=/usr/bin --sbindir=/usr/bin
  make PROGRAMS="dbclient dropbear dropbearconvert dropbearkey scp" SCPPROGRESS=1
}

package_dropbear() {
  install -d "$pkgdir/etc/$pkgname"
  make -C "$pkgbase-$pkgver" install DESTDIR="$pkgdir"
  install -Dm644 "$pkgname.service" \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 "$pkgbase-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_scp() {
  pkgdesc='Lightweight application for copying files over SSH'
  conflicts=('openssh')

  make -C "$pkgbase-$pkgver" install PROGRAMS="scp" DESTDIR="$pkgdir"
  install -Dm644 "$pkgbase-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
