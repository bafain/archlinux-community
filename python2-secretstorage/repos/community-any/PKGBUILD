# $Id$
# Maintainer: Filipe La√≠ns (FFY00) <filipe.lains@gmail.com>

pkgname=python2-secretstorage
_pkgname=${pkgname#python2-}
pkgver=2.3.1
pkgrel=1
epoch=1
pkgdesc="Securely store passwords and other private data using the SecretService DBus API"
arch=('any')
url="https://github.com/mitya57/secretstorage"
license=('BSD')
depends=('python2-dbus' 'python2-cryptography')
makedepends=('python2-setuptools')
checkdepends=('gnome-keyring' 'xorg-server-xvfb')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('0a22937d5772ac43e3a5f5a0dc101911322386f23cb46036d1f0962b879a1896ba7a2852aed85ebb5cb6b0919a0da6517b3eb2f5dc7c69c7b5c4b7875145f66e')

build() {
  cd $_pkgname-$pkgver

  python2 setup.py build
}

check() {
  cd $_pkgname-$pkgver

  dbus-launch xvfb-run -a python2 -m unittest discover -s tests || warning "Tests failed"
}

package() {
  cd $_pkgname-$pkgver

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  # Install the license
  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
