# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: lowercase

pkgname=unace
pkgver=2.5
pkgrel=10
pkgdesc="An extraction tool for the proprietary ace archive format"
arch=('x86_64')
url="http://www.winace.com/"
license=("custom")
depends=(ncurses)
install=unace.install
source=(http://ftp.debian.org/debian/pool/non-free/u/unace-nonfree/unace-nonfree_2.5.orig.tar.gz \
        01-makefiles.dpatch 04-64bit.dpatch)
sha512sums=('f7e9f3431f882160693cb55a90449e881aead9dc501bb4bb8303f04530bd3f6e1d84bbc2775d23e7c680e1e2d55a06872658c4b14c7bf51dd2db73fdcdea953b'
            '65d019999b75e3443bf015fc3c69f6d323c0441a05c9d04b4d52a7c4f85bd2719bb543023266598ebbf16976d650e44dff874cbf22ba4c022920fba8608e371b'
            '1fb9e7ec089fe7eee8983bef54b3b50d8f9feac7e27f07114cb91ce98edd7026edde4920f32d7d06abb4dc236a05a7c6eb0bcfa3eb99872e5c17fce6e61651e8')

build() {
  cd ${srcdir}/unace-$pkgver

  patch -p1 < ../01-makefiles.dpatch
  [ "$CARCH" = "x86_64" ] && (patch -p1 < ../04-64bit.dpatch)
  make
}

package() {
  cd ${srcdir}/unace-$pkgver

  install -D -m755 unace ${pkgdir}/usr/bin/unace
  install -D -m644 licence ${pkgdir}/usr/share/licenses/unace/license
}
