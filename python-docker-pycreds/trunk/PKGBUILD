# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Hideaki Takahashi <mymelo+aur@gmail.com>
# Contributor: Vincent Demeester <vincent@sbr.pm>

pkgbase=python-docker-pycreds
pkgname=(python-docker-pycreds python2-docker-pycreds)
pkgver=0.2.2
pkgrel=1
pkgdesc="Python bindings for the docker credentials store API"
arch=('any')
license=('Apache')
url="https://github.com/shin-/docker-pycreds"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'flake8' 'python2-flake8'
              'python-pytest-cov' 'python2-pytest-cov')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/shin-/dockerpy-creds/archive/$pkgver.tar.gz")
sha512sums=('3b4d28fc1354d13ad517a96af415c76a3168432a2df6b416f50eede08419739e5ac3373997b218c3accf33ab80c61aca238aa60261e1b567dabae39ad01bf1d6')

prepare() {
  sed -i 's/==.*$//' dockerpy-creds-$pkgver/test-requirements.txt

  cp -a dockerpy-creds-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/dockerpy-creds-$pkgver
  python setup.py build

  cd "$srcdir"/dockerpy-creds-$pkgver-py2
  python2 setup.py build
}

# We don't have docker-credential-secretservice
check_disabled() {
  cd "$srcdir"/dockerpy-creds-$pkgver
  python setup.py pytest

  cd "$srcdir"/dockerpy-creds-$pkgver-py2
  python2 setup.py pytest
}

package_python-docker-pycreds() {
  depends=('python-six')

  cd dockerpy-creds-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-docker-pycreds() {
  depends=('python2-six')

  cd dockerpy-creds-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
