# $Id: PKGBUILD 122344 2014-11-11 08:16:49Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sebastien Leduc <sebastien@sleduc.fr>
# Contributor: Roberto Catini <roberto.catini@gmail.com>

pkgbase=python-requests-oauthlib
pkgname=('python-requests-oauthlib' 'python2-requests-oauthlib')
pkgver=1.0.0
pkgrel=2
pkgdesc="First-class OAuth library support for Requests"
arch=('any')
url="https://pypi.python.org/pypi/requests-oauthlib"
license=('custom:ISC')
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests'
             'python-oauthlib' 'python2-oauthlib' 'git')
checkdepends=('python-pytest' 'python2-pytest' 'python-requests-mock' 'python2-requests-mock'
              'python-mock' 'python2-mock' 'python-pyjwt' 'python2-pyjwt')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/requests/requests-oauthlib/archive/v$pkgver.tar.gz")
sha512sums=('5ceac61f14044b7c7ec4c26f68fc7b4ab6eda1529c367fead3e8aa5049b4aad12c76511861c13f2d62d11ec7ecd1d870ff69c7261bd51bcb01aa6d2e3ba5aa99')

prepare() {
  cp -a requests-oauthlib-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/requests-oauthlib-$pkgver
  python setup.py build

  cd "$srcdir"/requests-oauthlib-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/requests-oauthlib-$pkgver
  py.test

  cd "$srcdir"/requests-oauthlib-$pkgver-py2
  py.test2
}

package_python-requests-oauthlib() {
  depends=('python-requests' 'python-oauthlib')

  cd requests-oauthlib-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-requests-oauthlib() {
  depends=('python2-requests' 'python2-oauthlib')

  cd requests-oauthlib-$pkgver-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
