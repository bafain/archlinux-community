# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Andrew O'Neill <andrew at meanjollies dot com>
# Contributor: poisonby <poisonby@tutanota.com>
# Contributor: Jason Scurtu (scujas) <jscurtu@gmail.com>
# Contributor: Marcin Tydelski <marcin.tydelski@gmail.com>
# Contributor: Jan Lukas Gernert (JeanLuc) <https://launchpad.net/~eviltwin1>

pkgname=feedreader
_pkgname=FeedReader
pkgver=2.3.0
pkgrel=1
pkgdesc="Modern desktop application designed to complement existing web-based RSS accounts"
arch=(x86_64)
url="https://jangernert.github.io/FeedReader/"
license=(GPL3)
depends=(curl gnome-keyring gnome-online-accounts gumbo-parser libgee libpeas)
makedepends=(git gobject-introspection meson vala)
_commit=b187ce0f2fd1c916c42db4c0129a5fae6156572d  # tags/v2.3.0
source=("git+https://github.com/jangernert/$_pkgname#commit=$_commit"
        "git+https://github.com/fivefilters/ftr-site-config")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $_pkgname
  git config submodule.data/GrabberConfig.url "$srcdir/ftr-site-config"
  git submodule update --init
}

build() {
  arch-meson $_pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
