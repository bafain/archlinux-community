# $Id$
# Maintainer: Filipe La√≠ns (FFY00) <filipe.lains@gmail.com>
# Contributor: Michal Krenek (Mikos) <m.krenek@gmail.com>

pkgname=sdrangel
pkgver=4.0.7
pkgrel=2
pkgdesc="Qt5/OpenGL SDR and signal analyzer frontend."
arch=('x86_64')
url="https://github.com/f4exb/sdrangel"
license=('GPL3')
depends=('pkg-config' 'log4cpp' 'opencv'
	'qt5-base>=5.9' 'qt5-tools' 'qt5-multimedia' # QT5
	'fftw' 'lz4' 'nanomsg' 'ffmpeg>=3.1'
	'cm256cc' 'dsdcc>=1.8.3'
	'pulseaudio')
makedepends=('cmake' 'ffmpeg' 'airspy' 'bladerf' 'hackrf' 'limesuite' 'rtl-sdr' 'boost')
optdepends=('ffmpeg: DATV demodulator'
	    'libmirisdr4: SDRPlay support'
	    'rtl-sdr: RTLSDR support'
	    'hackrf: HackRF support'
	    'libad9361-iio: PlutoSDR support'
	    'limesuite: LimeSDR support'
	    'bladerf: BladeRF support'
	    'airspy: AirSPY support')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('677a240bdb2ff0138a6ba951394a43bf008935e4c705ce702922e639862f217e5f6cf6ec0bc6ba73646364c87aea905135665cf1eade411561e9d9fe21659596')

prepare() {
  sed -i 's|sdrangel_icon.xpm|sdrangel.xpm|' $pkgname-$pkgver/desktop/sdrangel.desktop
}

build() {
  mkdir -p $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBDSDCC_INCLUDE_DIR=/usr/include/dsdcc \
    -DCM256CC_INCLUDE_DIR=/usr/include/cm256cc

  make
}

package() {
  cd $pkgname-$pkgver/build

  make DESTDIR="$pkgdir" install

  # Install .desktop file
  install -Dm 644 ../desktop/sdrangel_icon.xpm "$pkgdir"/usr/share/pixmaps/sdrangel.xpm
  install -Dm 644 ../desktop/sdrangel.desktop "$pkgdir"/usr/share/applications/adrangel.desktop
}
