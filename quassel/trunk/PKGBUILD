# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>

pkgbase=quassel
pkgname=('quassel-core'
         'quassel-client'
         'quassel-client-small'
         'quassel-monolithic')
pkgver=0.13rc1
pkgrel=1
pkgdesc="Next-generation distributed IRC client"
arch=('x86_64')
url="https://quassel-irc.org/"
license=('GPL')
makedepends=('qt5-base' 'qt5-tools' 'qt5-webengine' 'qca-qt5' 'qt5-script'
             'qt5-multimedia' 'knotifyconfig' 'sonnet' 'libldap' 'cmake'
             'extra-cmake-modules' 'python' 'hicolor-icon-theme' 'ninja' 'git')
_commit=60dde7fac7b03220ac13b2e7f80733c221c1c01e  # tags/0.13-rc1^0
source=("git+https://github.com/quassel/quassel#commit=$_commit"
        $pkgbase.service
        $pkgbase.sysusers
        $pkgbase.tmpfiles
        $pkgbase.conf)
sha256sums=('SKIP'
            '5dbe20290f3361b9b7a74a52905137e76b656976febf2d31082a2276f9dcde7f'
            '3c72441a99e2668c6a8a751fa07beeb44f937576c8a1b5f615e4a55f841d93d9'
            '2afd4340c7713f6533e5d175a86b28fd118ecd907776c2b10925d1a4fb31cdca'
            'f3031ea8217e01ba42cea14606169e3e27affa5918968ffd5a03c21ae92fe2b8')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/-rc/rc/;s/-/+/g'
}

prepare() {
  cd $pkgbase
}

_build() (
  mkdir -p build-$1
  cd build-$1
  cmake -G Ninja ../$pkgbase \
    -DCMAKE_INSTALL_PREFIX=/usr/ \
    -DCMAKE_BUILD_TYPE=Release \
    -DHAVE_SSL=ON \
    "${@:2}" \
    -Wno-dev
  cmake --build .
)

_install() {
  DESTDIR="${pkgdir}" cmake --build build-$1 --target install
}

build() {
  _build core \
    -DWANT_QTCLIENT=OFF \
    -DWANT_MONO=OFF \
    -DWITH_BUNDLED_ICONS=OFF \
    -DWITH_WEBENGINE=OFF

  _build mono \
    -DWANT_CORE=OFF \
    -DWANT_QTCLIENT=OFF \
    -DWITH_KDE=ON

  _build client \
    -DWANT_CORE=OFF \
    -DWANT_MONO=OFF \
    -DWITH_KDE=ON

  _build client-small \
    -DWANT_CORE=OFF \
    -DWANT_MONO=OFF \
    -DWITH_WEBENGINE=OFF \
    -DCMAKE_DISABLE_FIND_PACKAGE_dbusmenu-qt5=ON \
    -DCMAKE_DISABLE_FIND_PACKAGE_Qt5Multimedia=ON \
    -DCMAKE_DISABLE_FIND_PACKAGE_Phonon4Qt5=ON
}

package_quassel-core() {
pkgdesc="Next-generation distributed IRC client - core only"
depends=('icu' 'qca-qt5' 'qt5-script' 'libldap')
optdepends=('postgresql: PostgreSQL database support')
backup=(etc/conf.d/quassel)

  _install core

  # Remove unneeded pixmap icons
  rm -r "${pkgdir}"/usr/share

  install -Dm644 $pkgbase.sysusers \
    "${pkgdir}"/usr/lib/sysusers.d/$pkgbase.conf
  install -Dm644 $pkgbase.tmpfiles \
    "${pkgdir}"/usr/lib/tmpfiles.d/$pkgbase.conf
  install -Dm644 $pkgbase.service \
    "${pkgdir}"/usr/lib/systemd/system/$pkgbase.service
  install -Dm644 $pkgbase.conf \
    "${pkgdir}"/etc/conf.d/$pkgbase
}

package_quassel-client() {
pkgdesc="Next-generation distributed IRC client - client only"
depends=('qt5-base' 'qt5-webengine' 'qt5-multimedia' 'knotifyconfig'
         'hicolor-icon-theme')
conflicts=('quassel-monolithic' 'quassel-client-small')

  _install client
}

package_quassel-client-small() {
pkgdesc="Next-generation distributed IRC client - client only, reduced deps"
depends=('qt5-base' 'sonnet' 'hicolor-icon-theme')
conflicts=('quassel-monolithic' 'quassel-client')
replaces=('quassel-remote')

  _install client-small
}

package_quassel-monolithic() {
pkgdesc="Next-generation distributed IRC client - monolithic"
depends=('qt5-base' 'qt5-webengine' 'qt5-multimedia' 'qt5-script' 'qca-qt5'
         'knotifyconfig' 'libldap' 'hicolor-icon-theme')
optdepends=('postgresql: PostgreSQL database support')
conflicts=('quassel-client' 'quassel-client-small')

  _install mono
}
