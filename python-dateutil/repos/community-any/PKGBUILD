# $Id$
# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: lilydjwg <lilydjwg@gmail.com>

pkgbase=python-dateutil
pkgname=('python-dateutil' 'python2-dateutil')
pkgver=2.7.0
pkgrel=1
pkgdesc="Provides powerful extensions to the standard datetime module"
arch=('any')
license=('BSD' 'Apache')
url="https://github.com/dateutil/dateutil"
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
checkdepends=('python-pytest' 'python2-pytest' 'python-freezegun' 'python2-freezegun')
source=("$pkgbase-$pkgver.tar.gz"::"https://github.com/dateutil/dateutil/archive/$pkgver.tar.gz")
source=("$url/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"{,.asc})
sha256sums=('8f95bb7e6edbb2456a51a1fb58c8dca942024b4f5844cae62c90aa88afe6e300'
            'SKIP')
validpgpkeys=('6B49ACBADCF6BD1CA20667ABCD54FCE3D964BEFB') # Paul Ganssle <paul@ganssle.io>

prepare() {
  cd "$srcdir"

  cp -r  python-dateutil-$pkgver python2-dateutil-$pkgver
}

build() {
  cd "$srcdir"/python-dateutil-$pkgver
  python setup.py build

  cd "$srcdir"/python2-dateutil-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/python-dateutil-$pkgver
  pytest

  cd "$srcdir"/python2-dateutil-$pkgver
  pytest2
}

package_python-dateutil() {
depends=('python-six')
  cd "$srcdir"/python-dateutil-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-dateutil() {
depends=('python2-six')
  cd "$srcdir"/python2-dateutil-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
