# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>

pkgname=nodejs-lts-carbon
pkgver=8.11.4
pkgrel=1
pkgdesc='Evented I/O for V8 javascript (LTS release: Carbon)'
arch=('x86_64')
url="https://nodejs.org/"
license=('MIT')
depends=('openssl-1.0' 'zlib' 'icu' 'libuv' 'http-parser' 'c-ares' 'libnghttp2')
makedepends=('python2' 'procps-ng')
optdepends=('npm: nodejs package manager')
install=node.install
provides=("nodejs=$pkgver")
conflicts=('nodejs')
source=("${url}/dist/v${pkgver}/node-v${pkgver}.tar.xz"
        add-icu-prefix-before-ICU-symbols.patch)
# https://nodejs.org/download/release/latest-carbon/SHASUMS256.txt.asc
sha256sums=('fbce7de6d96b0bcb0db0bf77f0e6ea999b6755e6930568aedaab06847552a609'
            'b0b22c490c4b4e7ed4bad46d1989e980a0ba30299855ec0164ff3d11384833c7')
validpgpkeys=('C4F0DFFF4E8C1A8236409D08E73BC641CC11F4C8' # Myles Borins <mylesborins@google.com>
              '77984A986EBC2AA786BC0F66B01FBB92821C587A' # Gibson Fahnestock <gibfahn@gmail.com>
              'B9AE9905FFD7803F25714661B63B535A4C206CA9' # Evan Lucas <evanlucas@me.com> 
              'DD8F2338BAE7501E3DD5AC78C273792F7D83545D') # Rod Vagg <rod@vagg.org>

prepare() {
  cd node-v${pkgver}

  # Fix build with ICU 61 https://github.com/nodejs/node/pull/18667
  patch -Np1 -i ../add-icu-prefix-before-ICU-symbols.patch

  msg 'Fixing for python2 name'
  find -type f -exec sed \
    -e 's_^#!/usr/bin/env python$_&2_' \
    -e 's_^\(#!/usr/bin/python2\).[45]$_\1_' \
    -e 's_^#!/usr/bin/python$_&2_' \
    -e 's_^\( *exec \+\)python\( \+.*\)$_\1python2\2_'\
    -e 's_^\(.*\)python\( \+-c \+.*\)$_\1python2\2_'\
    -e "s_'python'_'python2'_" -i {} \;
  find test/ -type f -exec sed 's_python _python2 _' -i {} \;
}

build() {
  cd node-v${pkgver}

  export PKG_CONFIG_PATH=/usr/lib/openssl-1.0/pkgconfig
  export PYTHON=python2
  ./configure \
    --prefix=/usr \
    --with-intl=system-icu \
    --without-npm \
    --shared-openssl \
    --shared-zlib \
    --shared-libuv \
    --shared-http-parser \
    --shared-cares \
    --shared-nghttp2

  make
}

check() {
  cd node-v${pkgver}
  # Expected failure: https://github.com/nodejs/node/issues/11627
  make test || warning "Tests failed"
}

package() {
  cd node-v${pkgver}

  make DESTDIR="${pkgdir}" install

  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
