# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=pandoc-crossref
pkgver=0.3.2.1
pkgrel=79
pkgdesc="Pandoc filter for cross-references"
url="https://hackage.haskell.org/package/${pkgname}"
license=("GPL2")
arch=('x86_64')
depends=('ghc-libs' 'haskell-data-default' 'haskell-data-accessor' 'haskell-data-accessor-template'
         'haskell-data-accessor-transformers' 'haskell-file-embed' 'haskell-open-browser'
         'haskell-optparse-applicative' 'pandoc' 'haskell-pandoc-types' 'haskell-roman-numerals'
         'haskell-syb' 'haskell-utility-ht')
makedepends=('ghc' 'haskell-hspec')
conflicts=('haskell-pandoc-crossref')
replaces=('haskell-pandoc-crossref')
source=("$pkgname-$pkgver.tar.gz::https://github.com/lierdakil/pandoc-crossref/archive/v$pkgver.tar.gz")
sha512sums=('224218d3a559fc6a2b574c4f62a7e99f28c25abb20f7fc2eb6b318f388d0c3e65f690c03b687964d50bb4fc87a77d79a6bebb20c196cd93f884f12c43ce1c68e')

build() {
    cd $pkgname-$pkgver
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    # https://github.com/lierdakil/pandoc-crossref/issues/181#issuecomment-398269601
    cd $pkgname-$pkgver
    runhaskell Setup test || warning "Tests failed"
}

package() {
    cd $pkgname-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
