# Maintainer: David Runge <dave@sleepmap.de>
pkgname=vim-csound
pkgver=0.7
pkgrel=1
pkgdesc="Csound tools for Vim"
arch=('any')
url="https://github.com/luisjure/csound-vim"
license=('MIT')
groups=('vim-plugins')
depends=('vim')
optdepends=('csound: Use vim with csound')
source=("$pkgname-$pkgver.tar.gz::https://github.com/luisjure/csound-vim/archive/v${pkgver}.tar.gz")
sha512sums=('35cd541d59a99e8b78059ef708ddb75801f79bd4175fc2ac2931f06d6e04d9fdd62c3c09c7c75a68883ffc724160ff0cbc70213041e4f8fcf8b2e31197795d25')

prepare() {
  mv -v "csound-vim-$pkgver" "$pkgname-$pkgver"
}

package() {
  cd "$pkgname-$pkgver"

  # plugin
  install -vDm644 doc/csound.txt \
    "${pkgdir}/usr/share/vim/vimfiles/doc/csound.txt"
  install -vDm644 ftdetect/csound.vim \
    "${pkgdir}/usr/share/vim/vimfiles/ftdetect/csound.vim"
  install -vDm644 macros/csound.vim \
    "${pkgdir}/usr/share/vim/vimfiles/macros/csound.vim"
  install -vDm644 plugin/csound.vim \
    "${pkgdir}/usr/share/vim/vimfiles/plugin/csound.vim"
  install -vDm644 syntax/*.vim \
    -t "${pkgdir}/usr/share/vim/vimfiles/syntax/"
  install -vDm644 templates/template.csd \
    "${pkgdir}/usr/share/vim/vimfiles/templates/template.csd"
  install -vDm644 words/csound.txt \
    "${pkgdir}/usr/share/vim/vimfiles/csounds/csound.txt"

  # license
  install -vDm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # docs
  install -vDm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}
