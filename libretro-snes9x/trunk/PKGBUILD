# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-snes9x
pkgver=831
pkgrel=1
pkgdesc='Super Nintendo Entertainment System core'
arch=('x86_64')
url='https://github.com/libretro/snes9x'
license=('custom' 'GPL2' 'LGPL2.1')
groups=('libretro')
depends=('libretro-core-info')
makedepends=('git')
_commit='ea68e8e542eab88acf8b7899115906aa24f10582'
source=("libretro-snes9x::git+https://github.com/libretro/snes9x.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd libretro-snes9x

  git rev-list --count HEAD
}

build() {
  cd libretro-snes9x/libretro

  make
}

package() {
  cd libretro-snes9x/libretro

  install -Dm 644 snes9x_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 ../docs/snes9x-license.txt -t "${pkgdir}"/usr/share/licenses/libretro-snes9x/
}

# vim: ts=2 sw=2 et:
