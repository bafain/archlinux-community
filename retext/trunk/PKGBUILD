# Maintainer: Jiachen Yang <farseerfc@gmail.com>
# AUR Maintainer: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>
# Contributor: Bartosz Chmura <chmurli@gmail.com>
# Contributor: Alessio Sergi <asergi at archlinux dot us>
# Contributor: alfplayer
# Contributor: menta

_pkgname=ReText
pkgname=retext
pkgver=7.0.3
pkgrel=3
pkgdesc="A simple editor for Markdown and ReStructuredText markup languages"
arch=('any')
url="https://github.com/retext-project/retext"
license=('GPL3')
# for desktop integration: 'shared-mime-info' 'xdg-utils' 'desktop-file-utils'
# for toolbar icons (see http://sourceforge.net/p/retext/tickets/44/): 'gconf'
depends=('python-pyqt5' 'python-markups>=2.0.0' 'shared-mime-info' 'xdg-utils'
         'desktop-file-utils' 'hicolor-icon-theme' 'python-markdown')
makedepends=('imagemagick' 'qt5-tools' 'python-setuptools')
checkdepends=('xorg-server-xvfb' 'python-chardet' 'python-docutils' 'python-pygments')
optdepends=('python-docutils: for reStructuredText language support'
            'qt5-webkit: for WebKit preview'
            'python-pyenchant: for spell checking support')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"{,.asc}
        "x-retext-markdown.xml"
        "x-retext-rst.xml")
sha256sums=('e549697c9b7254930c8529e5b1d44155980fcd3d182d95be39b01387af71673c'
            'SKIP'
            'b51611479d3224eec2b58264ed91ace3eccb502b7b806dae3e7a3ab4aab8c4b8'
            '6fef80cccb14813d9cc74810c397a6cd7831d1ca243536759a47c6e8b6cc977a')
validpgpkeys=('F24299FF1BBC9018B906A4CB6026936D2F1C8AE0') # Dmitry Shachnev <mitya57@debian.org>

build () {
    cd "$srcdir/${_pkgname}-${pkgver}"
    python3 setup.py build
}

check() {
    cd "$srcdir/${_pkgname}-${pkgver}"
    xvfb-run python3 setup.py test
}

package () {
    cd "$srcdir/${_pkgname}-${pkgver}"
    python3 setup.py install --root="$pkgdir" -O1

    # create /usr/share/* dirs
    _SHAREDIR="$pkgdir/usr/share"
    install -d -m 755 "$_SHAREDIR"/{applications,mime/packages}

    # install icons
    _ICONSDIR="$_SHAREDIR/icons/hicolor"
    for size in 16 22 24 32 48 128; do
      install -d -m 755 "$_ICONSDIR"/${size}x${size}/apps
      convert -resize $size icons/$pkgname.png "$_ICONSDIR"/${size}x${size}/apps/$pkgname.png
    done
    install -d -m 755 $_ICONSDIR/scalable/apps
    install -m 644 icons/$pkgname.svg "$_ICONSDIR"/scalable/apps/$pkgname.svg

    # install mime files
    install -m 644 "$srcdir"/x-retext-{markdown,rst}.xml "$_SHAREDIR"/mime/packages/
}

# vim:set ts=2 sw=2 et:
