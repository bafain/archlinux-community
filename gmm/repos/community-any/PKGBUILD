# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: eolianoe <eolianoe At GoogleMAIL DoT com>
# Contributor: Feng Wang <wanng.fenng AT gmail DOT com>
pkgname=gmm
pkgver=5.2
pkgrel=1
pkgdesc="Generic C++ finite element library"
arch=('any')
url="http://getfem.org/"
license=('LGPL')
source=("https://download-mirror.savannah.gnu.org/releases/getfem/stable/${pkgname}-${pkgver}.tar.gz")
sha512sums=('21ef4c11849de24ddfa3ca8673a3206c98dfac77756b0ca81af74a7aa732308560d1965beb2a3976913e4e61ffc2068a336f0db4d9c18c470f645cb79db3ea95')

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix check functionality
  sed -i "s/-ftemplate-depth-40/-ftemplate-depth=1024/" configure
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

check(){
  cd "${pkgname}-${pkgver}"
  make check
}

package(){
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}


