# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: farawayer <farwayer@gmail.com>

_gemname=public_suffix
pkgname=ruby-public_suffix
pkgver=3.0.2
pkgrel=1
pkgdesc='Domain name parser based on the Public Suffix List'
arch=('any')
url='https://simonecarletti.com/code/publicsuffix-ruby'
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/weppos/publicsuffix-ruby/archive/v${pkgver}.tar.gz)
sha512sums=('e275a2ae3fb5088abed466ad4f572233dc452e7002bec4960b838414f94b5af11722613785510f75092edbed446c210b30dea9022c08d6ab5545ad06f9281f92')

prepare() {
  cd publicsuffix-ruby-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
  sed 's|git ls-files -- {test,spec,features}/*|find -type f test spec features|' -i ${_gemname}.gemspec
  sed 's|git ls-files|find -type f|' -i ${_gemname}.gemspec
}

build() {
  cd publicsuffix-ruby-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd publicsuffix-ruby-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
