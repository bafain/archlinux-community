# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Magnus Therning <magnus@therning.org>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=hexer
pkgver=1.0.4
pkgrel=1
pkgdesc='Multi buffer editor for binary files with vi-like interface'
url='https://devel.ringlet.net/editors/hexer/'
arch=('x86_64')
license=('custom')
depends=('ncurses')
source=(https://devel.ringlet.net/files/editors/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc})
sha256sums=('0314afc74d650154b5bfae4ec7433fb90b43e11181e0e2ebb98457baaad41aa7'
            'SKIP')
sha512sums=('1c7ab32cf015d115fdbd2c4545cfe6592d5b67196c9f281d3a0654543e12f64df6a7862df1cb00d2fe85b9bb0e6c25705aeb4b5a9c3723cd0eb977e299635840'
            'SKIP')
validpgpkeys=('2EE7A7A517FC124CF115C354651EEFB02527DF13') # Peter Pentchev <roam@ringlet.net>

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr MANDIR=/usr/share/man/man1 install
  install -Dm 644 COPYRIGHT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
