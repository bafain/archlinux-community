# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=bash-language-server
pkgver=1.4.1
pkgrel=1
pkgdesc='Bash language server implementation based on Tree Sitter and its grammar for Bash'
arch=('x86_64')
url='https://github.com/mads-hartmann/bash-language-server'
license=('MIT')
depends=('nodejs' 'acorn')
makedepends=('npm' 'python2')
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=("$pkgname-$pkgver.tgz")
sha512sums=('46c321d228b47647f0ac603030ae274595186905ca898d2945d8226f93c3ced3400e6220f3e3039a2cc4c3a5d2493e213d4b59a092349f6e0eb9777ec94c28c0')

package() {
  PYTHON=python2 npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Fix permissions
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +

  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in acorn; do
    rm -r $dep;
  done

}
