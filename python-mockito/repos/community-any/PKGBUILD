# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgbase=python-mockito
pkgname=('python-mockito' 'python2-mockito')
pkgver=1.1.1
pkgrel=1
pkgdesc='Spying framework'
arch=('any')
url='https://github.com/kaste/mockito-python'
license=('MIT')
makedepends=('python-pytest' 'python2-pytest' 'python-setuptools' 'python2-setuptools'
             'python2-funcsigs')
checkdepends=('python-numpy' 'python2-numpy')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/kaste/mockito-python/archive/$pkgver.tar.gz")
sha512sums=('ae721fbea3b6ed810411ba795480060a080f217112cd06dfb95ebb6e2c5c86acc073559e8ee7d63367832dd8aa05ae63e7a6f6868b21261a0aedd6ade8591697')

prepare() {
  cp -a mockito-python-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/mockito-python-$pkgver
  python setup.py build

  cd "$srcdir"/mockito-python-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/mockito-python-$pkgver
  py.test build/lib

  cd "$srcdir"/mockito-python-$pkgver-py2
  py.test2 build/lib
}

package_python-mockito() {
  depends=('python')

  cd mockito-python-$pkgver
  python setup.py install --root="$pkgdir" -O1
}

package_python2-mockito() {
  depends=('python2-funcsigs')

  cd mockito-python-$pkgver-py2
  python2 setup.py install --root="$pkgdir" -O1
}
