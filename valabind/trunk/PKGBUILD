# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=valabind
pkgver=1.6.0
pkgrel=3
pkgdesc='Tool to parse vala or vapi files to transform them into swig interface files, C++, NodeJS-ffi or GIR'
url='https://radare.org/r/'
arch=('x86_64')
license=('GPL3')
depends=('sh' 'glibc' 'glib2' 'vala' 'swig')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/radare/${pkgname}/archive/${pkgver}.tar.gz
        valabind-fix-rpath.patch::https://github.com/radare/valabind/commit/774707925962fe5865002587ef031048acbe9d89.patch)
sha256sums=('0d266486655c257fd993758c3e4cc8e32f0ec6f45d0c0e15bb6e6be986e4b78e'
            'b4a46f94d90da957e57a6f2483eb4d7ce0c1769590c910a493599497aaa2cd8e')
sha512sums=('872e4d1feed4a79cef3a43982a317cffa513b8935bd8274d54935acd263e90132ed37d34e2735e79238a0498f6bb37b6e8355dd8485e5b91902664269fbb240d'
            '829bf15a10a4d8f420993f5e378d00dcf836e06068e340aea3306fe4a684a064aeff34cc79ac0e61c2de4ac012183c5c6961cac3f373213f80ca44851e5eb546')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < ../valabind-fix-rpath.patch
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
