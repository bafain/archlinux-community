# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Spyros Stathopoulos <foucault.online@gmail.com>
# Contributor: richli <rich at dranek dot com>
# Contributor: rememberthemer <rememberthemer@_GMAIL_DOT_COM_>

_pkg=netCDF4
pkgbase=python-${_pkg,,}
pkgname=("python-${_pkg,,}" "python2-${_pkg,,}")
pkgver=1.4.1
pkgrel=2
pkgdesc="Python/NumPy interface to the netCDF C library"
arch=('x86_64')
url="https://unidata.github.io/${_pkg,,}-python"
license=('MIT')
makedepends=('cython' 'python-setuptools' 'cython2' 'python2-setuptools' 'python-numpy' 'python-cftime' 'python2-numpy' 'python2-cftime' 'netcdf')
source=("https://pypi.io/packages/source/n/${_pkg}/${_pkg}-${pkgver}.tar.gz")
sha256sums=('d4fc65b98e348c39d082ab6b4b7f6d636b1b4e63bec016e5bca189fee5d46403')

prepare() {
    cp -a ${_pkg}-${pkgver}{,-py2}
}

build() {
    cd ${_pkg}-${pkgver}
    USE_NCCONFIG=1 python setup.py build

    cd ../${_pkg}-${pkgver}-py2
    USE_NCCONFIG=1 python2 setup.py build
}

check() {
    cd ${_pkg}-${pkgver}/test
    PYTHONPATH="../build/lib.linux-${CARCH}-3.7" python -B ./run_all.py

    cd ../../${_pkg}-${pkgver}-py2/test
    PYTHONPATH="../build/lib.linux-${CARCH}-2.7" python2 -B ./run_all.py
}

package_python-netcdf4() {
    depends=('python-numpy' 'python-cftime' 'netcdf')

    cd ${_pkg}-${pkgver}
    USE_NCCONFIG=1 python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=2

    install -Dm644 docs/netCDF4/* -t "${pkgdir}"/usr/share/doc/${pkgname}/docs/netCDF4
    install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

package_python2-netcdf4() {
    depends=('python2-numpy' 'python2-cftime' 'netcdf')

    cd ${_pkg}-${pkgver}-py2
    USE_NCCONFIG=1 python2 setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=2

    for binary in "${pkgdir}"/usr/bin/* ;
        do mv ${binary}{,-py2} ;
    done
    install -Dm644 docs/netCDF4/* -t "${pkgdir}"/usr/share/doc/${pkgname}/docs/netCDF4
    install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
