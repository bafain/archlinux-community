# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: sh0 <mee@sh0.org>
# Contributor: Maxwel <msg.maxwel@gmail.com>

pkgname=pax-utils
pkgver=1.2.3
pkgrel=3
pkgdesc='ELF utils that can check files for security relevant properties'
url='https://wiki.gentoo.org/wiki/Hardened/PaX_Utilities'
arch=('x86_64')
license=('GPL2')
depends=('bash' 'libcap' 'libseccomp' 'python-pyelftools')
source=(https://dev.gentoo.org/~slyfox/distfiles/${pkgname}-${pkgver}.tar.xz)
sha256sums=('d231b1a34aea2b9205c24fada79a8251d4c3fbcce49367fc055fe43e4c9783b2')
sha512sums=('efcbce49aa7e5cd433f9b3fbc8fa82f6d7b0eb80ec40aafde453d6fe96e3565e066e5020f04d71537d8d4e820147f4203c0df2dd330ebb1358a26def3dd5484c')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -E 's|(  paths =) \[\]|\1 ["/usr/lib"]|g' -i lddtree.py
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --with-caps \
    --with-seccomp \
    --with-python
  make
}

check() {
  cd ${pkgname}-${pkgver}
  LD_LIBRARY_PATH=/usr/lib make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
