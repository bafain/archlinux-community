# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Peter Simons <simons@cryp.to>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=rfc
pkgver=20180817
pkgrel=1
pkgdesc="RFCs from rfc-editor.org"
arch=('any')
url="https://www.rfc-editor.org/"
license=("custom:as-is")
options=('docs')
source=(https://sources.archlinux.org/other/community/rfc/rfc-index-$pkgver.txt
        https://sources.archlinux.org/other/community/rfc/RFC-all-$pkgver.tar.gz
        license)
sha256sums=('2e4f09ef30fe0ba3c92eddb249f436dea94834ec3d0c389c297a5d1754e4e902'
            'dd43f7f7f8c181124e8b8b4891e8e23a865dfa8e13abddc756781e47b37db200'
            'ac9cd91c2e15b0840b6ecd4f7d30d6d12c49e18b4f9eb383a0ce05a64901624e')

package() {
    cd "$srcdir"

    mkdir -p "$pkgdir/usr/share/doc/rfc"
    cp rfc-index-$pkgver.txt "$pkgdir/usr/share/doc/rfc/rfc-index.txt"
    rm -f rfc-index-$pkgver.txt

    tar xf rfc1305.tar
    rm -f rfc1305.tar
    for n in ntpv3*; do
      mv "$n" "rfc1305-$n"
    done

    for fmt in txt ps pdf; do
      mkdir -p "$pkgdir/usr/share/doc/rfc/${fmt}"
      mv *.${fmt} "$pkgdir/usr/share/doc/rfc/${fmt}/"
    done

    cd "$pkgdir/usr/share/doc/rfc"
    find . -type f -exec chmod 444 {} \;
    find . -type d -exec chmod 555 {} \;
    chown -R root:root .
    install -D -m0644 "$srcdir"/license "$pkgdir"/usr/share/licenses/$pkgname/license
}
