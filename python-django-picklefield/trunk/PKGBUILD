# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-django-picklefield
pkgver=1.1.0
pkgrel=1
pkgdesc='A pickled object field for Django'
arch=('any')
url='https://github.com/gintas/django-picklefield'
license=('MIT')
depends=('python-django')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/gintas/django-picklefield.git#tag=v${pkgver}")
sha256sums=('SKIP')

build() {
  cd django-picklefield

  python setup.py build
}

package() {
  cd django-picklefield

  python setup.py install --skip-build --root="${pkgdir}" --optimize='1' --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-django-picklefield/
}

# vim: ts=2 sw=2 et:
