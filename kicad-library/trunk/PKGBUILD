# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgbase=kicad-library
pkgname=('kicad-library' 'kicad-library-3d')
pkgver=5.0.0
pkgrel=1
pkgdesc="Kicad component and footprint libraries"
arch=('any')
url="https://github.com/KiCad/kicad-library"
license=('GPL')
makedepends=('cmake')
options=('!strip')
source=("kicad-sym.$pkgver.tgz::https://github.com/KiCad/kicad-symbols/archive/$pkgver.tar.gz"
        "kicad-foot.$pkgver.tgz::https://github.com/KiCad/kicad-footprints/archive/$pkgver.tar.gz"
        "kicad-3d.$pkgver.tgz::https://github.com/KiCad/kicad-packages3D/archive/$pkgver.tar.gz")
md5sums=('03cb33312062571118a277bafd01c06d'
         'e8ce40c47b108280d7878f858899ab10'
         'aac66499bbbd9f21ad3e90af7ba854d2')

build() {
  cd "$srcdir/kicad-symbols-$pkgver/"
  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make

  cd "$srcdir/kicad-footprints-$pkgver/"
  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make

  cd "$srcdir/kicad-packages3D-$pkgver/"
  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package_kicad-library() {
  pkgdesc="Kicad component and footprint libraries"
  cd "$srcdir/kicad-symbols-$pkgver/"
  make DESTDIR="$pkgdir" install

  cd "$srcdir/kicad-footprints-$pkgver/"
  make DESTDIR="$pkgdir" install
}

package_kicad-library-3d() {
  pkgdesc="Kicad 3D render model libraries"
  cd "$srcdir/kicad-packages3D-$pkgver/"
  make DESTDIR="$pkgdir" install
}
