# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=openfire
pkgver=4.2.3
pkgrel=1
pkgdesc="High performance XMPP (Jabber) server."
arch=('any')
url='http://www.igniterealtime.org/projects/openfire/'
license=('APACHE')
depends=('java-runtime-headless')
makedepends=('apache-ant')
backup=('etc/conf.d/openfire'
        'etc/openfire/openfire.xml'
        'etc/openfire/security.xml'
        'etc/openfire/crowd.properties'
        'etc/openfire/security/truststore'
        'etc/openfire/security/client.truststore'
        'etc/openfire/security/keystore')
source=("http://www.igniterealtime.org/downloadServlet?filename=openfire/openfire_src_${pkgver//./_}.tar.gz"
        'openfire.conf'
        'openfire.service'
        'user.conf'
        'tmpfile.conf')
sha256sums=('9a901bcd06920ccb5b417517db029a4a20b775d49aa39d918a996d4aaf47430c'
            'c8a612abee90bdc9a2869e562a4bbbf596159c60adcd23368833a0e2ca27c1d4'
            '3e7bca77161777daefd3533d4d6f42ff7970533c887d6f4b7f1c9fdcf7771d05'
            '8bfa51cf8cc88fc69547a8f6e9e3fb242926ca2a100659363e1a6b332f223ba9'
            'c63396991984a067d05e21094a664255d6aed2bf294bddd3885a7da75472b886')

build() {
	cd "$srcdir"/openfire_src/build
	rm -rf ../src/resources/nativeAuth/
	ant openfire
}

package() {
	cd "$pkgdir"
	install -dm755 usr/lib usr/share/openfire/resources
	install -dm750 etc/openfire

	cd "$srcdir"
	install -Dm644 openfire.conf "$pkgdir"/etc/conf.d/openfire
	install -Dm644 openfire.service "$pkgdir"/usr/lib/systemd/system/openfire.service
	install -Dm644 user.conf "$pkgdir"/usr/lib/sysusers.d/openfire.conf
	install -Dm644 tmpfile.conf "$pkgdir"/usr/lib/tmpfiles.d/openfire.conf

	cd openfire_src/target/openfire
	rm -rf bin logs lib/tray.dll

	cp -R conf/* "$pkgdir"/etc/openfire/

	cp -R resources/security "$pkgdir"/etc/openfire/
	ln -s /etc/openfire/security "$pkgdir"/usr/share/openfire/resources/security

	cp -R plugins "$pkgdir"/usr/share/openfire/
	cp -R resources/{database,spank} "$pkgdir"/usr/share/openfire/resources/
	cp -R lib "$pkgdir"/usr/lib/openfire
}
