# $Id$
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: simo <simo@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=oidentd
pkgver=2.3.1
pkgrel=2
pkgdesc='An RFC1413 compliant ident daemon'
arch=('x86_64')
url="https://oidentd.janikrabe.com/"
license=('GPL')
depends=('glibc' 'bash')
backup=(etc/oidentd.conf)
source=(https://github.com/janikrabe/oidentd/releases/download/v$pkgver/oidentd-$pkgver.tar.xz{,.asc}
        service socket sysusers.conf)

md5sums=('0a3d61e12f25fe2a8f5fc2540bfe82bf'
         'SKIP'
         '8e1fec6ceece6bbf394381c58b531f76'
         '4442fb9fc10754914568e6ffc3d5fc42'
         '62cda7be7064da326e091bf7110c6ca4')

validpgpkeys=('EB19A17D816542635D13B19FE3E8FA19DC9A1AA9') # Ryan McCabe

build() {
	cd oidentd-$pkgver
	./configure \
		--sysconfdir=/etc \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--mandir=/usr/share/man
	make
}

package() {
	cd oidentd-$pkgver
	make DESTDIR="$pkgdir" install

	install -D -m644 "$srcdir"/service "$pkgdir"/usr/lib/systemd/system/oidentd@.service
	install -D -m644 "$srcdir"/socket "$pkgdir"/usr/lib/systemd/system/oidentd.socket
	install -D -m644 "$srcdir"/sysusers.conf "$pkgdir"/usr/lib/sysusers.d/oidentd.conf
}

