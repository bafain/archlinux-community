# $Id$
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: simo <simo@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=oidentd
pkgver=2.2.0
pkgrel=1
pkgdesc='An RFC1413 compliant ident daemon'
arch=('x86_64')
url="http://oidentd.janikrabe.com/"
license=('GPL')
depends=('glibc' 'bash')
backup=(etc/oidentd.conf)
source=(https://github.com/janikrabe/oidentd/releases/download/v2.2.0/oidentd-2.2.0.tar.gz{,.asc}
        service socket)

md5sums=('05051bc3b4b3b66c36a2ba2dee79d325'
         'SKIP'
         'f54c5ed2a532a559c837af18735a28f1'
         '4442fb9fc10754914568e6ffc3d5fc42')

validpgpkeys=('EB19A17D816542635D13B19FE3E8FA19DC9A1AA9') # Ryan McCabe

build() {
	cd $pkgname-$pkgver
	./configure \
		--sysconfdir=/etc \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--mandir=/usr/share/man
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install

	install -D -m644 "$srcdir/service" "$pkgdir/usr/lib/systemd/system/oidentd@.service"
	install -D -m644 "$srcdir/socket" "$pkgdir/usr/lib/systemd/system/oidentd.socket"
}

