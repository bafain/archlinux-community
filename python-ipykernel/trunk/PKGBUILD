# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgbase=python-ipykernel
pkgname=(python-ipykernel python2-ipykernel)
pkgver=4.9.0
pkgrel=1
pkgdesc="The ipython kernel for Jupyter"
arch=('any')
url="https://pypi.org/project/ipykernel/"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' ipython 'ipython2' 'python2-jupyter_client' 'python-jupyter_client')
checkdepends=('python-pytest' 'python2-pytest' 'python-mock' 'python2-mock' 'python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tgz::https://github.com/ipython/ipykernel/archive/$pkgver.tar.gz")
md5sums=('a849a23a2db8d3c79fac0fcbe8703f04')

# dep cycle with python-jupyter_client

prepare() {
  cd "$srcdir"
  cp -r ipykernel-$pkgver python2-ipykernel-$pkgver

  # https://github.com/ipython/ipykernel/issues/332
  sed -i "s/executable='python'/executable='python2'/" python2-ipykernel-$pkgver/setup.py
}

build() {
  cd "$srcdir/ipykernel-$pkgver"
  python3 setup.py build

  cd "$srcdir/python2-ipykernel-$pkgver"
  python2 setup.py build

}

check() {
  cd "$srcdir/ipykernel-$pkgver"
  python -m pytest

  cd "$srcdir/python2-ipykernel-$pkgver"
  python2 -m pytest
}

package_python-ipykernel() {
  depends=('python-traitlets' 'python-tornado' 'ipython' 'python-jupyter_client')
  cd "$srcdir/ipykernel-$pkgver"

  python3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-ipykernel() {
  depends=('python2-traitlets' 'python2-tornado' 'ipython2' 'python2-jupyter_client')
  cd "$srcdir/python2-ipykernel-$pkgver"

  python2 setup.py install  --root="$pkgdir" --optimize=1
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
