# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python2-scandir
pkgver=1.7
pkgrel=1
pkgdesc="A better directory iterator and faster os.walk()"
arch=('x86_64')
url="https://github.com/benhoyt/scandir"
license=('BSD')
depends=('python2')
makedepends=('python2-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/benhoyt/scandir/archive/v$pkgver.tar.gz")
sha512sums=('d48a6b4b465eb996377467393bf5c67c86415cb65c5001982d4fbf99425bbef2cadfbae08a03b5eddbb6e2150db439ef410a69e98de3b615c3f8c8b0cb87fe31')

build() {
  cd scandir-$pkgver
  python2 setup.py build
}

check() {
  cd scandir-$pkgver
  LC_CTYPE=en_US.UTF-8 python2 test/run_tests.py
}

package() {
  cd scandir-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

# vim:set ts=2 sw=2 et:
