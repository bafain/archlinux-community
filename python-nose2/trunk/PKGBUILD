# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Simon Hanna <simon dot hanna AT serve-me DOT info>

_name=nose2
pkgbase=python-nose2
pkgname=('python-nose2' 'python2-nose2')
pkgver=0.6.5
pkgrel=2
pkgdesc="The next generation of nicer testing for python"
arch=('any')
url="https://github.com/nose-devs/nose2"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=("${pkgname[0]}-${pkgver}.tar.gz::https://github.com/nose-devs/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('05a22d6a4950f58dd4fcef9564c63ac2d23aa946a49ebc45fd78cd6ae01f2a46d669caced4ac8615b74a7baa1063f7d776b15b42758434cc1d509b8658c11b03')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

package_python-nose2() {
  depends=('python-six')
  cd "${pkgname[0]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -t "${pkgdir}/usr/share/doc/${pkgname}" -vDm644 {AUTHORS,README.rst}
}

package_python2-nose2() {
  depends=('python2-six')
  cd "${pkgname[1]}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -t "${pkgdir}/usr/share/doc/${pkgname}" -vDm644 {AUTHORS,README.rst}
  # move duplicates
  mv -v "${pkgdir}/usr/bin/nose2" "${pkgdir}/usr/bin/nose22"
}


# vim:set ts=2 sw=2 et:

