# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: kirek <rbuldo@gmail.com>
# Contributor: CÃ©dric Bellegarde <gnumdk@gmail.com>

pkgname=lollypop
pkgver=0.9.522
pkgrel=1
pkgdesc='Music player for GNOME'
arch=('any')
url='https://gitlab.gnome.org/gnumdk/lollypop'
license=('GPL')
depends=(
  'gst-plugins-base-libs' 'gtk3' 'python-beautifulsoup4' 'python-cairo'
  'python-dbus' 'python-gobject' 'totem-plparser'
)
makedepends=(
  'git' 'gobject-introspection' 'intltool' 'itstool' 'meson' 'python'
)
optdepends=('easytag: Modify tags'
            'flatpak: Flatpak Portal'
            'gst-libav: FFmpeg plugin for GStreamer'
            'gst-plugins-bad: "Bad" plugin libraries'
            'gst-plugins-base: "Base" plugin libraries'
            'gst-plugins-good: "Good" plugin libraries'
            'gst-plugins-ugly: "Ugly" plugin libraries'
            'kid3-qt: Store covers in tags'
            'libsecret: Last.FM support'
            'lollypop-portal: Flatpak portal for advanced functionalities'
            'python-pillow: Blur background in lyrics view'
            'python-pylast: Last.FM support'
            'python-wikipedia: Wikipedia support')
source=("git+https://gitlab.gnome.org/World/lollypop.git#tag=${pkgver}")
sha256sums=('SKIP')

build() {
  arch-meson lollypop build \
    --libexecdir='lib/lollypop'
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
