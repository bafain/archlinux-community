# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-asyncio
pkgver=0.9.0
pkgrel=1
pkgdesc="Pytest support for asyncio."
arch=('any')
license=('Apache')
url="https://github.com/pytest-dev/pytest-asyncio/"
depends=('python-pytest')
checkdepends=('python-pytest-cov' 'python-async_generator')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-asyncio/archive/v$pkgver.tar.gz")
sha512sums=('1b9c646b4025fe5ea0adb68a121c1c01a94278b64fd96e7edf588da12ad6d625f86674422fe52adb87e96a1c2617fcad9406770ddea00c1ef7954404ab6bdd53')

build() {
  cd pytest-asyncio-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-asyncio-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages:$PYTHONPATH:$PWD/tests" py.test
}

package() {
  cd pytest-asyncio-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
