# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=rack-protection
pkgname=ruby-${_gemname}
pkgver=2.0.1
pkgrel=1
pkgdesc='Protect against typical web attacks, works with all Rack apps, including Rails'
url='https://github.com/sinatra/sinatra/blob/master/rack-protection'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-rack')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/sinatra/sinatra/archive/v${pkgver}.tar.gz)
sha256sums=('f354286833680457d373cc28e52b423dda88b37c9dee2a8eac3510deec8680cc')
sha512sums=('ad897f39bfacfe76ca1997082e6104e545554ae04b11b7e75eabcafe47f77c3f24f2267f18940a5ae8f02b6372917a0ec7baadb76e349c274c57fcd6db1b8b2d')

prepare() {
  cd sinatra-${pkgver}/${_gemname}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd sinatra-${pkgver}/${_gemname}
  gem build ${_gemname}.gemspec
}

package() {
  cd sinatra-${pkgver}/${_gemname}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 License -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
