# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=rack-protection
pkgname=ruby-${_gemname}
pkgver=2.0.3
pkgrel=1
pkgdesc='Protect against typical web attacks, works with all Rack apps, including Rails'
url='https://github.com/sinatra/sinatra/blob/master/rack-protection'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-rack')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/sinatra/sinatra/archive/v${pkgver}.tar.gz)
sha256sums=('71a64ae1221554730d098a8c9b0a842c465350d65aedcf553e40aaee3b911b13')
sha512sums=('6188c6e372659522186649ae823f4fd3aa8a5b959cbb856f4cd42dc301e7430b3fad890a572d6f38d72aa99cf37a81e234e0c6ba9d03778a9c5a84b77a1dca76')

prepare() {
  cd sinatra-${pkgver}/${_gemname}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd sinatra-${pkgver}/${_gemname}
  gem build ${_gemname}.gemspec
}

package() {
  cd sinatra-${pkgver}/${_gemname}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 License -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
