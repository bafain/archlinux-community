# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: alphazo <alphazo@gmail.com>

pkgname=sshuttle
pkgver=0.78.4
pkgrel=4
pkgdesc='Transparent proxy server that forwards all TCP packets over ssh'
arch=('any')
url="https://github.com/sshuttle/sshuttle"
license=('GPL2')
depends=('python-setuptools' 'iptables' 'openssh' 'net-tools')
makedepends=('python-sphinx' 'python-setuptools_scm')
checkdepends=('python-pytest-runner' 'python-mock')
backup=('etc/sshuttle/tunnel.conf' 'etc/sshuttle/prefixes.conf')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sshuttle/$pkgname/archive/v$pkgver.tar.gz"
        'sshuttle.service' 'prefixes.conf' 'tunnel.conf')
sha512sums=('3c91d487ffe83a9b8019def6556586e0dc8e6c467275da403178807fb0ef5553781b69357484f58d8c3fda6ef9940caffc371b4698f8b83e4fcba2012575d730'
            'c22504798f6220cd6b0946d6975375437c52753a241cd2a6a03d3aeb005ed1d78e6356dde23716e2514ccf6d7f1c6aace43956dbeede1bf028753b9bdcdd8b7d'
            'cc5a5dee7991c7641fa8a4fb356e1977aa9d1cbfed63cb5ebf8732c4de76be841f66d9267472e70578b176f528d8cfb4f75e634d5d915f4c8bcdc0b801db8a13'
            'acd3d0024d4604cc6a96609286c3f27bce2f0b70a068f58a2110dacc235b22ba7cd83c8989f46d0a9391eda7d272040bb3b1b2c547ae3fa0c9f61bef0a187dda')

prepare() {
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd $pkgname-$pkgver
  python setup.py build

  cd docs
  make man
}

check() {
  cd $pkgname-$pkgver
  python setup.py pytest
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" -O1

  install -Dm644 docs/_build/man/sshuttle.1 "$pkgdir/usr/share/man/man1/sshuttle.1"

  install -d "$pkgdir/etc/sshuttle"
  install -m644 "$srcdir"/{tunnel.conf,prefixes.conf} "$pkgdir/etc/sshuttle"
  install -Dm644 "$srcdir/sshuttle.service" "$pkgdir/usr/lib/systemd/system/sshuttle.service"
}
