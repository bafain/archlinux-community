# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>

pkgname=vdirsyncer
pkgver=0.16.7
pkgrel=2
pkgdesc="Synchronize CalDAV and CardDAV."
arch=('any')
url="https://vdirsyncer.pimutils.org/en/stable/"
license=('BSD')
depends=('python-click' 'python-setuptools' 'python-requests-toolbelt'
'python-atomicwrites' 'python-click-threading' 'python-click-log')
makedepends=('python-setuptools-scm')
optdepends=('python-requests-oauthlib: Google support'
            'python-etesync: Etesync support')
checkdepends=('python-pytest' 'python-wsgi-intercept' 'radicale'
'python-pytest-xprocess' 'python-pytest-localserver' 'python-hypothesis'
'python-pytest-subtesthack')
source=("${pkgname}-${pkgver}.tar.gz::https://pypi.io/packages/source/v/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "https://raw.githubusercontent.com/pimutils/${pkgname}/${pkgver}/contrib/${pkgname}.service"
        "https://raw.githubusercontent.com/pimutils/${pkgname}/${pkgver}/contrib/${pkgname}.timer")
sha512sums=('218c6781139a9e979077308dc732756cc27ecb67966e4b52be31cb1ebf04d224443036102d5c4bad253b9dcf533939f0b72082166f0fe94e042a7e946b8fad0e'
            'f0d1f9755b21358b0371d94cb0d2f1fc12b8dc095d2536c7fc9025906d666ec33b85b3a3924b16a3311dc81e136e0790d50f84c7c8f9f65aaed80613fa69d066'
            'ac2656edc5ad39dbdc31a71117d98bfdc4ab5001729fc6a76f54e1e34ff81192f29e6162b2f8e429a5e02b8842fe1f2f51b00e84637eda53ba1448b8f08c6b80')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  make DETERMINISTIC_TESTS=true test
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # systemd
  install -t "${pkgdir}/usr/lib/systemd/user/" \
    -vDm 644 "${srcdir}/${pkgname}."{service,timer}
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,CHANGELOG,README}.rst
}

