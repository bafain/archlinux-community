# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=Ranged-sets
pkgname=haskell-ranged-sets
pkgver=0.3.0
pkgrel=14
pkgdesc="Ranged sets for Haskell"
url="http://code.haskell.org/ranged-sets"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-hunit' 'haskell-quickcheck')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz
        ghc-8.4.patch)
sha512sums=('4145fcfe12616a044a8152bbe90742c6ea4ff2400bb289c97a490a72910a0f5d7c6f3317bebc559499b4447ddb01434c573baf25afffa8a4b2a92bb2e3c7a4c8'
            '5d47e5ea4ee9ddc7c48f489714304794d644a3e489230de63bd414965169c6c0e4236162174cba8c89f01f3d322e0c30bc9264a5896f3bf2d894f17a549d991e')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../ghc-8.4.patch
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE.txt"
}
