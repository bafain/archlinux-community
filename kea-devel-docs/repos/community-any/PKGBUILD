# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
pkgname='kea-devel-docs'
pkgver=1.4.0
pkgrel=1
pkgdesc='Developer documentation for Kea, a high-performance, extensible DHCP server engine'
arch=('any')
url='http://kea.isc.org'
license=('MPL2')
depends=()
makedepends=('botan' 'boost' 'log4cplus' # Only here to make ./configure happy, not actually used.
             'libxslt' 'docbook-xsl' 'elinks' 'doxygen' 'graphviz')
source=(https://ftp.isc.org/isc/kea/${pkgver}/kea-${pkgver}.tar.gz{,.asc})
sha512sums=('ce4e2bcdc6f06472c4dccfc9cc26b06ff10ce1ce947b2bdc8753bbb15ff77831f7317625bdd81d24c945cf0d4c33cf9eef0ae0be21149532b0b9b9bc8892dd14'
            'SKIP')
validpgpkeys=('BE0E9748B718253A28BB89FFF1B11BF05CF02E57') # Internet Systems Consortium, Inc. (Signing key, 2017-2018) <codesign@isc.org>

build() {
  cd "${srcdir}/kea-${pkgver}"
  autoreconf --install
  ./configure \
      --prefix='/usr' \
      --sbindir='/usr/bin' \
      --libexecdir='/usr/bin' \
      --sysconfdir='/etc' \
      --localstatedir='/var'
  make -C doc devel
}

package() {
  cd "${srcdir}/kea-${pkgver}"
  install -d "${pkgdir}/usr/share/doc/kea/"
  cp -r doc/html "${pkgdir}/usr/share/doc/kea/devel"
}
