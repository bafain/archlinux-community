# $Id$
# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=soundconverter
pkgver=3.0.0
pkgrel=3
pkgdesc="A simple sound converter application for GNOME"
arch=('any')
license=('GPL3')
url="http://soundconverter.org/"
depends=('gst-plugins-base' 'gst-plugins-good' 'gtk3' 'libnotify' 'python-gobject')
makedepends=('intltool')
optdepends=('gst-plugins-ugly: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'
            'gst-libav: Extra media codecs')
source=("https://launchpad.net/soundconverter/trunk/$pkgver/+download/soundconverter-$pkgver.tar.xz"
        "drop-gconf.patch")
sha512sums=('6f5518711c76e7748447805dbf16ac8622c5041c3f3cac2de6f31968aab4eff43a2fde2da48c47d12093ce1732208923d83d57b6a97ea99fbf882dfef239a62d'
            '703954e9f5aac320bc3639ffc8f17cb5ae9e70b4344375cef8fb3be295519a8ef1d2631ca7f54ee2702e53a55c18fa674f56bda371048096c3a51877ff069e88')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Remove deprecated GConf dependency
  patch -Np0 -i "${srcdir}/drop-gconf.patch"

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr 
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
