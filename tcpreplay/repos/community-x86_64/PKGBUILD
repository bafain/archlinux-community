# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: SÃ©bastien Duquette <ekse.0x@gmail.com>
# Contributor: Dawid Wrobel <cromo@klej.net>

pkgname=tcpreplay
pkgver=4.2.6
pkgrel=2
pkgdesc='Gives the ability to replay previously captured traffic in a libpcap format'
url='http://tcpreplay.appneta.com'
arch=('x86_64')
license=('GPL3')
depends=('libpcap' 'libdnet' 'autogen')
makedepends=('tcpdump')
optdepends=('tcpdump: decoding of packets support')
options=('!emptydirs')
source=(https://github.com/appneta/tcpreplay/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz{,.asc})
sha512sums=('a46846b29e3cd7fbc635faee9c4be90db0d346c0d0f291ac8e007e1fbf614289094e14480c1c5b40feb5a175d3cbad018e2dd4d1a5762b7d31a4325af6102e7e'
            'SKIP')
validpgpkeys=('84E4FA215C934A7D97DC76D5E9E2149793BDE17E') # Fred Klassen <tcpreplay@appneta.com>

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --disable-local-libopts \
    --enable-dynamic-link \
    --enable-shared \
    --with-libdnet \
    --with-tcpdump=/usr/bin/tcpdump \
    ac_cv_header_net_bpf_h=n
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -j1 -C test tcpprep
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
