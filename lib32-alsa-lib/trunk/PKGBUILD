# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=lib32-alsa-lib
pkgver=1.1.6
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support (32 bit)"
arch=('x86_64')
url="http://www.alsa-project.org"
depends=('lib32-glibc' 'alsa-lib')
makedepends=('gcc-multilib')
license=('LGPL')
source=(ftp://ftp.alsa-project.org/pub/lib/alsa-lib-$pkgver.tar.bz2)

build() {
  cd alsa-lib-$pkgver

  export CC='gcc -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  ./configure --without-debug --prefix=/usr --libdir=/usr/lib32 --disable-python
  make
}

package() {
  cd alsa-lib-$pkgver
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/{bin,include,share}
}
md5sums=('2f981a8f7897c59ec2ddc44916d33788')
