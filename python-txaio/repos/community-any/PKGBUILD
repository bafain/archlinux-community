# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Anatol Pomozov

pkgbase=python-txaio
pkgname=(python-txaio python2-txaio)
pkgver=2.9.0
pkgrel=1
pkgdesc='Compatibility API between asyncio/Twisted/Trollius'
arch=('any')
url='https://github.com/tavendo/txaio'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-twisted' 'python2-twisted')
source=("https://pypi.io/packages/source/t/txaio/txaio-$pkgver.tar.gz")
sha512sums=('ee07d776fa5894b716fdced36aaa6436336a1f1db97dedb0fd0575d61a7b48b12dfc90ed6078602d7a2c2a00bfa7f815ca76cd75e472a27ff7bfdb56291efa46')

prepare() {
  cp -a txaio-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/txaio-$pkgver
  python setup.py build

  cd "$srcdir"/txaio-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/txaio-$pkgver
  python setup.py test

  cd "$srcdir"/txaio-$pkgver-py2
  python2 setup.py test
}

package_python-txaio() {
  depends=('python-six')

  cd txaio-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-txaio() {
  depends=('python2-six')

  cd txaio-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
