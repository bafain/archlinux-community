# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: David Adler <d dot adler aet posteo dot de>

pkgname=yoshimi
pkgver=1.5.6
pkgrel=4
pkgdesc="A sophisticated soft-synth originally forked from ZynAddSubFX"
arch=('x86_64')
url="https://yoshimi.github.io/"
license=('GPL')
depends=('jack' 'fltk' 'fftw' 'mxml' 'cairo' 'lv2')
makedepends=('cmake' 'mesa')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('90a5b99f97b4ef07ef4441563c9918f6f4670bec05ea050854580d329a4c15e82b27523e5ecc056c6ffc57bfcbcab3cec0e46e4535c868d9f70070dcbb34d875')

build() {
  cd "${pkgname}-${pkgver}/src"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_DATAROOTDIR='/usr/share' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib' .
  make
}

package() {
  cd "${pkgname}-${pkgver}/src"
  make DESTDIR="${pkgdir}" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm644 ../{Changelog,Dependencies,README.txt,Yoshimi_Helpers}
}

# vim:set ts=2 sw=2 et:

