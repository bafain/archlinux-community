# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: David Adler <d dot adler aet posteo dot de>

pkgname=yoshimi
pkgver=1.5.8.2
pkgrel=1
pkgdesc="A sophisticated soft-synth originally forked from ZynAddSubFX"
arch=('x86_64')
url="https://yoshimi.github.io/"
license=('GPL')
groups=('pro-audio')
depends=('cairo' 'fftw' 'fltk' 'jack' 'mxml')
makedepends=('cmake' 'lv2' 'mesa')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('6f37d1c8b1ffbf1028632f0f581671cd9cb165a05c6ce89a88f82bd959f1a07c4926b21fff1adf3d49e760713f6687f6aaceadccd46aac8799dfa09a8ba551d1')

build() {
  cd "${pkgname}-${pkgver}/src"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_DATAROOTDIR='/usr/share' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib' .
  make
}

package() {
  cd "${pkgname}-${pkgver}/src"
  make DESTDIR="${pkgdir}" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 ../{Changelog,Dependencies,README.txt,Yoshimi_Helpers}
}

# vim:set ts=2 sw=2 et:

