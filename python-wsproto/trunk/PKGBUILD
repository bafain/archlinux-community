# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgbase=python-wsproto
pkgname=(python-wsproto python2-wsproto)
pkgver=0.11.0
pkgrel=4
pkgdesc="WebSockets state-machine based protocol implementation"
url="https://github.com/python-hyper/wsproto"
license=('MIT')
arch=('any')
makedepends=('python-h11' 'python2-h11' 'python-setuptools' 'python2-setuptools' 'python2-enum34')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("https://pypi.io/packages/source/w/wsproto/wsproto-$pkgver.tar.gz"
        "pr54-h11-update.patch::$url/pull/54.patch")

sha512sums=('583e030dc88971b3733e5e84e5fbaa70d6043d01485cabbbf45470baa31e4f511aea55e108132fc298aeae0031542af7bdd22b7ae25b11832179025a2fde4bdd'
            'e123716dc239cb049419f6a668ba2b159c3c1c8db5c5f8147641f96f6ed74a730192929115b560df0be17197fc255dd7f4b31aec5734f7116742d3d4a0086747')

prepare() {
  patch -p1 -d wsproto-$pkgver < pr54-h11-update.patch
  cp -a wsproto-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/wsproto-$pkgver
  python setup.py build

  cd "$srcdir"/wsproto-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/wsproto-$pkgver
  python setup.py pytest

  cd "$srcdir"/wsproto-$pkgver-py2
  python2 setup.py pytest
}

package_python-wsproto() {
  depends=('python-h11')

  cd wsproto-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-wsproto() {
  depends=('python2-h11' 'python2-enum34')

  cd wsproto-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
