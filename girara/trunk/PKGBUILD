# $Id$
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: mlq <mlq@pwmt.org>

pkgname=girara
pkgver=0.3.0
pkgrel=2

pkgdesc="User interface library focused on simplicity and minimalism"
url="http://pwmt.org/projects/girara"
arch=('x86_64')
license=('custom')

provides=('girara-gtk3' 'girara-common')
replaces=('girara-gtk3' 'girara-common')
conflicts=('girara-gtk3' 'girara-common')

depends=('gtk3' 'glib2' 'json-c' 'pango')
makedepends=('meson' 'ninja')

source=(https://pwmt.org/projects/girara/download/girara-$pkgver.tar.xz)

sha512sums=('3994cd2707d549f9dd5f05f8e5f6e713b517c84fb40b6333d170bafdc9c9807dc5ef80913f557f549bd08a8679a8e0ad6243872a3231c5164d6b30a7441d94c2')

build() {
  cd girara-$pkgver
  arch-meson build

  cd build
  ninja
}

package() {
  cd girara-$pkgver/build
  DESTDIR="$pkgdir/" ninja install

  install -D -m664 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
