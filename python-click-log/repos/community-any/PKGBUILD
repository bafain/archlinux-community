# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: eolianoe <eolianoe At GoogleMAIL DoT com>

_name=click-log
pkgname=("python-${_name}" "python2-${_name}")
pkgver=0.3.2
pkgrel=2
pkgdesc="Logging integration for Click"
arch=("any")
url="https://pypi.python.org/pypi/click-log"
license=("MIT")
makedepends=("python-setuptools" "python2-setuptools")
checkdepends=("python-pytest" "python-click" "python2-pytest" "python2-click")
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('8ae5c43d739127367bbcdcf32bfd4b34b2ea1c377948df3b8588e4bfe2b0f5e7a61d1ccc6cb32c77045b15251222866daffa5a7bfb7a907487548784ba345351')

prepare() {
  mv "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

check() {
  # Needed for the tests, especially when building in clean chroot
  [[ ${LANG} = C ]] && export LANG=en_US.UTF8
  cd "${pkgname[0]}-${pkgver}"
  export PYTHONPATH=build/lib:$PYTHONPATH
  py.test

  cd ../"${pkgname[1]}-${pkgver}"
  export PYTHONPATH=build/lib:$PYTHONPATH
  py.test2
}

package_python-click-log() {
  depends=("python-click")
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 "LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}/"
}

package_python2-click-log() {
  depends=("python2-click")
  cd "${pkgname}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 "LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}/"
}

