# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=postgresql-binary
pkgname=haskell-postgresql-binary
pkgver=0.12.1.1
pkgrel=43
pkgdesc="Encoders and decoders for the PostgreSQL's binary format"
url="https://github.com/nikita-volkov/postgresql-binary"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-base-prelude' 'haskell-binary-parser'
         'haskell-bytestring-strict-builder' 'haskell-loch-th' 'haskell-network-ip'
         'haskell-placeholders' 'haskell-scientific' 'haskell-unordered-containers'
         'haskell-uuid' 'haskell-vector')
makedepends=('ghc' 'haskell-postgresql-libpq' 'haskell-tasty' 'haskell-tasty-quickcheck'
             'haskell-tasty-hunit' 'haskell-quickcheck' 'haskell-quickcheck-instances'
             'haskell-json-ast' 'haskell-conversion' 'haskell-conversion-bytestring'
             'haskell-conversion-text' 'haskell-rerebase' 'pifpaf' 'postgresql')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('386079b1816861da9e6e57e6ff6e30d3e01865102418173fc0e69145d924114b69d1ca99134158168c354d4fdac74bbb54d591849f87ed24548ef31fe22e04d1')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver

    eval $(pifpaf run postgresql --host 127.0.0.1 --port 5432)
    createuser -s postgres
    runhaskell Setup test
    pifpaf_stop
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
