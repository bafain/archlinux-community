# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: <kaptoxic _at_ yahoo _dot_ com >
# Contributor: Peter Simons <simons@cryp.to>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=maude
pkgver=2.7.1
pkgrel=3
pkgdesc="High-level Specification Language"
arch=('x86_64')
url="http://maude.cs.uiuc.edu"
license=('GPL')
depends=('buddy' 'cvc4' 'libtecla' 'gmp' 'libsigsegv')
makedepends=('flex' 'bison')
source=("http://maude.cs.illinois.edu/w/images/d/d8/Maude-$pkgver.tar.gz"
        "http://maude.cs.illinois.edu/w/images/c/ca/Full-Maude-$pkgver.zip"
        maude.sh maude-cvc4-1.5.patch)
sha512sums=('b4f172c679959a4c3386003f174a5938690926accd750ae2f5dcf945321a359d0428cf78d66ced9a560610a9cff5dae51f938542049e106c5f8bcba55c10f1a9'
            '19a665408db13ee60ffdd47d156ba8572a7f23fd51efac5b6dbc2922d022dd4c687541e59c94eb6ec6b27eb7e7b5ef5245409ae50775a15dfd663d98e106f805'
            'f62e1709d035e996c9f9a887840492527415f157990cc2d52355d8a7fe9382a757c06bed95f97e298f8eea0dd2a846bf7efb8fb84987784a4a797820807be12e'
            '87c50ee550bb5e8644dbc8b6b598bab1c74fa4f65d29b23b1d073b0844f879e1e314a8f2e7e72f1b8138f41c07ac92b0e98b50396909f77620987daaccb898a7')

prepare() {
  cd "$srcdir/maude-$pkgver"
  autoreconf -i

  patch -p1 -i ../maude-cvc4-1.5.patch # Fix build with CVC4 1.5
}

build() {
  cd "$srcdir/maude-$pkgver"
  ./configure --prefix=/usr --datadir=/usr/share/maude
  make -j1 all CVC4_LIB="-lcvc4 -lcln"
}

check() {
  cd "$srcdir/maude-$pkgver"
  make check
}

package() {
  cd "$srcdir/maude-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -D -m 444 "$srcdir/full-maude.maude" \
    "$pkgdir/usr/share/maude/full-maude.maude"

  install -D -m 555 "$srcdir/maude.sh" "$pkgdir/etc/profile.d/maude.sh"
}
