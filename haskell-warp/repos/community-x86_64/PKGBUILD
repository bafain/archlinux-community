# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=warp
pkgname=haskell-warp
pkgver=3.2.18.1
pkgrel=6
pkgdesc="A fast, light-weight web server for WAI applications."
url="https://github.com/yesodweb/wai"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-auto-update' 'haskell-blaze-builder'
         'haskell-case-insensitive' 'haskell-hashable' 'haskell-http-date' 'haskell-http-types'
         'haskell-http2' 'haskell-iproute' 'haskell-network' 'haskell-simple-sendfile' 'haskell-stm'
         'haskell-streaming-commons' 'haskell-text' 'haskell-unix-compat' 'haskell-vault'
         'haskell-wai' 'haskell-word8')
makedepends=('ghc' 'haskell-doctest' 'haskell-http-client' 'haskell-hspec' 'haskell-hunit'
             'haskell-lifted-base' 'haskell-quickcheck' 'haskell-silently')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('35eb7cf01c64fd36cc88bcb23330311dfa169c91fea434cfb5c19854ae75f6539b4c201445ac529406b266e0a2c543379cf3ba9c1161bae025b9cc50bbc6cb14')

prepare() {
    sed -i 's/"Network"/"Network", "-dynamic"/' $_hkgname-$pkgver/test/doctests.hs
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-warp-debug -fallow-sendfilefd -f-network-bytestring
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
