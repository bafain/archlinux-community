# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Based on extra/make

pkgname=remake
_remakever="dbg-1.4"
pkgver=4.2.1+dbg1.4
pkgrel=1
pkgdesc="Enhanced GNU Make - tracing, error reporting, debugging, profiling and more"
arch=('x86_64')
url="http://bashdb.sourceforge.net/remake/"
license=('GPL3')
depends=('glibc' 'guile')
source=("https://sourceforge.net/projects/bashdb/files/remake/4.2%2B${_remakever}/remake-4.2.1%2B${_remakever}.tar.bz2")
sha256sums=('55df3b2586ab90ac0983a049f1911c4a1d9b68f7715c69768fbb0405e96a0e7b')

prepare() {
  cd "${pkgname}-4.2.1+${_remakever}"
  autoreconf -fi
}

build() {
  cd "${pkgname}-4.2.1+${_remakever}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-4.2.1+${_remakever}"
  make DESTDIR=${pkgdir} install
  rm "$pkgdir"/usr/share/info/make.info{,-1,-2}
}
