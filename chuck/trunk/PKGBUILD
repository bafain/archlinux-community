# $Id$
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: tardo <tardo@nagi-fanboi.net>

pkgname=chuck
pkgver=1.4.0.0
pkgrel=2
pkgdesc='Concurrent, on-the-fly audio programming language'
arch=('x86_64')
url='http://chuck.cs.princeton.edu/'
license=('GPL')
depends=('jack')
groups=('pro-audio')
source=("http://chuck.cs.princeton.edu/release/files/$pkgname-$pkgver.tgz")
sha256sums=('2caee332b8d48e2fddad0f8a0a1811b6cf4c5afab73ae8a17b85ec759cce27ac')

build() {
  # Other build alternatives are: linux-alsa linux-oss osx win32
  make -C "$pkgname-$pkgver/src" linux-jack
}

package() {
  cd "$pkgname-$pkgver"
  install -d "$pkgdir/usr/share/doc/$pkgname"
  cp -r examples "$pkgdir/usr/share/doc/$pkgname"
  install -Dm755 "src/$pkgname" "$pkgdir/usr/bin/$pkgname"
}

# getver: chuck.cs.princeton.edu/release
# vim: ts=2 sw=2 et:
