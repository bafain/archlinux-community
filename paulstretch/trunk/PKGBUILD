# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
_pkgver=2.2-2
pkgname=paulstretch
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="An experimental program for extreme audio stretching"
arch=('x86_64')
url="http://hypermammut.sourceforge.net/paulstretch/"
license=('GPL2')
groups=('pro-audio')
depends=('audiofile' 'fftw' 'fltk' 'libvorbis' 'libmad' 'mxml' 'portaudio')
makedepends=('gendesk')
source=("https://sourceforge.net/projects/hypermammut/files/${pkgname}/${_pkgver//-*/}/${pkgname}-${_pkgver}.tar.bz2")
sha512sums=('cb076edc8ffcaae651220345b7205b8317cc8eed175c2a88992e8e27e2173b42272da4267dbd5054a2fc01eec7c104e816369eee86b39d746bf9eab3d7f55bc6')

prepare() {
  mv -v "$pkgname-${_pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # XDG desktop file
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --name Paulstretch \
          --categories "AudioVideo;Audio"
}

build() {
  cd "$pkgname-$pkgver"
  fluid -c GUI.fl
  fluid -c FreeEditUI.fl

  g++ -ggdb GUI.cxx FreeEditUI.cxx *.cpp Input/*.cpp Output/*.cpp \
      `fltk-config --cflags` \
      `fltk-config --ldflags` \
      -laudiofile \
      -lfftw3f \
      -lvorbisenc \
      -lvorbis \
      -logg \
      -lz \
      -lvorbisenc \
      -lvorbisfile \
      -lportaudio \
      -lpthread \
      -lmad \
      -lmxml \
      `pkg-config --cflags --libs jack samplerate` \
      -DHAVE_JACK \
      -DENABLE_RESAMPLING \
      -o ${pkgname}
}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 755 ${pkgname} -t "${pkgdir}/usr/bin/"
  install -vDm 644 readme.txt -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # XDG desktop integration
  install -vDm 644 "${pkgname}.desktop" -t "${pkgdir}/usr/share/applications/"

}
