# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Beej Jorgensen <beej@beej.us>

pkgname=wavemon
pkgver=0.8.2
pkgrel=1
pkgdesc='Ncurses-based monitoring application for wireless network devices'
url='https://github.com/uoaerg/wavemon'
arch=('x86_64')
license=('GPL3')
depends=('ncurses' 'libcap' 'libnl')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/uoaerg/${pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('383e8444602f1262015e5706ad76ae0cfd5ff9560cf8037edc92242bd89c1f8d4044a83d24009cac2c25b5bf3bda3ef52f0302d2ff0f21959c51ffdebe76ef25')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -e '/^CFLAGS=/d' -i configure.ac
  sed -r 's|(/share)|\1/doc|g' -i Makefile.in
  sed -r 's|\?=|=|g' -i Makefile.in
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
