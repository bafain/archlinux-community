# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Jason Chu <jchu@xentac.net>

pkgname=mahjong
pkgver=1.14
pkgrel=2
pkgdesc='The classical game of Mah Jong (not the solitare version) with network support'
arch=('x86_64')
url='http://mahjong.julianbradfield.org/'
license=('GPL' 'custom')
depends=('gtk2')
makedepends=('gendesk' 'imagemagick')
source=("http://mahjong.julianbradfield.org/Source/mj-$pkgver-src.tar.gz")
sha512sums=('b4c631ce388041475b877cd4b413fda200932aea19212c00cb606d2ef0bb6a498fe12b7b52b04ef744b296c37c531bc44d9e35f9aed44edf89651580acde91e5')

prepare() {
  gendesk -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" \
    --categories 'Application;Game' --exec xmj 
  convert "mj-$pkgver-src/icon.ico" "$pkgname.png"
}

build() {
  cd "mj-$pkgver-src"

  sed -i 's/& Calling/Calling/' gui-dial.c
  make LDLIBS="-lm"
}

package() {
  cd "mj-$pkgver-src"

  make DESTDIR="$pkgdir/usr/" MANDIR=share/man/man1 install install.man
  install -Dm644 tiles-v1/README "$pkgdir/usr/share/licenses/$pkgname/tiles-license.txt"
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$srcdir/$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
