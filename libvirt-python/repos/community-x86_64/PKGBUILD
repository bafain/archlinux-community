# ${Id}: PKGBUILD 101980 2013-12-03 09:48:38Z spupykin ${}
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

pkgbase=libvirt-python
pkgname=('libvirt-python2' 'libvirt-python')
pkgver=4.6.0
pkgrel=1
pkgdesc="libvirt python binding"
arch=('x86_64')
url="https://pypi.python.org/pypi/libvirt-python"
license=('LGPL')
makedepends=('python' 'python2' 'libvirt')
options=('emptydirs')
validpgpkeys=('C74415BA7C9C7F78F02E1DC34606B8A5DE95BC1F')
source=("http://libvirt.org/sources/python/libvirt-python-${pkgver}.tar.gz")
sha512sums=('15c71820ea3c3f71cfa127b82374f05cc99d4ed5189ad9ec0e471b3491fc4050f7f938590a46f0eb9d9efcbf45971881ed4f40376619a9f6b3796459626d645c')

package_libvirt-python2() {
  depends=('python2' 'libvirt')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  python2 setup.py clean
  python2 setup.py install --root="${pkgdir}" --optimize=1
}

package_libvirt-python() {
  depends=('python' 'libvirt')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py clean
  python setup.py install --root="${pkgdir}" --optimize=1
}
