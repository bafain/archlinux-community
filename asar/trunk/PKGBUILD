# $Id$
# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=asar
pkgver=0.14.3
pkgrel=1
pkgdesc='Creating Electron app packages'
arch=('any')
url='https://github.com/electron/asar'
license=('MIT')
depends=('nodejs')
makedepends=('npm')
options=(!emptydirs)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/electron/asar/archive/v${pkgver}.tar.gz")
sha256sums=('8e9ec795e25edbebaaae7ef00e362feee28a10bd342c63afe0194bf047e396c2')

package() {
    cd ${pkgname}-${pkgver}

    npm install -g --prefix="${pkgdir}"/usr "${srcdir}/${pkgname}-${pkgver}.tar.gz"

    moduledir=/usr/lib/node_modules/${pkgname}

    install -d "${pkgdir}/usr/share/licenses/${pkgname}"
    ln -s $(realpath \
            --relative-to="${pkgdir}"/usr/share/licenses/${pkgname} \
            "${pkgdir}"${moduledir})/LICENSE.md \
        "${pkgdir}/usr/share/licenses/${pkgname}"

    find "${pkgdir}" -type d -exec chmod 755 {} \;

    # Remove useless stuff and occurrences of ${pkgdir} and ${srcdir}
    find "${pkgdir}"${moduledir} \
        -name '.*' -prune -exec rm -r '{}' \; \
        -or -name '*.sh' -exec rm '{}' \; \
        -or -name 'appveyor.yml' -exec rm '{}' \; \
        -or -name 'benchmark' -prune -exec rm -r '{}' \; \
        -or -path "*/${pkgname}/node_modules/*/bin" -prune -exec rm -r '{}' \; \
        -or -name 'bin.js' -exec rm '{}' \; \
        -or -name 'bower.json' -exec rm '{}' \; \
        -or -name 'cleanup.sh' -exec rm '{}' \; \
        -or -name 'component.json' -exec rm '{}' \; \
        -or -name 'example' -prune -exec rm -r '{}' \; \
        -or -name 'example.js' -exec rm '{}' \; \
        -or -name 'examples' -prune -exec rm -r '{}' \; \
        -or -name 'Gruntfile.js' -exec rm '{}' \; \
        -or -name 'Makefile' -exec rm '{}' \; \
        -or -name 'man' -prune -exec rm -r '{}' \; \
        -or -name 'package.json' -exec sed -e "s|${pkgdir}||" \
            -e "s|${srcdir}/${pkgname}-${pkgver}|${moduledir}|" -i '{}' \; \
        -or -name 'perf' -prune -exec rm -r '{}' \; \
        -or -name 'scripts' -prune -exec rm -r '{}' \; \
        -or -name 'sync-package-managers.js' -exec rm '{}' \; \
        -or -name 'test' -prune -exec rm -r '{}' \; \
        -or -name 'test.js' -exec rm '{}' \; \
        -or -name 'tools' -prune -exec rm -r '{}' \; \
        -or -name 'tst' -prune -exec rm -r '{}' \;
}
