# Maintainer: David Runge <dave@sleepmap.de>
_name=pymediainfo
pkgbase=python-pymediainfo
pkgname=('python-pymediainfo' 'python2-pymediainfo')
pkgver=2.2.1
pkgrel=1
pkgdesc="A Python wrapper around the MediaInfo library"
arch=('any')
url="https://github.com/sbraz/pymediainfo/"
license=('MIT')
makedepends=('mediainfo' 'python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python-pytest-runner' 'python2-pytest' 'python2-pytest-runner')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sbraz/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('2db061800df608c5fdffc1a45a56683fc7397bb78ad7c81f9c5cc5c090e4b55f4f092380b93fe9e291e348b25fcfe70d7bdf6f36539f4fb90ad871bb16654278')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py test
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py test
}

package_python-pymediainfo() {
  depends=('mediainfo' 'python')
  cd "${pkgname[0]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # license
  install -vDm644 LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname[0]}/LICENSE"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname[0]}" \
    -vDm644 {AUTHORS,README.rst}
}

package_python2-pymediainfo() {
  depends=('mediainfo' 'python2')
  cd "${pkgname[1]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # license
  install -vDm644 LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname[1]}/LICENSE"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname[1]}" \
    -vDm644 {AUTHORS,README.rst}
}
